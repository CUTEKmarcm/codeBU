[RC.BRANCH.STATS - GATHER INFORMATION FROM VARIOUS REPORTS TO COMBINE INTO 1 FOR RAYE ANNE
                   New branches must be added to the arrays in the order requested by Raye Anne]
[08/14/14 GH Modified to get both old and new numbers for ATMs]
[09/03/14 GH Added Balcones Centre]
[10/04/16 GH Added Branch sequence and totals transactions counts from Teller Totals]
[10/04/16 GH Removed Sequence count per Raye Anne]
[10/07/19 GH Modified program to include new ATM numbers]
[04/21/20 JJ Modified program to total old and new ATM numbers]
[02/02/22 JJ Modified code to add branch 8 to branch 28]

TARGET=ACCOUNT

DEFINE
 #INCLUDE "IS.DEF"
 FLERROR=CHARACTER
 FNUMBER=NUMBER
 FLINE=CHARACTER
 FERROR=CHARACTER
 RLINE=CHARACTER
 CNTTOTALS=NUMBER     ARRAY(100,15)
 CNTATMS=NUMBER       ARRAY(100,4)
 MN=CHARACTER
 MY=CHARACTER(4)
 RPTNAME=CHARACTER
 SEQNUM=CHARACTER(6)
 GETTOTALS=CHARACTER(1)
 BR=NUMBER
 STDATE=DATE
 I=NUMBER
 J=NUMBER
 K=NUMBER
 X=NUMBER
 CL=NUMBER
 BRORDER=NUMBER       ARRAY(100)
 ORD=NUMBER
 TERMNUMBER=NUMBER    ARRAY(100,4)
 TID=NUMBER
 TMP=CHARACTER
 RUNDATE=DATE
 SL=CHARACTER(1)
 CH=CHARACTER
 B=NUMBER
 G=NUMBER
 V=NUMBER
 VT=NUMBER
 TMPNUMB=NUMBER
 TMPATM=CHARACTER
 FNDENGLISH=CHARACTER(1)
 FNDSPANISH=CHARACTER(1)
 [TRANSACTION COUNT VARIABLES]
 BGDATE=DATE
 EDDATE=DATE
 TRANCNT=NUMBER             ARRAY(30,2)
 SB=NUMBER
 SI=NUMBER
 SEQS=CHARACTER           ARRAY(31)
 SS=NUMBER
END

SETUP
 #INCLUDE "IS.SETUP"
 CH=UPPERCASE(CHARACTERREAD("C-CURRENT OR H-HISTORICAL"))
 FNDENGLISH="N"
 RUNDATE=DATEOFFSET(SYSTEMDATE,-1,31)
 MN=MONTHNAME(MONTH(RUNDATE))
 MY=FORMAT("9999",FULLYEAR(RUNDATE))
 STDATE=RUNDATE-7
 CALL FILLINORDER
END

SELECT 
 NONE
END

PRINT TITLE="Branch Statistic Summary "+MN+" "+MY
 HEADER=""

 CALL GETMONTHLYTOTALDEPOSITS
 CALL GETMONTHLYACCOUNTTABLE
 CALL GETATMNUMBERS
 CALL GETBRANCHSTATS
 CALL GETGLS
 CALL GETTELLERBRANCHTOTALS
END

TOTAL
 WHILELIMIT=800000000
 CL=35
 FOR I=1 TO 4
  DO
   X=7
   
   IF I=1 THEN BR=1
   ELSE IF I=2 THEN BR=9       [REVIEW ORDER AT BOTTOM]
   ELSE IF I=3 THEN BR=17
   ELSE IF I=4 THEN BR=25
   [ELSE 
    DO
     X=0
     BR=26
    END]
   
[
   IF I=1 THEN BR=1
   ELSE IF I=2 THEN BR=9       [REVIEW ORDER AT BOTTOM]
   ELSE IF I=3 THEN BR=17
   ELSE 
    DO
     X=0
     BR=25
    END
]
   CL=35
   COL=1 "Totals by Branch"
   FOR J=0 TO X
    DO
     IF BRORDER(BR+J)=12 THEN COL=CL RIGHT "Frontier Tr"
     ELSE IF BRORDER(BR+J)=28 THEN COL=CL RIGHT "Balcones"
     ELSE COL=CL RIGHT [BRANCHNAME(BRORDER(BR+J))][FIXME:]FORMAT("9999",BRORDER(BR+J))
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "# Deposit Trans"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),1)
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "Deposits for Month"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),2)
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "# New Accounts"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),3)
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "# New Checking"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),4)
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "Total ATM Trans"
   FOR J=0 TO X
    DO
     TMPATM=""
     IF TERMNUMBER(BRORDER(BR+J),2)=0 AND TERMNUMBER(BRORDER(BR+J),4)=0 THEN                [ONLY 1 TERMINAL AT THE BRANCH]
      DO
       IF CNTATMS(BRORDER(BR+J),1)+CNTATMS(BRORDER(BR+J),3)=0 THEN TMPATM="0"      [NO TRANSACTION AT ATM]
       ELSE 
        DO            
         TMPNUMB=CNTATMS(BRORDER(BR+J),1)+CNTATMS(BRORDER(BR+J),3)                 
         CALL GETATMCHRS                                  [CONVERT TO CHARACTER]
        END   
      END
     ELSE
      DO
       IF CNTATMS(BRORDER(BR+J),1)+CNTATMS(BRORDER(BR+J),3)=0 AND CNTATMS(BRORDER(BR+J),2)+CNTATMS(BRORDER(BR+J),4) =0 THEN TMPATM="0/0" [NO TRANSACTION AT EITHER TERMINAL]
       ELSE IF CNTATMS(BRORDER(BR+J),1)+CNTATMS(BRORDER(BR+J),3)=0 THEN TMPATM="0/"                            [NO TRANSACTION AT 1ST TERMINAL]
       ELSE
        DO
         TMPNUMB=CNTATMS(BRORDER(BR+J),1)+CNTATMS(BRORDER(BR+J),3)
         CALL GETATMCHRS  
         TMPATM=TMPATM+"/"                                                            [ADD SLASH BETWEEN TERMINAL NUMBERS]
        END  
       IF CNTATMS(BRORDER(BR+J),2)+CNTATMS(BRORDER(BR+J),4)=0 THEN TMPATM=TMPATM+"0"
       ELSE
        DO
         TMPNUMB=CNTATMS(BRORDER(BR+J),2)+CNTATMS(BRORDER(BR+J),4)
         CALL GETATMCHRS
        END     
      END
     COL=CL RIGHT TMPATM
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "Total Members"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),6)
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "Total Checking Accts"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),7)
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "Total Shares"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),8)
     CL=CL+13
    END
   NEWLINE
   CL=35
   COL=1 "Total Loans"
   FOR J=0 TO X
    DO
     COL=CL CNTTOTALS(BRORDER(BR+J),9)
     CL=CL+13
    END
   NEWLINE
   [CL=35
   COL=1 "# Branch Sequence"
   FOR J=0 TO X
    DO
     COL=CL TRANCNT(BRORDER(BR+J),1)
     CL=CL+13
    END
   NEWLINE]
   CL=35
   COL=1 "Total Branch Trans"
   FOR J=0 TO X
    DO
     COL=CL TRANCNT(BRORDER(BR+J),2)
     CL=CL+13
    END
   NEWLINE
   IF I=1 THEN
    DO
     COL=1 "CU Mortgage Loans"
     COL=35 CNTTOTALS(0,10)
     NEWLINE
     COL=1 "OD Tollerance"
     COL=35 CNTTOTALS(0,11)
     NEWLINE
     COL=1 "CUBS Loans"
     COL=35 CNTTOTALS(0,12)
     NEWLINE
     NEWLINE
     COL=1 "Overdrawn Accts"
     COL=35 CNTTOTALS(0,13)
     NEWLINE
    END
   NEWLINE
  END
END

PROCEDURE GETMONTHLYTOTALDEPOSITS
 WHILELIMIT=800000000
 RPTNAME="MONTHLY TOTAL DEPOSITS"
 CALL GETNEWESTREPORT
 IF SEQNUM<>"" THEN
  DO
   FILEOPEN("REPORT",SEQNUM,"READ",FNUMBER,FERROR)
   IF FERROR<>"" THEN 
    DO
     PRINT "ERROR OPENING SEQNUM# "+SEQNUM+"  "+FERROR
     NEWLINE
    END
   WHILE FERROR=""
    DO
     FILEREADLINE(FNUMBER,FLINE,FERROR)
     IF SEGMENT(FLINE,1,22)="Grand Totals For Month" THEN 
      DO
       GETTOTALS="Y"
       FILEREADLINE(FNUMBER,FLINE,FERROR)
      END
     IF GETTOTALS="Y" THEN
      DO
       IF SEGMENT(FLINE,1,5)="=====" THEN GETTOTALS="N"
       ELSE
        DO
         BR=VALUE(SEGMENT(FLINE,1,2))
         CNTTOTALS(BR,1)=VALUE(SEGMENT(FLINE,19,25))
         CNTTOTALS(BR,2)=VALUE(SEGMENT(FLINE,27,42))
         IF VALUE(SEGMENT(FLINE,44,45))>=50 THEN CNTTOTALS(BR,2)=CNTTOTALS(BR,2)+1
        END
      END
    END
   FILECLOSE(FNUMBER,FERROR)
  END
END

PROCEDURE GETMONTHLYACCOUNTTABLE
 WHILELIMIT=800000000
 RPTNAME="MONTHLY ACCOUNT TABLE"
 CALL GETNEWESTREPORT
 IF SEQNUM<>"" THEN
  DO
   FILEOPEN("REPORT",SEQNUM,"READ",FNUMBER,FERROR)
   IF FERROR<>"" THEN 
    DO
     PRINT "ERROR OPENING SEQNUM# "+SEQNUM+"  "+FERROR
     NEWLINE
    END
   WHILE FERROR=""
    DO
     FILEREADLINE(FNUMBER,FLINE,FERROR)
     IF SEGMENT(FLINE,30,41)="GRAND TOTALS" THEN 
      DO
       GETTOTALS="Y"
       FILEREADLINE(FNUMBER,FLINE,FERROR)
      END
     IF GETTOTALS="Y" THEN
      DO
       IF SEGMENT(FLINE,1,5)="-----" THEN GETTOTALS="N"
       ELSE
        DO
         BR=VALUE(SEGMENT(FLINE,1,2))
         CNTTOTALS(BR,3)=VALUE(SEGMENT(FLINE,19,25))
         CNTTOTALS(BR,4)=VALUE(SEGMENT(FLINE,38,45))
        END
      END
    END
   FILECLOSE(FNUMBER,FERROR)
  END
END


PROCEDURE GETATMNUMBERS
 WHILELIMIT=800000000
 RPTNAME="VISA+EF500413-RP01"
 CALL GETNEWESTREPORT
 IF SEQNUM="" THEN
  DO
   COL=1 "*** Month End VISA Dps need to be loaded for ATM totals ***"
   NEWLINE
   NEWLINE
  END
 ELSE
  DO
   FILEOPEN("REPORT",SEQNUM,"READ",FNUMBER,FERROR)
   IF FERROR<>"" THEN 
    DO
     PRINT "ERROR OPENING SEQNUM# "+SEQNUM+"  "+FERROR
     NEWLINE
    END
   WHILE FERROR=""
    DO
     FILEREADLINE(FNUMBER,FLINE,FERROR)
     IF SEGMENT(FLINE,1,11)="TERMINAL ID" THEN TID=VALUE(SEGMENT(FLINE,14,19))
     IF SEGMENT(FLINE,1,19)="INSTITUTION SUMMARY" THEN GETTOTALS="F"
     BR=3
     FOR I=1 TO LASTBR
      DO
       FOR K=1 TO 4
        DO
         IF TERMNUMBER(I,K)=TID THEN
          DO
           BR=I
           V=K
          END
        END
      END
     IF SEGMENT(FLINE,1,7)="ENGLISH" THEN FNDENGLISH="Y"
     [IF SEGMENT(FLINE,1,7)="SPANISH" THEN FNDSPANISH="Y"
     IF SEGMENT(FLINE,1,12)="TOTAL APPRVD" AND FNDSPANISH="Y" THEN
      DO
       print tid
       newline
       PRINT FLINE
       NEWLINE
       FNDSPANISH="N"
      END]
     IF SEGMENT(FLINE,1,12)="GRAND TOTALS" AND GETTOTALS<>"F" THEN
      DO
       IF FNDENGLISH="N" THEN GETTOTALS="Y"
       FNDENGLISH="N"
      END
     IF SEGMENT(FLINE,1,12)="TOTAL APPRVD" AND GETTOTALS="Y" THEN
      DO
       TMP=SEGMENT(FLINE,14,27)
       CNTATMS(BR,V)=CNTATMS(BR,V)+VALUE(TMP)
       [print tid
       print " br "
       print br
       print " v "
       print v
       print " tmp "
       print tmp
       print " CNTATMS "
       print CNTATMS(BR,V)
       PRINT " FNDENGLISH "+FNDENGLISH
       newline]
       TMP=""
       GETTOTALS="N"
       FNDENGLISH="N"
      END
    END
   FILECLOSE(FNUMBER,FERROR)
  END
END

PROCEDURE GETBRANCHSTATS
 WHILELIMIT=800000000
 FILELISTOPEN("REPORT","Consolidated Statistics by Branch",FLERROR)
  SEQNUM=""
  WHILE FLERROR="" AND SEQNUM=""
   DO
    FILELISTREAD(RLINE,FLERROR)
    SEQNUM=SEGMENT(RLINE,110,115)
     FILEOPEN("REPORT",SEQNUM,"READ",FNUMBER,FERROR)                       [Opens Report currently being evaluated]
      FILEREADLINE(FNUMBER,FLINE,FERROR) 
      IF DATEVALUE(SEGMENT(FLINE,83,90))<>RUNDATE THEN SEQNUM=""
     FILECLOSE(FNUMBER,FERROR)  
   END
  FILELISTCLOSE(FLERROR)
 IF SEQNUM<>"" THEN
  DO
   FILEOPEN("REPORT",SEQNUM,"READ",FNUMBER,FERROR)
   IF FERROR<>"" THEN 
    DO
     PRINT "ERROR OPENING SEQNUM# "+SEQNUM+"  "+FERROR
     NEWLINE
    END
   GETTOTALS="Y"
   WHILE FERROR=""
    DO
     IF CHARACTERSEARCH(FLINE,"INSTITUTION TOTALS")>0 THEN GETTOTALS="N"
     FILEREADLINE(FNUMBER,FLINE,FERROR)
     B=CHARACTERSEARCH(FLINE,"Branch")
     IF B>0 THEN BR=VALUE(SEGMENT(FLINE,B+7,B+8))
     IF BR=11 THEN BR=4
     [IF BR=8 THEN BR=28]
     IF SEGMENT(FLINE,28,41)="SHARES BY TYPE" THEN SL="S"
     ELSE IF SEGMENT(FLINE,28,40)="LOANS BY TYPE" THEN SL="L"
     IF GETTOTALS="Y" THEN
      DO
       IF SL="S" THEN
        DO
         IF SEGMENT(FLINE,2,20)="00  PRIMARY SAVINGS" THEN CNTTOTALS(BR,6)=CNTTOTALS(BR,6)+VALUE(SEGMENT(FLINE,52,60))
         IF SEGMENT(FLINE,2,21)="80  PRIMARY CHECKING" THEN CNTTOTALS(BR,7)=CNTTOTALS(BR,7)+VALUE(SEGMENT(FLINE,52,60))
         IF SEGMENT(FLINE,6,11)="TOTALS" THEN 
          DO
           CNTTOTALS(BR,8)=CNTTOTALS(BR,8)+VALUE(SEGMENT(FLINE,32,47))
           IF VALUE(SEGMENT(FLINE,49,50))>=50 THEN  CNTTOTALS(BR,8)=CNTTOTALS(BR,8)+1
          END
        END
       ELSE IF SL="L" AND SEGMENT(FLINE,6,11)="TOTALS" THEN 
        DO
         CNTTOTALS(BR,9)=CNTTOTALS(BR,9)+VALUE(SEGMENT(FLINE,32,47))
         IF VALUE(SEGMENT(FLINE,49,50))>=50 THEN  CNTTOTALS(BR,9)=CNTTOTALS(BR,9)+1
        END
      END
    END
   FILECLOSE(FNUMBER,FERROR)
  END
END

PROCEDURE GETNEWESTREPORT
 FILELISTOPEN("REPORT",RPTNAME,FLERROR)
 SEQNUM=""
 WHILE FLERROR=""
  DO
   FILELISTREAD(RLINE,FLERROR)
   IF DATEVALUE(SEGMENT(RLINE,62,69))>=STDATE THEN SEQNUM=SEGMENT(RLINE,110,115)
  END
 FILELISTCLOSE(FLERROR)
END

PROCEDURE GETGLS
 FOR GLACCOUNT WITH NUMBER "7010170015"
  DO
   G=10
   CALL GLDATA
  END
 FOR GLACCOUNT WITH NUMBER "7010170030"
  DO
   G=10
   CALL GLDATA
  END
 FOR GLACCOUNT WITH NUMBER "7010710015"
  DO
   G=10
   CALL GLDATA
  END
 FOR GLACCOUNT WITH NUMBER "7010710030"
  DO
   G=10
   CALL GLDATA
  END
 FOR GLACCOUNT WITH NUMBER "7011000000"
  DO
   G=11
   CALL GLDATA
  END
 FOR GLACCOUNT WITH NUMBER "7010720000"
  DO
   G=12
   CALL GLDATA
  END
 FOR GLACCOUNT WITH NUMBER "7010730000"
  DO
   G=12
   CALL GLDATA
  END
 FOR GLACCOUNT WITH NUMBER "9018100000"
  DO
   G=13
   CALL GLDATA
  END
END

PROCEDURE GLDATA
 TMP=""
 IF CH="H" THEN TMP=FORMAT("99,999,999,999.99",ABS(GLACCOUNT:HISTORICALBALANCE:1))
 ELSE TMP=FORMAT("99,999,999,999.99",ABS(GLACCOUNT:BALANCE))
 CNTTOTALS(0,G)=CNTTOTALS(0,G)+VALUE(SEGMENT(TMP,1,14))
 IF VALUE(SEGMENT(TMP,16,17))>=50 THEN CNTTOTALS(0,G)=CNTTOTALS(0,G)+1
END

PROCEDURE FILLINORDER
 BRORDER(1)=4     [SHOAL CREEK]
 BRORDER(2)=7     [CEDAR PARK]
 BRORDER(3)=1     [NORTH EAST - 290]
 BRORDER(4)=2     [TAOS]
 BRORDER(5)=10    [LA FRONTERA]
 BRORDER(6)=9     [JOLLYVILLE]
 BRORDER(7)=13    [LEANDER]
 BRORDER(8)=14    [BASTROP]
 BRORDER(9)=15    [LAKELINE]
 BRORDER(10)=16   [LOUIS HENNA]
 BRORDER(11)=17   [PECAN PARK]
 BRORDER(12)=18   [BRODIE]
 BRORDER(13)=19   [GEORGETOWN]
 BRORDER(14)=12   [FRONTIER TRAIL]
 BRORDER(15)=20   [BUDA]
 BRORDER(16)=21   [STONE HILL]
 BRORDER(17)=22   [UNIVERSITY OAKS]
 BRORDER(18)=23   [TECH RIDGE]
 BRORDER(19)=24   [LAKEWAY]
 BRORDER(20)=25   [TAYLOR]
 BRORDER(21)=26   [SAN MARCOS]
 BRORDER(22)=27   [LOCKHART]
 BRORDER(23)=28   [BALCONES CENTRE]
 BRORDER(24)=29   [OAK HILL]
 BRORDER(25)=8    [BALCONES HQ]
 BRORDER(26)=5    [ONLINE]

 TERMNUMBER(1,1)=354301  [290]
 TERMNUMBER(1,3)=354501
 TERMNUMBER(2,1)=354302  [Taos] 
 TERMNUMBER(2,2)=354314
 TERMNUMBER(2,3)=354502
 TERMNUMBER(2,4)=354514
 TERMNUMBER(3,1)=354303  [Applied Materials]
 TERMNUMBER(3,2)=354311
 TERMNUMBER(3,3)=354503 
 TERMNUMBER(3,4)=354511 
 TERMNUMBER(4,1)=354304  [Shoal Creek]
 TERMNUMBER(4,2)=354308
 TERMNUMBER(4,3)=354504
 TERMNUMBER(4,4)=354508
 TERMNUMBER(7,1)=354306  [Cedar Park]
 TERMNUMBER(7,2)=354307 
 TERMNUMBER(7,3)=354506
 TERMNUMBER(7,4)=354507
 TERMNUMBER(9,1)=354309  [Jollyville]
 TERMNUMBER(9,3)=354509
 TERMNUMBER(10,1)=354313 [La Frontera]
 TERMNUMBER(10,3)=354513 
 TERMNUMBER(13,1)=354315 [Leander]
 TERMNUMBER(13,3)=354515 
 TERMNUMBER(14,1)=354316 [Bastrop]
 TERMNUMBER(14,3)=354516
 TERMNUMBER(15,1)=354317 [Lakeline]
 TERMNUMBER(15,3)=354517
 TERMNUMBER(16,1)=354318 [Louis Henna]
 TERMNUMBER(16,3)=354518
 TERMNUMBER(17,1)=354319 [Pecan Park]
 TERMNUMBER(17,3)=354519
 TERMNUMBER(18,1)=354320 [Brodie]
 TERMNUMBER(18,3)=354520
 TERMNUMBER(19,1)=354321 [Georgetown]
 TERMNUMBER(19,3)=354521
 TERMNUMBER(20,1)=354322 [Buda]
 TERMNUMBER(20,3)=354522
 TERMNUMBER(21,1)=354323 [Stone Hill]
 TERMNUMBER(21,3)=354523
 TERMNUMBER(22,1)=354324 [University Oaks]
 TERMNUMBER(22,3)=354524
 TERMNUMBER(23,1)=354325 [Tech Ridge]
 TERMNUMBER(23,3)=354525
 TERMNUMBER(24,1)=354326 [Lakeway]
 TERMNUMBER(24,3)=354526
 TERMNUMBER(25,1)=354327 [Taylor]
 TERMNUMBER(25,3)=354527
 TERMNUMBER(26,1)=354328 [San Marcos]
 TERMNUMBER(26,3)=354528
 TERMNUMBER(27,1)=354329 [Lockhart]
 TERMNUMBER(27,3)=354529
 TERMNUMBER(28,1)=354330 [Balcones Centre]
 TERMNUMBER(28,3)=354530 
 TERMNUMBER(29,1)=354331 [Oak Hill]
 TERMNUMBER(29,3)=354531
END

PROCEDURE GETATMCHRS
 IF TMPNUMB<10 THEN TMPATM=TMPATM+FORMAT("9",TMPNUMB)
 ELSE IF TMPNUMB<100 THEN TMPATM=TMPATM+FORMAT("99",TMPNUMB)
 ELSE IF TMPNUMB<1000 THEN TMPATM=TMPATM+FORMAT("999",TMPNUMB)
 ELSE IF TMPNUMB<10000 THEN TMPATM=TMPATM+FORMAT("9,999",TMPNUMB)
 ELSE IF TMPNUMB<100000 THEN TMPATM=TMPATM+FORMAT("99,999",TMPNUMB)
END

PROCEDURE GETTELLERBRANCHTOTALS
 RPTNAME="BRANCHSTATS."+SHORTMONTH(MONTH(RUNDATE))+FORMAT("9999",FULLYEAR(RUNDATE))
 FILEOPEN("LETTER",RPTNAME,"READ",FNUMBER,FERROR) 
 WHILE FERROR=""
  DO                                              
   FILEREADLINE(FNUMBER,FLINE,FERROR)
   SB=VALUE(SEGMENT(FLINE,1,2))
   TRANCNT(SB,1)=TRANCNT(SB,1)+VALUE(SEGMENT(FLINE,5,15))
   TRANCNT(SB,2)=TRANCNT(SB,2)+VALUE(SEGMENT(FLINE,17,LENGTH(FLINE)))
  END
 FILECLOSE(FNUMBER,FERROR)
END


 #INCLUDE "IS.PRO"

