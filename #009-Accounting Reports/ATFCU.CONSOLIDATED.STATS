[
---------------------------------------------------------------
 Copyright 2020 Austin Telco Federal Credit Union
---------------------------------------------------------------
Repgen Name: ATFCU.CONSOLIDATED.STATS
Type       : Batch
Programmer : N/A 
Date       : N/A
Request #  : N/A
Requestor  : N/A
Department : Accounting
Summary    : Consolidated statistics on share, loan, and account level.
 
---------------------------------------------------------------
Requires   : 
 
---------------------------------------------------------------
Revisions  :
  09/02/09 GH Created from the old Monthly Consolidated statistics.  Title will be 
             Monthly Consolidated statistics for last day of the month and
             Daily Consolidated statistics all other days.
  05/02/11 GH Modified so totals will not print stars when ove 1 billion
  02/10/12 GH Changed range totals for RV/Boat per Tony
  05/01/12 GH Fixed problem with incorrect cert 72
  08/14/19 GH Added new subtotal for Non Member Certificates
  05/12/20 JJ Modified report so that non-member certs total correctly
  09/08/21 JJ #26731 Moved share type 85 and 86 to non member certs
---------------------------------------------------------------
Notes      : 
 
---------------------------------------------------------------
]
TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 PRIMARYSHARE = 00
 I=NUMBER
 LASTCHECKING=NUMBER

 SHBALBYTYPE=MONEY ARRAY(100)
 SHCOUNTBYTYPE=NUMBER ARRAY(100)
 LNBALBYTYPE=MONEY ARRAY(100)
 LNCOUNTBYTYPE=NUMBER ARRAY(100)

 SHSUBTOTBAL=MONEY
 SHSUBTOTCOUNT=NUMBER
 PREVSHSUBTOTBAL=MONEY
 PREVSHSUBTOTCOUNT=NUMBER
 TMPSUBTOTBAL=MONEY
 TMPSUBTOTCOUNT=NUMBER
 PREVTMPSUBTOTBAL=MONEY
 PREVTMPSUBTOTCOUNT=NUMBER

 PREVDATE=DATE
 PREVREPT=CHARACTER
 REPORTNO=CHARACTER(6)
 FERROR=CHARACTER
 FLINE=CHARACTER
 RLINE=CHARACTER
 FNUMBER=NUMBER
 PCNEG=CHARACTER

 PREVSHBALBYTYPE=MONEY ARRAY(100)
 PREVSHCOUNTBYTYPE=NUMBER ARRAY(100)
 PREVLNBALBYTYPE=MONEY ARRAY(100)
 PREVLNCOUNTBYTYPE=NUMBER ARRAY(100)
 
 SHBALBYDOLLAR=MONEY ARRAY(15)
 SHCOUNTBYDOLLAR=NUMBER ARRAY(15)
 LNBALBYDOLLAR=MONEY ARRAY(15)
 LNCOUNTBYDOLLAR=NUMBER ARRAY(15)

 ACCTSHBALANCE=MONEY
 ACCTLNBALANCE=MONEY

 ACCTSHBALBYDOLLAR=MONEY ARRAY(16)
 ACCTSHCOUNTBYDOLLAR=NUMBER ARRAY(16)
 ACCTLNBALBYDOLLAR=MONEY ARRAY(16)
 ACCTLNCOUNTBYDOLLAR=NUMBER ARRAY(16)

 SHINSCUTOFF=$2,000.00
 SHINSBAL=MONEY
 SHINSCOUNT=NUMBER
 SHINSEXCESSBAL=MONEY
 SHINSEXCESSCOUNT=NUMBER
 SHUNINSBAL=MONEY
 SHUNINSCOUNT=NUMBER

 LNINSCUTOFF=$10,000.00
 LNINSBAL=MONEY
 LNINSCOUNT=NUMBER
 LNINSEXCESSBAL=MONEY
 LNINSEXCESSCOUNT=NUMBER
 LNUNINSBAL=MONEY
 LNUNINSCOUNT=NUMBER

 MEMBERCOUNT=NUMBER
 ORGANIZATIONCOUNT=NUMBER
 MEMBERWITHBALCOUNT=NUMBER
 COUNTER=NUMBER
 SHDOLLARCATEGORY=NUMBER
 LNDOLLARCATEGORY=NUMBER
 SHAREDESC=CHARACTER ARRAY(100)
 LOANDESC=CHARACTER ARRAY(100)
 SHDOLLARDESC=CHARACTER ARRAY(16)
 LNDOLLARDESC=CHARACTER ARRAY(16)
 SUBTLOANBAL=MONEY ARRAY(6,2)
 SUBTLOANCNT=NUMBER ARRAY(6,2)
 ST=NUMBER
 TONYBALS=MONEY               ARRAY(20,2)
 TONYCNTS=NUMBER              ARRAY(20,2)
 TONYDESC=CHARACTER           ARRAY(20)
 TC=NUMBER
 LT=NUMBER
 LM=MONEY
 LB=NUMBER
 LC=NUMBER
 TCTOTBAL=MONEY               ARRAY(2)
 TCTOTCNT=NUMBER              ARRAY(2)
 MMBALS=MONEY                 ARRAY(6,2)
 MMCNTS=NUMBER                ARRAY(5,2)
 MMNAME=CHARACTER             ARRAY(5)
 MM=NUMBER
 PREVPAGE=NUMBER
 RPTTITLE=CHARACTER
 SUBTOTALDESC=CHARACTER
 SHARETOTALDESC=CHARACTER     ARRAY(20)
 SHARETOTALBALS=MONEY         ARRAY(20,2)
 SHARETOTALCNTS=NUMBER        ARRAY(20,2)
 SH=NUMBER
 SUBBALS=MONEY                ARRAY(3)
 SUBCNTS=NUMBER               ARRAY(3)
 BB=NUMBER  
 BE=NUMBER
 CB=NUMBER
 CE=NUMBER
END

SETUP
 IF DATEOFFSET(SYSTEMDATE,0,31)=SYSTEMDATE THEN RPTTITLE="Month End Consolidated Statistics"
 ELSE RPTTITLE="Daily Consolidated Statistics"
 IF SYSTEMDATE>='06/01/11' THEN                         [NEW MONTH END CONSOLIDATED DATA POSITIONS]
  DO
   BB=36
   BE=55
   CB=57
   CE=65
  END
 ELSE                                                   [OLD MONTH END CONSOLIDATED DATA POSITIONS]
  DO
   BB=36
   BE=50
   CB=51
   CE=60
  END
 PREVPAGE=1
 MMNAME(1)="$  0.00 - $  1,999.99"
 MMNAME(2)="$ 2,000 - $ 24,999.99"
 MMNAME(3)="$25,000 - $ 74,999.99"
 MMNAME(4)="$75,000 - $124,999.99"
 MMNAME(5)="$125,000 and Over"
 TONYDESC(1)="New Auto"
 TONYDESC(2)="Used Auto"
 TONYDESC(3)="RV's <=72"
 TONYDESC(4)="RV's 84"
 TONYDESC(5)="RV's 120"
 TONYDESC(6)="Dep Sec"
 TONYDESC(7)="Stock"
 TONYDESC(8)="Sign"
 TONYDESC(9)="HH"
 TONYDESC(10)="LOC"
 TONYDESC(11)="HE"
 TONYDESC(12)="HELOC"
 TONYDESC(13)="HI"
 TONYDESC(14)="M/C"
 TONYDESC(15)="VISA C"
 TONYDESC(16)="VISA G"
 TONYDESC(17)="Bus Lns"
 TONYDESC(18)="Non MBL Bus Lns"
 TONYDESC(19)="Other"

 FOR I=0 TO 99
  DO
   SHAREDESC(I)=GETDATACHARACTER(GETDEFAULTSHARE,I,055)
   LOANDESC(I)=GETDATACHARACTER(GETDEFAULTLOAN,I,077)
  END

 SHAREDESC(100)="TOTALS"
 LOANDESC(100)="TOTALS"

 REPORTNO=""
 FILELISTOPEN("REPORT", "Month End Consolidated Statistics", FERROR)
  CALL ERRORCHECK
  FILELISTREAD(FLINE,FERROR)
  REPORTNO=SEGMENT(FLINE,110,115)
 FILELISTCLOSE(FERROR)
 FILEOPEN("REPORT",REPORTNO,"READ",FNUMBER,FERROR)
 CALL ERRORCHECK
 FILEREADLINE(FNUMBER,RLINE,FERROR)
 CALL ERRORCHECK
 PREVREPT=SEGMENT(RLINE,116,119)
 PREVDATE=DATEVALUE(SEGMENT(RLINE,83,90))
 WHILE FERROR=""
  DO
   FILEREADLINE(FNUMBER,RLINE,FERROR)
   IF CHARACTERSEARCH(UPPERCASE(RLINE),"TOTALS")>0 THEN PREVPAGE=PREVPAGE+1
   COUNTER=VALUE(SEGMENT(RLINE,1,4))
   [PRINT "COUNTER - " + SEGMENT(RLINE,1,4)
   NEWLINE]
   IF COUNTER>0 OR CHARACTERSEARCH(UPPERCASE(RLINE),"PRIMARY SAVINGS")>0 THEN
    DO
     IF PREVPAGE=1 THEN
      DO
       IF SHAREDESC(COUNTER)="" THEN SHAREDESC(COUNTER)=SEGMENT(RLINE,6,35)
       IF COUNTER=50 THEN
        DO
         PREVSHBALBYTYPE(50)=$0.00
         PREVSHCOUNTBYTYPE(50)=0
         FOR MM=1 TO 5
          DO
           FILEREADLINE(FNUMBER,RLINE,FERROR)
           MMBALS(MM,2)=MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
           MMCNTS(MM,2)=VALUE(SEGMENT(RLINE,CB,CE))
           MMBALS(6,2)=MMBALS(6,2)+MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
           PREVSHBALBYTYPE(50)=PREVSHBALBYTYPE(50)+MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
           PREVSHCOUNTBYTYPE(50)=PREVSHCOUNTBYTYPE(50)+VALUE(SEGMENT(RLINE,CB,CE))
           PREVSHBALBYTYPE(100)=PREVSHBALBYTYPE(100)+MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
           PREVSHCOUNTBYTYPE(100)=PREVSHCOUNTBYTYPE(100)+VALUE(SEGMENT(RLINE,CB,CE))
          END
        END
       ELSE
        DO
         PREVSHBALBYTYPE(COUNTER)=MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
         PREVSHCOUNTBYTYPE(COUNTER)=VALUE(SEGMENT(RLINE,CB,CE))
         PREVSHBALBYTYPE(100)=PREVSHBALBYTYPE(100)+MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
         PREVSHCOUNTBYTYPE(100)=PREVSHCOUNTBYTYPE(100)+VALUE(SEGMENT(RLINE,CB,CE))
        END
      END
     ELSE IF PREVPAGE=2 THEN
      DO
       IF LOANDESC(COUNTER)="" THEN LOANDESC(COUNTER)=SEGMENT(RLINE,6,35) 
       IF COUNTER<100 THEN
        DO
         LT=COUNTER
         LB=2
         LM=MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
         LC=VALUE(SEGMENT(RLINE,CB,CE))
         CALL GETTONYBALS
        END
       PREVLNBALBYTYPE(COUNTER)=MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
       PREVLNCOUNTBYTYPE(COUNTER)=VALUE(SEGMENT(RLINE,CB,CE))
       PREVLNBALBYTYPE(100)=PREVLNBALBYTYPE(100)+MONEY(VALUE(SEGMENT(RLINE,BB,BE)))
       PREVLNCOUNTBYTYPE(100)=PREVLNCOUNTBYTYPE(100)+VALUE(SEGMENT(RLINE,CB,CE))
      END
    END
  END

 SHDOLLARDESC(00)="     $0.00 AND UNDER"
 SHDOLLARDESC(01)="      0.01-     5.00"
 SHDOLLARDESC(02)="      5.01-    10.00"
 SHDOLLARDESC(03)="     10.01-    50.00"
 SHDOLLARDESC(04)="     50.01-   100.00"
 SHDOLLARDESC(05)="    100.01-   250.00"
 SHDOLLARDESC(06)="    250.01-   500.00"
 SHDOLLARDESC(07)="    500.01- 1,000.00"
 SHDOLLARDESC(08)="  1,000.01- 2,000.00"
 SHDOLLARDESC(09)="  2,000.01- 5,000.00"
 SHDOLLARDESC(10)="  5,000.01-10,000.00"
 SHDOLLARDESC(11)=" 10,000.01-15,000.00"
 SHDOLLARDESC(12)=" 15,000.01-20,000.00"
 SHDOLLARDESC(13)=" 20,000.01-40,000.00"
 SHDOLLARDESC(14)=" 40,000.01-99,999.99"
 SHDOLLARDESC(15)="$100,000.00 AND OVER"
 SHDOLLARDESC(16)="       TOTALS       "

 LNDOLLARDESC(00)="     $0.00 AND UNDER"
 LNDOLLARDESC(01)="      0.01-   250.00"
 LNDOLLARDESC(02)="    250.01-   500.00"
 LNDOLLARDESC(03)="    500.01- 1,000.00"
 LNDOLLARDESC(04)="  1,000.01- 2,000.00"
 LNDOLLARDESC(05)="  2,000.01- 3,000.00"
 LNDOLLARDESC(06)="  3,000.01- 4,000.00"
 LNDOLLARDESC(07)="  4,000.01- 5,000.00"
 LNDOLLARDESC(08)="  5,000.01- 7,500.00"
 LNDOLLARDESC(09)="  7,500.01-10,000.00"
 LNDOLLARDESC(10)=" 10,000.01-12,500.00"
 LNDOLLARDESC(11)=" 12,500.01-15,000.00"
 LNDOLLARDESC(12)=" 15,000.01-20,000.00"
 LNDOLLARDESC(13)=" 20,000.01-40,000.00"
 LNDOLLARDESC(14)=" 40,000.01-99,999.99"
 LNDOLLARDESC(15)="$100,000.00 AND OVER"
 LNDOLLARDESC(16)="       TOTALS       "
END

SELECT
 ACCOUNT:CLOSEDATE='--/--/--'
END

PRINT TITLE=RPTTITLE

 HEADER=""

 ACCTSHBALANCE=$0.00

 FOR EACH SHARE WITH (SHARE:CLOSEDATE='--/--/--')
  DO
   [todo:]
   IF SHARE:TYPE=PRIMARYSHARE THEN
    DO
     MEMBERCOUNT=MEMBERCOUNT+1
    END
    
   LASTCHECKING=91
   IF SHARE:TYPE=90 THEN
    LASTCHECKING=90
   SHBALBYTYPE(SHARE:TYPE)=SHBALBYTYPE(SHARE:TYPE)+SHARE:BALANCE
   SHCOUNTBYTYPE(SHARE:TYPE)=SHCOUNTBYTYPE(SHARE:TYPE)+1
   IF SHARE:TYPE=50 THEN
    DO
     IF SHARE:BALANCE<$2,000.00 THEN MM=1
     ELSE IF SHARE:BALANCE<$25,000.00 THEN MM=2
     ELSE IF SHARE:BALANCE<$75,000.00 THEN MM=3
     ELSE IF SHARE:BALANCE<$125,000.00 THEN MM=4
     ELSE MM=5
     MMBALS(MM,1)=MMBALS(MM,1)+SHARE:BALANCE
     MMCNTS(MM,1)=MMCNTS(MM,1)+1
     MMBALS(6,1)=MMBALS(6,1)+SHARE:BALANCE
    END

   SHBALBYTYPE(100)=SHBALBYTYPE(100)+SHARE:BALANCE
   SHCOUNTBYTYPE(100)=SHCOUNTBYTYPE(100)+1

   IF SHARE:BALANCE<=$0.00 THEN SHDOLLARCATEGORY=00
   ELSE IF SHARE:BALANCE<=$5.00 THEN SHDOLLARCATEGORY=01
   ELSE IF SHARE:BALANCE<=$10.00 THEN SHDOLLARCATEGORY=02
   ELSE IF SHARE:BALANCE<=$50.00 THEN SHDOLLARCATEGORY=03
   ELSE IF SHARE:BALANCE<=$100.00 THEN SHDOLLARCATEGORY=04
   ELSE IF SHARE:BALANCE<=$250.00 THEN SHDOLLARCATEGORY=05
   ELSE IF SHARE:BALANCE<=$500.00 THEN SHDOLLARCATEGORY=06
   ELSE IF SHARE:BALANCE<=$1,000.00 THEN SHDOLLARCATEGORY=07
   ELSE IF SHARE:BALANCE<=$2,000.00 THEN SHDOLLARCATEGORY=08
   ELSE IF SHARE:BALANCE<=$5,000.00 THEN SHDOLLARCATEGORY=09
   ELSE IF SHARE:BALANCE<=$10,000.00 THEN SHDOLLARCATEGORY=10
   ELSE IF SHARE:BALANCE<=$15,000.00 THEN SHDOLLARCATEGORY=11
   ELSE IF SHARE:BALANCE<=$20,000.00 THEN SHDOLLARCATEGORY=12
   ELSE IF SHARE:BALANCE<=$40,000.00 THEN SHDOLLARCATEGORY=13
   ELSE IF SHARE:BALANCE<=$99,999.99 THEN SHDOLLARCATEGORY=14
   ELSE SHDOLLARCATEGORY=15

   SHBALBYDOLLAR(SHDOLLARCATEGORY)=
       SHBALBYDOLLAR(SHDOLLARCATEGORY)+SHARE:BALANCE
   SHCOUNTBYDOLLAR(SHDOLLARCATEGORY)=
       SHCOUNTBYDOLLAR(SHDOLLARCATEGORY)+1

   ACCTSHBALANCE=ACCTSHBALANCE+SHARE:BALANCE

   IF ACCOUNT:NUMBER="" THEN
    DO
     [Share Has Insurance]
     IF SHARE:BALANCE>SHINSCUTOFF THEN
      DO
       SHINSBAL=SHINSBAL+SHINSCUTOFF
       SHINSCOUNT=SHINSCOUNT+1
       SHINSEXCESSBAL=SHINSEXCESSBAL+SHARE:BALANCE-SHINSCUTOFF
       SHINSEXCESSCOUNT=SHINSEXCESSCOUNT+1
      END
     ELSE
      DO
       SHINSBAL=SHINSBAL+SHARE:BALANCE
       SHINSCOUNT=SHINSCOUNT+1
      END
    END
   ELSE
    DO
     [Share Does Not Have Insurance]
     SHUNINSBAL=SHUNINSBAL+SHARE:BALANCE
     SHUNINSCOUNT=SHUNINSCOUNT+1
    END
  END

 ACCTLNBALANCE=$0.00

 FOR EACH LOAN WITH
         (LOAN:CLOSEDATE='--/--/--' AND
          LOAN:CHARGEOFFDATE='--/--/--')
  DO
   
   LT=LOAN:TYPE
   LM=LOAN:BALANCE
   LB=1
   LC=1
   CALL GETTONYBALS

   LNBALBYTYPE(LOAN:TYPE)=LNBALBYTYPE(LOAN:TYPE)+LOAN:BALANCE
   LNCOUNTBYTYPE(LOAN:TYPE)=LNCOUNTBYTYPE(LOAN:TYPE)+1

   LNBALBYTYPE(100)=LNBALBYTYPE(100)+LOAN:BALANCE
   LNCOUNTBYTYPE(100)=LNCOUNTBYTYPE(100)+1

   IF LOAN:BALANCE<=$0.00 THEN LNDOLLARCATEGORY=00
   ELSE IF LOAN:BALANCE<=$250.00 THEN LNDOLLARCATEGORY=01
   ELSE IF LOAN:BALANCE<=$500.00 THEN LNDOLLARCATEGORY=02
   ELSE IF LOAN:BALANCE<=$1,000.00 THEN LNDOLLARCATEGORY=03
   ELSE IF LOAN:BALANCE<=$2,000.00 THEN LNDOLLARCATEGORY=04
   ELSE IF LOAN:BALANCE<=$3,000.00 THEN LNDOLLARCATEGORY=05
   ELSE IF LOAN:BALANCE<=$4,000.00 THEN LNDOLLARCATEGORY=06
   ELSE IF LOAN:BALANCE<=$5,000.00 THEN LNDOLLARCATEGORY=07
   ELSE IF LOAN:BALANCE<=$7,500.00 THEN LNDOLLARCATEGORY=08
   ELSE IF LOAN:BALANCE<=$10,000.00 THEN LNDOLLARCATEGORY=09
   ELSE IF LOAN:BALANCE<=$12,500.00 THEN LNDOLLARCATEGORY=10
   ELSE IF LOAN:BALANCE<=$15,000.00 THEN LNDOLLARCATEGORY=11
   ELSE IF LOAN:BALANCE<=$20,000.00 THEN LNDOLLARCATEGORY=12
   ELSE IF LOAN:BALANCE<=$40,000.00 THEN LNDOLLARCATEGORY=13
   ELSE IF LOAN:BALANCE<=$99,999.99 THEN LNDOLLARCATEGORY=14
   ELSE LNDOLLARCATEGORY=15

   LNBALBYDOLLAR(LNDOLLARCATEGORY)=
       LNBALBYDOLLAR(LNDOLLARCATEGORY)+LOAN:BALANCE
   LNCOUNTBYDOLLAR(LNDOLLARCATEGORY)=
       LNCOUNTBYDOLLAR(LNDOLLARCATEGORY)+1

   ACCTLNBALANCE=ACCTLNBALANCE+LOAN:BALANCE

[The following 2 sections must be customized by each CU]

   IF ACCOUNT:NUMBER="" THEN
    DO
     [Loan Has Insurance]
     IF LOAN:BALANCE>LNINSCUTOFF THEN
      DO
       LNINSBAL=LNINSBAL+LNINSCUTOFF
       LNINSCOUNT=LNINSCOUNT+1
       LNINSEXCESSBAL=LNINSEXCESSBAL+LOAN:BALANCE-LNINSCUTOFF
       LNINSEXCESSCOUNT=LNINSEXCESSCOUNT+1
      END
     ELSE
      DO
       LNINSBAL=LNINSBAL+LOAN:BALANCE
       LNINSCOUNT=LNINSCOUNT+1
      END
    END
   ELSE
    DO
     [Loan Does Not Have Insurance]
     LNUNINSBAL=LNUNINSBAL+LOAN:BALANCE
     LNUNINSCOUNT=LNUNINSCOUNT+1
    END

  END

[todo:MEMBERCOUNT=MEMBERCOUNT+1]

[The following must be customized by each CU]

 IF ANYWARNING(ACCOUNT,16) OR ACCOUNT:TYPE=01 THEN
  ORGANIZATIONCOUNT=ORGANIZATIONCOUNT+1

 IF ACCTSHBALANCE>$0.00 THEN MEMBERWITHBALCOUNT=MEMBERWITHBALCOUNT+1

 IF ACCTSHBALANCE<=$0.00 THEN SHDOLLARCATEGORY=00
 ELSE IF ACCTSHBALANCE<=$250.00 THEN SHDOLLARCATEGORY=01
 ELSE IF ACCTSHBALANCE<=$500.00 THEN SHDOLLARCATEGORY=02
 ELSE IF ACCTSHBALANCE<=$1,000.00 THEN SHDOLLARCATEGORY=03
 ELSE IF ACCTSHBALANCE<=$2,000.00 THEN SHDOLLARCATEGORY=04
 ELSE IF ACCTSHBALANCE<=$3,000.00 THEN SHDOLLARCATEGORY=05
 ELSE IF ACCTSHBALANCE<=$4,000.00 THEN SHDOLLARCATEGORY=06
 ELSE IF ACCTSHBALANCE<=$5,000.00 THEN SHDOLLARCATEGORY=07
 ELSE IF ACCTSHBALANCE<=$7,500.00 THEN SHDOLLARCATEGORY=08
 ELSE IF ACCTSHBALANCE<=$10,000.00 THEN SHDOLLARCATEGORY=09
 ELSE IF ACCTSHBALANCE<=$12,500.00 THEN SHDOLLARCATEGORY=10
 ELSE IF ACCTSHBALANCE<=$15,000.00 THEN SHDOLLARCATEGORY=11
 ELSE IF ACCTSHBALANCE<=$20,000.00 THEN SHDOLLARCATEGORY=12
 ELSE IF ACCTSHBALANCE<=$40,000.00 THEN SHDOLLARCATEGORY=13
 ELSE IF ACCTSHBALANCE<=$99,999.99 THEN SHDOLLARCATEGORY=14
 ELSE SHDOLLARCATEGORY=15

 ACCTSHBALBYDOLLAR(SHDOLLARCATEGORY)=
     ACCTSHBALBYDOLLAR(SHDOLLARCATEGORY)+ACCTSHBALANCE
 ACCTSHCOUNTBYDOLLAR(SHDOLLARCATEGORY)=
     ACCTSHCOUNTBYDOLLAR(SHDOLLARCATEGORY)+1
 ACCTSHBALBYDOLLAR(16)=ACCTSHBALBYDOLLAR(16)+ACCTSHBALANCE
 ACCTSHCOUNTBYDOLLAR(16)=ACCTSHCOUNTBYDOLLAR(16)+1

 IF ACCTLNBALANCE<=$0.00 THEN LNDOLLARCATEGORY=00
 ELSE IF ACCTLNBALANCE<=$250.00 THEN LNDOLLARCATEGORY=01
 ELSE IF ACCTLNBALANCE<=$500.00 THEN LNDOLLARCATEGORY=02
 ELSE IF ACCTLNBALANCE<=$1,000.00 THEN LNDOLLARCATEGORY=03
 ELSE IF ACCTLNBALANCE<=$2,000.00 THEN LNDOLLARCATEGORY=04
 ELSE IF ACCTLNBALANCE<=$3,000.00 THEN LNDOLLARCATEGORY=05
 ELSE IF ACCTLNBALANCE<=$4,000.00 THEN LNDOLLARCATEGORY=06
 ELSE IF ACCTLNBALANCE<=$5,000.00 THEN LNDOLLARCATEGORY=07
 ELSE IF ACCTLNBALANCE<=$7,500.00 THEN LNDOLLARCATEGORY=08
 ELSE IF ACCTLNBALANCE<=$10,000.00 THEN LNDOLLARCATEGORY=09
 ELSE IF ACCTLNBALANCE<=$12,500.00 THEN LNDOLLARCATEGORY=10
 ELSE IF ACCTLNBALANCE<=$15,000.00 THEN LNDOLLARCATEGORY=11
 ELSE IF ACCTLNBALANCE<=$20,000.00 THEN LNDOLLARCATEGORY=12
 ELSE IF ACCTLNBALANCE<=$40,000.00 THEN LNDOLLARCATEGORY=13
 ELSE IF ACCTLNBALANCE<=$99,999.99 THEN LNDOLLARCATEGORY=14
 ELSE LNDOLLARCATEGORY=15

 ACCTLNBALBYDOLLAR(LNDOLLARCATEGORY)=
     ACCTLNBALBYDOLLAR(LNDOLLARCATEGORY)+ACCTLNBALANCE
 ACCTLNCOUNTBYDOLLAR(LNDOLLARCATEGORY)=
     ACCTLNCOUNTBYDOLLAR(LNDOLLARCATEGORY)+1
 ACCTLNBALBYDOLLAR(16)=ACCTLNBALBYDOLLAR(16)+ACCTLNBALANCE
 ACCTLNCOUNTBYDOLLAR(16)=ACCTLNCOUNTBYDOLLAR(16)+1

END

TOTAL

 PRINT "---------------------------SHARES BY TYPE-----------------------------"
 PRINT "---------------------COMPARED TO "+FORMAT("##/##/##",PREVDATE)+"--------------"
 NEWLINE
 PRINT "TYPE DESCRIPTION"
 COL=55 RIGHT "Balance"
 COL=65 RIGHT "Count"
 COL=75 RIGHT "Percent"
 COL=95 RIGHT "Balance"
 COL=105 RIGHT "Change"
 COL=115 RIGHT "Count"
 COL=125 RIGHT "Change"
 NEWLINE
 COUNTER=0
 WHILE COUNTER<=100
  DO
   IF SHCOUNTBYTYPE(COUNTER)>0 OR COUNTER=99 THEN
    DO
     IF COUNTER<100 THEN
      DO
       CALL SHSUBTOT
       IF (COUNTER<68 OR COUNTER>72) AND COUNTER<>99 AND COUNTER<>85 AND COUNTER<>86 AND COUNTER<>LASTCHECKING THEN 
         COL=2 FORMAT("99",COUNTER) [EXCLUDES CERT IRAS 68-72 PRINTED IN SUBTOTAL]
      END
     ELSE
      DO
       PRINT REPEATCHR("-",125)
       NEWLINE
      END
     IF (COUNTER<68 OR COUNTER>72) AND COUNTER<>99 THEN 
       CALL PRINTSHARES [EXCLUDES CERT IRAS 68-72 PRINTED FROM SUBTOTAL SECTION]
    END
   COUNTER=COUNTER+1
  END
 NEWLINE
 NEWLINE
 COL=1 REPEATCHR("-",52)
 PRINT "SHARE BALANCE CHANGES"
 PRINT REPEATCHR("-",52)
 NEWLINE
 FOR COUNTER=1 TO SH
  DO
   COL=1 SHARETOTALDESC(COUNTER)
   COL=45 SHARETOTALBALS(COUNTER,1)
   COL=55 SHARETOTALCNTS(COUNTER,1)
   COL=75 SHARETOTALBALS(COUNTER,2)
   COL=95 SHARETOTALCNTS(COUNTER,2)
   COL=115 SHARETOTALBALS(COUNTER,1)-SHARETOTALBALS(COUNTER,2)
   COL=125 SHARETOTALCNTS(COUNTER,1)-SHARETOTALCNTS(COUNTER,2)
   NEWLINE
  END
 COL=1 REPEATCHR("-",125)
 NEWLINE
 COL=45 SUBBALS(1)
 COL=55 SUBCNTS(1)
 COL=75 SUBBALS(2)
 COL=95 SUBCNTS(2)
 COL=115 SUBBALS(3)
 COL=125 SUBCNTS(3)

 NEWPAGE
 PRINT "---------------------------LOANS BY TYPE------------------------------"
 PRINT "---------------------COMPARED TO "+FORMAT("##/##/##",PREVDATE)+"--------------"
 NEWLINE
 PRINT "TYPE DESCRIPTION"
 COL=55 RIGHT "Balance"
 COL=65 RIGHT "Count"
 COL=75 RIGHT "Percent"
 COL=95 RIGHT "Balance"
 COL=105 RIGHT "Change"
 COL=115 RIGHT "Count"
 COL=125 RIGHT "Change"
 NEWLINE
 COUNTER=0
 WHILE COUNTER<=100
  DO
   IF LNCOUNTBYTYPE(COUNTER)>0 OR PREVLNCOUNTBYTYPE(COUNTER)>0 THEN
    DO
     IF COUNTER<100 THEN
      DO
       COL=2 FORMAT("99",COUNTER)
      END
     ELSE
      DO
       PRINT REPEATCHR("-",125)
       NEWLINE
      END
     IF LOANDESC(COUNTER)<>"TOTALS" THEN
      DO
       ST=5
       IF COUNTER=1 OR (COUNTER>=60 AND COUNTER<=65) THEN ST=1
       ELSE IF COUNTER=2 OR (COUNTER>=50 AND COUNTER<=55) THEN ST=2
       ELSE IF (COUNTER>=30 AND COUNTER<=38) THEN ST=3
       ELSE IF (COUNTER>=40 AND COUNTER<=49) THEN ST=4
       SUBTLOANBAL(ST,1)=SUBTLOANBAL(ST,1)+LNBALBYTYPE(COUNTER)
       SUBTLOANCNT(ST,1)=SUBTLOANCNT(ST,1)+LNCOUNTBYTYPE(COUNTER)
       SUBTLOANBAL(ST,2)=SUBTLOANBAL(ST,2)+PREVLNBALBYTYPE(COUNTER)
       SUBTLOANCNT(ST,2)=SUBTLOANCNT(ST,2)+PREVLNCOUNTBYTYPE(COUNTER)
       SUBTLOANBAL(6,1)=SUBTLOANBAL(6,1)+LNBALBYTYPE(COUNTER)
       SUBTLOANCNT(6,1)=SUBTLOANCNT(6,1)+LNCOUNTBYTYPE(COUNTER)
       SUBTLOANBAL(6,2)=SUBTLOANBAL(6,2)+PREVLNBALBYTYPE(COUNTER)
       SUBTLOANCNT(6,2)=SUBTLOANCNT(6,2)+PREVLNCOUNTBYTYPE(COUNTER)
      END
     COL=6 LOANDESC(COUNTER)
     COL=55 LNBALBYTYPE(COUNTER)
     COL=65 LNCOUNTBYTYPE(COUNTER)
     COL=75 LNBALBYTYPE(COUNTER)/LNBALBYTYPE(100)
     IF PREVLNBALBYTYPE(COUNTER)<>$0.00 THEN
      DO
       COL=95 PREVLNBALBYTYPE(COUNTER)
       IF PREVLNBALBYTYPE(COUNTER)>LNBALBYTYPE(COUNTER) THEN
        DO
         PCNEG=FORMAT("##9.999",(PREVLNBALBYTYPE(COUNTER)-
          LNBALBYTYPE(COUNTER))/PREVLNBALBYTYPE(COUNTER))
         CALL MAKEPCNEG
         COL=105 RIGHT PCNEG
        END
       IF PREVLNBALBYTYPE(COUNTER)<LNBALBYTYPE(COUNTER) THEN
        COL=105 (LNBALBYTYPE(COUNTER)-PREVLNBALBYTYPE(COUNTER))/
         PREVLNBALBYTYPE(COUNTER)
       COL=115 PREVLNCOUNTBYTYPE(COUNTER)
       IF PREVLNCOUNTBYTYPE(COUNTER)>LNCOUNTBYTYPE(COUNTER) THEN
        DO
         PCNEG=FORMAT("##9.999",(PREVLNCOUNTBYTYPE(COUNTER)-
          LNCOUNTBYTYPE(COUNTER))/PREVLNCOUNTBYTYPE(COUNTER))
         CALL MAKEPCNEG
         COL=125 RIGHT PCNEG
        END
       IF PREVLNCOUNTBYTYPE(COUNTER)<LNCOUNTBYTYPE(COUNTER) THEN
        COL=125 (LNCOUNTBYTYPE(COUNTER)-PREVLNCOUNTBYTYPE(COUNTER))/
         PREVLNCOUNTBYTYPE(COUNTER)
      END
     NEWLINE
    END
   COUNTER=COUNTER+1
  END
 NEWLINE
 NEWLINE
 NEWLINE
 FOR ST=1 TO 6
  DO
   IF ST=1 THEN COL=1 "New Vehicles"
   ELSE IF ST=2 THEN COL=1 "Used Vehicles"
   ELSE IF ST=3 THEN COL=1 "Home Equity/Home Improvement"
   ELSE IF ST=4 THEN COL=1 "VISA"
   ELSE IF ST=5 THEN COL=1 "All Other Loans"
   ELSE IF ST=6 THEN 
    DO
     PRINT REPEATCHR("-",125)
     NEWLINE
     COL=1 "Totals"
    END
   COL=55 SUBTLOANBAL(ST,1)
   COL=65 SUBTLOANCNT(ST,1)
   COL=75 SUBTLOANBAL(ST,1)/LNBALBYTYPE(100)
   IF SUBTLOANBAL(ST,2)<>$0.00 THEN
    DO
     COL=95 SUBTLOANBAL(ST,2)
     IF SUBTLOANBAL(ST,2)>SUBTLOANBAL(ST,1) THEN
      DO
       PCNEG=FORMAT("##9.999",(SUBTLOANBAL(ST,2)-SUBTLOANBAL(ST,1))/SUBTLOANBAL(ST,2))
       CALL MAKEPCNEG
       COL=105 RIGHT PCNEG
      END
     IF SUBTLOANBAL(ST,2)<SUBTLOANBAL(ST,1) THEN COL=105 (SUBTLOANBAL(ST,1)-SUBTLOANBAL(ST,2))/SUBTLOANBAL(ST,2)
     COL=115 SUBTLOANCNT(ST,2)
     IF SUBTLOANCNT(ST,2)>SUBTLOANCNT(ST,1) THEN
      DO
       PCNEG=FORMAT("##9.999",(SUBTLOANCNT(ST,2)-SUBTLOANCNT(ST,1))/SUBTLOANCNT(ST,2))
       CALL MAKEPCNEG
       COL=125 RIGHT PCNEG
      END
     IF SUBTLOANCNT(ST,2)<SUBTLOANCNT(ST,1) THEN COL=125 (SUBTLOANCNT(ST,1)-SUBTLOANCNT(ST,2))/SUBTLOANCNT(ST,2)
    END
   NEWLINE
  END
 NEWPAGE
 PRINT "---------------------------SUMMARY LOANS------------------------------"
 PRINT "---------------------COMPARED TO "+FORMAT("##/##/##",PREVDATE)+"--------------"
 NEWLINE
 PRINT "GROUP DESCRIPTION"
 COL=55 RIGHT "Balance"
 COL=65 RIGHT "Count"
 COL=75 RIGHT "Percent"
 COL=95 RIGHT "Balance"
 COL=105 RIGHT "Change"
 COL=115 RIGHT "Count"
 COL=125 RIGHT "Change"
 NEWLINE
 FOR TC=1 TO 19
  DO
   IF TC<19 OR (TC=19 AND (TONYBALS(TC,1)>$0.00 OR TONYBALS(TC,2)>$0.00)) THEN
    DO
     COL=1 TONYDESC(TC)
     COL=55 TONYBALS(TC,1)
     COL=65 TONYCNTS(TC,1)
     COL=75 TONYBALS(TC,1)/TCTOTBAL(1)
     IF TONYBALS(TC,2)<>$0.00 THEN
      DO
       COL=95 TONYBALS(TC,2)
       IF TONYBALS(TC,2)>TONYBALS(TC,1) THEN
        DO
         PCNEG=FORMAT("##9.999",(TONYBALS(TC,2)-TONYBALS(TC,1))/TONYBALS(TC,2))
         CALL MAKEPCNEG
         COL=105 RIGHT PCNEG
        END
       ELSE IF TONYBALS(TC,2)<TONYBALS(TC,1) THEN COL=105 (TONYBALS(TC,1)-TONYBALS(TC,2))/TONYBALS(TC,2)
       COL=115 TONYCNTS(TC,2)
       IF TONYCNTS(TC,2)>TONYCNTS(TC,1) THEN
        DO
         PCNEG=FORMAT("##9.999",(TONYCNTS(TC,2)-TONYCNTS(TC,1))/TONYCNTS(TC,2))
         CALL MAKEPCNEG
         COL=125 RIGHT PCNEG
        END
       ELSE IF TONYCNTS(TC,2)<TONYCNTS(TC,1) THEN COL=125 (TONYCNTS(TC,1)-TONYCNTS(TC,2))/TONYCNTS(TC,2)
      END
     NEWLINE
    END
  END
 COL=1 REPEATCHR("-",125)
 NEWLINE
 COL=55 TCTOTBAL(1)
 COL=65 TCTOTCNT(1)
 COL=95 TCTOTBAL(2)
 IF TCTOTBAL(2)>TCTOTBAL(1) THEN
  DO
   PCNEG=FORMAT("##9.999",(TCTOTBAL(2)-TCTOTBAL(1))/TCTOTBAL(2))
   CALL MAKEPCNEG
   COL=105 RIGHT PCNEG
  END
 ELSE IF TCTOTBAL(2)<TCTOTBAL(1) THEN COL=105 (TCTOTBAL(1)-TCTOTBAL(2))/TCTOTBAL(2)
 COL=115 TCTOTCNT(2)
 IF TCTOTCNT(2)>TCTOTCNT(1) THEN
  DO
   PCNEG=FORMAT("##9.999",(TCTOTCNT(2)-TCTOTCNT(1))/TCTOTCNT(2))
   CALL MAKEPCNEG
   COL=125 RIGHT PCNEG
  END
 ELSE IF TCTOTCNT(2)<TCTOTCNT(1) THEN COL=125 (TCTOTCNT(1)-TCTOTCNT(2))/TCTOTCNT(2)
 NEWLINE
 NEWPAGE
PRINT "     ----------------SHARES BY BALANCE RANGE----------------"
 PRINT " "
 PRINT "     -----------------LOANS BY BALANCE RANGE----------------"
 NEWLINE
 COL=12 "RANGE"
 COL=50 RIGHT "Balance"
 COL=60 RIGHT "Count"
 COL=73 "RANGE"
 COL=111 RIGHT "Balance"
 COL=121 RIGHT "Count"
 NEWLINE
 COUNTER=0
 WHILE COUNTER<=15
  DO
   COL=5   SHDOLLARDESC(COUNTER)
   COL=50  SHBALBYDOLLAR(COUNTER)
   COL=60  SHCOUNTBYDOLLAR(COUNTER)
   COL=66  LNDOLLARDESC(COUNTER)
   COL=111 LNBALBYDOLLAR(COUNTER)
   COL=121 LNCOUNTBYDOLLAR(COUNTER)
   NEWLINE
   COUNTER=COUNTER+1
  END

 NEWLINE
 NEWLINE
 PRINT "     ------------ACCOUNT SHARES BY BALANCE RANGE------------"
 PRINT " "
 PRINT "     -------------ACCOUNT LOANS BY BALANCE RANGE------------"
 NEWLINE
 COL=12 "RANGE"
 COL=50 RIGHT "Balance"
 COL=60 RIGHT "Count"
 COL=73 "RANGE"
 COL=111 RIGHT "Balance"
 COL=121 RIGHT "Count"
 NEWLINE
 COUNTER=0
 WHILE COUNTER<=16
  DO
   IF COUNTER=16 THEN
    DO
     PRINT "     -------------------------------------------------------"
     PRINT " "
     PRINT "     -------------------------------------------------------"
     NEWLINE
    END
   COL=5   SHDOLLARDESC(COUNTER)
   COL=50  ACCTSHBALBYDOLLAR(COUNTER)
   COL=60  ACCTSHCOUNTBYDOLLAR(COUNTER)
   COL=66  LNDOLLARDESC(COUNTER)
   COL=111 ACCTLNBALBYDOLLAR(COUNTER)
   COL=121 ACCTLNCOUNTBYDOLLAR(COUNTER)
   NEWLINE
   COUNTER=COUNTER+1
  END

 NEWLINE
 NEWLINE
[PRINT "SHARES INSURED TO $"
 PRINT SHINSCUTOFF
 NEWLINE
 COL=20 RIGHT "Insured"
 COL=30 RIGHT "Count"
 COL=50 RIGHT "Excess"
 COL=60 RIGHT "Count"
 COL=80 RIGHT "Uninsured"
 COL=90 RIGHT "Count"
 NEWLINE
 COL=20 SHINSBAL
 COL=30 SHINSCOUNT
 COL=50 SHINSEXCESSBAL
 COL=60 SHINSEXCESSCOUNT
 COL=80 SHUNINSBAL
 COL=90 SHUNINSCOUNT

 NEWLINE
 NEWLINE
 PRINT "LOANS INSURED TO $"
 PRINT LNINSCUTOFF
 NEWLINE
 COL=20 RIGHT "Insured"
 COL=30 RIGHT "Count"
 COL=50 RIGHT "Excess"
 COL=60 RIGHT "Count"
 COL=80 RIGHT "Uninsured"
 COL=90 RIGHT "Count"
 NEWLINE
 COL=20 LNINSBAL
 COL=30 LNINSCOUNT
 COL=50 LNINSEXCESSBAL
 COL=60 LNINSEXCESSCOUNT
 COL=80 LNUNINSBAL
 COL=90 LNUNINSCOUNT
]
 NEWLINE
 NEWLINE
 PRINT "Number of Members: "
 PRINT MEMBERCOUNT
 NEWLINE
 PRINT "Number of Organizations: "
 PRINT ORGANIZATIONCOUNT
 NEWLINE
 PRINT "Number of Members With Balance > $0.00: "
 PRINT MEMBERWITHBALCOUNT
 NEWLINE
END

PROCEDURE SHSUBTOT
 IF COUNTER=12 OR COUNTER=40 OR COUNTER=50 OR COUNTER=61 OR
    COUNTER=80 OR COUNTER=LASTCHECKING OR COUNTER=99 THEN
  DO
   IF COUNTER=40 THEN                       
    DO
     COUNTER=68                             [TEMP COUNTER SET TO PRINT CERT IRAS 68-72]
     WHILE COUNTER<=72
      DO
       IF SHCOUNTBYTYPE(COUNTER)>0 OR PREVSHCOUNTBYTYPE(COUNTER)>0 THEN
        DO
         COL=2 FORMAT("99",COUNTER)
         CALL PRINTSHARES
         SHSUBTOTBAL=SHSUBTOTBAL+SHBALBYTYPE(COUNTER)
         SHSUBTOTCOUNT=SHSUBTOTCOUNT+SHCOUNTBYTYPE(COUNTER)
         PREVSHSUBTOTBAL=PREVSHSUBTOTBAL+PREVSHBALBYTYPE(COUNTER)
         PREVSHSUBTOTCOUNT=PREVSHSUBTOTCOUNT+PREVSHCOUNTBYTYPE(COUNTER)
        END
       COUNTER=COUNTER+1
      END
     COUNTER=40                             [SET BACK TO ORIGINAL COUNTER]
    END

   PRINT REPEATCHR(":",125)
   NEWLINE
   SUBTOTALDESC=""
   IF COUNTER=12 THEN SUBTOTALDESC="Subtotal Regular Savings"
   ELSE IF COUNTER=40 THEN SUBTOTALDESC="Subtotal IRA Certs"
   ELSE IF COUNTER=50 THEN SUBTOTALDESC="Subtotal IRA Savings"
   ELSE IF COUNTER=61 THEN 
    DO
     SUBTOTALDESC="Subtotal Money Markets"
     PREVSHSUBTOTBAL=PREVSHBALBYTYPE(50)
     PREVSHSUBTOTCOUNT=PREVSHCOUNTBYTYPE(50)
    END
   ELSE IF COUNTER=80 THEN SUBTOTALDESC="Subtotal Regular Certs"
   ELSE IF COUNTER=LASTCHECKING THEN SUBTOTALDESC="Subtotal Checking"
   ELSE IF COUNTER=99 THEN SUBTOTALDESC="Subtotal Non Member Certs"
   IF SUBTOTALDESC<>"" THEN COL=10 SUBTOTALDESC

   SH=SH+1
   SHARETOTALDESC(SH)=SUBTOTALDESC
   SHARETOTALBALS(SH,1)=SHSUBTOTBAL
   SHARETOTALCNTS(SH,1)=SHSUBTOTCOUNT
   SHARETOTALBALS(SH,2)=PREVSHSUBTOTBAL
   SHARETOTALCNTS(SH,2)=PREVSHSUBTOTCOUNT
   
   SUBBALS(1)=SUBBALS(1)+SHSUBTOTBAL
   SUBCNTS(1)=SUBCNTS(1)+SHSUBTOTCOUNT
   SUBBALS(2)=SUBBALS(2)+PREVSHSUBTOTBAL
   SUBCNTS(2)=SUBCNTS(2)+PREVSHSUBTOTCOUNT
   SUBBALS(3)=SUBBALS(3)+(SHSUBTOTBAL-PREVSHSUBTOTBAL)
   SUBCNTS(3)=SUBCNTS(3)+(SHSUBTOTCOUNT-PREVSHSUBTOTCOUNT)

   COL=55 SHSUBTOTBAL
   COL=65 SHSUBTOTCOUNT
   COL=75 SHSUBTOTBAL/SHBALBYTYPE(100)
   COL=95 PREVSHSUBTOTBAL

   IF PREVSHSUBTOTBAL>SHSUBTOTBAL THEN
    DO
     PCNEG=FORMAT("##9.999",(PREVSHSUBTOTBAL-SHSUBTOTBAL)/PREVSHSUBTOTBAL)
     CALL MAKEPCNEG
     COL=105 RIGHT PCNEG
    END
   ELSE IF PREVSHSUBTOTBAL<SHSUBTOTBAL THEN COL=105 RIGHT (SHSUBTOTBAL-PREVSHSUBTOTBAL)/PREVSHSUBTOTBAL
   COL=115 PREVSHSUBTOTCOUNT
   IF PREVSHSUBTOTCOUNT>SHSUBTOTCOUNT THEN
    DO
     PCNEG=FORMAT("##9.999",(PREVSHSUBTOTCOUNT-SHSUBTOTCOUNT)/PREVSHSUBTOTCOUNT)
     CALL MAKEPCNEG
     COL=125 RIGHT PCNEG
    END
   ELSE IF PREVSHSUBTOTCOUNT<SHSUBTOTCOUNT THEN COL=125 RIGHT (SHSUBTOTCOUNT-PREVSHSUBTOTCOUNT)/PREVSHSUBTOTCOUNT
   NEWLINE
   PRINT REPEATCHR(":",125)
   NEWLINE
  END

 IF COUNTER=0 OR COUNTER=12 OR COUNTER=40 OR COUNTER=50 OR
    COUNTER=61 OR COUNTER=80 OR COUNTER=LASTCHECKING THEN
  DO
   SHSUBTOTBAL=$0.00
   SHSUBTOTCOUNT=0
   PREVSHSUBTOTBAL=$0.00
   PREVSHSUBTOTCOUNT=0
  END

 IF COUNTER=LASTCHECKING THEN
  DO
   SHSUBTOTBAL=SHSUBTOTBAL+TMPSUBTOTBAL
   SHSUBTOTCOUNT=SHSUBTOTCOUNT+TMPSUBTOTCOUNT
   PREVSHSUBTOTBAL=PREVSHSUBTOTBAL+PREVTMPSUBTOTBAL
   PREVSHSUBTOTCOUNT=PREVSHSUBTOTCOUNT+PREVTMPSUBTOTCOUNT
   SHSUBTOTBAL=SHSUBTOTBAL+SHBALBYTYPE(COUNTER)
   SHSUBTOTCOUNT=SHSUBTOTCOUNT+SHCOUNTBYTYPE(COUNTER)
   PREVSHSUBTOTBAL=PREVSHSUBTOTBAL+PREVSHBALBYTYPE(COUNTER)
   PREVSHSUBTOTCOUNT=PREVSHSUBTOTCOUNT+PREVSHCOUNTBYTYPE(COUNTER)
  END
 ELSE IF COUNTER=85 OR COUNTER=86 THEN
  DO
   TMPSUBTOTBAL=TMPSUBTOTBAL+SHBALBYTYPE(COUNTER)
   TMPSUBTOTCOUNT=TMPSUBTOTCOUNT+SHCOUNTBYTYPE(COUNTER)
   PREVTMPSUBTOTBAL=PREVTMPSUBTOTBAL+PREVSHBALBYTYPE(COUNTER)
   PREVTMPSUBTOTCOUNT=PREVTMPSUBTOTCOUNT+PREVSHCOUNTBYTYPE(COUNTER)
  END
 ELSE IF COUNTER<68 OR COUNTER>72 THEN
  DO
   SHSUBTOTBAL=SHSUBTOTBAL+SHBALBYTYPE(COUNTER)
   SHSUBTOTCOUNT=SHSUBTOTCOUNT+SHCOUNTBYTYPE(COUNTER)
   PREVSHSUBTOTBAL=PREVSHSUBTOTBAL+PREVSHBALBYTYPE(COUNTER)
   PREVSHSUBTOTCOUNT=PREVSHSUBTOTCOUNT+PREVSHCOUNTBYTYPE(COUNTER)
  END
END

PROCEDURE ERRORCHECK
 IF FERROR<>"" THEN
  DO
   PRINT "Error found: "
   PRINT FERROR
   NEWLINE
  END
END

PROCEDURE MAKEPCNEG
 WHILE SEGMENT(PCNEG,1,1)=""
  DO
   PCNEG=SEGMENT(PCNEG,2,LENGTH(PCNEG))
  END
 PCNEG="-"+PCNEG+"%"
END

PROCEDURE PRINTSHARES
 IF COUNTER=50 THEN
  DO
   COL=6 SHAREDESC(COUNTER)
   NEWLINE
   FOR MM=1 TO 5
    DO
     COL=6 MMNAME(MM)
     COL=55 MMBALS(MM,1)
     COL=65 MMCNTS(MM,1)
     COL=75 MMBALS(MM,1)/MMBALS(6,1)
     COL=95 MMBALS(MM,2)
     IF MMBALS(MM,2)>MMBALS(MM,1) THEN
      DO
       PCNEG=FORMAT("##9.999",(MMBALS(MM,2)-MMBALS(MM,1))/MMBALS(MM,2))
       CALL MAKEPCNEG
       COL=105 RIGHT PCNEG
      END
     IF MMBALS(MM,2)<MMBALS(MM,1) THEN COL=105 (MMBALS(MM,1)-MMBALS(MM,2))/MMBALS(MM,2)
     COL=115 MMCNTS(MM,2)
     IF MMCNTS(MM,2)>MMCNTS(MM,1) THEN
      DO
       PCNEG=FORMAT("##9.999",(MMCNTS(MM,2)-MMCNTS(MM,1))/MMCNTS(MM,2))
       CALL MAKEPCNEG
       COL=125 RIGHT PCNEG
      END
     IF MMCNTS(MM,2)<MMCNTS(MM,1) THEN COL=125 (MMCNTS(MM,1)-MMCNTS(MM,2))/MMCNTS(MM,2)
     NEWLINE
    END
  END
 ELSE IF COUNTER=LASTCHECKING THEN
  DO
   FOR I=85 TO 86
    DO
     COL=2 FORMAT("99",I)
     COL=6 SHAREDESC(I)
     COL=55 SHBALBYTYPE(I)
     COL=65 SHCOUNTBYTYPE(I)
     COL=75 SHBALBYTYPE(I)/SHBALBYTYPE(100)
     IF PREVSHBALBYTYPE(I)<>$0.00 THEN
      DO
       COL=95 PREVSHBALBYTYPE(I)
       IF PREVSHBALBYTYPE(I)>SHBALBYTYPE(I) THEN
        DO
         PCNEG=FORMAT("##9.999",(PREVSHBALBYTYPE(I)-SHBALBYTYPE(I))/PREVSHBALBYTYPE(I))
         CALL MAKEPCNEG
         COL=105 RIGHT PCNEG
        END
       ELSE IF PREVSHBALBYTYPE(I)<SHBALBYTYPE(I) THEN 
         COL=105 (SHBALBYTYPE(I)- PREVSHBALBYTYPE(I))/PREVSHBALBYTYPE(I)
       COL=115 PREVSHCOUNTBYTYPE(I)
       IF PREVSHCOUNTBYTYPE(I)>SHCOUNTBYTYPE(I) THEN
        DO
         PCNEG=FORMAT("##9.999",(PREVSHCOUNTBYTYPE(I)-SHCOUNTBYTYPE(I))/PREVSHCOUNTBYTYPE(I))
         CALL MAKEPCNEG
         COL=125 RIGHT PCNEG
        END
       ELSE IF PREVSHCOUNTBYTYPE(I)<SHCOUNTBYTYPE(I) THEN 
        COL=125 (SHCOUNTBYTYPE(I)- PREVSHCOUNTBYTYPE(I))/PREVSHCOUNTBYTYPE(I)
      END
     NEWLINE
   END
   COL=2 FORMAT("99",LASTCHECKING)
   COL=6 SHAREDESC(LASTCHECKING)
   COL=55 SHBALBYTYPE(LASTCHECKING)
   COL=65 SHCOUNTBYTYPE(LASTCHECKING)
   COL=75 SHBALBYTYPE(LASTCHECKING)/SHBALBYTYPE(100)
   IF PREVSHBALBYTYPE(LASTCHECKING)<>$0.00 THEN
    DO
     COL=95 PREVSHBALBYTYPE(LASTCHECKING)
     IF PREVSHBALBYTYPE(LASTCHECKING)>SHBALBYTYPE(LASTCHECKING) THEN
      DO
       PCNEG=FORMAT("##9.999",(PREVSHBALBYTYPE(LASTCHECKING)-SHBALBYTYPE(LASTCHECKING))/PREVSHBALBYTYPE(LASTCHECKING))
       CALL MAKEPCNEG
       COL=105 RIGHT PCNEG
      END
     ELSE IF PREVSHBALBYTYPE(LASTCHECKING)<SHBALBYTYPE(LASTCHECKING) THEN 
       COL=105 (SHBALBYTYPE(LASTCHECKING)- PREVSHBALBYTYPE(LASTCHECKING))/PREVSHBALBYTYPE(LASTCHECKING)
     COL=115 PREVSHCOUNTBYTYPE(LASTCHECKING)
     IF PREVSHCOUNTBYTYPE(LASTCHECKING)>SHCOUNTBYTYPE(LASTCHECKING) THEN
      DO
       PCNEG=FORMAT("##9.999",(PREVSHCOUNTBYTYPE(LASTCHECKING)-SHCOUNTBYTYPE(LASTCHECKING))/PREVSHCOUNTBYTYPE(LASTCHECKING))
       CALL MAKEPCNEG
       COL=125 RIGHT PCNEG
      END
     ELSE IF PREVSHCOUNTBYTYPE(LASTCHECKING)<SHCOUNTBYTYPE(LASTCHECKING) THEN 
      COL=125 (SHCOUNTBYTYPE(LASTCHECKING)- PREVSHCOUNTBYTYPE(LASTCHECKING))/PREVSHCOUNTBYTYPE(LASTCHECKING)
    END
   NEWLINE
  END
 ELSE IF COUNTER<>85 AND COUNTER<>86 THEN
  DO
   COL=6 SHAREDESC(COUNTER)
   COL=55 SHBALBYTYPE(COUNTER)
   COL=65 SHCOUNTBYTYPE(COUNTER)
   COL=75 SHBALBYTYPE(COUNTER)/SHBALBYTYPE(100)
   IF PREVSHBALBYTYPE(COUNTER)<>$0.00 THEN
    DO
     COL=95 PREVSHBALBYTYPE(COUNTER)
     IF PREVSHBALBYTYPE(COUNTER)>SHBALBYTYPE(COUNTER) THEN
      DO
       PCNEG=FORMAT("##9.999",(PREVSHBALBYTYPE(COUNTER)-SHBALBYTYPE(COUNTER))/PREVSHBALBYTYPE(COUNTER))
       CALL MAKEPCNEG
       COL=105 RIGHT PCNEG
      END
     ELSE IF PREVSHBALBYTYPE(COUNTER)<SHBALBYTYPE(COUNTER) THEN 
       COL=105 (SHBALBYTYPE(COUNTER)- PREVSHBALBYTYPE(COUNTER))/PREVSHBALBYTYPE(COUNTER)
     COL=115 PREVSHCOUNTBYTYPE(COUNTER)
     IF PREVSHCOUNTBYTYPE(COUNTER)>SHCOUNTBYTYPE(COUNTER) THEN
      DO
       PCNEG=FORMAT("##9.999",(PREVSHCOUNTBYTYPE(COUNTER)-SHCOUNTBYTYPE(COUNTER))/PREVSHCOUNTBYTYPE(COUNTER))
       CALL MAKEPCNEG
       COL=125 RIGHT PCNEG
      END
     ELSE IF PREVSHCOUNTBYTYPE(COUNTER)<SHCOUNTBYTYPE(COUNTER) THEN 
      COL=125 (SHCOUNTBYTYPE(COUNTER)- PREVSHCOUNTBYTYPE(COUNTER))/PREVSHCOUNTBYTYPE(COUNTER)
    END
   NEWLINE
  END
END

PROCEDURE GETTONYBALS
 IF LT=1 OR (LT>=60 AND LT<=65) OR (LT=66 OR LT=69 OR LT=81 OR LT=83) THEN TC=1
 ELSE IF LT=2 OR (LT>=50 AND LT<=55) OR (LT=56 OR LT=59 OR LT=82 OR LT=84) THEN TC=2
 ELSE IF LT>=20 AND LT<=25 THEN TC=3
 ELSE IF LT=26 THEN TC=4
 ELSE IF LT=27 THEN TC=5
 ELSE IF LT=3 OR (LT>=13 AND LT<=16) THEN TC=6
 ELSE IF LT=4 THEN TC=7
 ELSE IF LT=5 THEN TC=8
 ELSE IF LT=6 THEN TC=9
 ELSE IF LT=8 THEN TC=10
 ELSE IF (LT>=31 AND LT<=35) THEN TC=11
 ELSE IF LT=30 THEN TC=12
 ELSE IF (LT>=36 AND LT<=38) THEN TC=13
 ELSE IF LT=11 THEN TC=14
 ELSE IF LT=40 THEN TC=15
 ELSE IF LT=41 THEN TC=16
 ELSE IF (LT>=70 AND LT<90) THEN TC=17
 ELSE IF LT=90 THEN TC=18
 ELSE TC=19
 TONYBALS(TC,LB)=TONYBALS(TC,LB)+LM
 TONYCNTS(TC,LB)=TONYCNTS(TC,LB)+LC
 TCTOTBAL(LB)=TCTOTBAL(LB)+LM
 TCTOTCNT(LB)=TCTOTCNT(LB)+LC
END





