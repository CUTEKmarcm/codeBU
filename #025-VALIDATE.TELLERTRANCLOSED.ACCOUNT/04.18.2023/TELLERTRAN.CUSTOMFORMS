[ TELLERTRAN.CUSTOMFORMS.228
 Credit Union Name   : Austin Telco Credit Union
 Credit Union Number : 228
 Original Programmer : Sarah Williams
 Project Start Date  : 4/12/2010
 Case Number         : 4459086
  ** Customizations  : CTR, Wycom, OKIDATA receipts

 Modified Date       : 2/5/2019
 Modified By         : Colin Osborn 
 Modifications       : Launch custom ECTR.VERIFYPERSON.SET.228 instead of RD.ECTR.VERIFYPERSON.SET
 Case Number         : 11159029

 Modified Date       : 08/15/2022       
 Modified By         : Corey Brown         
 Modifications       : Changed SHOWAVAILBAL from FALSE to TRUE. Changed approved by Sandie Pena.
 Case Number         :


 PF Thermal Receipts : Printer           : 9998
                       Custom Format     : Yes
                       Check Detail Lines: 0
                       Printer Type      : Standard printer
                       Lines Per Page    : 132 (1 copy) / 131 (2 copies)
                       Blank Lines       : 4 

 PF Laser Receipts   : Printer           : 9998
                       Custom Format     : Yes
                       Check Detail Lines: 0
                       Printer Type      : Standard printer
                       Lines Per Page    : 66 (1 copy) / 65 (2 copies)
                       Blank Lines       : 4
]

[12/07/10 GH INSTALLED NEW CHANGES AFFECTING OKI RECEIPTS]

CUSTOMFORMSWINDOWS

TARGET = ACCOUNT

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 #INCLUDE "RD.WINDOWS.DEF"
 #INCLUDE "RD.SPELL.DEF"
 #INCLUDE "RD.TELLERTRAN.CUSTOMFORMS.DEF"
 [#INCLUDE "TT.CF.CTR.F104.DEF"        Uncomment for CTR]
 #INCLUDE "TELLERTRAN.SIGPAD.DEF"
 [INCLUDE "RD.ECTR.VERIFYPERSON.DEF"]
 #INCLUDE "RD.ECTR.VERIFYPERSON.DEF"
[# INCLUDE "TELLERTRAN.CASHBOX.DEF"]  [ ** Uncomment for cashbox ** ]
 
 EDITXANDY       = CHARACTER 
 EDITBOX         = CHARACTER
 TEMPLINE1       = CHARACTER
 DTEMP           = CHARACTER
 TEMPLINE2       = CHARACTER
 NUMCOPIES       = CHARACTER
 ACCOUNTNUMBER   = CHARACTER
 INCOMING        = CHARACTER
 OUTGOING        = CHARACTER
 SSNHOLDER       = CHARACTER
 TIMEDATA        = CHARACTER
 [MICRPOSARRAY    = CHARACTER(14) ARRAY(58)]

 SHORTRECEIPT    = NUMBER
 THRMLLINEWDTH   = NUMBER
 [CHECKSUM        = NUMBER]
 [DIGVALUE        = NUMBER]
 CHECKDIGIT      = NUMBER
 HOWLONG         = NUMBER 
 HOWLONG2        = NUMBER
 ADDRESSLINES    = NUMBER
 [SUM             = NUMBER]
 A               = NUMBER
 [X               = NUMBER]
 USECASHBOX      = NUMBER
 CASHBOXLOAD     = NUMBER
 DIGIT           = NUMBER
 DIGITTOTAL      = NUMBER
 ODD             = NUMBER
 TENDIGIT        = NUMBER
 NEXTTEN         = NUMBER
 MLNUMBER        = NUMBER
 MLWIDTH         = NUMBER
 CBLINES         = NUMBER
 TRAILERLINES    = NUMBER
 ADDLINES        = NUMBER
 TEMPTIME        = NUMBER
 CNTRLID         = NUMBER ARRAY(5)
 DATAPOSID       = NUMBER ARRAY(1,8)
 TRUE            = 1
 FALSE           = 0
 CBYPOS          = NUMBER
 SHORTTHERMAL    = NUMBER
 REMOVECHAR      = CHARACTER
 USEIMGLOGO      = NUMBER
 IMGLOGONAME     = CHARACTER(40)
 IMGLOGOPROP     = CHARACTER(25)
 LOGOIDS         = CHARACTER(25)

 MASKACCOUNT      = NUMBER
 MASKXNUMBER      = NUMBER
 ACCTID           = NUMBER ARRAY(5)
 TRANACCTID       = NUMBER ARRAY(99)
 TRANACCTCOUNT    = NUMBER
 PAGECOUNT        = NUMBER
 IMAGINGTYPE     = NUMBER
 OTG             = 1
 SYNERGY         = 2 
 SILHOUETTE      = 3
 WYCOMCHECKID    = CHARACTER
 WYCOMCHECKS     = 2
 LAUNCHCTR       = NUMBER
 USEIMGCTR       = NUMBER

 NWPATH1         = "X:\\SYMFORMOFFLINE"
 NWPATH2         = "C:\\"

 TEMPACCOUNT     = CHARACTER
 UNMASKEDACCOUNT = CHARACTER
 SHOWAVAILBAL    = NUMBER
 OKIRECEIPTS     = NUMBER
 PRINTOPTION     = NUMBER

 [MYOKILINECOUNT  = NUMBER]
 OKIHEADERSTART  = NUMBER
 OKIHEADEREND    = NUMBER
 MYOKIPAGECOUNT  = NUMBER
 MYHEADERSTART   = NUMBER
 MYHEADEREND     = NUMBER
 MYGLSTART       = NUMBER
 MYGLEND         = NUMBER
 BRANCHNAME      = CHARACTER   ARRAY(50)
 CN              = NUMBER
 ISFNUMBER       = NUMBER
 ISFLINE         = CHARACTER
 ISFERROR        = CHARACTER
 
 [CLOSED.ACCOUNT FORM DEFINITIONS]
 ACLNUMBER=NUMBER
 ACLERROR=CHARACTER
 ACLLINE=CHARACTER
 ACLACCOUNT=CHARACTER(10)
 SUBERRORTEXT=CHARACTER
 FULLACCOUNTNUMBER=CHARACTER(10)
END


SETUP
 #INCLUDE "RD.SPELL.SET"
 #INCLUDE "TELLERTRAN.SIGPAD.SET"
 WHILELIMIT=1000000  
 CALL FILLBRANCHNAMES                 
 CALL CLEARCCRARRAYS 
 NONANSISTANDARD
 CUSTOMRECEIPTS        = TRUE         [SET FLAG FOR CUSTOM RECEIPTS    ]
 CAPTURETRNSACTR       = TRUE         [SET TO TRUE FOR eCTR            ]
 CUSTOMCHECKS          = TRUE         [SET FLAG FOR CUSTOM CHECKS      ]
 IMAGINGTYPE           = 0            [OTG=1, SYNERGY=2, SILHOUETTE=3  ]
 MASKACCOUNT           = 3            [NON=0, BEFORE LOAD=1, BEFORE IMAGE=2, BEFORE PRINT=3]
 MASKXNUMBER           = 6            [Number of Xs                    ]
 SHOWAVAILBAL          = TRUE         [DISPLAY AVAILABLE BALANCE ON SHARES AND AVAILABLE CREDIT]
                                      [ON LOANS FOR NON TRANSFER ACCOUNTS]
 USEIMGLOGO            = TRUE         [CALL AN IMAGE LOGO              ]
 SHORTTHERMAL          = FALSE        [USE SHORT THERMAL SFF FILES     ]
 ENABLEEXTRNLLOAN      = FALSE        [EXTERNAL LOAN DATA - Ex: FICS   ]
 USECASHBOX            = FALSE        [USE CASHBOX                     ]
 CASHBOXLOAD           = FALSE        [AUTOMATICALLY LOAD CASHBOX FROM RECEIPT]
 OTGNAME               = ""
 FONTNAME              = "Courier New" 
 ESC                   = CTRLCHR(27)
 QUOTE                 = CTRLCHR(34)
 DEBUGMODE             = 0            [SEE .DEF FOR MODES              ]
 LASERMARGIN           = 10           [LEFT MARGIN FOR LASER RECEIPTS  ]
 DOTMATRIXMARGIN       = 0            [LEFTMARGIN FOR OKI RECEIPTS     ]
 CUSTOMKD              = TRUE
 CUSTOMKO              = TRUE
 CHECKMODE             = WYCOMCHECKS  [0=DOTMATRIXCHECKS               ]
                                      [1=LASERCHECKS                   ]
                                      [2=WYCOMCHECKS                   ]
 PROMPTFORCHECKTYPES   = FALSE        [FLAG IF MENU/PROMPTED           ]
 NUMBEROFCOPIESWANTED  = 1            [DEFAULT # OF RECEIPT COPIES     ]
 BLANKPAYEEFIELDS      = FALSE        [FLAG TO BLANK CHECK PAYEE       ]
 CLOSEFORM             = FALSE
 MICRPOS               = 510          [POSITION FOR MICR ON LASER CKS  ]
 MICRFONT ="(2K"+ESC+"(s0p8h10vs0b0T" [DEFINES THE FONT, NOT PLACEMENT ]
 CCRBRANCH  = FORMAT("9999",GETDATANUMBER(GETCONSOLEBRANCH,SYSCONSOLENUM))
 SIGID                 = ""
 SSNHOLDER             = ""
 CBYPOS                = 2265         [CASH BOX STARTING Y POSITION    ]                
 NUMCOPIES             = "1"
 WYCOMCHECKID          = ""
 LAUNCHCTR             = 1            [NO=0, BEFORE RECEIPT=1, AFTER RECEIPT=2]
 USEIMGCTR             = FALSE        [SET TO USE TELLER CAPTURE FOR IMAGE CENTER]

 PRINTOPTION           = 2            [ 0  - CLOSE AFTER SUBMIT THEN PRINT ]
                                      [ 1  - CLOSE AFTER JUST PRINT        ]
                                      [ 2  - AUTO PRINT                    ]
                                      [ 3  - AND UP - USER DEFINED         ]

 FOR TEMPNUM = 1 TO 99
  DO
   TRANACCTID(TEMPNUM) = 0
  END

 CALL SETUPACTIONCODES
 CALL SETUPCHECKESCAPECODES    
 CALL SETUPPRINTERLINES
  
 OKIRECEIPTS = FALSE
 IF RECEIPTLINES = 44 THEN
  DO
   RECEIPTLINES = 39
   OKIRECEIPTS = TRUE
   SHORTRECEIPT = FALSE
   MASKACCOUNT = 1      [** RESET ACCOUNT MASKING POSITION TO BEFORE VIEW B/C **]
                        [** NO SF TO UPDATE AFTER VIEW, BEFORE PRINT          **]
   MYOKIPAGECOUNT = 1
  END
 ELSE IF RECEIPTLINES>66 THEN
  DO
   RECEIPTNAME    = "THERMAL.BLANK."
   SHORTRECEIPT   = TRUE
   IF RECEIPTLINES=131 THEN
     NUMCOPIES="2"
   IF SHORTTHERMAL=TRUE THEN
    RECEIPTLINES  = 78
   ELSE
    RECEIPTLINES  = 300
   LINEHEIGHT     = 40
   COLCOUNT       = 37
   THRMLLINEWDTH  = 37     [FONT 8 = 37 CHARS, FONT 10 = 33 CHARS] 
   MLWIDTH        = 37     [FONT 8 = 37 CHARS, FONT 10 = 33 CHARS]
   FONTSIZE       = 8           
   EDITBOX        =",CT=09,W=800,H=750,E=0,FB=1,FW="
   EDITXANDY      =",X=10"
   IMGLOGONAME    = "RECEIPTLOGO.228.jpg"
   IMGLOGOPROP    = "W=700,H=200,X=50,Y=10"
   IF USECASHBOX = TRUE THEN
    DO
     ADDLINES     = 88
     [CALL SETUPTHERMALCASHBOX]
    END
   ELSE
    ADDLINES      = 10
  END
 ELSE CALL SETUPLASER

 FOR FORMLOOP = 0 TO 600
  DO
   FORMDATA(FORMLOOP) = ""
  END

 YPOS           = 300
 MODULOS        = 1
 DIV            = 0
 CCRGLACCTCOUNT = 0
 LINESPRINTED   = 1
 CCRBODYCOUNT   = 0
 IDCOUNT        = 0
 PAGE           = 1
 LOGOIDS        = ""

 NONANSISTANDARD
 CALL PARSERECEIPTFILE
 FILECLOSE(CHECKFILE,CHECKERROR) 
 FOR ACCOUNT CCRHEADER(CCRACCOUNT)
  DO  
   FULLACCOUNTNUMBER=ACCOUNT:NUMBER
[TODO:]CALL LAUNCHCLOSEFORM
   IF ANYWARNING(ACCOUNT,77) THEN CALL SETUPLASER  
  END
 
 IF CCRCHECKFLAG = FALSE THEN 
  DO
   IF LAUNCHCTR = 1 THEN
    DO
     IF CAPTURETRNSACTR = TRUE THEN
      DO
       [INCLUDE "RD.ECTR.VERIFYPERSON.SET"] 
       #INCLUDE "ECTR.VERIFYPERSON.SET.228"
      END

     [USECTR = TRUE
     # INCLUDE "TT.CF.CTR.F104.SET"]  [Uncomment for CTR]
    END
   IF MASKACCOUNT = 1 THEN
    DO
     UNMASKEDACCOUNT = CCRHEADER(CCRACCOUNT)
     CCRHEADER(CCRACCOUNT) = REPEATCHR("X",MASKXNUMBER)+SEGMENT(CCRHEADER(CCRACCOUNT),MASKXNUMBER+1,10)
     FOR CCRBODYLOOP = 1 TO CCRBODYCOUNT
      DO
       IF CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)<>"" THEN
          CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT) = REPEATCHR("X",MASKXNUMBER)+
          SEGMENT(CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT),MASKXNUMBER+1,10)
      END
    END
   CALL LOADRECEIPT

   IF OKIRECEIPTS = FALSE THEN
    DO
     LINESPRINTED=LINESPRINTED+ADDLINES
     CALL SENDRECEIPTTOSYMFORM
     LINESPRINTED=LINESPRINTED-ADDLINES
     CALL SENDDATAPOSITIONS

     IF MASKACCOUNT = 1 THEN
      DO
       TEMPACCOUNT = CCRHEADER(CCRACCOUNT)
       CCRHEADER(CCRACCOUNT) = UNMASKEDACCOUNT
       CALL CHECKACCOUNTFOROTG
       UNMASKEDACCOUNT = CCRHEADER(CCRACCOUNT)
       CCRHEADER(CCRACCOUNT) = TEMPACCOUNT
      END
     ELSE
      CALL CHECKACCOUNTFOROTG
   
     WHILE (VALUE(RESPSF)>0 OR RESPSF="") AND CLOSEFORM = FALSE
      DO                              
       CALL ENABLESIGPAD [GETRESPONSE]
       CALL PROCESSFIELDEVENT2
       [*** PROCESS COMMAND EVENT *****]
       IF RESPCMD<>"" THEN        
        DO
         IF RESPCMD="SAVE" THEN
          DO
           CALL PRINTSAVECLOSE
          END
        END
       IF RESPTID<>"" THEN
         CALL PROCESSTOOLBAREVENT
      END
    END
  END

 IF CCRCHECKFLAG=FALSE AND OKIRECEIPTS = FALSE THEN
  DO
   IF LAUNCHCTR = 2 THEN
    DO
     IF CAPTURETRNSACTR = TRUE THEN
      DO
       IF MASKACCOUNT = 1 THEN
        CCRHEADER(CCRACCOUNT)=UNMASKEDACCOUNT
       [INCLUDE "RD.ECTR.VERIFYPERSON.SET" ]
       #INCLUDE "ECTR.VERIFYPERSON.SET.228"
      END
     [USECTR = TRUE
      # INCLUDE "TT.CF.CTR.F104.SET"]    [Uncomment for CTR]
    END
   TERMINATE
  END
 ELSE IF CCRCHECKFLAG = TRUE THEN
  DO
   IF CCRCHECKFLAG=TRUE AND PROMPTFORCHECKTYPES=TRUE THEN
    CALL GETCHECKTYPEWANTED
  
   IF CCRCHECK(CCRCHECKACCTCODE)="01" THEN
    DO [KD, OKIDATA]
     CHECKMODE = DOTMATRIXCHECKS
    END
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="02" THEN
    DO
     CHECKMODE = DOTMATRIXCHECKS
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="21" THEN
    DO
     CHECKMODE = WYCOMCHECKS
     WYCOMCHECKID="[S,001,0021,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="22" THEN
    DO
     CHECKMODE = WYCOMCHECKS
     WYCOMCHECKID="[S,001,0022,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="31" THEN
    DO
     CHECKMODE = WYCOMCHECKS
     WYCOMCHECKID="[S,001,0031,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="33" THEN
    DO
     CHECKMODE = WYCOMCHECKS
     WYCOMCHECKID="[S,001,0033,"
    END 

[
   IF CCRCHECK(CCRCHECKACCTCODE)="00" THEN
    DO
     CHECKLINES=CHECKKDLINES
     CHECKBLANKLINES=CHECKKDBLANKLINES
     TYPEOFCHECK=0
     WYCOMCHECKID="[S,001,0001,"
    END
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="01" THEN
    DO
     CHECKLINES=CHECKKOLINES
     CHECKBLANKLINES=CHECKKOBLANKLINES
     TYPEOFCHECK=1
     WYCOMCHECKID="[S,001,0002,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="02" THEN
    DO
     CHECKLINES=CHECKMOLINES(1)
     CHECKBLANKLINES=CHECKMOBLANKLINES(1)
     TYPEOFCHECK=2
     WYCOMCHECKID="[S,001,0003,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="03" THEN
    DO
     CHECKLINES=CHECKMOLINES(2)
     CHECKBLANKLINES=CHECKMOBLANKLINES(2)
     TYPEOFCHECK=3
     WYCOMCHECKID="[S,001,0004,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="04" THEN
    DO
     CHECKLINES=CHECKMOLINES(3)
     CHECKBLANKLINES=CHECKMOBLANKLINES(3)
     TYPEOFCHECK=4
     WYCOMCHECKID="[S,001,0005,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="05" THEN
    DO
     CHECKLINES=CHECKMOLINES(4)
     CHECKBLANKLINES=CHECKMOBLANKLINES(4)
     TYPEOFCHECK=5
     WYCOMCHECKID="[S,001,0006,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="06" THEN
    DO
     CHECKLINES=CHECKMOLINES(5)
     CHECKBLANKLINES=CHECKMOBLANKLINES(5)
     TYPEOFCHECK=6
     WYCOMCHECKID="[S,001,0007,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="07" THEN
    DO
     CHECKLINES=CHECKMOLINES(6)
     CHECKBLANKLINES=CHECKMOBLANKLINES(6)
     TYPEOFCHECK=7
     WYCOMCHECKID="[S,001,0008,"
    END 
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="08" THEN
    DO
     CHECKLINES=CHECKMOLINES(7)
     CHECKBLANKLINES=CHECKMOBLANKLINES(7)
     TYPEOFCHECK=8
     WYCOMCHECKID="[S,001,0009,"
    END
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="09" THEN
    DO
     CHECKLINES=CHECKMOLINES(8)
     CHECKBLANKLINES=CHECKMOBLANKLINES(8)
     TYPEOFCHECK=9
     WYCOMCHECKID="[S,001,0010,"
    END
   ELSE IF CCRCHECK(CCRCHECKACCTCODE)="10" THEN
    DO
     CHECKLINES=CHECKMOLINES(9)
     CHECKBLANKLINES=CHECKMOBLANKLINES(9)
     TYPEOFCHECK=10
     WYCOMCHECKID="[S,001,0011,"
    END
]]]]]]]]]]]]
  END 
END


PRINT TITLE= CCRTITLE
 IF CCRCHECKFLAG=TRUE THEN
  DO
   IF CHECKMODE=LASERCHECKS THEN
    DO
    [HPESC "&l5H" Directs print to Tray 5 (optional lower tray)
                  Change this to "&l1H" for Tray 2 (primary drawer)
                  Change to "&l4H"  for manual feed tray.   ***]
     HPFONT(39,12)
     HPYPOS(100)
     CALL PRINTREC
     HPYPOS(1200)
     CALL PRINTREC
     CALL PRINTCHECK
    END
   ELSE IF CHECKMODE=WYCOMCHECKS THEN
    DO
     CALL PRINTSTANDARDWYCOMCHECK
    END
   ELSE IF CHECKMODE=DOTMATRIXCHECKS THEN
    DO
     CALL OKIPRINTRECEIPT
     CALL OKIPRINTCHECK
    END
  END
 ELSE IF OKIRECEIPTS = TRUE THEN
  DO
   MYOKIPAGECOUNT = 1
   FOR FORMLOOP = 1 TO LINESPRINTED
    DO
     PRINT FORMDATA(FORMLOOP)
     IF FORMLOOP+1 <= (RECEIPTLINES-BLANKLINES)*MYOKIPAGECOUNT THEN
      NEWLINE
     ELSE
      DO
       MYOKIPAGECOUNT = MYOKIPAGECOUNT + 1
       NEWPAGE
      END
    END

   IF CCRTRAILEREXIST = TRUE THEN
    DO
     IF CCRTRAILER(CCRSIGNATURETYPE)<>"" THEN
      DO
       PRINT CCRTRAILER(CCRSIGNATURETYPE)+": ________________________________________"
       CALL OKINEXTLINE
      END
     CALL OKINEXTLINE
     IF CCRTRAILER(CCRIDLINE)="Y" THEN
      DO
       PRINT "ID Source: [ ]Driver Lic_____________ "+"[ ]SigCard [ ]Known [ ]Other____________"
       CALL OKINEXTLINE
      END
    END

   LINEMAX = 0
   IF CCRADDRESSEXIST = TRUE THEN
    DO
     LINEMAX = CCRADDRESSLINES
     IF CCRADDRESS(LINEMAX) = "" THEN
      LINEMAX = LINEMAX - 1
    END
   IF CCRMESSAGEEXIST = TRUE THEN
    LINEMAX = LINEMAX + CCRMESSAGELINES
   IF LINEMAX <> 0 THEN
    LINEMAX = LINEMAX + 3

   IF LINESPRINTED > (RECEIPTLINES-BLANKLINES)*MYOKIPAGECOUNT-LINEMAX THEN
    DO
     NEWPAGE
     MYOKIPAGECOUNT = MYOKIPAGECOUNT + 1
    END
   FOR FORMLOOP = LINESPRINTED TO (RECEIPTLINES-BLANKLINES)*MYOKIPAGECOUNT-LINEMAX
    DO
     NEWLINE
     LINESPRINTED = LINESPRINTED + 1
    END
   IF CCRMESSAGEEXIST = TRUE THEN
    DO
     FOR LINELOOP = 1 TO CCRMESSAGELINES
      DO
       IF CCRMESSAGE(LINELOOP)<>"" THEN 
        PRINT CCRMESSAGE(LINELOOP)
       CALL OKINEXTLINE [always print newline]
      END
    END
   CALL OKINEXTLINE
   CALL OKINEXTLINE
   FOR LINELOOP = 1 TO CCRADDRESSLINES
    DO
     IF CCRADDRESS(LINELOOP)<>"" AND CCRADDRESSEXIST = TRUE THEN
      DO
       COL = 10 CCRADDRESS(LINELOOP)
       LINESPRINTED = LINESPRINTED + 1
       NEWLINE
      END
    END
  END
 SUPPRESSNEWLINE   
END


PROCEDURE CLEARCCRARRAYS
 FOR CCRHEADERLOOP = 1 TO CCRHEADERLINES
  DO
   CCRHEADER(CCRHEADERLOOP) = ""
  END
 FOR CCRTRAILERLOOP = 1 TO CCRTRAILERLINES
  DO
   CCRTRAILER(CCRTRAILERLOOP)=""
  END
 FOR CCRMESSAGELOOP = 1 TO CCRMESSAGELINES
  DO
   CCRMESSAGE(CCRMESSAGELOOP)=""
  END
 FOR CCRADDRESSLOOP = 1 TO CCRADDRESSLINES
  DO
   CCRADDRESS(CCRADDRESSLOOP)=""
  END
 FOR CCRCHECKLOOP = 1 TO CCRCHECKLINES
  DO
   CCRCHECK(CCRCHECKLOOP)=""
  END
 FOR FORMLOOP = 1 TO 99
  DO
   FOR CCRBODYLOOP = 1 TO CCRBODYLINES
    DO
     CCRBODY(FORMLOOP,CCRBODYLOOP)=""
    END
   FOR CCRGLACCTLOOP = 1 TO CCRGLACCTLINES
    DO
     CCRGLACCT(FORMLOOP,CCRGLACCTLOOP)=""
    END
   FOR CCRGLLOOP = 1 TO CCRGLLINES
    DO
     CCRGL(FORMLOOP,CCRGLLOOP)=""
    END
  END

END


PROCEDURE GETCHECKTYPEWANTED
 IF SYSWINDOWSLEVEL>=WINDOWSLEVELVERSION1 THEN
  DO
   WINDOWSSEND(WINCODEMENUINIT,"Check Types")
   WINDOWSSEND(WINCODEMENUENTRY,"L01TELLER CHECK (OFFICIAL CHECK)")
   WINDOWSSEND(WINCODEMENUENTRY,"L02CASHIERS CHECK")
   WINDOWSSEND(WINCODEMENUENTRY,"L03MONEY ORDER")
   WINDOWSSEND(WINCODEMENUENTRY,"L04EXPENSE CHECK") 
   WINDOWSSEND(WINCODEMENUDISPLAY,"000")
  END
 ELSE
  DO
   NEWLINE
   PRINT " 0 = TELLER CHECK (OFFICAL CHECK)"
   NEWLINE
   PRINT " 1 = CASHIERS CHECK"
   NEWLINE
   PRINT " 2 = MONEY ORDER "
    NEWLINE
   PRINT " 3 = EXPENSE CHECK" 
   NEWLINE
   NEWLINE
  END
 TYPEOFCHECK=ENTERCODE("WHICH TYPE OF CHECK?",3,0)
END


PROCEDURE LOADRECEIPT
 CCRGLLOOP     = 1
 CCRCHECKLOOP  = 1
 CCRBODYLOOP   = 1
 PAGECOUNT     = 0
 TRANACCTCOUNT = 0

 CALL PRINTCCRHEADER
 MYHEADERSTART = OKIHEADERSTART
 MYHEADEREND = OKIHEADEREND

 FOR CCRBODYLOOP = 1 TO CCRBODYCOUNT
  DO
   CALL PRINTCCRBODY
   FOR CCRLOANSEGLOOP = 1 TO CCRLOANSEGCOUNT
    DO
     IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)="L" AND
        VALUE(CCRBODY(CCRBODYLOOP,CCRID))=
        VALUE(CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGLOANID)) AND
       (CCRBODY(CCRBODYLOOP,CCRSOURCECODE)<>"J" OR [not LS xfer]
       (CCRBODY(CCRBODYLOOP,CCRSOURCECODE)="J" AND [LS xfer/not in desc]
        CHARACTERSEARCH(CCRBODY(CCRBODYLOOP,CCRTRANDESCRIPTION),
        "Seg "+CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGID))=0)) THEN
       CALL PRINTLOANSEGMENT
    END
   IF CCRBODYLOOP<CCRBODYCOUNT THEN
    DO
     OMITBLANKLINES=FALSE
     FORMDATA(LINESPRINTED)=""
     CALL STORELINE1
     OMITBLANKLINES=TRUE
    END
  END

 FOR CCRGLACCTLOOP = 1 TO CCRGLACCTCOUNT
  DO
   CALL PRINTCCRGLACCT
  END

 IF ENABLEEXTRNLLOAN = TRUE THEN
  FOR CCREXTRNLLOANLOOP = 1 TO CCREXTRNLLOANCOUNT       
   DO
    CALL PRINTCCREXTRNLLOAN
   END

 IF OKIRECEIPTS = FALSE THEN
  CALL LINEBREAK
 ELSE
  DO
   FORMDATA(LINESPRINTED)=REPEATCHR("_",80)
   CALL STORELINE1
   OMITBLANKLINES = FALSE
   FORMDATA(LINESPRINTED)=""
   CALL STORELINE1
   OMITBLANKLINES = TRUE
  END

 IF CCRSEQREFCOUNT>0 THEN
  CALL PRINTSEQUENCEREF

 MYGLSTART = LINESPRINTED
 FOR CCRGLLOOP= 1 TO CCRGLCOUNT
  DO
   CALL PRINTCCRGL
  END
 MYGLEND = LINESPRINTED

 OMITBLANKLINES=FALSE
 FORMDATA(LINESPRINTED)=""
 CALL STORELINE1
 OMITBLANKLINES=TRUE

 CALL MESSAGE  
 CALL SETUPENDLINES
 IF OKIRECEIPTS = FALSE THEN
  CALL SETPAGE
END


PROCEDURE PRINTCCRHEADER
  OMITBLANKLINES = FALSE
  PAGECOUNT = PAGECOUNT + 1
[If the CU wants the logo to be in the first
 page only for Short Thermal receipts]
[
  IF SHORTRECEIPT=FALSE OR 
     SHORTTHERMAL=TRUE AND PAGE=1 THEN 
]
  IF USEIMGLOGO=TRUE AND OKIRECEIPTS = FALSE THEN
   DO
    LOGOIDS = LOGOIDS+FORMAT("999,",LINESPRINTED)
    CALL STORELINE1
   END
  IF OKIRECEIPTS = FALSE THEN
   DO
    FOR FORMLOOP = 1 TO BLANKLINES
     DO
      FORMDATA(FORMLOOP)= ""
      CALL STORELINE1
     END
   END
  OKIHEADERSTART = LINESPRINTED
  IF SHORTRECEIPT=TRUE THEN 
    OMITBLANKLINES = TRUE
  IF CCRHEADEREXIST = TRUE THEN
   DO
    IF CCRFILETYPE = "RECEIPT" THEN
     DO
      IF CCRHEADER(CCRINSTITUTION)<>"" THEN
       DO
        FORMDATA(LINESPRINTED)=CCRHEADER(CCRINSTITUTION)
        IF SHORTRECEIPT=TRUE THEN
         CALL STORELINE1
       END
      IF CCRHEADER(CCRHEADERDOCNUM)<>"" THEN
       DO
        IF SHORTRECEIPT=FALSE THEN 
         DO
          FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
          SEGMENT(BLANK,1,49-LENGTH(CCRHEADER(CCRINSTITUTION)))+
                  "Doc Number:       "+CCRHEADER(CCRHEADERDOCNUM)
         END
       END
      IF SHORTRECEIPT=FALSE THEN
       DO
        CALL STORELINE1
       END
      IF CCRHEADER(CCREXTRAADDRESS)<>"" THEN
       DO
        FORMDATA(LINESPRINTED)=CCRHEADER(CCREXTRAADDRESS)
        CALL STORELINE1
       END
      IF SHORTRECEIPT=TRUE THEN 
       DO
        IF CCRHEADER(CCRSTREET)<>"" THEN
         DO
          FORMDATA(LINESPRINTED)=CCRHEADER(CCRSTREET)
          CALL STORELINE1
         END
        IF CCRHEADER(CCRCITY)<>"" OR
           CCRHEADER(CCRSTATE)<>"" OR
           CCRHEADER(CCRZIPCODE)<>"" THEN
         DO
          FORMDATA(LINESPRINTED)=CCRHEADER(CCRCITY)+" "+
          CCRHEADER(CCRSTATE)+" "+CCRHEADER(CCRZIPCODE)
          CALL STORELINE1
         END
        IF CCRHEADER(CCRPHONE)<>"" THEN
         DO
          DTEMP="Inquiries Call:"
          TEMPAMOUNT=CCRHEADER(CCRPHONE)
          CALL THERMALTRANSACTION
         END
       END
      ELSE
       DO
        IF CCRHEADER(CCRSTREET)<>"" THEN
         DO
          FORMDATA(LINESPRINTED)=CCRHEADER(CCRSTREET)
          IF CCRHEADER(CCRPHONE)<>"" THEN
           FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
              SEGMENT(BLANK,1,78-LENGTH(FORMDATA(LINESPRINTED))-
              LENGTH("Inquiries Call:"+CCRHEADER(CCRPHONE)))+
                            "Inquiries Call:"+CCRHEADER(CCRPHONE)
          CALL STORELINE1
         END
        IF CCRHEADER(CCRCITY)<>"" OR CCRHEADER(CCRSTATE)<>"" OR
           CCRHEADER(CCRZIPCODE)<>"" THEN
         DO
          FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
                 CCRHEADER(CCRCITY)+" "+CCRHEADER(CCRSTATE)+" "+
                 CCRHEADER(CCRZIPCODE)
          CALL STORELINE1
         END
        OMITBLANKLINES=FALSE
        FORMDATA(LINESPRINTED)=""
        CALL STORELINE1
        OMITBLANKLINES=TRUE
       END
     END
    ELSE
     DO
      FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
                  SEGMENT(BLANK,1,50)+ "Document Number:"
      FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
                  SEGMENT(BLANK,1,78-LENGTH(FORMDATA(LINESPRINTED))-
                          LENGTH(CCRHEADER(CCRHEADERDOCNUM)))+
                  CCRHEADER(CCRHEADERDOCNUM)
      CALL STORELINE1
     END
    IF SHORTRECEIPT=TRUE THEN 
     DO
      OMITBLANKLINES=FALSE
      FORMDATA(LINESPRINTED)=""
      CALL STORELINE1
      OMITBLANKLINES=TRUE
      DTEMP="Acct: XXXXXXX"+SEGMENT(CCRHEADER(CCRACCOUNT),8,10)
      TEMPAMOUNT=CCRHEADER(CCRNAME)
      ACCTID(PAGECOUNT) = LINESPRINTED
      CALL THERMALTRANSACTION
      DTEMP="Eff: "+CCRHEADER(CCREFFECTIVEDATE)
      TEMPAMOUNT="Date: "+CCRHEADER(CCRPOSTDATE)  
      CALL THERMALTRANSACTION
      DTEMP="Tlr: "+CCRHEADER(CCRTELLER)
      TEMPTIME=SYSACTUALTIME
      CALL FORMATTIME
      TEMPAMOUNT=TEMPCHAR 
      CALL THERMALTRANSACTION
      IF CCRHEADER(CCRHEADERDOCNUM)<>"" THEN
       DO
        DTEMP="Doc Number:"
        TEMPAMOUNT=CCRHEADER(CCRHEADERDOCNUM)
        CALL THERMALTRANSACTION
       END
     END
    ELSE
     DO
      FORMDATA(LINESPRINTED)= "Account: XXXXXXX"+SEGMENT(CCRHEADER(CCRACCOUNT),8,10)+"  "+CCRHEADER(CCRNAME)
      FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
                    SEGMENT(BLANK,1,37-LENGTH(FORMDATA(LINESPRINTED)))+
                    "Effect: "+CCRHEADER(CCREFFECTIVEDATE)+
                    " Post: "+CCRHEADER(CCRPOSTDATE)+
                    "  Tlr: "+CCRHEADER(CCRTELLER)
      ACCTID(PAGECOUNT) = LINESPRINTED
      CALL STORELINE1
     END
    IF CCRHEADER(CCRHEADERSEQUENCE)<>"" THEN
     DO
      FORMDATA(LINESPRINTED)="Seq: "+CCRHEADER(CCRHEADERSEQUENCE)
      CALL STORELINE1
     END

    IF OKIRECEIPTS = TRUE THEN
     DO
      CALL STORELINE1
      OMITBLANKLINES = FALSE
      FORMDATA(LINESPRINTED)=""
      CALL STORELINE1
      OMITBLANKLINES = TRUE
     END
   
    IF CCRBODYCOUNT>0 THEN
     DO
      IF CHARACTERSEARCH(CCRBODY(CCRBODYLOOP,CCRIDTYPE),"See ")=0 THEN
       DO
        IF SHORTRECEIPT=FALSE THEN 
         DO
          FORMDATA(LINESPRINTED)= "ID"+REPEATCHR(" ",CCRIDLEN-1)+
            "DUE DATE  "+REPEATCHR(" ",4-CCRIDLEN)+"PRINCIPAL  INTEREST     FEES   "
          IF CCRPREVBALFLAG=FALSE THEN
           FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+"NEW "  
          ELSE
           FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+"    "
          FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
                                      "BALANCE  TRAN AMOUNT     SEQ"
          CALL STORELINE1
         END
       END
       IF SHORTRECEIPT=TRUE AND SHORTTHERMAL=TRUE THEN
        CALL PRINTSHORTTHERMALHEADER

       IF OKIRECEIPTS = FALSE THEN
        CALL LINEBREAK
       ELSE
        DO
         FORMDATA(LINESPRINTED)=REPEATCHR("_",80)
         CALL STORELINE1
         OMITBLANKLINES = FALSE
         FORMDATA(LINESPRINTED)=""
         CALL STORELINE1
         OMITBLANKLINES = TRUE
        END
     END
   END
  OKIHEADEREND = LINESPRINTED
END


PROCEDURE PRINTCCRBODY
 IF CHARACTERSEARCH(CCRBODY(CCRBODYLOOP,CCRIDTYPE),"See ")>0 THEN
  DO
   FORMDATA(LINESPRINTED)="("+
     SEGMENT(CCRBODY(CCRBODYLOOP,CCRIDTYPE),1,12)
   FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
                         SEGMENT(CCRBODY(CCRBODYLOOP,CCRIDTYPE),30,
                         LENGTH(CCRBODY(CCRBODYLOOP,CCRIDTYPE)))
   FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED) + " for reference)"
  END
 ELSE
  DO
   CALL PRINTCCRBODYDESCRIPTION
   IF CCRBODY(CCRBODYLOOP,CCRID)<>"" THEN
    DO
     IF CCRBODY(CCRBODYLOOP,CCRCOMMENTTYPE)="Y" THEN
      DO
       IF SHORTRECEIPT=TRUE THEN
        DO
         FORMDATA(LINESPRINTED)=CCRBODY(CCRBODYLOOP,CCRCOMMENT)
         CALL STORELINE1
         TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRSEQUENCE)
         DTEMP="Seq:"
         CALL THERMALTRANSACTION
        END 
       ELSE
        DO
         FORMDATA(LINESPRINTED)=CCRBODY(CCRBODYLOOP,CCRID) 
          FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
          SEGMENT(BLANK,1,70-LENGTH(FORMDATA(LINESPRINTED))-
                   LENGTH(CCRBODY(CCRBODYLOOP,CCRCOMMENT)))+
                   CCRBODY(CCRBODYLOOP,CCRCOMMENT)
         FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
          SEGMENT(BLANK,1,78-LENGTH(FORMDATA(LINESPRINTED)+
          CCRBODY(CCRBODYLOOP,CCRSEQUENCE)))+
          CCRBODY(CCRBODYLOOP,CCRSEQUENCE)
         CALL STORELINE1
        END   
      END
     ELSE
      DO
       IF SHORTRECEIPT=TRUE THEN
        DO
         IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)="S" THEN
          DO
           IF CCRBODY(CCRBODYLOOP,CCRMATURITYDATE)<>"" THEN
            DO
             TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRMATURITYDATE)
             DTEMP="Maturity date:"
             CALL THERMALTRANSACTION
            END
          END
         IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)="L" THEN
          DO
           IF CCRBODY(CCRBODYLOOP,CCRMATURITYDATE)<>"" THEN
            DO
             TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRMATURITYDATE)
             DTEMP="Due date:"
             CALL THERMALTRANSACTION
            END

           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRAMOUNT)
           DTEMP="Amount:"
           CALL THERMALTRANSACTION

           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRBALANCECHANGE)
           DTEMP="Principal:"
           CALL THERMALTRANSACTION
          END

         IF VALUE(CCRBODY(CCRBODYLOOP,CCRINTEREST))<>0 THEN
          DO       
           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRINTEREST)
           DTEMP="Interest:"
           CALL THERMALTRANSACTION
          END
         IF VALUE(CCRBODY(CCRBODYLOOP,CCRFEE))<>0 THEN
          DO
           TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRFEE)
           DTEMP="Fees:"
           CALL THERMALTRANSACTION
          END
         IF VALUE(CCRBODY(CCRBODYLOOP,CCRSALESTAXAMOUNT))<>0 THEN
          DO
           TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRSALESTAXAMOUNT)
           DTEMP="Sales Tax:"
           CALL THERMALTRANSACTION
          END  

         IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)<>"L" THEN
          DO
           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRAMOUNT)
           DTEMP="Amount:"
           CALL THERMALTRANSACTION
          END

         IF CCRBODY(CCRBODYLOOP,CCRBALANCE)<>"" THEN
          DO
           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRBALANCE)
           DTEMP="New Bal:"
           CALL THERMALTRANSACTION
          END

         IF SHOWAVAILBAL=TRUE THEN
          DO
           IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)="S" AND 
              CCRBODY(CCRBODYLOOP,CCRBALANCE)<>"" AND 
              CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)="" THEN
            DO
             FOR ACCOUNT CCRHEADER(CCRACCOUNT)
              DO
               FOR EACH SHARE WITH SHARE:ID = CCRBODY(CCRBODYLOOP,CCRID)
                DO
                 TEMPAMOUNT = FORMAT("#,###,###.99+",SHARE:AVAILABLEBALANCE)
                END
              END
             TEMPLINE=TEMPAMOUNT
             CALL REMOVELEADINGSPACES
             TEMPAMOUNT=TEMPLINE
             DTEMP="Avail. Bal:"
             CALL THERMALTRANSACTION
            END
           ELSE IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)="L" AND 
                   CCRBODY(CCRBODYLOOP,CCRBALANCE)<>"" AND 
                   CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)="" THEN
            DO
             FOR ACCOUNT CCRHEADER(CCRACCOUNT)
              DO
               FOR EACH LOAN WITH LOAN:ID = CCRBODY(CCRBODYLOOP,CCRID)
                DO
                 TEMPAMOUNT = FORMAT("#,###,###.99+",LOAN:AVAILABLECREDIT)
                END
              END
             TEMPLINE=TEMPAMOUNT
             CALL REMOVELEADINGSPACES
             TEMPAMOUNT=TEMPLINE
             DTEMP="Avail. Credit:"
             CALL THERMALTRANSACTION
            END
          END

         IF CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER)<>"" OR
            CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)<>"" THEN
          DO      
           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER)
           DTEMP="Check number:"
           CALL THERMALTRANSACTION

           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)
           DTEMP="Tracer number:"
           CALL THERMALTRANSACTION
          END
         IF CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)<>"" THEN
          DO      
           TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)
           DTEMP="Tot Curr. Yr Cont:"
           CALL THERMALTRANSACTION
          END
        IF CCRBODY(CCRBODYLOOP,CCRCONTLASTYEAR)<>"" THEN
         DO      
          TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRCONTLASTYEAR)
          DTEMP="Tot Last Yr Cont:"
          CALL THERMALTRANSACTION
         END
        IF CCRBODY(CCRBODYLOOP,CCRBILLEDFEEAMOUNT)<>"" THEN
         DO      
          TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRBILLEDFEEAMOUNT)
          DTEMP="Billed Fee:"
          CALL THERMALTRANSACTION
         END
  
         TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRSEQUENCE)
         DTEMP="Seq:"
         CALL THERMALTRANSACTION
        END              
       ELSE
        IF SHORTRECEIPT=FALSE THEN
         DO
          FORMDATA(LINESPRINTED)=CCRBODY(CCRBODYLOOP,CCRID)+" "+
                               CCRBODY(CCRBODYLOOP,CCRMATURITYDATE)
          AMTCOL = 24
          TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRBALANCECHANGE)
          CALL PRINTSYMFORMAMOUNT
          AMTCOL = 34
          TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRINTEREST)
          CALL PRINTSYMFORMAMOUNT
          AMTCOL = 43
          TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRFEE)
          CALL PRINTSYMFORMAMOUNT
          AMTCOL = 57
          TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRBALANCE)
          CALL PRINTSYMFORMAMOUNT
          TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRAMOUNT)
          AMTCOL     = 70
          CALL PRINTSYMFORMAMOUNT
          FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
               SEGMENT(BLANK,1,78-LENGTH(FORMDATA(LINESPRINTED)+
                    CCRBODY(CCRBODYLOOP,CCRSEQUENCE)))+
                                 CCRBODY(CCRBODYLOOP,CCRSEQUENCE)
          CALL STORELINE1

          IF SHOWAVAILBAL=TRUE THEN
           DO
            IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)="S" AND 
               CCRBODY(CCRBODYLOOP,CCRBALANCE)<>"" AND 
               CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)="" THEN
             DO
              AMTCOL = 57
              FOR ACCOUNT CCRHEADER(CCRACCOUNT)
               DO
                FOR EACH SHARE WITH SHARE:ID = CCRBODY(CCRBODYLOOP,CCRID)
                 DO
                  TEMPAMOUNT = FORMAT("#,###,###.99+",SHARE:AVAILABLEBALANCE)
                 END
               END
              TEMPLINE=TEMPAMOUNT
              CALL REMOVELEADINGSPACES
              TEMPAMOUNT=TEMPLINE
              FORMDATA(LINESPRINTED)="Available Balance:"
              CALL PRINTSYMFORMAMOUNT
              CALL STORELINE1
             END
            ELSE IF CCRBODY(CCRBODYLOOP,CCRIDTYPE)="L" AND 
                    CCRBODY(CCRBODYLOOP,CCRBALANCE)<>"" AND 
                    CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)="" THEN
             DO
              AMTCOL = 57
              FOR ACCOUNT CCRHEADER(CCRACCOUNT)
               DO
                FOR EACH LOAN WITH LOAN:ID = CCRBODY(CCRBODYLOOP,CCRID)
                 DO
                  TEMPAMOUNT = FORMAT("#,###,###.99+",LOAN:AVAILABLECREDIT)
                 END
               END
              TEMPLINE=TEMPAMOUNT
              CALL REMOVELEADINGSPACES
              TEMPAMOUNT=TEMPLINE
              FORMDATA(LINESPRINTED)="Available Credit:"
              CALL PRINTSYMFORMAMOUNT
              CALL STORELINE1
             END
           END

          IF CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER)<>"" OR
             CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)<>"" THEN
           DO
             FORMDATA(LINESPRINTED)="Draft number: "+
             CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER) +
             "  Tracer number: "+CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)
              CALL STORELINE1
           END
          IF CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)<>"" OR 
             CCRBODY(CCRBODYLOOP,CCRCONTLASTYEAR)<>"" THEN
           DO   
            CALL STORELINE1 
            FORMDATA(LINESPRINTED)="Total Previous Year Contributions:"
            TEMPAMOUNT=CCRBODY(CCRBODYLOOP,CCRCONTLASTYEAR)
            AMTCOL = 70
            CALL PRINTSYMFORMAMOUNT
            CALL STORELINE1
            FORMDATA(LINESPRINTED)= "Total Current Year Contributions:"
            TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)
            AMTCOL = 70
            CALL PRINTSYMFORMAMOUNT
            CALL STORELINE1
           END
         END
      END
    END
   IF CCRBODY(CCRBODYLOOP,CCRTRANDESCRIPTION)<>"" THEN
    DO
     FORMDATA(LINESPRINTED)="Description: "+
          CCRBODY(CCRBODYLOOP,CCRTRANDESCRIPTION)
     CALL STORELINE1
    END 
   IF CCRBODYLOOP<CCRBODYCOUNT THEN
    DO
     FORMDATA(LINESPRINTED)=""
     CALL STORELINE1
    END
  END
END


PROCEDURE PRINTCCRBODYDESCRIPTION
 IF CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)<>"" THEN
  DO
   IF CCRBODYLOOP<>1 THEN
    DO
     FORMDATA(LINESPRINTED)=""
     CALL STORELINE1
    END
  
   IF CCRBODY(CCRBODYLOOP,CCRCOMMENTTYPE)="Y" THEN
    DO
     FORMDATA(LINESPRINTED)="Comment for "+CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)
     IF SHORTRECEIPT=TRUE THEN
      FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+" "+CCRBODY(CCRBODYLOOP,CCRID)
     CALL STORELINE1
    END
   ELSE IF ACTIONDESC(CHARACTERSEARCH(ACTIONCODELIST,CCRBODY(CCRBODYLOOP,CCRACTIONCODE)))<>"" THEN
    DO
     FORMDATA(LINESPRINTED)=ACTIONDESC(CHARACTERSEARCH(ACTIONCODELIST,
     CCRBODY(CCRBODYLOOP,CCRACTIONCODE)))+" "+
     CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)+" "+CCRBODY(CCRBODYLOOP,CCRID)
     IF SHORTRECEIPT=TRUE THEN CALL STORELINE1
     END
   ELSE
    DO
     IF SHORTRECEIPT=TRUE THEN
      DO
       FORMDATA(LINESPRINTED)= CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)+" "+CCRBODY(CCRBODYLOOP,CCRID)
       CALL STORELINE1
      END 
     ELSE
      DO 
       FORMDATA(LINESPRINTED)=CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)+" "+CCRBODY(CCRBODYLOOP,CCRID)
      END
    END
   IF CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)<>"" THEN
    DO
     IF TRANACCTCOUNT < 99 THEN TRANACCTCOUNT = TRANACCTCOUNT + 1
     IF SHORTRECEIPT=TRUE THEN
      DO
       FORMDATA(LINESPRINTED)="Acct XXXXXXX"+SEGMENT(CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT),8,10)
       TRANACCTID(TRANACCTCOUNT) = LINESPRINTED
       CALL STORELINE1
       FORMDATA(LINESPRINTED)=CCRBODY(CCRBODYLOOP,CCRSHORTNAME)
       CALL STORELINE1
      END
     ELSE
      DO
       FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+ " Acct XXXXXXX"+
       SEGMENT(CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT),8,10)+" "+
       CCRBODY(CCRBODYLOOP,CCRSHORTNAME)
       TRANACCTID(TRANACCTCOUNT) = LINESPRINTED
      END
    END
   IF CCRBODY(CCRBODYLOOP,CCRIRSDESCRIPTION)<>"" THEN
    DO
     IF SHORTRECEIPT=TRUE THEN
      DO
       FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
       " "+CCRBODY(CCRBODYLOOP,CCRIRSDESCRIPTION)+" "+
       CCRBODY(CCRBODYLOOP,CCRID)
       CALL STORELINE1
      END
     ELSE
      DO
       FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
               CCRBODY(CCRBODYLOOP,CCRIRSDESCRIPTION)
      END
     END
   ELSE IF CCRBODY(CCRBODYLOOP,CCRPREVBALANCE) <> "" THEN
    DO 
     IF SHORTRECEIPT=TRUE THEN 
      DO
       TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRPREVBALANCE)
       DTEMP="Prev Bal:"
       CALL THERMALTRANSACTION
      END
     ELSE
      DO 
       IF LENGTH(FORMDATA(LINESPRINTED))<34 THEN
        FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
        SEGMENT(BLANK,1,35-LENGTH(FORMDATA(LINESPRINTED)))+
        "  Prev Bal: " 
       ELSE
        FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+" Prev Bal:"
      TEMPAMOUNT = CCRBODY(CCRBODYLOOP,CCRPREVBALANCE)
      AMTCOL  = 57
      IF CHARACTERSEARCH(TEMPAMOUNT,"-")>0 THEN
       DO
        IF LENGTH(FORMDATA(LINESPRINTED)+TEMPAMOUNT)>AMTCOL+1 THEN
         AMTCOL  = 70
       END
      ELSE
       DO
        IF LENGTH(FORMDATA(LINESPRINTED)+TEMPAMOUNT)>AMTCOL THEN
         AMTCOL  = 70
       END 
       CALL PRINTSYMFORMAMOUNT
       CALL STORELINE1
      END
    END  
   IF SHORTRECEIPT=FALSE THEN
    CALL STORELINE1
  END
END


PROCEDURE PRINTCCRGL
 IF CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)<>"" THEN
  DO
   IF CCRGLLOOP = 1 THEN
    DO
     FORMDATA(LINESPRINTED)=""
     CALL STORELINE1
    END
   IF SHORTRECEIPT=TRUE THEN 
    DO
     TEMPAMOUNT=CCRGL(CCRGLLOOP,CCRGLAMOUNT)
     IF CHARACTERSEARCH(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),"On Us")>0 OR
        CHARACTERSEARCH(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),"Foreign Cash")>0 THEN
      DTEMP=CCRGL(CCRGLLOOP,CCRGLCOMMENT)
     ELSE
      DTEMP=CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)
     IF USECASHBOX=TRUE AND CASHBOXLOAD=TRUE THEN
      DO
       IF UPPERCASE(SEGMENT(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),1,8))="CASH REC" THEN
        INCOMING=FORMAT("#########.99",MONEY(VALUE(INCOMING))+MONEY(VALUE(TEMPAMOUNT)))
       IF UPPERCASE(SEGMENT(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),1,8))="CASH DIS" THEN
        OUTGOING=FORMAT("#########.99",MONEY(VALUE(OUTGOING))+MONEY(VALUE(TEMPAMOUNT)))

       [CALL CASHBOXALTLOAD] [ ** Uncomment for cashbox ** ]
      END
     IF VALUE(TEMPAMOUNT)=0 AND
        CHARACTERSEARCH(UPPERCASE(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)),
                                                       "WIRE CREATION")>0 THEN
       TEMPAMOUNT = ""
     CALL THERMALTRANSACTION
     IF CCRGL(CCRGLLOOP,CCRGLPAYEE)<>"" THEN
      DO
       FORMDATA(LINESPRINTED)=CCRGL(CCRGLLOOP,CCRGLPAYEE)
       CALL STORELINE1
      END
     IF CCRGL(CCRGLLOOP,CCRGLREFERENCE)<>"" THEN
      DO
       TEMPAMOUNT=CCRGL(CCRGLLOOP,CCRGLREFERENCE)
       DTEMP="Ref number:"
       CALL THERMALTRANSACTION
      END 
    END
   ELSE
    DO
     TEMPAMOUNT = CCRGL(CCRGLLOOP,CCRGLAMOUNT)
     IF USECASHBOX=TRUE AND CASHBOXLOAD=TRUE THEN
      DO
       IF UPPERCASE(SEGMENT(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),1,8))="CASH REC" THEN
        INCOMING=FORMAT("#########.99",MONEY(VALUE(INCOMING))+MONEY(VALUE(TEMPAMOUNT)))
       IF UPPERCASE(SEGMENT(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),1,8))="CASH DIS" THEN
        OUTGOING=FORMAT("#########.99",MONEY(VALUE(OUTGOING))+MONEY(VALUE(TEMPAMOUNT)))

       [CALL CASHBOXALTLOAD] [ ** Uncomment for cashbox ** ]
      END
     IF CHARACTERSEARCH(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),"On Us")>0 OR
        CHARACTERSEARCH(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION),"Foreign Cash")>0 THEN
      FORMDATA(LINESPRINTED)= CCRGL(CCRGLLOOP,CCRGLCOMMENT)
     ELSE
      FORMDATA(LINESPRINTED)= CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)
     IF CCRGL(CCRGLLOOP,CCRGLREFERENCE)<>"" THEN 
      DO
       [This code was added to simulate the System standard receipts, 
        adding one space between the Account Code and the Check
        number when the check number is 8 digit long:
        Before : 0034567890 After: 00 34567890 ]
       IF SEGMENT(CCRGL(CCRGLLOOP,CCRGLREFERENCE),3,3)<>" " THEN
        CCRGL(CCRGLLOOP,CCRGLREFERENCE)=
                     SEGMENT(CCRGL(CCRGLLOOP,CCRGLREFERENCE),1,2)+" "+
                     SEGMENT(CCRGL(CCRGLLOOP,CCRGLREFERENCE),3,
                     LENGTH(CCRGL(CCRGLLOOP,CCRGLREFERENCE)))
       FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+" "+
                            CCRGL(CCRGLLOOP,CCRGLREFERENCE)
      END
     IF CCRGL(CCRGLLOOP,CCRGLPAYEE)<>"" THEN
      FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+" "+
                               CCRGL(CCRGLLOOP,CCRGLPAYEE)
     TEMPAMOUNT = CCRGL(CCRGLLOOP,CCRGLAMOUNT)
     IF VALUE(TEMPAMOUNT)=0 AND
        CHARACTERSEARCH(UPPERCASE(CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)),
                                                 "WIRE CREATION")>0 THEN
      TEMPAMOUNT = ""
      AMTCOL = 70
     CALL PRINTSYMFORMAMOUNT
     CALL STORELINE1
    END
  END
END


PROCEDURE PRINTCCRGLACCT
  TEMPAMOUNT = CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTAMOUNT)
  IF SHORTRECEIPT=TRUE THEN 
   DO
    IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)<>"" THEN
     DO
      IF UPPERCASE(SEGMENT(CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC),1,13))="BOND CLEARING" THEN
       DO
        DTEMP=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)+" "+
              CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREFERENCE)
        CALL THERMALTRANSACTION
       END
      ELSE IF [ENABLEEXTRNLLOAN=FALSE AND]
              CHARACTERSEARCH(UPPERCASE(CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)),
              "MISCELLANEOUS PAYMENT CLEARING")>0 AND
              CHARACTERSEARCH(UPPERCASE(CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)),"PRINBAL")>0 THEN
       DO
        DTEMP="FICS Payment"
        CALL THERMALTRANSACTION
        IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)<>"" THEN
         DO
          FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)
          CALL STORELINE1
         END
       END 
      ELSE 
       DO
        DTEMP=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)
        CALL THERMALTRANSACTION
       END
     END 
    ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)<>"" THEN
     DO
      IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)<>"" THEN
       DO
        FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)
        CALL STORELINE1
       END
      FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)
      HOWLONG=LENGTH(TEMPAMOUNT)
      IF SEGMENT(TEMPAMOUNT,HOWLONG,HOWLONG)="-" THEN
       FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+"   -"+
       SEGMENT(TEMPAMOUNT,1,HOWLONG-1)
      ELSE
       FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+" "+TEMPAMOUNT
      CALL STORELINE1
     END
    ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)<>"" THEN
     DO
      DTEMP="Check hold to be released "+CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)+" "
      CALL THERMALTRANSACTION
      IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)<>"" THEN
       FORMDATA(LINESPRINTED)=" due to "+
       CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)
      CALL STORELINE1
     END
   END
  IF SHORTRECEIPT=FALSE THEN
   DO 
    AMTCOL  = 70
    IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)<>"" THEN
     DO
      IF UPPERCASE(SEGMENT(CCRGLACCT(CCRGLACCTLOOP,
             CCRGLACCTCODEDESC),1,13))="BOND CLEARING" THEN
       DO
        FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)+" "+
                               CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREFERENCE)
        CALL PRINTSYMFORMAMOUNT
        CALL STORELINE1
       END    
      ELSE IF [ENABLEEXTRNLLOAN=FALSE AND]
              CHARACTERSEARCH(UPPERCASE(CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)),
              "MISCELLANEOUS PAYMENT CLEARING")>0 AND
              CHARACTERSEARCH(UPPERCASE(CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)),"PRINBAL")>0 THEN
       DO
        FORMDATA(LINESPRINTED)="FICS Payment"  
        CALL PRINTSYMFORMAMOUNT
        CALL STORELINE1
        IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)<>"" THEN
         DO
          FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)
          CALL STORELINE1
         END
       END
      ELSE
       DO
        FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)
        CALL PRINTSYMFORMAMOUNT
        CALL STORELINE1
       END 
     END
    ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)<>"" THEN
     DO
      IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)<>"" THEN
       DO
        FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)
        CALL STORELINE1
       END
      FORMDATA(LINESPRINTED)=CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1
     END
    ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)<>"" THEN
     DO
      FORMDATA(LINESPRINTED)= "Check hold to be released "+
                          CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1 
      IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)<>"" THEN
       DO
        FORMDATA(LINESPRINTED)=" due to "+
        CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)
        CALL STORELINE1
       END
     END
   END  
END

PROCEDURE PRINTCCREXTRNLLOAN                  
    OMITBLANKLINES=FALSE
    FORMDATA(LINESPRINTED)=""
    CALL STORELINE1
    OMITBLANKLINES=TRUE
    FORMDATA(LINESPRINTED)=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRPROCESSORNAME)+
                           " "+CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRLOANNUMBER)
    CALL STORELINE1
    FORMDATA(LINESPRINTED)=" Payment Amount Applied To:"
    CALL STORELINE1
    IF SHORTRECEIPT=TRUE THEN 
     DO
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRCURTAILMENTAMOUNT)
      DTEMP="  Curtailment"
      CALL THERMALTRANSACTION
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRPRINCIPLEAMOUNT)
      DTEMP="  Principal"
      CALL THERMALTRANSACTION
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCREXTRNLLOANINT)
      DTEMP="  Interest"
      CALL THERMALTRANSACTION
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRTAXANDINSURANCE)
      DTEMP="  Taxes & Insurance"
      CALL THERMALTRANSACTION
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRLATECHARGE)
      DTEMP="  Current Late Charge"
      CALL THERMALTRANSACTION
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRTOTALPAYMENT)
      DTEMP=" Total Payment"
      CALL THERMALTRANSACTION
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRPRINBALANCE)
      DTEMP=" Principal Balance"
      CALL THERMALTRANSACTION
      TEMPAMOUNT=CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRINTERESTYTD)
      DTEMP=" Interest Paid YTD"
      CALL THERMALTRANSACTION
     END
    ELSE
     DO
      AMTCOL  = 50
      FORMDATA(LINESPRINTED)="  Curtailment "  
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRCURTAILMENTAMOUNT)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1 
      FORMDATA(LINESPRINTED)="  Principal"
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRPRINCIPLEAMOUNT)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1 
      FORMDATA(LINESPRINTED)="  Interest"
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCREXTRNLLOANINT)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1
      FORMDATA(LINESPRINTED)="  Taxes & Insurance"
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRTAXANDINSURANCE)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1
      FORMDATA(LINESPRINTED)="  Current Late Charge"
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRLATECHARGE)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1
      FORMDATA(LINESPRINTED)=" Total Payment"
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRTOTALPAYMENT)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1
      FORMDATA(LINESPRINTED)=" Principal Balance"
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRPRINBALANCE)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1
      FORMDATA(LINESPRINTED)=" Interest Paid YTD"
      TEMPAMOUNT = CCREXTRNLLOAN(CCREXTRNLLOANLOOP,CCRINTERESTYTD)
      CALL PRINTSYMFORMAMOUNT
      CALL STORELINE1
     END
END

PROCEDURE PRINTLOANSEGMENT
  FORMDATA(LINESPRINTED)=" Seg "+CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGID)
  FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+" "+
           CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGDESC)
  IF SHORTRECEIPT=TRUE THEN
   DO
    CALL STORELINE1
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGPREVBALANCE)
    DTEMP="  Prev Bal:"
    CALL THERMALTRANSACTION
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGBALCHANGE)
    DTEMP="  Principal:"
    CALL THERMALTRANSACTION
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGINTEREST)
    DTEMP="  Interest:"
    CALL THERMALTRANSACTION
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGBALANCE)
    DTEMP="  New Bal:"
    CALL THERMALTRANSACTION
   END
  ELSE
   DO
    FORMDATA(LINESPRINTED)="  "+FORMDATA(LINESPRINTED)
    FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
             SEGMENT(BLANK,1,35-LENGTH(FORMDATA(LINESPRINTED)))+"  Prev Bal: "
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGPREVBALANCE)
    IF LENGTH(FORMDATA(LINESPRINTED))>50 THEN
      AMTCOL = 71
    ELSE
      AMTCOL = 58
    CALL PRINTSYMFORMAMOUNT
    CALL STORELINE1
    AMTCOL = 24
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGBALCHANGE)
    CALL PRINTSYMFORMAMOUNT
    AMTCOL = 34
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGINTEREST)
    CALL PRINTSYMFORMAMOUNT
    AMTCOL = 58
    TEMPAMOUNT = CCRLOANSEGMENT(CCRLOANSEGLOOP,CCRLOANSEGBALANCE)
    CALL PRINTSYMFORMAMOUNT
    CALL STORELINE1
   END
END

PROCEDURE SENDDATAPOSITIONS
 FORMSAVED  = FALSE
 FORMSBMT   = FALSE
 IDCOUNT    = 1
 TRAILERPOS = LINEHEIGHT*(LINESPRINTED-(PAGE-1)*RECEIPTLINES)

 WHILE IDCOUNT<LINESPRINTED
  DO
   IF SHORTRECEIPT=TRUE THEN
    YPOS = MODULOS*LINEHEIGHT+NUMBER(LINEHEIGHT/2)
   ELSE
    YPOS = MODULOS*47+25
   IF CHARACTERSEARCH(LOGOIDS,FORMAT("999",IDCOUNT))>0 THEN
    DO
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CT=11,E=0,"+IMGLOGOPROP+","+"IFNA="+IMGLOGONAME)
    END
   ELSE
    DO
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+LINEPROP+
                                    FORMAT("Y=99999",YPOS))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,VA="+
                                                  FORMDATA(IDCOUNT))
    END
   IDCOUNT = IDCOUNT + 1
   DIV = NUMBER(IDCOUNT/RECEIPTLINES)
   MODULOS = IDCOUNT-DIV*RECEIPTLINES
  END

 CALL SENDSIGNATURELINE
 CALL SENDIDLINES
 CALL SENDMESSAGEBOX
 CALL SENDADDRESSLINES
[CALL SENDTHERMALCASHBOX]
[CALL SENDLASERCASHBOX] 

 IF PRINTOPTION=2 THEN
  DO
   WINDOWSSEND(332,"MBNA=NOTHING")
   CALL GETRESPONSE
   IF RECEIPTLINES=66 THEN
     WINDOWSSEND(332,"SAVE=PRINT,COPIES="+NUMCOPIES)
   ELSE
     WINDOWSSEND(332,"SAVE=DEFAULTPRINT,PRINTERNAME=RECEIPT,COPIES="+NUMCOPIES)
   CALL GETRESPONSE
   WINDOWSSEND(332,"CLOSE,MIN")
   CLOSEFORM=TRUE
  END
 ELSE IF USECASHBOX=TRUE THEN
  WINDOWSSEND(332,"SP="+FORMAT("9",PAGE)+",SF="+
                        FORMAT("999",DATAPOSID(0,0))) 
 ELSE IF CCRTRAILER(CCRIDLINE) = "Y" THEN
   WINDOWSSEND(332,"SP=1,SF="+FORMAT("999",CNTRLID(0)))
 ELSE IF CCRTRAILER(CCRSIGNATURETYPE)<>"" THEN
  DO
   WINDOWSSEND(332,"SP=1,SF="+FORMAT("999",VALUE(SIGID)))
   SIGFIELD=SIGID
   CALL SIGPADDISPLAY
  END
 ELSE IF CCRTRAILER(CCRSIGNATURETYPE)="" AND 
             CCRTRAILER(CCRIDLINE) = "N" THEN
   WINDOWSSEND(332,"SP=1,SF=1")
END


PROCEDURE SENDSIGNATURELINE
 IF CCRTRAILER(CCRSIGNATURETYPE)<>"" AND CCRTRAILEREXIST=TRUE THEN
  DO
   IF SHORTRECEIPT=TRUE THEN 
    DO
     IDCOUNT=IDCOUNT+1
     SIGID=FORMAT("999",IDCOUNT)
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",E=1,CON=1,CT=04,SUL=1,W=600,"+
     "H=200,BURN=1,FW=18,"+"X=100,Y="+FORMAT("99999",TRAILERPOS+90)) 
     IDCOUNT=IDCOUNT+1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=1,E=0,FW=050,"+
     "BURN=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*3))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,VA="+
     "    _________________________")
     IDCOUNT=IDCOUNT+1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,"+
     "FW=060,BURN=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*5))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,VA="+
     "        "+CCRTRAILER(CCRSIGNATURETYPE))
     TRAILERPOS=TRAILERPOS+LINEHEIGHT*7
    END
   ELSE
    DO
     IDCOUNT=IDCOUNT+1
     SIGID=FORMAT("999",IDCOUNT)
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",E=1,CON=1,CT=04,SUL=1,W=600,"+
     "H=200,BURN=1,FW=18,"+"X=530,Y="+
     FORMAT("99999",TRAILERPOS-LINEHEIGHT+125))
     IDCOUNT=IDCOUNT+1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,"+
     "FW=100,BURN=1,X=10,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*2))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,"+
     "VA="+CCRTRAILER(CCRSIGNATURETYPE)+
     " ___________________________________________")
     TRAILERPOS=TRAILERPOS+LINEHEIGHT*4
    END
  END
END


PROCEDURE SENDIDLINES
 IF CCRTRAILER(CCRIDLINE) = "Y" THEN
  DO
   IF SHORTRECEIPT=TRUE THEN 
    DO
     IDCOUNT = IDCOUNT + 1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,"+
     "FW=050,BURN=1,X=5,Y="+FORMAT("99999",TRAILERPOS))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,"+
     "VA=ID Source:")
     IDCOUNT = IDCOUNT + 1
     CNTRLID(0)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT))
     IDCOUNT = IDCOUNT + 1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,"+
     "FW=050,BURN=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,VA="+
     "   Drv Lic ______________________")
     IDCOUNT = IDCOUNT + 1
     CNTRLID(1)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,FW=20,"+
     "BURN=1,X=250,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT))
     IDCOUNT = IDCOUNT + 1
     CNTRLID(2)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*2))
     IDCOUNT = IDCOUNT + 1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,"+
     "FW=050,BURN=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*2))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,VA="+
     "   SigCard")
     IDCOUNT = IDCOUNT + 1
     CNTRLID(3)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*3))
     IDCOUNT = IDCOUNT + 1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,"+
     "FW=050,BURN=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*3))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,VA="+
     "   Known")
     IDCOUNT = IDCOUNT + 1
     CNTRLID(4)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*4))
     IDCOUNT = IDCOUNT + 1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,"+
     "FW=050,BURN=1,X=5,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*4))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,VA="+
     "   Other   ______________________")
     IDCOUNT = IDCOUNT + 1
     CNTRLID(5)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,FW=20,"+
     "BURN=1,X=250,Y="+FORMAT("99999",TRAILERPOS+LINEHEIGHT*4))
     TRAILERPOS=TRAILERPOS+LINEHEIGHT*6
    END
   ELSE
    DO
     IDCOUNT = IDCOUNT + 1
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,FW=80,"+
     "BURN=1,X=10,Y="+FORMAT("99999",TRAILERPOS))
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1,"+
     "VA=ID Source:    Driver Lic ____________   SigCard    Known"+
     "    Other ____________")
     IDCOUNT = IDCOUNT + 1
     CNTRLID(0)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=375,Y="+FORMAT("99999",TRAILERPOS+12))
     IDCOUNT = IDCOUNT + 1
     CNTRLID(1)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,FW=12,"+
     "BURN=1,X=755,Y="+FORMAT("99999",TRAILERPOS))
     IDCOUNT = IDCOUNT + 1
     CNTRLID(2)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=1155,Y="+FORMAT("99999",TRAILERPOS+12))
     IDCOUNT = IDCOUNT + 1
     CNTRLID(3)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=1480,Y="+FORMAT("99999",TRAILERPOS+12))
     IDCOUNT = IDCOUNT + 1
     CNTRLID(4)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=02,B=1,W=35,"+
     "BURN=1,H=35,FW=1,X=1750,Y="+FORMAT("99999",TRAILERPOS+12))
     IDCOUNT = IDCOUNT + 1
     CNTRLID(5)=IDCOUNT
     WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,FW=12,"+
     "BURN=1,X=1975,Y="+FORMAT("99999",TRAILERPOS))
     TRAILERPOS=TRAILERPOS+LINEHEIGHT*2
    END
  
   WINDOWSSEND(332,"MX="+FORMAT("999",CNTRLID(4))+"+"+
                         FORMAT("999",CNTRLID(3))+"+"+
                         FORMAT("999",CNTRLID(2))+"+"+
                         FORMAT("999",CNTRLID(0))) 
  END
END


PROCEDURE SENDMESSAGEBOX
 IF SHORTRECEIPT=TRUE THEN
  DO
   EDITXANDY=EDITXANDY+",Y="+FORMAT("99999",TRAILERPOS)
   IDCOUNT=IDCOUNT+1
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+EDITBOX+EDITXANDY)
   TEMPLINE2=CCRMESSAGE(1)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+TEMPLINE1)
   TEMPLINE2=CCRMESSAGE(2)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+" "+TEMPLINE1)
   TEMPLINE2=CCRMESSAGE(3)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+" "+TEMPLINE1) 
   TEMPLINE2=CCRMESSAGE(4)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+" "+TEMPLINE1)
   TRAILERPOS=TRAILERPOS+LINEHEIGHT*MLNUMBER   
  END
 ELSE
  DO
   IF USECASHBOX=TRUE THEN
     EDITXANDY=EDITXANDY+",Y=2260,SP="+FORMAT("99",PAGE)
   ELSE
     EDITXANDY=EDITXANDY+",Y="+FORMAT("99999",TRAILERPOS+200)
   IDCOUNT=IDCOUNT+1
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+EDITBOX+EDITXANDY)
   TEMPLINE2=CCRMESSAGE(1)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+TEMPLINE1)
   TEMPLINE2=CCRMESSAGE(2)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+" "+TEMPLINE1)
   TEMPLINE2=CCRMESSAGE(3)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+" "+TEMPLINE1) 
   TEMPLINE2=CCRMESSAGE(4)
   CALL SLASHIT
   WINDOWSSEND(332,"ID="+FORMAT("999",IDCOUNT)+",LN="+" "+TEMPLINE1)
  END
END


PROCEDURE SENDADDRESSLINES
 IF CCRADDRESSEXIST = TRUE THEN
  DO
   FOR LINELOOP = 1 TO CCRADDRESSLINES
    DO
     IF CCRADDRESS(LINELOOP)<>"" THEN
      DO
       IF SHORTRECEIPT=TRUE THEN
        DO
         IDCOUNT = IDCOUNT + 1
         WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,FW="+FORMAT("999",THRMLLINEWDTH)+","+
                         "BURN=1,X=5,Y="+FORMAT("9999",TRAILERPOS+LINEHEIGHT*LINELOOP)) 
         WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1"+
                         ",VA="+SEGMENT(CCRADDRESS(LINELOOP),1,THRMLLINEWDTH))
        END
       ELSE
        DO
         IDCOUNT = IDCOUNT + 1
         WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",CON=1,CT=01,E=0,FW="+FORMAT("999",THRMLLINEWDTH)+","+
                         "BURN=1,X=310,Y="+FORMAT("9999",2750+LINEHEIGHT*LINELOOP)+",SP="
                         +FORMAT("99",PAGE))
         WINDOWSSEND(332,FORMAT("ID=999",IDCOUNT)+",DC=BLACK,FB=1"+
                         ",VA="+SEGMENT(CCRADDRESS(LINELOOP),1,THRMLLINEWDTH))
        END
      END
    END
   TRAILERPOS=TRAILERPOS+LINEHEIGHT*CCRADDRESSLINES+2
  END
END


PROCEDURE SETUPACTIONCODES
 ACTIONCODELIST = "ADNPRWC"
 ACTIONDESC(0) = ""
 ACTIONDESC(1) = "Advance from "
 ACTIONDESC(2) = "Deposit to "
 ACTIONDESC(3) = "New Loan "
 ACTIONDESC(4) = "Payment to "
 ACTIONDESC(5) = "Refinance of "
 ACTIONDESC(6) = "Withdraw from "
 ACTIONDESC(7) = "Comment for "
END


PROCEDURE SETUPCHECKESCAPECODES
 CHECKESCAPECODES(0)="&f1y4X" 
 CHECKESCAPECODES(1)="&f2y4X" 
 CHECKESCAPECODES(2)="&f3y4X"
 CHECKESCAPECODES(3)="&f4y4X"
 CHECKESCAPECODES(4)="&f5y4X"
 CHECKESCAPECODES(5)="&f6y4X"
 CHECKESCAPECODES(6)="&f7y4X"                      
 CHECKESCAPECODES(7)="&f8y4X"
END


PROCEDURE FORMATTIME
  IF HOUR(TEMPTIME)<13 THEN
    TEMPCHAR=FORMAT("#9",HOUR(TEMPTIME))
  ELSE
    TEMPCHAR=FORMAT("#9",HOUR(TEMPTIME)-12)

  TEMPCHAR=TEMPCHAR+":"+FORMAT("99",MINUTE(TEMPTIME))

  IF HOUR(TEMPTIME)<12 THEN
    TEMPCHAR=TEMPCHAR+"am"
  ELSE
    TEMPCHAR=TEMPCHAR+"pm"
END
 

PROCEDURE LINEBREAK
 IF CCRBODYCOUNT>0 THEN
  FORMDATA(LINESPRINTED)=SEGMENT("____________________________________"+
    "____________________________________________",1,THRMLLINEWDTH)
 ELSE
   FORMDATA(LINESPRINTED)=SEGMENT("___________________________________"+
    "____________________________________________",1,THRMLLINEWDTH)
 
 CALL STORELINE1
 OMITBLANKLINES = FALSE
 FORMDATA(LINESPRINTED)=""
 CALL STORELINE1
 OMITBLANKLINES = TRUE
END


PROCEDURE MESSAGE
 IF CCRFILETYPE = "RECEIPT" THEN
  DO
   IF CCRMESSAGEEXIST = TRUE THEN
    DO
     HOWLONG2=0
     FOR LINELOOP = 1 TO CCRMESSAGELOOP  
      DO
       HOWLONG2=HOWLONG2+LENGTH(CCRMESSAGE(LINELOOP))
      END 
     EDITBOX=EDITBOX+FORMAT("999",HOWLONG2)
     HOWLONG=NUMBER(HOWLONG2/MLWIDTH)
     IF SHORTRECEIPT=TRUE THEN
      MLNUMBER=HOWLONG+4
     ELSE
      MLNUMBER=HOWLONG+2
    END
  END
 ELSE
  DO
   LINESPRINTED = RECEIPTLINES*PAGE-1
   FORMDATA(LINESPRINTED)=FORMDATA(LINESPRINTED)+
              "Purpose of check: "+CCRCHECK(CCRPURPOSE)+
              SEGMENT(BLANK,1,46-LENGTH(CCRCHECK(CCRPURPOSE)))+
              CCRCHECK(CCRCHECKACCTCODE)+" "+CCRCHECK(CCRDOCUMENTNUMBER)
  END
END


PROCEDURE SETUPENDLINES
 IF USECASHBOX = TRUE THEN
  CBLINES     = 13
 ELSE
  CBLINES     = 0
 ADDRESSLINES = 6
 ENDLINES = CBLINES + ADDRESSLINES
END


PROCEDURE SETPAGE  [<<-- ADDED]
 TRAILERLINES=0
 IF SHORTRECEIPT=FALSE THEN
  DO
 IF CCRTRAILER(CCRIDLINE)="Y" THEN
  TRAILERLINES=TRAILERLINES+2
 IF CCRTRAILER(CCRSIGNATURETYPE)<>"" THEN
  TRAILERLINES=TRAILERLINES+4
  END
 IF SHORTRECEIPT=TRUE THEN
  DO
   IF CCRTRAILER(CCRIDLINE)="Y" THEN
    TRAILERLINES=TRAILERLINES+6
   IF CCRTRAILER(CCRSIGNATURETYPE)<>"" THEN
    TRAILERLINES=TRAILERLINES+7
  END


 TRAILERLINES=TRAILERLINES+MLNUMBER

 IF LINESPRINTED+TRAILERLINES > (RECEIPTLINES*PAGE)-ENDLINES THEN
  DO
   PGC(PAGE)=LINESPRINTED
   LINESPRINTED = RECEIPTLINES*PAGE+1
   PAGE = PAGE + 1
   CALL PRINTCCRHEADER
  END
END


PROCEDURE THERMALTRANSACTION
  IF SEGMENT(TEMPAMOUNT,LENGTH(TEMPAMOUNT),LENGTH(TEMPAMOUNT))="-" THEN
   TEMPAMOUNT="-"+SEGMENT(TEMPAMOUNT,1,LENGTH(TEMPAMOUNT)-1) 
  FORMDATA(LINESPRINTED)=DTEMP+SEGMENT(BLANK,1,THRMLLINEWDTH-
  (LENGTH(DTEMP)+LENGTH(TEMPAMOUNT)))+TEMPAMOUNT
  CALL STORELINE1
END


PROCEDURE PROCESSFIELDEVENT2
 IF CHARACTERSEARCH(SIGID,FORMAT("999",VALUE(RESPSF)))>0 THEN
  DO
   SIGFIELD=FORMAT("999",VALUE(RESPSF))
   CALL SIGPADDISPLAY
  END

 IF CCRTRAILER(CCRIDLINE)="Y"  THEN
  DO
   IF VALUE(RESPID)=CNTRLID(0) THEN
    DO
     WINDOWSSEND(332,FORMAT("ID=9999",CNTRLID(1))+",E="+RESPVA+",V="+
                                                               RESPVA)
     IF FORMDATA(VALUE(RESPID))="1" THEN
      WINDOWSSEND(332,FORMAT("ID=9999",CNTRLID(5))+",E=0,V=0")
    END
     
   IF VALUE(RESPID)=CNTRLID(4) THEN
    DO
     WINDOWSSEND(332,FORMAT("ID=9999",CNTRLID(5))+",E="+RESPVA+",V="+
     RESPVA) 
      IF FORMDATA(VALUE(RESPID))="1" THEN
      WINDOWSSEND(332,FORMAT("ID=9999",CNTRLID(1))+",E=0,V=0")
    END
   IF VALUE(RESPID)=CNTRLID(2) OR VALUE(RESPID)=CNTRLID(3) THEN
    DO
     IF FORMDATA(VALUE(RESPID))="1" THEN
      DO
       WINDOWSSEND(332,FORMAT("ID=9999",CNTRLID(1))+",E=0,V=0")
       WINDOWSSEND(332,FORMAT("ID=9999",CNTRLID(5))+",E=0,V=0")
      END
    END
  END

 IF USECASHBOX = TRUE THEN
  DO  
   IF VALUE(RESPID)>=DATAPOSID(0,0) AND 
     VALUE(RESPID)<=DATAPOSID(1,8) THEN
    DO
     [CALL CALCCASHBOX] 
    END
  END

 IF CHARACTERSEARCH(SIGID,FORMAT("999",VALUE(RESPSF)))=0 THEN
  DO
   IF RESPSP<>"" THEN
    WINDOWSSEND(332,"SP="+RESPSP)
   IF RESPSF<>"" THEN
    WINDOWSSEND(332,"SF="+RESPSF)
  END         
END


PROCEDURE STORELINE1
 TEMPLINE = FORMDATA(LINESPRINTED)
 IF LENGTH(FORMDATA(LINESPRINTED))>COLCOUNT AND COLCOUNT > 0 THEN
  DO
   FINDBRAKE = COLCOUNT
   WHILE SEGMENT(FORMDATA(LINESPRINTED),FINDBRAKE,FINDBRAKE)<>" " AND 
                                                     FINDBRAKE>1
    DO
     FINDBRAKE = FINDBRAKE -1
    END
   IF FINDBRAKE = 1 THEN
     FINDBRAKE = COLCOUNT
   TEMPLINE = SEGMENT(FORMDATA(LINESPRINTED),FINDBRAKE+1,
    LENGTH(FORMDATA(LINESPRINTED)))
   FORMDATA(LINESPRINTED) = SEGMENT(FORMDATA(LINESPRINTED),1,FINDBRAKE)
   CALL SLASHSPECIALCHARACTERS1
   LINESPRINTED = LINESPRINTED +1
   TEMPLINE = SEGMENT(BLANK,1,THRMLLINEWDTH-LENGTH(TEMPLINE))+TEMPLINE
  END
 IF TEMPLINE<>"" OR OMITBLANKLINES = FALSE THEN
  DO
   FORMDATA(LINESPRINTED) = TEMPLINE   
   CALL SLASHSPECIALCHARACTERS1
   LINESPRINTED = LINESPRINTED + 1
   IF OKIRECEIPTS = FALSE THEN
    DO
     IF LINESPRINTED >=(PAGE*RECEIPTLINES) THEN
      DO
       LINESPRINTED = (PAGE * RECEIPTLINES)+1
       IF SHORTTHERMAL=TRUE THEN
        PGC(PAGE)=LINESPRINTED-1
       ELSE
        PGC(PAGE)=LINESPRINTED
       PAGE = PAGE + 1
       CALL PRINTCCRHEADER
      END
    END
   ELSE
    CALL OKINEWLINE
  END
END


PROCEDURE SLASHSPECIALCHARACTERS1
 IF OKIRECEIPTS = FALSE THEN
  DO
   TEMPLINE1 = ""
   FOR CHARCNT = 1 to LENGTH(FORMDATA(LINESPRINTED))
    DO
     CHECKCHAR = SEGMENT(FORMDATA(LINESPRINTED),CHARCNT,CHARCNT)
     IF CHECKCHAR = "," OR CHECKCHAR = "\" THEN
       TEMPLINE1 = TEMPLINE1 + "\"
     TEMPLINE1 = TEMPLINE1 + CHECKCHAR
    END
   FORMDATA(LINESPRINTED) = TEMPLINE1
  END
END


PROCEDURE SLASHIT
 TEMPLINE1 = ""
 FOR CHARCNT = 1 to LENGTH(TEMPLINE2)
  DO
   CHECKCHAR = SEGMENT(TEMPLINE2,CHARCNT,CHARCNT)
   IF CHECKCHAR = "," OR CHECKCHAR = "\" THEN
     TEMPLINE1 = TEMPLINE1 + "\"
   TEMPLINE1 = TEMPLINE1 + CHECKCHAR
  END
END


PROCEDURE PRINTSHORTTHERMALHEADER
 TEMPAMOUNT = FORMAT("99",PAGE)
 DTEMP="Page:"
 CALL THERMALTRANSACTION   
END  


PROCEDURE REMOVELEADINGZEROS
  FORMLOOP = 1
  WHILE SEGMENT(TEMPCHAR,FORMLOOP,FORMLOOP)="0"
   DO
    FORMLOOP = FORMLOOP + 1
   END
  TEMPCHAR = SEGMENT(TEMPCHAR,FORMLOOP,LENGTH(TEMPCHAR))
END


PROCEDURE GETSSN
 FOR ACCOUNT CCRHEADER(CCRACCOUNT)
  DO
   FOR EACH NAME WITH NAME:TYPE=0
    DO
     SSNHOLDER=NAME:SSN 
    END
  END
END


PROCEDURE REMOVEHYPHENAPOSTROPHE
[Procedure expects variable TEMPLINE]
 REMOVECHAR=""
 IF CHARACTERSEARCH(TEMPLINE,"'")>0 OR 
    CHARACTERSEARCH(TEMPLINE,"-")>0 THEN
  DO
   REMOVECHAR=TEMPLINE
   TEMPLINE=""
   FOR FORMLOOP=1 TO LENGTH(REMOVECHAR)
    DO
     IF SEGMENT(REMOVECHAR,FORMLOOP,FORMLOOP)<>"'" AND
        SEGMENT(REMOVECHAR,FORMLOOP,FORMLOOP)<>"-" THEN
      TEMPLINE=TEMPLINE+SEGMENT(REMOVECHAR,FORMLOOP,FORMLOOP)
    END
  END
END


PROCEDURE SENDTOIMAGING
 IF IMAGINGTYPE = OTG THEN
   CALL SENDTOOTG
 ELSE IF IMAGINGTYPE = SYNERGY THEN
  DO
   WINDOWSSEND(332,"OTGN(0)=DOC TYPE,OTG(0)=RECEIPTS~_INSTITUTION_~_"+
               "CABINET_~_TYPE_~_DOCNAME_~ACCT NO~NAME~DATE~TELLER NO")
   WINDOWSSEND(332,"OTGN(1)=_INSTITUTION_,OTG(1)=00")
   WINDOWSSEND(332,"OTGN(2)=_CABINET_,OTG(2)=RECEIPTS")
   WINDOWSSEND(332,"OTGN(3)=_TYPE_,OTG(3)=RECEIPTS")
   WINDOWSSEND(332,"OTGN(4)=_DOCNAME_,OTG(4)=RECEIPTS")
   TEMPCHAR = CCRHEADER(CCRACCOUNT)
   CALL REMOVELEADINGZEROS
   WINDOWSSEND(332,"OTGN(5)=ACCT NO,OTG(5)="+TEMPCHAR)
   TEMPLINE=OTGNAME
   CALL REMOVEHYPHENAPOSTROPHE
   WINDOWSSEND(332,"OTGN(6)=NAME,OTG(6)="+TEMPLINE)
   WINDOWSSEND(332,"OTGN(7)=DATE,OTG(7)="+FORMAT("99/99/9999",SYSTEMDATE))
   WINDOWSSEND(332,"OTGN(8)=TELLER NO,OTG(8)="+CCRHEADER(CCRTELLER))
  END
 ELSE IF IMAGINGTYPE = SILHOUETTE THEN
  DO
   TEMPCHAR = CCRHEADER(CCRACCOUNT)
   CALL REMOVELEADINGZEROS
   CALL GETSSN
   WINDOWSSEND(332,"OTGN(0)=ID,OTG(0)=1")
   WINDOWSSEND(332,"OTGN(1)=Effective Date,OTG(1)="+
                       FORMAT("99/99/9999",SYSTEMDATE))
   WINDOWSSEND(332,"OTGN(2)=Account,OTG(2)="+TEMPCHAR)
   WINDOWSSEND(332,"OTGN(3)=ID TYPE,OTG(3)=0")
   WINDOWSSEND(332,"OTGN(4)=ID,OTG(4)=00")
   WINDOWSSEND(332,"OTGN(5)=SSN,OTG(5)="+SSNHOLDER)
   WINDOWSSEND(332,"OTGN(6)=Longname,OTG(6)="+SEGMENT(OTGNAME,1,40))
   WINDOWSSEND(332,"OTGN(7)=Shortname,OTG(7)="+SEGMENT(OTGNAME,1,16))
   WINDOWSSEND(332,"OTGN(8)=Teller,OTG(8)="+CCRHEADER(CCRTELLER))
  END
END
 
[*** Laser Checks ***]

PROCEDURE PRINTREC
 FOR FORMLOOP = 1 TO CHECKBLANKLINES
  DO
   NEWLINE
  END

 PRINT  "Check Purpose "+CCRCHECK(CCRPURPOSE)+"  Check# "+
  CCRHEADER(CCRHEADERDOCNUM)+" $"+CCRCHECK(CCRCHECKAMOUNT)
   NEWLINE
   PRINT "Acct XXXXXXX"+SEGMENT(CCRHEADER(CCRACCOUNT),8,10)+"  "+CCRHEADER(CCRNAME)+
         " Effect: "+CCRHEADER(CCREFFECTIVEDATE)+" Post: "+
    CCRHEADER(CCRPOSTDATE)+" Tlr: "+CCRHEADER(CCRTELLER)
   NEWLINE
   IF CCRBODYCOUNT>0 THEN
    DO
     IF CHARACTERSEARCH(CCRBODY(CCRBODYLOOP,CCRIDTYPE),"See ")=0 THEN
      DO
       NEWLINE
       PRINT "ID DUE DATE    PRINCIPAL  INTEREST     "+
             "FEES    NEW BALANCE  TRAN AMOUNT     SEQ"
       NEWLINE
       PRINT "______________________________________________________"+
        "____________________________"
       NEWLINE
      END
    END
 FOR  CCRBODYLOOP = 1 TO CCRBODYCOUNT
  DO
   IF CHARACTERSEARCH(CCRBODY(CCRBODYLOOP,CCRIDTYPE),"See ")>0 THEN
    DO
     PRINT "("+SEGMENT(CCRBODY(CCRBODYLOOP,CCRIDTYPE),1,12)+
      SEGMENT(CCRBODY(CCRBODYLOOP,CCRIDTYPE),30,
      LENGTH(CCRBODY(CCRBODYLOOP,CCRIDTYPE)))+" for reference)"
     COL=82 CCRBODY(CCRBODYLOOP,CCRSEQUENCE)
     NEWLINE
    END
   ELSE
    DO
     IF CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)<>"" THEN
      DO
       PRINT ACTIONDESC(CHARACTERSEARCH(ACTIONCODELIST,
       CCRBODY(CCRBODYLOOP,CCRACTIONCODE)))+" "+
       CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)+" "+
       CCRBODY(CCRBODYLOOP,CCRID)
       NEWLINE
       IF CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)<>"" THEN
        DO
         PRINT "Acct XXXXXXX"+SEGMENT(CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT),8,10)+" "+
               CCRBODY(CCRBODYLOOP,CCRSHORTNAME)+
               " "+CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)
         IF CCRBODY(CCRBODYLOOP,CCRPREVBALANCE) <> "" THEN
           COL=70 " Prev Bal:"+CCRBODY(CCRBODYLOOP,CCRPREVBALANCE)
         NEWLINE
        END
       IF CCRBODY(CCRBODYLOOP,CCRID)<>"" THEN
        DO
         PRINT CCRBODY(CCRBODYLOOP,CCRID)
         IF CCRBODY(CCRBODYLOOP,CCRCOMMENTTYPE)="Y" THEN
          DO
           PRINT CCRBODY(CCRBODYLOOP,CCRCOMMENT)
           NEWLINE
          END
         ELSE
          DO
           PRINT CCRBODY(CCRBODYLOOP,CCRID)+"   "+
                 CCRBODY(CCRBODYLOOP,CCRMATURITYDATE)
           COL = 27 RIGHT CCRBODY(CCRBODYLOOP,CCRBALANCECHANGE)
           COL = 38 RIGHT CCRBODY(CCRBODYLOOP,CCRINTEREST)
           COL = 47 RIGHT CCRBODY(CCRBODYLOOP,CCRFEE)
           COL = 61 RIGHT CCRBODY(CCRBODYLOOP,CCRBALANCE)
           COL = 75 RIGHT CCRBODY(CCRBODYLOOP,CCRAMOUNT)
           COL = 82 CCRBODY(CCRBODYLOOP,CCRSEQUENCE)
           NEWLINE 
           IF CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER)<>"" OR
             CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)<>"" THEN
            DO
             PRINT "Draft number: "+CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER)+
                "  Tracer number: "+CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)
             IF CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)="" THEN
             NEWLINE 
            END
           IF CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)<>"" THEN
            DO
             PRINT "Total Current Year Contributions:"+
             CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)
             NEWLINE
            END
          END
        END
      END
    END
  END
 FOR CCRGLACCTLOOP = 1 TO CCRGLACCTCOUNT
  DO
   IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)<> "" THEN
    PRINT CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)
   ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)<>"" THEN
    DO
     IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)<>"" THEN
      DO
       NEWLINE
       PRINT CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)
      END
     PRINT CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)
    END
   ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)<>"" THEN
    DO
     NEWLINE
     PRINT "Check hold to be released "+ 
           CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)
     IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)<>"" THEN
     PRINT " due to "+CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)
     NEWLINE
    END
  END
 FOR CCRGLLOOP= 1 TO CCRGLCOUNT
  DO
   IF CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)<>"" THEN
    DO
     PRINT  CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)+" "+
            CCRGL(CCRGLLOOP,CCRGLPAYEE)
     HOWLONG=LENGTH(CCRGL(CCRGLLOOP,CCRGLAMOUNT))
     IF SEGMENT(CCRGL(CCRGLLOOP,CCRGLAMOUNT),HOWLONG,HOWLONG)="-" THEN
      COL=75 RIGHT CCRGL(CCRGLLOOP,CCRGLAMOUNT)
     ELSE 
      COL=76 RIGHT CCRGL(CCRGLLOOP,CCRGLAMOUNT)
     NEWLINE
    END
  END
END


PROCEDURE PRINTCHECK
 IF BLANKPAYEEFIELDS=TRUE THEN 
  CALL CLEARPAYEE

 HPFONT(34,12)
 HPXPOS(1960)
 HPYPOS(2290)
 PRINT CCRCHECK(CCRCHECKACCTCODE)+" "+CCRCHECK(CCRDOCUMENTNUMBER)
 HPFONT(33,10)
 HPXPOS(1500)
 HPYPOS(2430)
 PRINT CCRCHECK(CCRCHECKEFFECTIVE)
 HPFONT(34,12)
 HPXPOS(1950)
 HPYPOS(2430)
 PRINT "$"+CCRCHECK(CCRCHECKAMOUNT)
 HPXPOS(0075)
 HPYPOS(2680)
 HPFONT(33,10)
 SPELLMONEY =MONEY(VALUE(CCRCHECK(CCRCHECKAMOUNT)))
 CALL MONEYSPELL
 IF LENGTH(SPELLCHAR) > 80 THEN
  HPFONT(33,10)
 ELSE
  HPFONT(33,12)
 PRINT "** " + SPELLCHAR + " DOLLARS **"
 HPFONT(33,10)
 HPXPOS(0260)
 HPYPOS(2800)
 PRINT CCRCHECK(CCRPAYEELINE1)
 HPXPOS(0260)
 HPYPOS(2845)
 PRINT CCRCHECK(CCRPAYEELINE2)
 HPXPOS(0260)
 HPYPOS(2890)
 PRINT CCRCHECK(CCRPAYEELINE3)
 HPXPOS(0260)
 HPYPOS(2935)
 PRINT CCRCHECK(CCRPAYEELINE4)
 HPXPOS(0260)
 HPYPOS(2980)
 PRINT CCRCHECK(CCRPAYEELINE5)

[*** ORIGINAL CODE BELOW ***]
 IF CHECKMODE=LASERCHECKS THEN
  DO
   CALL GETDIGIT
   CALL PRINTMICR
   HPESC CHECKESCAPECODES(TYPEOFCHECK)
   LINESPRINTED = 0 
   SUPPRESSNEWLINE
  END
END


PROCEDURE GETDIGIT 
 ACCOUNTNUMBER = ""
 CHECKDIGIT    = 0
 DIGITTOTAL    = 0
 TENDIGIT      = 0
 NEXTTEN       = 0
 ODD           = FALSE

 A=13
 WHILE (A>=1)
  DO
   IF ODD=FALSE THEN
    DIGIT=2*VALUE(SEGMENT(ACCOUNTNUMBER,A,A))
   ELSE
    DIGIT=VALUE(SEGMENT(ACCOUNTNUMBER,A,A))
   IF DIGIT=10 THEN
    DIGIT=1
   ELSE IF DIGIT=12 THEN
    DIGIT=3
   ELSE IF DIGIT=14 THEN
    DIGIT=5
   ELSE IF DIGIT=16 THEN
    DIGIT=7
   ELSE IF DIGIT=18 THEN
    DIGIT=9
   DIGITTOTAL=DIGITTOTAL+DIGIT
   IF ODD=FALSE THEN
    ODD=TRUE
   ELSE
    ODD=FALSE
   A=A-1
  END

 TENDIGIT=NUMBER(DIGITTOTAL/10)
 NEXTTEN=(TENDIGIT+1)*10
 CHECKDIGIT=NEXTTEN-DIGITTOTAL
 IF CHECKDIGIT=10 THEN
  CHECKDIGIT=0
END


PROCEDURE PRINTMICR
 CALL SETUPMICRLINE
 HPESC MICRFONT
 SUPPRESSNEWLINE
 FOR A=58 TO 1 BY -1
  DO
   HPESC FORMAT("&a9999h7350V",MICRPOS)  [standard alignment]
   PRINT MICRARRAY(A)
   SUPPRESSNEWLINE
   MICRPOS=MICRPOS+90 [aligned to MICR template positioning]
  END
END


PROCEDURE SETUPMICRLINE
 MICRARRAY(58)=" "
 MICRARRAY(57)=" "
 MICRARRAY(56)=":"
 MICRARRAY(55)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),1,1)
 MICRARRAY(54)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),2,2)
 MICRARRAY(53)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),3,3)
 MICRARRAY(52)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),4,4)
 MICRARRAY(51)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),5,5)
 MICRARRAY(50)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),6,6)
 MICRARRAY(49)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),7,7)
 MICRARRAY(48)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),8,8)
 MICRARRAY(47)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),9,9)
 MICRARRAY(46)=SEGMENT(CCRCHECK(CCRDOCUMENTNUMBER),10,10)
 MICRARRAY(45)=":"
 MICRARRAY(44)=" "
 MICRARRAY(43)=";"
 MICRARRAY(42)=" " [ROUTING NUMBER]
 MICRARRAY(41)=" " [ROUTING NUMBER]
 MICRARRAY(40)=" " [ROUTING NUMBER]
 MICRARRAY(39)=" " [ROUTING NUMBER]
 MICRARRAY(38)=" " [ROUTING NUMBER]
 MICRARRAY(37)=" " [ROUTING NUMBER]
 MICRARRAY(36)=" " [ROUTING NUMBER]
 MICRARRAY(35)=" " [ROUTING NUMBER]
 MICRARRAY(34)=" " [ROUTING NUMBER]
 MICRARRAY(33)=";"
 MICRARRAY(32)=" "
 MICRARRAY(31)=" "
 MICRARRAY(30)=" "
 MICRARRAY(29)=" "
 MICRARRAY(28)=" " [CU GL ACCOUNT]
 MICRARRAY(27)=" " [CU GL ACCOUNT]
 MICRARRAY(26)=" " [CU GL ACCOUNT]               
 MICRARRAY(25)=" " [CU GL ACCOUNT]
 MICRARRAY(24)=" " [CU GL ACCOUNT]
 MICRARRAY(23)=" " [CU GL ACCOUNT]
 MICRARRAY(22)=" " [CU GL ACCOUNT]
 MICRARRAY(21)=" " [CU GL ACCOUNT]
 MICRARRAY(20)=" " [CU GL ACCOUNT]
 MICRARRAY(19)=" " [CU GL ACCOUNT]
 MICRARRAY(18)=":"
 MICRARRAY(17)=" "
 MICRARRAY(16)=" "
 MICRARRAY(15)=" "
 MICRARRAY(14)=" "
[*** THESE SHOULD BE BLANK FOR PROCESSOR TO ADD DATA ***]
 MICRARRAY(13)=" "
 MICRARRAY(12)=" "
 MICRARRAY(11)=" "
 MICRARRAY(10)=" "
 MICRARRAY(09)=" "
 MICRARRAY(08)=" "
 MICRARRAY(07)=" "
 MICRARRAY(06)=" "
 MICRARRAY(05)=" "
 MICRARRAY(04)=" "
 MICRARRAY(03)=" "
 MICRARRAY(02)=" "
 MICRARRAY(01)=" "
END

[*** Wycom Checks ***]

PROCEDURE PRINTSTANDARDWYCOMCHECK
[This format is provided by Wycom and should only be changed at the request of  
 a Wycom representative.  Any issues with the checks should first be directed 
 to a Wycom representative. Wycom will contact Symitar if changes to the format 
 are needed.   

 PF Settings:
 Lines Per Page: 44 lines
 Blank Lines   :  5 lines] 

 TEMPTIME=SYSACTUALTIME
 CALL FORMATTIME
 TIMEDATA=TEMPCHAR

 [line#] [Be very careful.  These fields go to Wycom and CoWWW also]
         CN=GETDATANUMBER(GETCONSOLEBRANCH,SYSCONSOLENUM)
         IF CN=3 OR CN=8 OR BRANCHNAME(CN)="Unknown" THEN CN=4
 [01]    COL = 001 WYCOMCHECKID+FORMAT("9999",GETDATANUMBER(GETCONSOLEBRANCH,SYSCONSOLENUM))+"]"
         PRINT  " Branch: "+BRANCHNAME(CN)
         NEWLINE
 [02]    COL = 001 "Acct: "+CCRHEADER(CCRACCOUNT)+" Teller: "+CCRHEADER(CCRTELLER)+
                   " Date: "+CCRHEADER(CCRPOSTDATE)+" Time: "+TIMEDATA
         NEWLINE
 [03]    COL = 001 "------------------------------------------------------------------"
         NEWLINE
 [04]    COL = 001 "See receipt for reference"
         NEWLINE
 [05]    COL = 001 "------------------------------------------------------------------"
         NEWLINE
 [06]    COL = 001 "Check Number: "+CCRCHECK(CCRCHECKACCTCODE)+" "+CCRCHECK(CCRDOCUMENTNUMBER)
         NEWLINE
 [07]    COL = 001 "Purpose : "+CCRCHECK(CCRPURPOSE)
         NEWLINE
 [08]    COL = 001 "Amount : $"+CCRCHECK(CCRCHECKAMOUNT)
         NEWLINE
 [09]    COL = 001 "Pay to : "+CCRCHECK(CCRPAYEELINE1)
         NEWLINE
 [10]    COL = 010  CCRCHECK(CCRPAYEELINE2)
         NEWLINE
 [11]    COL = 010  CCRCHECK(CCRPAYEELINE3)
         NEWLINE
 [12]    COL = 010  CCRCHECK(CCRPAYEELINE4)
         NEWLINE
 [13]    COL = 010  CCRCHECK(CCRPAYEELINE5)
         NEWLINE
 [14]    NEWLINE
 [15]    NEWLINE
 [16]    NEWLINE 
 [17]    NEWLINE 
 [18]    NEWLINE 
 [19]    NEWLINE 
 [20]    NEWLINE 
 [21]    NEWLINE
 [22]    COL = 064 CCRCHECK(CCRCHECKACCTCODE)+" "+CCRCHECK(CCRDOCUMENTNUMBER)
         NEWLINE
 [23]    NEWLINE 
 [24]    COL = 063  CCRCHECK(CCRCHECKEFFECTIVE)
         NEWLINE
 [25]    NEWLINE 
 [26]    COL = 064  "$"+CCRCHECK(CCRCHECKAMOUNT)
         NEWLINE
 [27]    NEWLINE
 [28]    NEWLINE
 [29]    NEWLINE
 [30]    NEWLINE
 [31]    NEWLINE
 [32]    NEWLINE
 [33]    COL = 001 CCRCHECK(CCRPAYEELINE1)
         NEWLINE
 [34]    COL = 001 CCRCHECK(CCRPAYEELINE2)
         NEWLINE
 [35]    COL = 001 CCRCHECK(CCRPAYEELINE3)
         NEWLINE
 [36]    COL = 001 CCRCHECK(CCRPAYEELINE4)
         NEWLINE
 [37]    COL = 001 CCRCHECK(CCRPAYEELINE5)
         NEWLINE
 [38]    NEWLINE 
 [39]    NEWLINE
 [40]    NEWLINE 
 [41]    NEWLINE 
 [42]    NEWLINE
 [43]    NEWLINE
 [44]    PRINT CTRLCHR(12)
END

[*** Dot-Matrix Checks ***]

PROCEDURE OKIPRINTRECEIPT
 CCRGLLOOP    = 1
 CCRCHECKLOOP = 1
 CCRBODYLOOP  = 1
 LINESPRINTED = 0
 FOR FORMLOOP = 1 TO CHECKBLANKLINES
  DO
   CALL OKINEXTLINE
  END
 IF CCRHEADER(CCRHEADERDOCNUM)<>"" THEN
  DO
   COL = 50 "Document Number:"
   COL = 78 RIGHT CCRCHECK(CCRCHECKACCTCODE)+" "+CCRHEADER(CCRHEADERDOCNUM)
  END
 CALL OKINEXTLINE
 CALL OKINEXTLINE
 COL = 1 "Account XXXXXXX"+SEGMENT(CCRHEADER(CCRACCOUNT),8,10)
 COL = 20 CCRHEADER(CCRNAME)
 COL = 38 "Effect: "+CCRHEADER(CCREFFECTIVEDATE)
 COL = 55 "Post: "+CCRHEADER(CCRPOSTDATE)
 COL = 70 "Tlr: "+CCRHEADER(CCRTELLER)
 CALL OKINEXTLINE
 CALL OKINEXTLINE
 IF CCRBODYCOUNT>0 THEN
  DO
   IF CHARACTERSEARCH(CCRBODY(CCRBODYLOOP,CCRIDTYPE),"See ")=0 THEN
     PRINT "ID DUE DATE    PRINCIPAL  INTEREST     FEES       BALANCE  TRAN AMOUNT SEQ"
   CALL OKINEXTLINE
   PRINT "------------------------------------------------------------------------------"
   CALL OKINEXTLINE
  END
 FOR  CCRBODYLOOP = 1 TO CCRBODYCOUNT
  DO
   IF CHARACTERSEARCH(CCRBODY(CCRBODYLOOP,CCRIDTYPE),"See ")>0 THEN
    DO
     PRINT "("+SEGMENT(CCRBODY(CCRBODYLOOP,CCRIDTYPE),1,12)
     PRINT SEGMENT(CCRBODY(CCRBODYLOOP,CCRIDTYPE),30,
                   LENGTH(CCRBODY(CCRBODYLOOP,CCRIDTYPE)))
     PRINT " for reference)"
     CALL OKINEXTLINE
     PRINT "------------------------------------------------------------------------------"
     CALL OKINEXTLINE
    END
   ELSE
    DO
     IF CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)<>"" THEN
      DO
       PRINT ACTIONDESC(CHARACTERSEARCH(ACTIONCODELIST,
       CCRBODY(CCRBODYLOOP,CCRACTIONCODE)))+" "+
       CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)
       IF CCRBODY(CCRBODYLOOP,CCRPREVBALANCE)<>"" THEN
        DO
         COL = 43 RIGHT "Prev Bal:"
         COL = 57 RIGHT CCRBODY(CCRBODYLOOP,CCRPREVBALANCE)
        END
       CALL OKINEXTLINE
       IF CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT)<>"" THEN
        DO
         PRINT "Acct XXXXXXX"+SEGMENT(CCRBODY(CCRBODYLOOP,CCRTRANACCOUNT),8,10)+" "+
               CCRBODY(CCRBODYLOOP,CCRSHORTNAME)+
               " "+CCRBODY(CCRBODYLOOP,CCRDESCRIPTION)
         CALL OKINEXTLINE
        END
       IF CCRBODY(CCRBODYLOOP,CCRID)<>"" THEN
        DO
         IF CCRBODY(CCRBODYLOOP,CCRCOMMENTTYPE)="Y" THEN
          DO
           PRINT CCRBODY(CCRBODYLOOP,CCRID)+"  "+
                 CCRBODY(CCRBODYLOOP,CCRCOMMENT)
           CALL OKINEXTLINE
          END
         ELSE
          DO
           PRINT CCRBODY(CCRBODYLOOP,CCRID)+"   "+
                 CCRBODY(CCRBODYLOOP,CCRMATURITYDATE)
           COL = 24 RIGHT CCRBODY(CCRBODYLOOP,CCRBALANCECHANGE)
           COL = 34 RIGHT CCRBODY(CCRBODYLOOP,CCRINTEREST)
           COL = 43 RIGHT CCRBODY(CCRBODYLOOP,CCRFEE)
           COL = 57 RIGHT CCRBODY(CCRBODYLOOP,CCRBALANCE)
           COL = 70 RIGHT CCRBODY(CCRBODYLOOP,CCRAMOUNT)
           COL = 73 CCRBODY(CCRBODYLOOP,CCRSEQUENCE)
           CALL OKINEXTLINE 
           IF CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER)<>"" OR
             CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)<>"" THEN
            DO
             PRINT "Draft number: "+CCRBODY(CCRBODYLOOP,CCRDRAFTNUMBER)+
                "  Tracer number: "+CCRBODY(CCRBODYLOOP,CCRTRACERNUMBER)
             IF CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)="" THEN
             CALL OKINEXTLINE 
            END
           IF CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)<>"" THEN
            DO
             PRINT "Total Current Year Contributions:"+
             CCRBODY(CCRBODYLOOP,CCRCONTTHISYEAR)
             CALL OKINEXTLINE
            END
          END
        END
      END
     PRINT "------------------------------------------------------------------------------"
     CALL OKINEXTLINE
     CALL OKINEXTLINE
    END
  END
 FOR CCRGLACCTLOOP = 1 TO CCRGLACCTCOUNT
  DO
   IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)<> "" THEN
    PRINT CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCODEDESC)
   ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)<>"" THEN
    DO
     IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)<>"" THEN
      DO
       CALL OKINEXTLINE
       PRINT CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTCOMMENT)
      END
     PRINT CCRGLACCT(CCRGLACCTLOOP,CCRGLACCOUNT)
    END
   ELSE IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)<>"" THEN
    DO
     CALL OKINEXTLINE
     PRINT "Check hold to be released "+ 
           CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTRELEASE)
     IF CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)<>"" THEN
     PRINT " due to "+CCRGLACCT(CCRGLACCTLOOP,CCRGLACCTREASON)
     CALL OKINEXTLINE
    END
  END
 FOR CCRGLLOOP= 1 TO CCRGLCOUNT
  DO
   IF CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)<>"" THEN
    DO
     PRINT  CCRGL(CCRGLLOOP,CCRGLDESCRIPTION)+" "+
            CCRGL(CCRGLLOOP,CCRGLPAYEE)
     HOWLONG=LENGTH(CCRGL(CCRGLLOOP,CCRGLAMOUNT))
     IF SEGMENT(CCRGL(CCRGLLOOP,CCRGLAMOUNT),HOWLONG,HOWLONG)="-" THEN
      COL=71 RIGHT CCRGL(CCRGLLOOP,CCRGLAMOUNT)
     ELSE 
      COL=70 RIGHT CCRGL(CCRGLLOOP,CCRGLAMOUNT)
     CALL OKINEXTLINE
    END
  END
 IF CCRTRAILER(CCRSIGNATURETYPE)<>"" AND CCRTRAILEREXIST=TRUE THEN
  DO
   CALL OKINEXTLINE
   PRINT CCRTRAILER(CCRSIGNATURETYPE)+
         ": ________________________________________"
   CALL OKINEXTLINE
  END
 IF CCRTRAILER(CCRIDLINE)="Y" AND CCRTRAILEREXIST=TRUE THEN
  DO
   CALL OKINEXTLINE
   PRINT "ID Source: [ ]Driver Lic_____________ [ ]SigCard [ ]Known [ ]Other____________"
   CALL OKINEXTLINE
  END
 FOR LINELOOP = LINESPRINTED TO CHECKLINES-1
  DO
   CALL OKINEXTLINE
  END
 IF CCRCHECK(CCRPURPOSE)<>"" THEN
  DO
   PRINT "Purpose of check: "+CCRCHECK(CCRPURPOSE)
   CALL OKINEXTLINE
  END
 ELSE
  CALL OKINEXTLINE
END


PROCEDURE OKIPRINTCHECK
 FOR FORMLOOP = 1 TO CHECKBLANKLINES
  DO
   NEWLINE
  END
 COL = 64 CCRCHECK(CCRCHECKACCTCODE)+" "+CCRCHECK(CCRDOCUMENTNUMBER)
 NEWLINE
 NEWLINE
 COL = 70 RIGHT CCRCHECK(CCRCHECKEFFECTIVE)
 NEWLINE
 IF LENGTH(CCRCHECK(CCRFORMATTEDAMOUNT))<=61 THEN
  DO
   COL = 4 "** "+CCRCHECK(CCRFORMATTEDAMOUNT)+" **"
   NEWLINE
  END
  ELSE
   DO
    TEMPNUM=61
    WHILE TEMPNUM>0 AND
          SEGMENT(CCRCHECK(CCRFORMATTEDAMOUNT),TEMPNUM,TEMPNUM)<>" "
     DO
      TEMPNUM=TEMPNUM-1
     END
    COL = 4 "** "+SEGMENT(CCRCHECK(CCRFORMATTEDAMOUNT),1,TEMPNUM-1)+" **"
    NEWLINE
    COL = 4 "** "+SEGMENT(CCRCHECK(CCRFORMATTEDAMOUNT),TEMPNUM+1,LENGTH(CCRCHECK(CCRFORMATTEDAMOUNT)))+" **"
   END

 COL = 70 RIGHT "$"+CCRCHECK(CCRCHECKAMOUNT)
 NEWLINE
 NEWLINE
 NEWLINE
 NEWLINE
 NEWLINE
 NEWLINE
 NEWLINE
 NEWLINE
 COL = 10 CCRCHECK(CCRPAYEELINE1)
 NEWLINE
 COL = 10 CCRCHECK(CCRPAYEELINE2)
 NEWLINE
 COL = 10 CCRCHECK(CCRPAYEELINE3)
 NEWLINE
 COL = 10 CCRCHECK(CCRPAYEELINE4)
 NEWLINE
 COL = 10 CCRCHECK(CCRPAYEELINE5)
 NEWLINE
END

PROCEDURE ACCTMASKING
 FOR TEMPNUM = 1 TO PAGECOUNT
  DO
   FORMDATA(ACCTID(TEMPNUM))=SEGMENT(FORMDATA(ACCTID(TEMPNUM)),1,5)+REPEATCHR("X",MASKXNUMBER)+
                             SEGMENT(FORMDATA(ACCTID(TEMPNUM)),
                             6+MASKXNUMBER,LENGTH(FORMDATA(ACCTID(TEMPNUM))))
   WINDOWSSEND(332,"ID="+FORMAT("999",ACCTID(TEMPNUM))+",VA="+FORMDATA(ACCTID(TEMPNUM)))
  END

 FOR TEMPNUM = 1 TO TRANACCTCOUNT
  DO
   IF SHORTRECEIPT = TRUE THEN
     FORMDATA(TRANACCTID(TEMPNUM))=SEGMENT(FORMDATA(TRANACCTID(TEMPNUM)),1,5)+REPEATCHR("X",MASKXNUMBER)+
                                   SEGMENT(FORMDATA(TRANACCTID(TEMPNUM)),6+
                                   MASKXNUMBER,LENGTH(FORMDATA(TRANACCTID(TEMPNUM))))
   ELSE
    DO
     FORMDATA(TRANACCTID(TEMPNUM))=SEGMENT(FORMDATA(TRANACCTID(TEMPNUM)),1,
                                   CHARACTERSEARCH(FORMDATA(TRANACCTID(TEMPNUM)),"Acct")+4)+
                                   REPEATCHR("X",MASKXNUMBER)+SEGMENT(FORMDATA(TRANACCTID(TEMPNUM)),
                                   CHARACTERSEARCH(FORMDATA(TRANACCTID(TEMPNUM)),"Acct")+5+MASKXNUMBER,
                                   LENGTH(FORMDATA(TRANACCTID(TEMPNUM))))
    END
   WINDOWSSEND(332,"ID="+FORMAT("999",TRANACCTID(TEMPNUM))+",VA="+FORMDATA(TRANACCTID(TEMPNUM)))
  END
END

PROCEDURE PRINTSEQUENCEREF
 CCRSEQTEXT = "(See Sequence #"+CCRSEQREF(1)
 IF CCRSEQREFCOUNT>1 THEN
  CCRSEQTEXT=CCRSEQTEXT+","
 FOR CCRSEQREFLOOP = 2 TO CCRSEQREFCOUNT
  DO
   IF LENGTH(CCRSEQTEXT+"#"+CCRSEQREF(CCRSEQREFLOOP))+1>=THRMLLINEWDTH THEN
    DO
     FORMDATA(LINESPRINTED)=CCRSEQTEXT
     CALL STORELINE1
     CCRSEQTEXT="#"+CCRSEQREF(CCRSEQREFLOOP)
    END
   ELSE
    CCRSEQTEXT=CCRSEQTEXT+"#"+CCRSEQREF(CCRSEQREFLOOP) 
   IF CCRSEQREFLOOP<CCRSEQREFCOUNT THEN
    CCRSEQTEXT=CCRSEQTEXT+","
  END
 IF LENGTH(CCRSEQTEXT+" for reference)")>THRMLLINEWDTH THEN
  DO
   FORMDATA(LINESPRINTED)=CCRSEQTEXT
   CALL STORELINE1
   FORMDATA(LINESPRINTED)="for reference)"
   CALL STORELINE1
  END
 ELSE
  DO
   FORMDATA(LINESPRINTED)=CCRSEQTEXT+" for reference)"
   CALL STORELINE1
  END

 OMITBLANKLINES = FALSE
 FORMDATA(LINESPRINTED)=""
 CALL STORELINE1
 OMITBLANKLINES = TRUE
END


PROCEDURE PRINTSAVECLOSE
 CALL CLEARSIGPAD
 IF IMAGINGTYPE>0 THEN 
  DO
   IF MASKACCOUNT = 2 THEN
     CALL ACCTMASKING
   IF CHARACTERSEARCH(OTGNAME,"'")>0 OR 
      CHARACTERSEARCH(OTGNAME,"-")>0 THEN
    DO
     TEMPCHAR = OTGNAME
     OTGNAME  = ""
     FOR A = 1 TO LENGTH(TEMPCHAR)
      DO
       IF SEGMENT(TEMPCHAR,A,A)<>"'" AND
          SEGMENT(TEMPCHAR,A,A)<>"-" THEN
        DO
         OTGNAME = OTGNAME+SEGMENT(TEMPCHAR,A,A)
        END
      END
    END

   IF MASKACCOUNT = 1 THEN
    DO
     TEMPACCOUNT = CCRHEADER(CCRACCOUNT)
     CCRHEADER(CCRACCOUNT) = UNMASKEDACCOUNT
     CALL SENDTOIMAGING
     CCRHEADER(CCRACCOUNT) = TEMPACCOUNT
    END
   ELSE
    CALL SENDTOIMAGING

   TEMPCHAR=""
   FILEOPEN("LETTER","OPTICAL.OVERFLOW","READ",TEMPNUM,TEMPCHAR)
   IF TEMPCHAR="" THEN
    RESPSAV="-2"
   ELSE
    DO
     WINDOWSSEND(332,"SAVE=SAVE")         
     CALL GETRESPONSE
    END
   FILECLOSE(TEMPNUM,TEMPCHAR)

   IF CHARACTERSEARCH(RESPSAV,"-")>0 THEN
    DO
     IF RESPSAV="-8" THEN
      DO
       WINDOWSSEND(332,"MBOK=You cannot send to optical. "+
                        "Your SymForm registration needs to be updated. "+
                        "Please contact Symitar")
      END
     ELSE
      DO
       IF RESPSAV="-9" THEN
        DO
         WINDOWSSEND(332,"SAVE=SAVE,SAVEMODE=1,SAVEPATH="+NWPATH1)
         CALL GETRESPONSE
        END
       ELSE
        DO
         WINDOWSSEND(332,"SAVE=SAVE,SAVEMODE=1,SAVEPATH="+NWPATH1)
         CALL GETRESPONSE
         IF CHARACTERSEARCH(RESPSAV,"-")=0 THEN
           WINDOWSSEND(332,"MBOK=Image saved to Network drive "+NWPATH1+".")
        END
       IF CHARACTERSEARCH(RESPSAV,"-")>0 THEN
        DO
         WINDOWSSEND(332,"MBOK=Image was not saved to Network path "+NWPATH1+
                               ". Trying "+NWPATH2+" drive.")
         WINDOWSSEND(332,"SAVE=SAVE,SAVEMODE=1,SAVEPATH="+NWPATH2)
         CALL GETRESPONSE
         IF CHARACTERSEARCH(RESPSAV,"-")>0 THEN
          DO
           WINDOWSSEND(332,"MBOK=Image could not be saved. Printing 2 copies.")
           NUMCOPIES="2"
          END
         ELSE
          WINDOWSSEND(332,"MBOK=Image saved to your "+NWPATH2+" drive.")
        END
      END
    END 
  END

 IF MASKACCOUNT = 3 THEN
   CALL ACCTMASKING
 IF RECEIPTLINES=66 THEN
   WINDOWSSEND(332,"SAVE=PRINT,COPIES="+NUMCOPIES)
 ELSE
   WINDOWSSEND(332,"SAVE=PRINT,PRINTERNAME=RECEIPT,COPIES="+NUMCOPIES)
 CALL GETRESPONSE
   
 IF RESPSAV="-6" or RESPSAV="-7" THEN
  DO
   WINDOWSSEND(332,"SAVE=PRINT,COPIES="+NUMCOPIES)
   CALL GETRESPONSE
  END
 
 IF CHARACTERSEARCH(RESPSAV,"-")=0 THEN
  DO            
    WINDOWSSEND(332,"CLOSE,MIN") 
    CLOSEFORM = TRUE
  END
END

PROCEDURE OKINEXTLINE
 LINESPRINTED = LINESPRINTED + 1
 IF LINESPRINTED <= (RECEIPTLINES-BLANKLINES)*MYOKIPAGECOUNT THEN
  NEWLINE
 ELSE
  DO
   MYOKIPAGECOUNT = MYOKIPAGECOUNT + 1
   NEWPAGE
   [PRINT REPEATCHR("*",40) NEWLINE]
[   FOR TEMPNUM = MYHEADERSTART TO MYHEADEREND-1
    DO
     PRINT FORMDATA(TEMPNUM)
     NEWLINE
     LINESPRINTED = LINESPRINTED + 1
    END]
  END
END

PROCEDURE OKINEWLINE
 IF LINESPRINTED <= (RECEIPTLINES-BLANKLINES)*MYOKIPAGECOUNT THEN
  NEWLINE
 ELSE
  DO
   MYOKIPAGECOUNT = MYOKIPAGECOUNT + 1
   FOR TEMPNUM = MYHEADERSTART TO MYHEADEREND-1
    DO
     FORMDATA(LINESPRINTED)=FORMDATA(TEMPNUM)
     LINESPRINTED = LINESPRINTED + 1
    END
  END
END

PROCEDURE SETUPLASER
 RECEIPTNAME     = "LASER.BLANK."
 SHORTRECEIPT    = FALSE
 IF RECEIPTLINES=65 THEN NUMCOPIES="2"
 RECEIPTLINES    = 66
 LINEHEIGHT      = 47
 COLCOUNT        = 80
 THRMLLINEWDTH   = 79
 FONTSIZE        = 12
 OMITBLANKLINES  = FALSE
 IF USECASHBOX=TRUE THEN
  DO
   ADDLINES      = 88
   MLWIDTH       = 40
   EDITBOX       = ",CT=09,W=1200,H=480,E=0,FB=1,FW="
   [CALL SETUPLASERCASHBOX]
  END
 ELSE
  DO
   ADDLINES      = 10
   MLWIDTH       = 79
   EDITBOX       = ",CT=09,W=2400,H=240,E=0,FB=1,FW="
  END
 EDITXANDY       =",X=10"
 IMGLOGONAME    = "RECEIPTLOGO.228.jpg"
 IMGLOGOPROP    = "W=1000,H=250,X=600,Y=10"
END

PROCEDURE FILLBRANCHNAMES
 FILEOPEN("LETTER","BRANCH.LIST","READ",ISFNUMBER,ISFERROR) [12/06/10 Get branch names from letter file so as they change you don not need to recompile all programs]
 IF ISFERROR<>" " THEN PRINT "Error in Opening BRANCH.LIST File - "+ISFERROR
 WHILE ISFERROR=""
  DO
   FILEREADLINE(ISFNUMBER,ISFLINE,ISFERROR)
   IF ISFERROR="" THEN
    DO
     IF SEGMENT(ISFLINE,2,11)="BRANCHNAME" THEN BRANCHNAME(VALUE(SEGMENT(ISFLINE,14,15)))=SEGMENT(ISFLINE,18,LENGTH(ISFLINE))
    END
  END
 FILECLOSE(ISFNUMBER,ISFERROR) 
END

PROCEDURE LAUNCHCLOSEFORM
 FILEOPEN("LETTER","CLOSE.REASONS","READ",ACLNUMBER,ACLERROR)
 WHILELIMIT=1000000
 WHILE ACLERROR=""
  DO
   FILEREADLINE(ACLNUMBER,ACLLINE,ACLERROR)
   IF ACLERROR="" THEN
    DO
     ACLACCOUNT=SEGMENT(ACLLINE,1,10)
    END
  END
 FILECLOSE(ACLNUMBER,ACLERROR)  
 POPUPMESSAGE(0,"ACLACCOUNT: "+ACLACCOUNT) 
 POPUPMESSAGE(0,"FULLACCOUNTNUMBER: "+FULLACCOUNTNUMBER) 
 IF ACLACCOUNT = FULLACCOUNTNUMBER THEN
  DO
   POPUPMESSAGE(0,"EXECUTE CLOSED.ACCOUNT")
   INITSUBROUTINE(SUBERRORTEXT)
   EXECUTE("CLOSED.ACCOUNT",SUBERRORTEXT)
   IF SUBERRORTEXT<>"" THEN
    DO
     POPUPMESSAGE(2,"SUBERRORTEXT: "+SUBERRORTEXT)
    END
  END
END

#INCLUDE "RD.SPELL.PRO"
#INCLUDE "RD.TELLERTRAN.CUSTOMFORMS.PRO"
#INCLUDE "TELLERTRAN.SIGPAD.PRO"
[RD.CTR.TELLERTRAN.PRO]
#INCLUDE "RD.ECTR.VERIFYPERSON.PRO"
[# INCLUDE "TELLERTRAN.CASHBOX.PRO"]

