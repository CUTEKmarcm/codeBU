[NEGATIVE.QUEUE.V3]

[ This report lists negative share accounts and breaks down the negative amount into the following catagories; 
  ACH, ACH FEE, ATM, ATM FEE, CK, CK FEE, and OTHER based on the transactions that caused the negative balance.]
  
[ 05/16/07 BDW = Added file I/O to sort and subtotal the accounts by negative date range 0-15, 16-30, 31-44, 45+, grand totals, $300+]
[ 05/17/07 BDW = Changed $300+ to Overdrawn more than ODT+$50 and sorted sections in days negative order.]
[ 06/07/07 GH  = Added outputswitch for 45 day report.]
[ 06/11/07 GH  = Split out Returned Checks and Fees from "Other" column.]

[ TODO:#015 = MODIFICATION FEBRUARY 2023 :: CUTEK-Marc Middleton ::  CERTS 32598
We're changing the time frame in which we charge off negative accounts from 45 days to 60 days. 
We need these changes to be made to the current Negative Share Queue Notices and the "Negative Share Queue - 45 Day" report. 

The report will now need to be called "Negative Share Queue - 60 Day" and reflect only accounts that have been negative 60 days or more. 

The Negative Share Queue is currently broken up into accounts that are 0-15 days negative, 16-30 days negative, and 31-44 days negative. 
We need that shifted to now show 30-44 days negative and 45-59 days negative.

When generating the notices that get mailed to members, we type NEGATIVETNOTICE in the specfile and are prompted to choose 
 either the 15 day or 30 day notice. We need that script changed to show 30 days and 45 days. 
]

TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.OUTPUT.DEF"
 ERRORTEXT=CHARACTER
 OUTPUTCHANNEL1=NUMBER
 NEGBAL=MONEY  [ The balance after the transaction.]
 NEGAMT=MONEY  [ The amount of the transaction that is negative.]
 NEGTOT=MONEY ARRAY(6)
 ACHAMT=MONEY
 ACHAMTTOT=MONEY ARRAY(6)
 ACHFEE=MONEY
 ACHFEETOT=MONEY ARRAY(6)
 ATMAMT=MONEY
 ATMAMTTOT=MONEY ARRAY(6)
 ATMFEE=MONEY
 ATMFEETOT=MONEY ARRAY(6)
 CHKAMT=MONEY
 CHKAMTTOT=MONEY ARRAY(6)
 CHKFEE=MONEY
 CHKFEETOT=MONEY ARRAY(6)
 OTHER=MONEY
 OTHERTOT=MONEY ARRAY(6)
 RETCKAMT=MONEY
 RETCKAMTTOT=MONEY ARRAY(6)
 RETCKFEE=MONEY
 RETCKFEETOT=MONEY ARRAY(6)
 RETCKCOMM=CHARACTER
 ICOUNT=NUMBER ARRAY(6)
 NEGDATE=DATE
 NEGDAYS=NUMBER
 ACCTNO=CHARACTER
 BEACON=NUMBER
 I=NUMBER
 FLINE=CHARACTER
 FERR=CHARACTER
 FN15=NUMBER
 FN30=NUMBER
 FN44=NUMBER
 FN59=NUMBER
 FN60=NUMBER
 FN300=NUMBER
 ERRLOC=CHARACTER
 HEADING=CHARACTER
 DKAMT=MONEY
END

SETUP
 NEGDAYS=0
 ERRLOC="SETUP Division"
 FILEDELETE("LETTER","NQ15",FERR)
 CALL FILEERROR
 FILEDELETE("LETTER","NQ30",FERR)
 CALL FILEERROR
 FILEDELETE("LETTER","NQ44",FERR)
 CALL FILEERROR
 FILEDELETE("LETTER","NQ59",FERR)
 CALL FILEERROR 
 FILEDELETE("LETTER","NQ60",FERR)
 CALL FILEERROR
 FILEDELETE("LETTER","NQ300",FERR)
 CALL FILEERROR 
 FILEOPEN("LETTER","NQ15","WRITE",FN15,FERR)
 CALL FILEERROR
 FILEOPEN("LETTER","NQ30","WRITE",FN30,FERR)
 CALL FILEERROR
 FILEOPEN("LETTER","NQ44","WRITE",FN44,FERR)
 CALL FILEERROR
 FILEOPEN("LETTER","NQ59","WRITE",FN59,FERR)
 CALL FILEERROR 
 FILEOPEN("LETTER","NQ60","WRITE",FN60,FERR)
 CALL FILEERROR
 FILEOPEN("LETTER","NQ300","WRITE",FN300,FERR)
 CALL FILEERROR
 OUTPUTOPEN(OUTPUTDEVREPORT,0,"Negative Share Queue - 60 Day","",OUTPUTCHANNEL1,ERRORTEXT)
END

SELECT
 ANY SHARE WITH
(SHARE:BALANCE<$0.00 AND
 SHARE:CLOSEDATE='--/--/--' AND
 SHARE:CHARGEOFFDATE='--/--/--')
END

PRINT TITLE="Negative Share Queue"
 BEACON=0
 ACCTNO=ACCOUNT:NUMBER
 WHILE SEGMENT(ACCTNO,1,1)="0"
  DO
   ACCTNO=SEGMENT(ACCTNO,2,LENGTH(ACCTNO))
  END
 FOR EACH TRACKING WITH (TRACKING:TYPE=31)
  DO
   IF NAME:SSN=TRACKING:USERCHAR1 THEN BEACON=TRACKING:USERNUMBER1 
   IF NAME:SSN=TRACKING:USERCHAR2 THEN BEACON=TRACKING:USERNUMBER2 
   IF NAME:SSN=TRACKING:USERCHAR3 THEN BEACON=TRACKING:USERNUMBER3 
   IF NAME:SSN=TRACKING:USERCHAR4 THEN BEACON=TRACKING:USERNUMBER4 
   IF NAME:SSN=TRACKING:USERCHAR5 THEN BEACON=TRACKING:USERNUMBER5 
  END
 FOR EACH SHARE WITH (SHARE:BALANCE<$0.00 AND SHARE:CHARGEOFFDATE='--/--/--' AND SHARE:CLOSEDATE='--/--/--')
  DO
   ACHAMT=$0.00
   ACHFEE=$0.00
   ATMAMT=$0.00
   ATMFEE=$0.00
   CHKAMT=$0.00
   CHKFEE=$0.00
   OTHER=$0.00
   RETCKFEE=$0.00
   RETCKAMT=$0.00
   RETCKCOMM=""
   DKAMT=$0.00
   NEGDATE=SYSTEMDATE
   NEGBAL=SHARE:BALANCE
   RETCKCOMM=""
   FOR EACH SHARE TRANSACTION
    DO
     IF SHARE TRANSACTION:COMMENTCODE=1 THEN
      DO 
       IF (CHARACTERSEARCH(SHARE TRANSACTION:COMMENT,"RET CK")>0 OR
           CHARACTERSEARCH(SHARE TRANSACTION:COMMENT,"RET CHECK")>0 OR
           CHARACTERSEARCH(SHARE TRANSACTION:COMMENT,"RETURN CK")>0 OR
           CHARACTERSEARCH(SHARE TRANSACTION:COMMENT,"RETURN CHECK")>0 OR
           CHARACTERSEARCH(SHARE TRANSACTION:COMMENT,"RETURNED CK")>0 OR
           CHARACTERSEARCH(SHARE TRANSACTION:COMMENT,"RETURNED CHECK")>0) 
       THEN RETCKCOMM=SHARE TRANSACTION:COMMENT
      END
     ELSE IF (SHARE TRANSACTION:COMMENTCODE=0 AND SHARE TRANSACTION:VOIDCODE=0) THEN
      DO
       IF SHARE TRANSACTION:BALANCECHANGE<NEGBAL THEN 
        NEGAMT=NEGBAL
       ELSE 
        NEGAMT=SHARE TRANSACTION:BALANCECHANGE 
       IF SHARE TRANSACTION:POSTDATE<NEGDATE THEN 
        NEGDATE=SHARE TRANSACTION:POSTDATE
       IF SHARE TRANSACTION:SOURCECODE="F" OR CHARACTERSEARCH(RETCKCOMM,"FEE")>0 THEN
        DO
         IF SHARE TRANSACTION:USERNUMBER=0907      THEN 
          ACHFEE=ACHFEE+NEGAMT
         ELSE IF SHARE TRANSACTION:USERNUMBER=0898 THEN 
          ATMFEE=ATMFEE+NEGAMT
         ELSE IF SHARE TRANSACTION:USERNUMBER=0902 THEN 
          CHKFEE=CHKFEE+NEGAMT
         ELSE IF CHARACTERSEARCH(RETCKCOMM,"FEE")>0 THEN 
          RETCKFEE=RETCKFEE+NEGAMT
         ELSE 
          OTHER=OTHER+NEGAMT
        END
       ELSE
        DO
         IF SHARE TRANSACTION:USERNUMBER=0907      THEN 
          ACHAMT=ACHAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:USERNUMBER=0898 THEN 
          ATMAMT=ATMAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:USERNUMBER=0902 THEN 
          CHKAMT=CHKAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:SOURCECODE="E"  THEN 
          ACHAMT=ACHAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:SOURCECODE="A"  THEN 
          ATMAMT=ATMAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:SOURCECODE="B"  THEN 
          ATMAMT=ATMAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:SOURCECODE="G"  THEN 
          ATMAMT=ATMAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:SOURCECODE="O"  THEN 
          ATMAMT=ATMAMT+NEGAMT
         ELSE IF SHARE TRANSACTION:SOURCECODE="D"  THEN 
          CHKAMT=CHKAMT+NEGAMT
         ELSE IF RETCKCOMM<>"" AND CHARACTERSEARCH(RETCKCOMM,"FEE")=0 THEN 
          RETCKAMT=RETCKAMT+NEGAMT
         ELSE IF RETCKCOMM<>"" AND CHARACTERSEARCH(RETCKCOMM,"FEE")>0 THEN 
          RETCKFEE=RETCKFEE+NEGAMT
         ELSE IF SHARE TRANSACTION:ACTIONCODE="D" AND SHARE TRANSACTION:SOURCECODE="K" THEN 
          DKAMT=DKAMT+NEGAMT
         ELSE 
          OTHER=OTHER+NEGAMT
        END
       NEGBAL=NEGBAL-NEGAMT
       RETCKCOMM=""
      END
    END
   UNTIL NEGBAL=$0.00 
   IF RETCKAMT<$0.00 AND DKAMT>$0.00 THEN 
    RETCKAMT=RETCKAMT+DKAMT 
   ELSE 
    OTHER=OTHER+DKAMT
   FLINE=ACCTNO
   IF SHARE:BALANCE<(SHARE:OVERDRAFTTOLERANCE+$50.00)*-1 THEN 
    FLINE=FLINE+"*" 
   ELSE 
    FLINE=FLINE+"-"
   FLINE=FLINE+SHARE:ID
   FLINE=REPEATCHR(" ",(10-LENGTH(FLINE)))+FLINE
   FLINE=FLINE+FORMAT(" ##9",BEACON)+FORMAT("###,##9.99+",SHARE:BALANCE)+
    FORMAT("#,##9.99",SHARE:OVERDRAFTTOLERANCE)+FORMAT("  ##9",SYSTEMDATE-NEGDATE)
   FLINE=FLINE+" "+FORMAT("99/99/99",NEGDATE)+FORMAT("##,##9.99+",ATMAMT)+
    FORMAT("##,##9.99+",CHKAMT)+FORMAT("##,##9.99+",ACHAMT)+FORMAT("#,##9.99+",ATMFEE)+
    FORMAT("#,##9.99+",CHKFEE)+FORMAT("#,##9.99+",ACHFEE)+FORMAT("##,##9.99+",OTHER)
   FLINE=FLINE+" "+FORMAT("##9.99+",RETCKFEE)+FORMAT("##,##9.99+",RETCKAMT)
   ERRLOC="PRINT Division"
   IF SYSTEMDATE-NEGDATE<=15 THEN
    DO
     FILEWRITELINE(FN15,FLINE,FERR)
     I=1
    END
   ELSE IF SYSTEMDATE-NEGDATE<=30 THEN
    DO
     FILEWRITELINE(FN30,FLINE,FERR)
     I=2
    END
   ELSE IF SYSTEMDATE-NEGDATE<=44 THEN
    DO
     FILEWRITELINE(FN44,FLINE,FERR)
     I=3
    END
   ELSE IF SYSTEMDATE-NEGDATE<=59 THEN
    DO
     FILEWRITELINE(FN59,FLINE,FERR)
     I=4
    END    
   ELSE
    DO
     FILEWRITELINE(FN60,FLINE,FERR)
     I=5
    END
   CALL FILEERROR
   NEGTOT(I)=NEGTOT(I)+SHARE:BALANCE
   ACHAMTTOT(I)=ACHAMTTOT(I)+ACHAMT
   ACHFEETOT(I)=ACHFEETOT(I)+ACHFEE
   ATMAMTTOT(I)=ATMAMTTOT(I)+ATMAMT
   ATMFEETOT(I)=ATMFEETOT(I)+ATMFEE
   CHKAMTTOT(I)=CHKAMTTOT(I)+CHKAMT
   CHKFEETOT(I)=CHKFEETOT(I)+CHKFEE
   OTHERTOT(I)=OTHERTOT(I)+OTHER
   RETCKAMTTOT(I)=RETCKAMTTOT(I)+RETCKAMT
   RETCKFEETOT(I)=RETCKFEETOT(I)+RETCKFEE
   ICOUNT(I)=ICOUNT(I)+1
   IF SHARE:BALANCE<(SHARE:OVERDRAFTTOLERANCE+$50.00)*-1 THEN
    DO
     FILEWRITELINE(FN300,FLINE,FERR)
     CALL FILEERROR
     I=6
     NEGTOT(I)=NEGTOT(I)+SHARE:BALANCE
     ACHAMTTOT(I)=ACHAMTTOT(I)+ACHAMT
     ACHFEETOT(I)=ACHFEETOT(I)+ACHFEE
     ATMAMTTOT(I)=ATMAMTTOT(I)+ATMAMT
     ATMFEETOT(I)=ATMFEETOT(I)+ATMFEE
     CHKAMTTOT(I)=CHKAMTTOT(I)+CHKAMT
     CHKFEETOT(I)=CHKFEETOT(I)+CHKFEE
     OTHERTOT(I)=OTHERTOT(I)+OTHER
     RETCKAMTTOT(I)=RETCKAMTTOT(I)+RETCKAMT
     RETCKFEETOT(I)=RETCKFEETOT(I)+RETCKFEE
     ICOUNT(I)=ICOUNT(I)+1
    END
  END
END

TOTAL
 HEADING="More than $50 over Tolerance"
 HEADERS
  PRINT HEADING
  NEWLINE
  IF HEADING<>"Grand Totals" THEN
   DO
    COL=082 "ATM/"
    COL=116 "Return"
    COL=126 "Return"
    NEWLINE
    COL=003 "Account"
    COL=011 "Score"
    COL=018 "Balance"
    COL=027 "ODT Amt"
    COL=035 "Days"
    COL=043 "Since"
   END
  ELSE
   DO
    COL=003 "Days"
    COL=082 "ATM/"
    COL=116 "Return"
    COL=126 "Return"
    NEWLINE
    COL=003 "Negative"
    COL=018 "Balance"
   END
  COL=050 "ATM/VCC"
  COL=062 "Check"
  COL=074 "ACH"
  COL=079 "VCC Fee"
  COL=089 "Ck Fee"
  COL=097 "ACH Fee"
  COL=109 "Other"
  COL=116 "CK Fee"
  COL=126 "CK Amt"
 END
 ERRLOC="TOTAL Division"
 FILECLOSE(FN15,FERR)
 CALL FILEERROR
 FILECLOSE(FN30,FERR)
 CALL FILEERROR
 FILECLOSE(FN44,FERR)
 CALL FILEERROR
 FILECLOSE(FN59,FERR)
 CALL FILEERROR 
 FILECLOSE(FN60,FERR)
 CALL FILEERROR
 FILECLOSE(FN300,FERR)
 CALL FILEERROR
 ERRLOC="Reading File"
 HEADING="More than $50 over Tolerance"
 FOR I=0 TO 99
  DO
   FILEOPEN("LETTER","NQ300","READ",FN300,FERR)
   CALL FILEERROR
   FILEREADLINE(FN300,FLINE,FERR)
   WHILE FERR=""
    DO
     IF VALUE(SEGMENT(FLINE,35,38))=I OR (I=99 AND VALUE(SEGMENT(FLINE,35,38))>I) THEN 
      DO
       PRINT FLINE
       NEWLINE
      END
     FILEREADLINE(FN300,FLINE,FERR)
     CALL FILEERROR
    END
   FILECLOSE(FN300,FERR)
   CALL FILEERROR
  END
 PRINT REPEATCHR("=",132)
 NEWLINE
 PRINT "Subtotals:"
 COL=024 NEGTOT(6)
 COL=029 " in "+FORMAT("##,##9",ICOUNT(6))+" Accts"
 COL=056 ATMAMTTOT(6)
 COL=076 ACHAMTTOT(6)
 COL=094 CHKFEETOT(6)
 COL=113 OTHERTOT(6)
 COL=131 RETCKAMTTOT(6)
 NEWLINE
 COL=066 CHKAMTTOT(6)
 COL=085 ATMFEETOT(6)
 COL=103 ACHFEETOT(6)
 COL=121 RETCKFEETOT(6)
 NEWLINE
 HEADING="Grand Totals"
 NEWPAGE
 FOR I=1 TO 5
  DO
   NEGTOT(0)=NEGTOT(0)+NEGTOT(I)
   ICOUNT(0)=ICOUNT(0)+ICOUNT(I)
   ATMAMTTOT(0)=ATMAMTTOT(0)+ATMAMTTOT(I)
   CHKAMTTOT(0)=CHKAMTTOT(0)+CHKAMTTOT(I)
   ACHAMTTOT(0)=ACHAMTTOT(0)+ACHAMTTOT(I)
   ATMFEETOT(0)=ATMFEETOT(0)+ATMFEETOT(I)
   CHKFEETOT(0)=CHKFEETOT(0)+CHKFEETOT(I)
   ACHFEETOT(0)=ACHFEETOT(0)+ACHFEETOT(I)
   OTHERTOT(0)=OTHERTOT(0)+OTHERTOT(I)
   RETCKAMTTOT(0)=RETCKAMTTOT(0)+RETCKAMTTOT(I)
   RETCKFEETOT(0)=RETCKFEETOT(0)+RETCKFEETOT(I)
   IF I=1 THEN 
    COL=3 "0-15"
   IF I=2 THEN 
    COL=3 "16-30"
   IF I=3 THEN 
    COL=3 "31-44"
   IF I=4 THEN 
    COL=3 "45-59" 
   IF I=5 THEN 
    COL=3 "60+"
   COL=024 NEGTOT(I)
   COL=029 " in "+FORMAT("##,##9",ICOUNT(I))+" Accts"
   COL=056 ATMAMTTOT(I)
   COL=076 ACHAMTTOT(I)
   COL=094 CHKFEETOT(I)
   COL=113 OTHERTOT(I)
   COL=131 RETCKAMTTOT(I)
   NEWLINE
   COL=066 CHKAMTTOT(I)
   COL=085 ATMFEETOT(I)
   COL=103 ACHFEETOT(I)
   COL=121 RETCKFEETOT(I)
   NEWLINE
  END
 PRINT REPEATCHR("=",132)
 NEWLINE
 PRINT "Grand Totals:"
 COL=024 NEGTOT(0)
 COL=029 " in "+FORMAT("##,##9",ICOUNT(0))+" Accts"
 COL=056 ATMAMTTOT(0)
 COL=076 ACHAMTTOT(0)
 COL=094 CHKFEETOT(0)
 COL=113 OTHERTOT(0)
 COL=131 RETCKAMTTOT(0)
 NEWLINE
 COL=066 CHKAMTTOT(0)
 COL=085 ATMFEETOT(0)
 COL=103 ACHFEETOT(0)
 COL=121 RETCKFEETOT(0)
 NEWLINE
 PRINT REPEATCHR("=",132) 
 HEADING="Negative 0-15 Days"
 NEWPAGE 
 FOR I=0 TO 15
  DO
   FILEOPEN("LETTER","NQ15","READ",FN15,FERR)
   CALL FILEERROR
   FILEREADLINE(FN15,FLINE,FERR)
   CALL FILEERROR
   WHILE FERR=""
    DO
     IF (SEGMENT(FLINE,8,8)="*" AND VALUE(SEGMENT(FLINE,35,38))=I) OR
      (VALUE(SEGMENT(FLINE,35,38))=I AND I=15) THEN 
      DO
       PRINT FLINE
       NEWLINE
      END
     FILEREADLINE(FN15,FLINE,FERR)
     CALL FILEERROR
    END
   FILECLOSE(FN15,FERR)
   CALL FILEERROR
  END
 PRINT REPEATCHR("=",132)
 NEWLINE
 PRINT "Subtotals:"
 COL=024 NEGTOT(1)
 COL=029 " in "+FORMAT("##,##9",ICOUNT(1))+" Accts"
 COL=056 ATMAMTTOT(1)
 COL=076 ACHAMTTOT(1)
 COL=094 CHKFEETOT(1)
 COL=113 OTHERTOT(1)
 COL=131 RETCKAMTTOT(1)
 NEWLINE
 COL=066 CHKAMTTOT(1)
 COL=085 ATMFEETOT(1)
 COL=103 ACHFEETOT(1)
 COL=121 RETCKFEETOT(1)
 HEADING="Negative 16-30 Days"
 NEWPAGE
 FOR I=16 TO 30
  DO
   FILEOPEN("LETTER","NQ30","READ",FN30,FERR)
   CALL FILEERROR
   FILEREADLINE(FN30,FLINE,FERR)
   WHILE FERR=""
    DO
     IF VALUE(SEGMENT(FLINE,35,38))=I THEN
      DO
       PRINT FLINE
       NEWLINE
      END
     FILEREADLINE(FN30,FLINE,FERR)
     CALL FILEERROR
    END
   FILECLOSE(FN30,FERR)
   CALL FILEERROR
  END
 PRINT REPEATCHR("=",132)
 NEWLINE
 PRINT "Subtotals:"
 COL=024 NEGTOT(2)
 COL=029 " in "+FORMAT("##,##9",ICOUNT(2))+" Accts"
 COL=056 ATMAMTTOT(2)
 COL=076 ACHAMTTOT(2)
 COL=094 CHKFEETOT(2)
 COL=113 OTHERTOT(2)
 COL=131 RETCKAMTTOT(2)
 NEWLINE
 COL=066 CHKAMTTOT(2)
 COL=085 ATMFEETOT(2)
 COL=103 ACHFEETOT(2)
 COL=121 RETCKFEETOT(2) 
 HEADING="Negative 31-44 Days"
 NEWPAGE
 FOR I=31 TO 44
  DO
   FILEOPEN("LETTER","NQ44","READ",FN44,FERR)
   CALL FILEERROR
   FILEREADLINE(FN44,FLINE,FERR)
   WHILE FERR=""
    DO
     IF VALUE(SEGMENT(FLINE,35,38))=I THEN
      DO
       PRINT FLINE
       NEWLINE
      END     
     FILEREADLINE(FN44,FLINE,FERR)
     CALL FILEERROR
    END
   FILECLOSE(FN44,FERR)
   CALL FILEERROR
  END
 PRINT REPEATCHR("=",132)
 NEWLINE
 PRINT "Subtotals:"
 COL=024 NEGTOT(3)
 COL=029 " in "+FORMAT("##,##9",ICOUNT(3))+" Accts"
 COL=056 ATMAMTTOT(3)
 COL=076 ACHAMTTOT(3)
 COL=094 CHKFEETOT(3)
 COL=113 OTHERTOT(3)
 COL=131 RETCKAMTTOT(3)
 NEWLINE
 COL=066 CHKAMTTOT(3)
 COL=085 ATMFEETOT(3)
 COL=103 ACHFEETOT(3)
 COL=121 RETCKFEETOT(3) 
 HEADING="Negative 45-59 Days"
 NEWPAGE
 FOR I=45 TO 59
  DO
   FILEOPEN("LETTER","NQ59","READ",FN59,FERR)
   CALL FILEERROR
   FILEREADLINE(FN59,FLINE,FERR)
   WHILE FERR=""
    DO
     IF VALUE(SEGMENT(FLINE,35,38))=I THEN
      DO
       PRINT FLINE
       NEWLINE
      END     
     FILEREADLINE(FN59,FLINE,FERR)
     CALL FILEERROR
    END
   FILECLOSE(FN59,FERR)
   CALL FILEERROR
  END
 PRINT REPEATCHR("=",132)
 NEWLINE
 PRINT "Subtotals:"
 COL=024 NEGTOT(4)
 COL=029 " in "+FORMAT("##,##9",ICOUNT(4))+" Accts"
 COL=056 ATMAMTTOT(4)
 COL=076 ACHAMTTOT(4)
 COL=094 CHKFEETOT(4)
 COL=113 OTHERTOT(4)
 COL=131 RETCKAMTTOT(4)
 NEWLINE
 COL=066 CHKAMTTOT(4)
 COL=085 ATMFEETOT(4)
 COL=103 ACHFEETOT(4)
 COL=121 RETCKFEETOT(4)  
 HEADING="Negative 60 Days and Over"
 OUTPUTSWITCH(OUTPUTCHANNEL1,ERRORTEXT)               [PRINT 60 DAY TO SEPARATE REPORT]
 FOR I=60 TO 99
  DO
   FILEOPEN("LETTER","NQ60","READ",FN60,FERR)
   CALL FILEERROR
   FILEREADLINE(FN60,FLINE,FERR)
   WHILE FERR=""
    DO
     IF VALUE(SEGMENT(FLINE,35,38))=I OR (I=99 AND VALUE(SEGMENT(FLINE,35,38))>I) THEN
      DO
       PRINT FLINE
       NEWLINE
      END     
     FILEREADLINE(FN60,FLINE,FERR)
     CALL FILEERROR
    END
   FILECLOSE(FN60,FERR)
   CALL FILEERROR
  END
 PRINT REPEATCHR("=",132)
 NEWLINE
 PRINT "Subtotals:"
 COL=024 NEGTOT(5)
 COL=029 " in "+FORMAT("##,##9",ICOUNT(5))+" Accts"
 COL=056 ATMAMTTOT(5)
 COL=076 ACHAMTTOT(5)
 COL=094 CHKFEETOT(5)
 COL=113 OTHERTOT(5)
 COL=131 RETCKAMTTOT(5)
 NEWLINE
 COL=066 CHKAMTTOT(5)
 COL=085 ATMFEETOT(5)
 COL=103 ACHFEETOT(5)
 COL=121 RETCKFEETOT(5)
 FILEDELETE("LETTER","NQ15",FERR)
 FILEDELETE("LETTER","NQ30",FERR)
 FILEDELETE("LETTER","NQ44",FERR)
 FILEDELETE("LETTER","NQ59",FERR)
 FILEDELETE("LETTER","NQ60",FERR)
END

PROCEDURE FILEERROR
 IF FERR<>"" AND FERR<>"EOF" AND FERR<>"No such file or directory" THEN
  DO
   PRINT "Error in "+ERRLOC+": "
   PRINT FERR
   NEWLINE
  END
END
