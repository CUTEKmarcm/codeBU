[GH.ADVERSE.ACTION.LOANLOOP]
[11/21/19 Addded ability to call program to create the tracking 31 before generating the form]

SUBROUTINE DEMAND WINDOWS

TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 #INCLUDE "CM.LOAN.BRANCHPRINTER.DEF"
 INITERROR   = CHARACTER
 EXECERROR   = CHARACTER
 ACCTNO      = CHARACTER
 LNID        = CHARACTER(2)  ARRAY(500)
 LNTYPE      = CHARACTER     ARRAY(500)
 LNDESC      = CHARACTER(40) ARRAY(500)
 LOANCT      = NUMBER
 I           = NUMBER
 COMBOSELECT = NUMBER
 LOANID      = CHARACTER(2)
 LNAMES      = CHARACTER     ARRAY(500,2)
 LNADDR      = CHARACTER     ARRAY(500)
 L           = NUMBER
 FULLREASONS = CHARACTER
 ALLCHECKS   = CHARACTER
 DATA9AND11AND13=CHARACTER
 DATA15AND16AND101=CHARACTER
 DATA22AND23 = CHARACTER
 DATA24AND38 = CHARACTER
 DATA39AND40 = CHARACTER
 DATA69AND70 = CHARACTER
 DECREASEAMT = MONEY
 STL         = NUMBER
 STLDATE     = DATE
 REQUESTDESC = CHARACTER
 TRM         = CHARACTER(1)
 LOOKUPDATECREDREP=DATE
 CB          = NUMBER
 FND31       = CHARACTER(1)
END

SETUP
 ACCTNO=ACCOUNT:NUMBER
 CONSOLEBRANCH=GETDATANUMBER(GETCONSOLEBRANCH,SYSCONSOLENUM)
 IF CONSOLEBRANCH<>8 THEN PRINTER="LOAN DUPLEX 2"
 WHILE SEGMENT(ACCTNO,1,1)="0"
  DO
   ACCTNO=SEGMENT(ACCTNO,2,LENGTH(ACCTNO))
  END
 TRM="N"
 LNAMES(1,1)=NAME:LONGNAME
 LNAMES(1,2)=NAME:SSN
 LNADDR(1)=CAPITALIZE(NAME:STREET)
 L=1
 [IF SYSSYMDIRECTORY=000 THEN CALL BEACONSETUPCHECK]
 LOOKUPDATECREDREP='--/--/--'
 FND31="N"
 FOR EACH TRACKING WITH TRACKING:TYPE=31
  DO
   FND31="Y"
  END
 IF FND31="N" THEN   
  DO
   INITSUBROUTINE(@ENVPARAMERRORTEXT) 
   @ENVARGCHAR1="Y"
   EXECUTE("GH.NEW.CREDIT.REPORTS",@ENVPARAMERRORTEXT)
   IF SYSUSERNUMBER=484 AND @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)                                                      
  END
 FOR EACH CREDREP WITH CREDREP:COMPLETIONDATE>=LOOKUPDATECREDREP 
  DO
   IF CREDREP:COMPLETIONDATE>=LOOKUPDATECREDREP THEN 
    DO
     LOOKUPDATECREDREP=CREDREP:COMPLETIONDATE
     CB=CREDREP:BUREAU
    END
  END
 CALL GETLOANNAMES
 FULLREASONS=""
 IF TRM="N" THEN
  DO
   FOR I=1 TO L
    DO
     WHILELIMIT=80000000
     INITSUBROUTINE(INITERROR)
     @ENVARGCHAR1=ACCTNO+"|"+LNAMES(I,1)+"|"+LNAMES(I,2)+"|"+PRINTER+"|"+REQUESTDESC
     @ENVARGCHAR2=LNADDR(I)+"|"+LNID(COMBOSELECT)+"|"+LNTYPE(COMBOSELECT)
     @ENVARGCHAR3=DATA9AND11AND13
     @ENVARGCHAR4=DATA15AND16AND101
     @ENVARGCHAR5=DATA22AND23
     @ENVARGCHAR6=DATA24AND38
     @ENVARGCHAR7=DATA39AND40
     @ENVARGCHAR8=DATA69AND70
     @ENVARGCHAR9=ALLCHECKS
     @ENVARGCHAR10=FULLREASONS
     @ENVARGMONEY1=DECREASEAMT
     @ENVARGNUMBER1=CB
     EXECUTE("ADVERSE.ACTION.2019",EXECERROR)
     IF EXECERROR<>"" THEN POPUPMESSAGE(1,"1 ERROR: "+EXECERROR)
     REQUESTDESC=@ENVARGCHAR1
     DATA9AND11AND13=@ENVARGCHAR3
     DATA15AND16AND101=@ENVARGCHAR4
     DATA22AND23=@ENVARGCHAR5
     DATA24AND38=@ENVARGCHAR6
     DATA39AND40=@ENVARGCHAR7
     DATA69AND70=@ENVARGCHAR8
     ALLCHECKS=@ENVARGCHAR9
     FULLREASONS=@ENVARGCHAR10
     DECREASEAMT=@ENVARGMONEY1
    END
  END
END


PRINT TITLE="LOOP FOR LOANS"
  SUPPRESSNEWLINE
END

PROCEDURE GETLOANNAMES
 IF LENGTH(NAME:EXTRAADDRESS)>0 THEN LNADDR(1)=LNADDR(1)+", "+CAPITALIZE(NAME:EXTRAADDRESS)
 IF LENGTH(NAME:CITY)>0         THEN LNADDR(1)=LNADDR(1)+", "+CAPITALIZE(NAME:CITY)
 IF LENGTH(NAME:STATE)>0        THEN LNADDR(1)=LNADDR(1)+", "+UPPERCASE(NAME:STATE)
 IF LENGTH(NAME:ZIPCODE)>0      THEN LNADDR(1)=LNADDR(1)+"  "+NAME:ZIPCODE
 LOANCT=0
 STLDATE='01/01/01'
 STL=1
 FOR EACH LOANAPP WITH (LOANAPP:APPLICATIONDATE>=SYSTEMDATE-90 OR 
                        LOANAPP:CREATIONDATE>=SYSTEMDATE-90)
  DO
   LOANCT=LOANCT+1
   LNDESC(LOANCT)=LOANAPP:LOANDESCRIPTION
   LNID(LOANCT)=FORMAT("99",VALUE(LOANAPP:ID))
   LNTYPE(LOANCT)="A"  
   IF LOANAPP:APPLICATIONDATE>STLDATE THEN 
    DO
     STL=LOANCT
     STLDATE=LOANAPP:APPLICATIONDATE
    END
  END
 FOR EACH LOAN WITH (LOAN:CLOSEDATE='--/--/--' AND LOAN:OPENDATE>=SYSTEMDATE-90)
  DO
   LOANCT=LOANCT+1
   LNDESC(LOANCT)=LOAN:DESCRIPTION
   LNID(LOANCT)=FORMAT("99",VALUE(LOAN:ID))
   LNTYPE(LOANCT)="L"
   IF LOAN:OPENDATE>STLDATE THEN 
    DO
     STL=LOANCT
     STLDATE=LOAN:OPENDATE
    END
  END
 IF LOANCT=0 THEN
  DO
   POPUPMESSAGE(1,"No new loans or Apps on this account")
   TRM="Y"
  END
 ELSE IF LOANCT=1 THEN 
  DO
   LOANID=LNID(1)
   COMBOSELECT=1
  END
 ELSE
  DO
   DIALOGSTART("Select a Loan/App",100%,1)
    DIALOGPROMPTCOMBOSTART("Loan ID",STL)
    FOR I=1 TO LOANCT
     DO
      IF LNTYPE(I)="L" THEN DIALOGPROMPTCOMBOOPTION(I,"Loan "+LNID(I)+": "+LNDESC(I))
      ELSE DIALOGPROMPTCOMBOOPTION(I,"Loan App "+LNID(I)+": "+LNDESC(I))
     END
    DIALOGPROMPTCOMBOEND
   DIALOGDISPLAY
    COMBOSELECT=ENTERCODE("Loan ID",LOANCT,STL)
   DIALOGCLOSE
   LOANID=LNID(COMBOSELECT)
  END
 IF TRM="N" THEN
  DO
   IF LNTYPE(COMBOSELECT)="A" THEN
    DO
     FOR EACH LOANAPP WITH LOANAPP:ID=LOANID
      DO
       FOR EACH LOANAPP PERSON WITH LOANAPP PERSON:SSN<>LNAMES(1,2)
        DO
         L=L+1
         LNAMES(L,1)=CAPITALIZE(LOANAPP PERSON:LONGNAME)
         LNAMES(L,2)=LOANAPP PERSON:SSN
         IF LENGTH(LOANAPP PERSON:STREET)>0 THEN
          DO
           LNADDR(L)=CAPITALIZE(LOANAPP PERSON:STREET)
           IF LENGTH(LOANAPP PERSON:EXTRAADDRESS)>0 THEN LNADDR(L)=LNADDR(L)+", "+CAPITALIZE(LOANAPP PERSON:EXTRAADDRESS)
           IF LENGTH(LOANAPP PERSON:CITY)>0         THEN LNADDR(L)=LNADDR(L)+", "+CAPITALIZE(LOANAPP PERSON:CITY)
           IF LENGTH(LOANAPP PERSON:STATE)>0        THEN LNADDR(L)=LNADDR(L)+", "+UPPERCASE(LOANAPP PERSON:STATE)
           IF LENGTH(LOANAPP PERSON:ZIPCODE)>0      THEN LNADDR(L)=LNADDR(L)+"  "+LOANAPP PERSON:ZIPCODE
          END
         ELSE LNADDR(L)=LNADDR(1)
        END
      END
    END
   ELSE
    DO
     FOR EACH LOAN WITH LOAN:ID=LOANID AND LOANID<>""
      DO
       FOR EACH LOAN NAME WITH LOAN NAME:EXPIRATIONDATE='--/--/--' OR LOAN NAME:EXPIRATIONDATE>SYSTEMDATE
        DO
         L=L+1
         LNAMES(L,1)=CAPITALIZE(LOAN NAME:LONGNAME)
         LNAMES(L,2)=LOAN NAME:SSN
         IF LENGTH(LOAN NAME:STREET)>0 THEN
          DO
           LNADDR(L)=CAPITALIZE(LOAN NAME:STREET)
           IF LENGTH(LOAN NAME:EXTRAADDRESS)>0 THEN LNADDR(L)=LNADDR(L)+", "+CAPITALIZE(LOAN NAME:EXTRAADDRESS)
           IF LENGTH(LOAN NAME:CITY)>0         THEN LNADDR(L)=LNADDR(L)+", "+CAPITALIZE(LOAN NAME:CITY)
           IF LENGTH(LOAN NAME:STATE)>0        THEN LNADDR(L)=LNADDR(L)+", "+UPPERCASE(LOAN NAME:STATE)
           IF LENGTH(LOAN NAME:ZIPCODE)>0      THEN LNADDR(L)=LNADDR(L)+"  "+LOAN NAME:ZIPCODE
          END
         ELSE LNADDR(L)=LNADDR(1)
        END
      END
    END
  END
END

 #INCLUDE "CM.LOAN.BRANCHPRINTER.PRO"
