[CUMORTGAGE.EXPIRE.TRACKING.FIX  - Run the last day of each month]

TARGET=TRACKING

DEFINE
 #INCLUDE "RD.OUTPUT.DEF"
 OUTPUTCHANNEL1=NUMBER
 ERRORTEXT=CHARACTER
 CNT=NUMBER
 BACKDATE=DATE
 H=NUMBER
END

SETUP
 BACKDATE=DATEOFFSET(SYSTEMDATE,0,1)
 OUTPUTOPEN(OUTPUTDEVREPORT,0,"Expired CU Mortgage Fix","",OUTPUTCHANNEL1,ERRORTEXT)
 H=1
END

SELECT
 ACCOUNT:CLOSEDATE='--/--/--' AND TRACKING:TYPE=0 AND 
 TRACKING:RECORDCHANGEDATE<BACKDATE AND TRACKING:EXPIREDATE='--/--/--'
END

SORT
 TRACKING:RECORDCHANGEDATE
END

PRINT TITLE="Expired CU Mortgage Tracking"
 HEADERS
  IF H=1 THEN
   DO
    COL=1 "Account"
    COL=12 "Name"
    COL=35 "CU Mortgage#"
    COL=49 "Last Balance"
    NEWLINE
    COL=1 REPEATCHR("-",84)
    NEWLINE
   END
 END 

 COL=001 ACCOUNT:NUMBER
 COL=017 NAME:SHORTNAME
 COL=035 TRACKING:USERCHAR1
 COL=060 TRACKING:USERAMOUNT1
 COL=062 TRACKING:USERCHAR3
 NEWLINE

 H=2
 OUTPUTSWITCH(OUTPUTCHANNEL1,ERRORTEXT)
 IF ANYWARNING(ACCOUNT,59) THEN
  DO
   PRINT "ACCOUNT "+ACCOUNT:NUMBER+" MODIFY"
   NEWLINE
   PRINT " CHANGE CLEARWARNING 59 --/--/--"
   NEWLINE
  END
 PRINT "ACCOUNT "+ACCOUNT:NUMBER+" MODIFY TRACKING LOC "
 PRINT TRACKING:LOCATOR
 NEWLINE
 PRINT " CHANGE EXPIREDATE FROM "
 PRINT TRACKING:EXPIREDATE
 PRINT " TO "+FORMAT("99/99/99",SYSTEMDATE)
 NEWLINE
 H=1
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
 CNT=CNT+1
END

TOTAL
 COL=1 "Total: "
 PRINT CNT
 NEWLINE
END


