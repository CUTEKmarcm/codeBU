[CUMORTGAGE.TRACK.FIX.REDO]
[12/11/06 GH USES LETTERSPEC FILE PLS1709.CSV PREVIOUSLY IMPORTED AS TEXT FROM THE 
             CU MORTGAGE DOWNLOAD. CHECKS FOR REG E AND DOES NOT EXPIRE TRACKIG WHEN 
             MORTGAGE IS PAID OFF SO IT WILL PRINT ONE LAST TIME ON THE STATEMENT.  
             
             IF REPROT "STOP AND FIX Missing Accounts" IS CREATED THEN THE PRINTOUT OF THIS 
             REPORT IS TO BE GIVEN TO TONY OR KEN IN LENDING TO GET THE SYMITAR ACCOUNT NUMBER. 
        
             THE CU MORTGAGE ACCOUNT NUMBERS AND STMITAR ACCOUNTS ARE INPUT INTO THE LETTERFILE 
             CUACCOUNTS. IF THERE IS NOT A SYMITAR ACCOUNT THEN 9999999999 SHOULD BE ENTERED AS
             THE SYMITAR ACCOUNT NUMBER.  SYMITAR ACCOUNT ARE STORED AS FULL 10 DIGITS.]
[07/26/13 GH Modified program fore revised format.  The second 2/3 field went from 02,XXXXX to 00XXXXX per Tom CU Mortgage]

TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.OUTPUT.DEF"
 OUTPUTCHANNEL1=NUMBER
 OUTPUTCHANNEL2=NUMBER
 OUTPUTCHANNEL3=NUMBER
 ERRORTEXT=CHARACTER
 FNUMBER=NUMBER
 FERROR=CHARACTER
 FLINE=CHARACTER
 TMP=CHARACTER
 ORD=NUMBER
 TLOC=NUMBER
 TSCORE=NUMBER
 TDATE=DATE
 TEXPIRE=DATE
 PROMOEXPIRE=DATE
 CURPOS=NUMBER
 INMONEY=MONEY             ARRAY(9999,4)
 INCHAR=CHARACTER(40)      ARRAY(9999,8)
 INDATE=DATE               ARRAY(9999,2)
 INNUMBER=NUMBER           ARRAY(9999)
 INRATE=RATE               ARRAY(9999,2)
 INCNT=NUMBER
 I=NUMBER
 J=NUMBER
 MDESC=CHARACTER
 FOUNDACCT=CHARACTER(1)
 FOUNDSSN=CHARACTER(1)
 TCHAR=CHARACTER(40)      ARRAY(9999,4)
 EREASON=CHARACTER(35)    ARRAY(7)
 FIRST=CHARACTER(1)
 STMTCODE=NUMBER
 QTYSTMTCODE=NUMBER
 BCNT=NUMBER              ARRAY(7)
 BC=NUMBER               
 TBCNT=NUMBER
 BMORTGAGE=MONEY          ARRAY(7)
 TBMORTGAGE=MONEY
 RPTDATE=DATE
 GOODACCT=CHARACTER(1)
 FOUNDREGE=CHARACTER(1)
 DCNT=NUMBER
 TT=CHARACTER
 Q=CHARACTER
 LF=CHARACTER
 CR=CHARACTER
 LEN=NUMBER
 LZ=NUMBER
 L=NUMBER
 P=NUMBER
 FC=CHARACTER
 LC=CHARACTER
 CUACCTS=CHARACTER(20)   ARRAY(9999,2)
 CU=NUMBER
 MISSINGACCTS=NUMBER     ARRAY(5000)
 M=NUMBER
 K=NUMBER
END

SETUP
 Q=CTRLCHR(34)
 LF=CTRLCHR(10)
 CR=CTRLCHR(13)
 WHILELIMIT=2147483646
 RPTDATE=DATEREAD("CU Members Report Date")                   [USUALLY THE 25 OF EACH MONTH]
 MDESC="Mortgage as of "+FORMAT("99/99/99",RPTDATE)
 CALL GETINPUT
 CALL GETACCOUNTS
 FIRST="Y"
 EREASON(1)="No Symitar Account Number"
 EREASON(2)="Symitar Account Closed"
 EREASON(3)="Low Share 00 Balance"
 EREASON(4)="SSN Not on Symitar Account"
 EREASON(5)="Symitar Account Not Found"
 EREASON(6)="Mortgage Recently Closed"
 EREASON(7)="0 Bal Tracking Already Expired"
 IF MONTH(SYSTEMDATE)=3 OR MONTH(SYSTEMDATE)=6 OR MONTH(SYSTEMDATE)=9 OR MONTH(SYSTEMDATE)=12 THEN QTYSTMTCODE=3
 ELSE QTYSTMTCODE=61
END

SELECT
 NONE
END

PRINT TITLE="CU Members Tracking Update"
 HEADER=""
 WHILELIMIT=2147483646

 OUTPUTOPEN(OUTPUTDEVREPORT,0,"CU Members Update Exceptions","",OUTPUTCHANNEL1,ERRORTEXT)
 IF ERRORTEXT<>"" THEN PRINT "Error: "+ERRORTEXT
 OUTPUTOPEN(OUTPUTDEVREPORT,0,"CU Members Accounts","",OUTPUTCHANNEL3,ERRORTEXT)
 IF ERRORTEXT<>"" THEN PRINT "Error: "+ERRORTEXT
 FOR J=1 TO INCNT
  DO
   OUTPUTSWITCH(OUTPUTCHANNEL3,ERRORTEXT)                 [PRINTS RECORD TO REPORT]
   CALL PRINTALL
   OUTPUTSWITCH(OUTPUTCHANNEL1,ERRORTEXT)                 [PRINTS CHANGES TO UPDATE REPORT FOR PROCESSING]
   FOUNDACCT="N"
   FOUNDSSN="N"
   TLOC=-1
    IF VALUE(INCHAR(J,1))=0 OR VALUE(INCHAR(J,1))=9999999999 THEN   [NO SYMITAR ACCOUNT OR MISSING]
    DO
     BC=1
     CALL PRINTEXCEPTION
    END
   ELSE
    DO
     STMTCODE=QTYSTMTCODE
     FOR ACCOUNT INCHAR(J,1)
      DO
       FOUNDACCT="Y"
       IF ACCOUNT:CLOSEDATE<>'--/--/--' THEN
        DO
         BC=2
         CALL PRINTEXCEPTION                                        [SYMITAR ACCOUNT WAS CLOSED]
        END
       ELSE
        DO
         FOUNDREGE="N"
         CALL CHECKFORSSN
         IF FOUNDREGE="Y" AND STMTCODE=61 THEN                      [CHANGES STATEMENT MAIL CODE AS NEEDED]
          DO
           IF MONTH(SYSTEMDATE)=3 OR MONTH(SYSTEMDATE)=6 OR MONTH(SYSTEMDATE)=9 OR 
              MONTH(SYSTEMDATE)=12 THEN STMTCODE=3
           ELSE STMTCODE=1
          END
         IF FOUNDSSN="Y" THEN
          DO
           IF GOODACCT="N" THEN                    [ADDS NOTE TO EXCEPTION REPORT IF ACCOUNT NOT IN GOOD STANDING]
            DO
             BC=3
             CALL PRINTEXCEPTION    
            END
           OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
           FOR EACH TRACKING WITH TRACKING:TYPE=0 AND               [GET CURRENT MORTGAGE TRACKING LOC]
               VALUE(TRACKING:USERCHAR1)=VALUE(INCHAR(J,2))
            DO
             TLOC=TRACKING:LOCATOR
             CALL MODIFYTRACKING
            END
           IF TLOC=-1 THEN                                          [NO TRACKING FOUND - ADD NEW]
            DO
             IF INMONEY(J,1)>$0.00 THEN CALL CREATENEWTRACKING
            END
          END
         ELSE                                                       [SSN NOT ON SYMITAR ACCOUNT]
          DO
           BC=4
           CALL PRINTEXCEPTION
          END
        END
      END
     IF FOUNDACCT="N" THEN
      DO
       BC=5
       CALL PRINTEXCEPTION                                          [SYMITAR ACCOUNT IN LETTER FILE IS BAD]
      END
    END
  END
END

TOTAL
 IF M>0 THEN OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
 ELSE OUTPUTSWITCH(OUTPUTCHANNEL1,ERRORTEXT)
 NEWLINE
 NEWLINE
 NEWLINE
 FOR J=1 TO 7
  DO 
   IF BCNT(J)>0 THEN
    DO
     COL=1 EREASON(J)
     COL=45 BCNT(J)
     COL=93 BMORTGAGE(J)
     NEWLINE
    END
  END
 COL=1 REPEATCHR("-",93)
 NEWLINE
 COL=1 "Number of Mortgages needing review: "
 COL=45 TBCNT
 COL=93 TBMORTGAGE
 NEWLINE
END

PROCEDURE GETINPUT
 PRINT "START GETINPUT"
 NEWLINE
 FILEOPEN("LETTER","PLS1709.CSV","READ",FNUMBER,FERROR) 
 IF FERROR<>"" THEN
  DO
   PRINT "ERROR OPENING FILE: "+FERROR
   NEWLINE
  END
 WHILE FERROR=""
  DO
   FILEREAD(FNUMBER,1,FLINE,FERROR)
   TMP=TMP+FLINE
   IF FLINE="," OR FLINE=LF OR FLINE=CR THEN                                       [LOOK FOR COMMA DELIMITOR]
    DO
     TMP=SEGMENT(TMP,1,LENGTH(TMP)-1)                                              [REMOVE COMMA AT END]
     IF SEGMENT(TMP,1,1)=Q THEN TMP=SEGMENT(TMP,2,LENGTH(TMP))                     [REMOVE QUOTE]
     LEN=LENGTH(TMP)
     IF SEGMENT(TMP,LEN,LEN)=Q THEN TMP=SEGMENT(TMP,1,LEN-1)                       [REMOVE QUOTE]
     LEN=LENGTH(TMP)
     TMP=SEGMENT(TMP,1,LEN)                                                        [REMOVES ENDING SPACES]
     IF LENGTH(TMP)=4 AND TMP="1709" THEN                                          [START OF EACH NEW RECORD]
      DO
       DCNT=1
       INCNT=INCNT+1
      END
     ELSE DCNT=DCNT+1
     IF DCNT=2 THEN INCHAR(INCNT,2)=SEGMENT(TMP,3,LENGTH(TMP))                      [COLONIAL ACCT NUMBER]
     IF DCNT=7 THEN                                                                 [PRINCIPAL]
      DO
       IF CHARACTERSEARCH(TMP,".")=0 THEN TMP=TMP+".00"                             [ADD CENTS IF NOT FOUND]
       INMONEY(INCNT,1)=MONEY(VALUE(TMP)) 
      END
     IF DCNT=8 THEN INNUMBER(INCNT)=VALUE(TMP)                                      [TERM] 
     IF DCNT=9 THEN                                                                [RATE]
      DO
       P=CHARACTERSEARCH(TMP,".")
       FC=SEGMENT(TMP,1,P-1)
       LC=SEGMENT(TMP,P+1,LENGTH(TMP))
       L=LENGTH(LC)
       TMP=FC+"."
       IF L=0 THEN TMP=TMP+"000"                                               [ADD NECESSARY DIGITS FOR RATE]
       ELSE IF L=1 THEN TMP=TMP+LC+"00"
       ELSE IF L=2 THEN TMP=TMP+LC+"0"
       ELSE IF L=3 THEN TMP=TMP+LC
       ELSE TMP=TMP+SEGMENT(LC,1,3)
       INRATE(INCNT,1)=RATE(MONEY(VALUE(TMP))/100000)
      END
     IF DCNT=10 THEN INDATE(INCNT,1)=DATEVALUE(TMP)                                 [ORIG DATE] 
     IF DCNT=13 THEN                                                                [ORIGINAL LOAN AMOUNT]
      DO
       IF CHARACTERSEARCH(TMP,".")=0 THEN TMP=TMP+".00"
       INMONEY(INCNT,2)=MONEY(VALUE(TMP))
      END
     IF DCNT=14 THEN                                                                [APPRAISAL TO BALANCE PERCENT]
      DO
       IF CHARACTERSEARCH(TMP,".")=0 THEN TMP=TMP+".00"
       INRATE(INCNT,2)=100.00%-(INMONEY(INCNT,1)/MONEY(VALUE(TMP))) 
      END
     IF DCNT=31 THEN INCHAR(INCNT,8)=TMP                                            [FULL NAME]
     IF DCNT=32 THEN INCHAR(INCNT,3)=TMP                                            [SSN1]
     IF DCNT=36 AND VALUE(TMP)>0 THEN INCHAR(INCNT,4)=TMP                           [SSN2]
     IF DCNT=37 THEN INCHAR(INCNT,5)=TMP                                            [ADDRESS - NUMBER]
     IF DCNT=38 AND LENGTH(TMP)>0 THEN INCHAR(INCNT,5)=INCHAR(INCNT,5)+" "+TMP      [ADDRESS - N,S,E,W]
     IF DCNT=39 THEN INCHAR(INCNT,5)=INCHAR(INCNT,5)+" "+TMP                        [ADDRESS - NAME]
     IF DCNT=40 THEN INCHAR(INCNT,6)=TMP                                            [CITY]
     IF DCNT=42 THEN                                                                [ZIP]
      DO
       LZ=LENGTH(TMP)
       IF SEGMENT(TMP,LZ-3,LZ)="0000" THEN TMP=SEGMENT(TMP,1,LZ-4)                  [REMOVE EXTRA ZEROS]
       INCHAR(INCNT,6)=INCHAR(INCNT,6)+" "+TMP 
      END
     IF DCNT=49 THEN INDATE(INCNT,2)=DATEVALUE(TMP)                                 [DUE DATE]
     IF DCNT=52 THEN                                                                [PAYMENT] 
      DO
       IF CHARACTERSEARCH(TMP,".")=0 THEN TMP=TMP+".00"
       INMONEY(INCNT,3)=MONEY(VALUE(TMP))
      END
     IF DCNT=68 THEN                                                                [LATE FEE]
      DO
       IF CHARACTERSEARCH(TMP,".")=0 THEN TMP=TMP+".00"
       INMONEY(INCNT,4)=MONEY(VALUE(TMP))
      END
     IF DCNT=74 THEN INCHAR(INCNT,7)=TMP                                            [DESCRIPTION]
     TMP=""
    END
   IF FERROR<>"" AND FERROR<>"EOF" THEN 
    DO
     PRINT "While loop error: "+FERROR
     NEWLINE
    END
  END
 FILECLOSE(FNUMBER,FERROR)
 PRINT "END GETINPUT ICNT "
 PRINT INCNT
 NEWLINE
END

PROCEDURE CHECKFORSSN
 FOR EACH NAME WITH VALUE(NAME:SSN)>0 AND FOUNDSSN="N" AND 
                    NAME:EXPIRATIONDATE='--/--/--' AND NAME:TYPE<=1
  DO 
   IF NAME:SSN=SEGMENT(INCHAR(J,3),1,LENGTH(INCHAR(J,3))) OR 
      NAME:SSN=SEGMENT(INCHAR(J,4),1,LENGTH(INCHAR(J,4))) THEN FOUNDSSN="Y"
  END
 GOODACCT="N"
 FOR EACH SHARE WITH SHARE:CLOSEDATE='--/--/--' AND SHARE:CHARGEOFFDATE='--/--/--'
  DO
   IF SHARE:ID="00" THEN
    DO
     IF SHARE:BALANCE>=$25.00 THEN GOODACCT="Y"                                [ACCOUNT IN GOOD STANDING]
    END
   IF SHARE:STATEMENTGROUP=1 THEN STMTCODE=1
   FOR EACH SHARE NAME WITH VALUE(SHARE NAME:SSN)>0 AND FOUNDSSN="N" AND 
            SHARE NAME:EXPIRATIONDATE='--/--/--' AND SHARE NAME:TYPE<=1
    DO
     IF SHARE NAME:SSN=INCHAR(J,3) OR SHARE NAME:SSN=INCHAR(J,4) THEN FOUNDSSN="Y"
    END
   FOR EACH SHARE TRANSACTION WITH SHARE TRANSACTION:COMMENTCODE=0 AND
            SHARE TRANSACTION:POSTDATE>=DATEOFFSET(SYSTEMDATE,0,1) 
    DO
     IF SHARE TRANSACTION:REGECODE=1 THEN FOUNDREGE="Y"
    END
   UNTIL SHARE TRANSACTION:POSTDATE<DATEOFFSET(SYSTEMDATE,0,1) OR FOUNDREGE="Y"
  END
 FOR EACH LOAN WITH LOAN:CLOSEDATE='--/--/--' AND LOAN:CHARGEOFFDATE='--/--/--'
  DO
   IF LOAN:STATEMENTGROUP=1 THEN STMTCODE=1
   FOR EACH LOAN NAME WITH VALUE(LOAN NAME:SSN)>0 AND FOUNDSSN="N" AND 
                           LOAN NAME:EXPIRATIONDATE='--/--/--' AND LOAN NAME:TYPE<=1
    DO
     IF LOAN NAME:SSN=INCHAR(J,3) OR LOAN NAME:SSN=INCHAR(J,4) THEN FOUNDSSN="Y"
    END
   FOR EACH LOAN TRANSACTION WITH LOAN TRANSACTION:COMMENTCODE=0 AND
            LOAN TRANSACTION:POSTDATE>=DATEOFFSET(SYSTEMDATE,0,1) 
    DO
     IF LOAN TRANSACTION:REGECODE=1 THEN FOUNDREGE="Y"
    END
   UNTIL LOAN TRANSACTION:POSTDATE<DATEOFFSET(SYSTEMDATE,0,1) OR FOUNDREGE="Y"
  END
END

PROCEDURE MODIFYTRACKING
 IF INMONEY(J,1)=$0.00 THEN
  DO
   IF ANYWARNING(ACCOUNT,59) THEN
    DO
     PRINT "ACCOUNT "+INCHAR(J,1)+" MODIFY"
     NEWLINE
     PRINT " CHANGE CLEARWARNING 59 --/--/--"
     NEWLINE
    END
   IF TRACKING:EXPIREDATE='--/--/--' THEN
    DO
     PRINT "ACCOUNT "+INCHAR(J,1)+" MODIFY TRACKING LOC "
     PRINT TLOC
     NEWLINE
     PRINT " CHANGE USERCODE2 FROM "
     PRINT TRACKING:USERCODE2 
     PRINT " TO "
     PRINT STMTCODE
     NEWLINE
     PRINT " CHANGE USERAMOUNT1 FROM "
     PRINT TRACKING:USERAMOUNT1
     PRINT " TO "
     PRINT INMONEY(J,1)
     NEWLINE
     BC=6
     CALL PRINTEXCEPTION
    END
   ELSE
    DO
     BC=7
     CALL PRINTEXCEPTION
    END
  END
 ELSE
  DO
   PRINT "ACCOUNT "+INCHAR(J,1)+" MODIFY"
   NEWLINE
   IF NOT ANYWARNING(ACCOUNT,59) THEN
    DO
     PRINT " CHANGE SETWARNING 59 --/--/--"
     NEWLINE
    END
   PRINT " CHANGE CORRESPONDDATE FROM "
   PRINT ACCOUNT:CORRESPONDDATE
   PRINT " TO "
   PRINT SYSTEMDATE
   NEWLINE
   PRINT "ACCOUNT "+INCHAR(J,1)+" MODIFY TRACKING LOC "
   PRINT TLOC
   NEWLINE
   IF TRACKING:USERCHAR1<>INCHAR(J,2) THEN
    DO
     PRINT " CHANGE USERCHAR1 FROM "
     COL=49 TRACKING:USERCHAR1
     COL=89 "TO "
     COL=93 INCHAR(J,2)
     NEWLINE
    END
   IF TRACKING:EXPIREDATE<>'--/--/--' THEN
    DO
     PRINT " CHANGE EXPIREDATE FROM "
     PRINT TRACKING:EXPIREDATE
     PRINT " TO --/--/--"
     NEWLINE
    END
   IF TRACKING:USERCHAR2<>INCHAR(J,7) THEN
    DO
     PRINT " CHANGE USERCHAR2 FROM "
     COL=49 TRACKING:USERCHAR2
     COL=89 "TO "
     COL=93 INCHAR(J,7)
     NEWLINE
    END
   PRINT " CHANGE USERCHAR3 FROM "
   COL=49 TRACKING:USERCHAR3
   COL=89 "TO "
   COL=93 MDESC
   NEWLINE
   IF TRACKING:USERCHAR6<>INCHAR(J,5) THEN
    DO
     PRINT " CHANGE USERCHAR6 FROM "
     COL=49 TRACKING:USERCHAR6
     COL=89 "TO "
     COL=93 INCHAR(J,5)
     NEWLINE
    END
   IF TRACKING:USERCHAR7<>INCHAR(J,6) THEN
    DO
     PRINT " CHANGE USERCHAR7 FROM "
     COL=49 TRACKING:USERCHAR7
     COL=89 "TO "
     COL=93 INCHAR(J,6)
     NEWLINE
    END
   IF TRACKING:USERCODE2<>STMTCODE THEN
    DO
     PRINT " CHANGE USERCODE2 FROM "
     PRINT TRACKING:USERCODE2 
     PRINT " TO "
     PRINT STMTCODE
     NEWLINE
    END
   IF TRACKING:USERCODE4<>INNUMBER(J) THEN
    DO
     PRINT " CHANGE USERCODE4 FROM "
     PRINT TRACKING:USERCODE4 
     PRINT " TO "
     PRINT INNUMBER(J)
     NEWLINE
    END
   IF TRACKING:USERAMOUNT1<>INMONEY(J,1) THEN
    DO
     PRINT " CHANGE USERAMOUNT1 FROM "
     PRINT TRACKING:USERAMOUNT1
     PRINT " TO "
     PRINT INMONEY(J,1)
     NEWLINE
    END
   IF TRACKING:USERAMOUNT3<>INMONEY(J,3) THEN
    DO
     PRINT " CHANGE USERAMOUNT3 FROM "
     PRINT TRACKING:USERAMOUNT3
     PRINT " TO "
     PRINT INMONEY(J,3)
     NEWLINE
    END
   IF TRACKING:USERAMOUNT8<>INMONEY(J,2) THEN
    DO
     PRINT " CHANGE USERAMOUNT8 FROM "
     PRINT TRACKING:USERAMOUNT8
     PRINT " TO "
     PRINT INMONEY(J,2)
     NEWLINE
    END
   IF TRACKING:USERDATE2<>INDATE(J,2) THEN
    DO
     PRINT " CHANGE USERDATE2 FROM "
     PRINT TRACKING:USERDATE2
     PRINT " TO "
     PRINT INDATE(J,2)
     NEWLINE
    END
   PRINT " CHANGE USERDATE4 FROM "
   PRINT TRACKING:USERDATE4
   PRINT " TO "
   PRINT SYSTEMDATE
   NEWLINE
   PRINT " CHANGE USERDATE5 FROM "
   PRINT TRACKING:USERDATE5
   PRINT " TO "
   PRINT DATEOFFSET(SYSTEMDATE,0,31)
   NEWLINE
   IF TRACKING:USERDATE6<>INDATE(J,1) THEN
    DO
     PRINT " CHANGE USERDATE6 FROM "
     PRINT TRACKING:USERDATE6
     PRINT " TO "
     PRINT INDATE(J,1)
     NEWLINE
    END
   IF TRACKING:USERRATE1<>INRATE(J,1) THEN
    DO
     PRINT " CHANGE USERRATE1 FROM "
     PRINT TRACKING:USERRATE1
     PRINT " TO "
     PRINT INRATE(J,1)
     NEWLINE
    END
   IF TRACKING:USERRATE2<>INRATE(J,2) THEN
    DO
     PRINT " CHANGE USERRATE2 FROM "
     PRINT TRACKING:USERRATE2
     PRINT " TO "
     PRINT INRATE(J,2)
     NEWLINE
    END
  END
END

PROCEDURE CREATENEWTRACKING
 PRINT "ACCOUNT "+INCHAR(J,1)+" MODIFY"
 NEWLINE
 PRINT " CHANGE SETWARNING 59 --/--/--"
 NEWLINE
 PRINT " CHANGE CORRESPONDDATE FROM "
 PRINT ACCOUNT:CORRESPONDDATE
 PRINT " TO "
 PRINT SYSTEMDATE
 NEWLINE
 PRINT "ACCOUNT "+INCHAR(J,1)+" CREATE TRACKING 0 "   
 NEWLINE
 PRINT " SET TYPE TO"
 COL=49 "0"
 NEWLINE
 PRINT " SET USERCHAR1 TO "
 COL=49 INCHAR(J,2)
 NEWLINE
 PRINT " SET USERCHAR2 TO "
 COL=49 INCHAR(J,7)
 NEWLINE
 PRINT " SET USERCHAR3 TO "
 COL=49 MDESC
 NEWLINE
 PRINT " SET USERCHAR6 TO "
 COL=49 INCHAR(J,5)
 NEWLINE
 PRINT " SET USERCHAR7 TO "
 COL=49 INCHAR(J,6)
 NEWLINE
 PRINT " SET USERCODE2 TO "
 COL=49 STMTCODE
 NEWLINE
 PRINT " SET USERCODE4 TO "
 COL=49 INNUMBER(J)
 NEWLINE
 PRINT " SET USERAMOUNT1 TO "
 COL=49 INMONEY(J,1)
 NEWLINE
 PRINT " SET USERAMOUNT3 TO "
 COL=49 INMONEY(J,3)
 NEWLINE
 PRINT " SET USERAMOUNT5 TO "
 COL=49 INMONEY(J,4)
 NEWLINE
 PRINT " SET USERAMOUNT8 TO "
 COL=49 INMONEY(J,2)
 NEWLINE
 PRINT " SET USERDATE2 TO "
 COL=49 INDATE(J,2)
 NEWLINE
 PRINT " SET USERDATE4 TO "
 COL=49 SYSTEMDATE
 NEWLINE
 PRINT " SET USERDATE5 TO "
 COL=49 DATEOFFSET(SYSTEMDATE,0,31)
 NEWLINE
 PRINT " SET USERDATE6 TO "
 COL=49 INDATE(J,1)
 NEWLINE
 PRINT " SET USERRATE1 TO "
 COL=49 INRATE(J,1)
 NEWLINE
 PRINT " SET USERRATE2 TO "
 COL=49 INRATE(J,2)
 NEWLINE
END

PROCEDURE PRINTEXCEPTION
 IF INMONEY(J,1)>0 OR BC=6 THEN
  DO
   OUTPUTSWITCH(OUTPUTCHANNEL1,ERRORTEXT)
   IF FIRST="Y" THEN
    DO
     COL=1 "Colonial#"
     COL=11 "Symitar#"
     COL=22 "SSN 1"
     COL=32 "SSN 2"
     COL=42 "Address"
     COL=87 "Balance"
     COL=97 "Problem"
     NEWLINE
     COL=1 REPEATCHR("-",132)
     NEWLINE
     FIRST="N"
    END
   COL=01 SEGMENT(INCHAR(J,2),1,10)
   COL=11 SEGMENT(INCHAR(J,1),1,10)
   COL=22 SEGMENT(INCHAR(J,3),1,9)
   COL=32 SEGMENT(INCHAR(J,4),1,9)
   COL=42 INCHAR(J,5)
   COL=93 INMONEY(J,1)
   COL=97 EREASON(BC)
   NEWLINE
   BCNT(BC)=BCNT(BC)+1
   BMORTGAGE(BC)=BMORTGAGE(BC)+INMONEY(J,1)
   TBCNT=TBCNT+1
   TBMORTGAGE=TBMORTGAGE+INMONEY(J,1)
  END
END

PROCEDURE PRINTALL
 IF J=1 OR K=1 THEN
  DO
   COL=1 "SYM Acct"
   COL=13 "CU Acct"
   COL=25 "Name"
   COL=57 "SSN1"
   COL=68 "SSN2"
   COL=80 "Address"
   COL=110 "City,Zip"
   NEWLINE
   COL=1 REPEATCHR("-",132)
   NEWLINE
  END
 IF VALUE(INCHAR(J,2))>0 THEN
  DO
   IF M>0 THEN COL=1 "__________" 
   ELSE COL=1 INCHAR(J,1)
   COL=13 INCHAR(J,2)                          [COLONIAL ACCT NUMBER]
   COL=25 SEGMENT(INCHAR(J,8),1,30)            [NAME]
   COL=57 INCHAR(J,3)                          [SSN1]
   COL=68 INCHAR(J,4)                          [SSN2]
   COL=80 SEGMENT(INCHAR(J,5),1,29)            [ADDRESS]
   COL=110 SEGMENT(INCHAR(J,6),1,30)           [CITY, ZIP]
   NEWLINE
  END
END

PROCEDURE GETACCOUNTS
 PRINT "START GETACCOUNTS"
 NEWLINE
 FILEOPEN("LETTER","CUACCOUNTS.REDO","READ",FNUMBER,FERROR) 
 WHILE FERROR=""
  DO
   FILEREADLINE(FNUMBER,FLINE,FERROR)
   CU=CU+1
   CUACCTS(CU,2)=SEGMENT(FLINE,1,10)
   CUACCTS(CU,1)=SEGMENT(FLINE,13,LENGTH(FLINE))
  END
 FILECLOSE(FNUMBER,FERROR) 
 PRINT "  CU CNT "
 PRINT CU
 PRINT " INCNT "
 PRINT INCNT
 NEWLINE
 WHILELIMIT=2147483646
 FOR I=1 TO INCNT
  DO
   J=1
   WHILE J<=CU AND VALUE(INCHAR(I,1))=0
    DO
     IF VALUE(INCHAR(I,2))=VALUE(CUACCTS(J,1)) THEN INCHAR(I,1)=CUACCTS(J,2)  [ADD SYMITAR ACCOUNT TO RECORD]
     J=J+1
    END
   IF VALUE(INCHAR(I,1))=0 AND VALUE(INCHAR(I,2))>0 THEN                [NOTE MISSING SYMITAR ACCOUNT NUMBER]
    DO
     M=M+1
     MISSINGACCTS(M)=I
    END
  END
 PRINT "END GETACCOUNTS"
 NEWLINE
END

