[CUMORTGAGE.MONTHEND.TRACK.FIX - Run after ACH but before statement cutoff on month end]

TARGET=ACCOUNT

DEFINE
 STARTMTH=DATE
 STMTCODE=NUMBER
END

SETUP
 STARTMTH=DATEOFFSET(SYSTEMDATE,0,1) 
END

SELECT
 ACCOUNT:CLOSEDATE='--/--/--' AND
 ANY TRACKING WITH (TRACKING:TYPE=0 AND TRACKING:USERCODE2<>1 AND TRACKING:EXPIREDATE='--/--/--')
END

PRINT TITLE="CU Members MONTHEND Update"
 HEADER=""
 STMTCODE=0
 FOR EACH SHARE
  DO
   IF SHARE:STATEMENTGROUP=1 THEN STMTCODE=1
   FOR EACH SHARE TRANSACTION WITH SHARE TRANSACTION:COMMENTCODE=0 AND
            SHARE TRANSACTION:POSTDATE>=STARTMTH
    DO
     IF SHARE TRANSACTION:REGECODE=1 THEN STMTCODE=1
    END
   UNTIL SHARE TRANSACTION:POSTDATE<STARTMTH OR STMTCODE=1
  END
 FOR EACH LOAN WITH LOAN:CLOSEDATE='--/--/--' AND LOAN:CHARGEOFFDATE='--/--/--'
  DO
   IF LOAN:STATEMENTGROUP=1 THEN STMTCODE=1
   FOR EACH LOAN TRANSACTION WITH LOAN TRANSACTION:COMMENTCODE=0 AND
            LOAN TRANSACTION:POSTDATE>=STARTMTH 
    DO
     IF LOAN TRANSACTION:REGECODE=1 THEN STMTCODE=1
    END
   UNTIL LOAN TRANSACTION:POSTDATE<STARTMTH OR STMTCODE=1
  END
 IF STMTCODE=1 THEN
  DO
   FOR EACH TRACKING WITH TRACKING:TYPE=0 AND TRACKING:EXPIREDATE='--/--/--'
    DO
     PRINT "ACCOUNT "+ACCOUNT:NUMBER+" MODIFY TRACKING LOC "
     PRINT TRACKING:LOCATOR
     NEWLINE
     PRINT " CHANGE USERCODE2 FROM "
     PRINT TRACKING:USERCODE2 
     PRINT " TO "
     PRINT STMTCODE
     NEWLINE
     PRINT " CHANGE USERDATE5 FROM "          [NEXT STATEMENT DATE]
     PRINT TRACKING:USERDATE5
     PRINT " TO "
     PRINT DATEOFFSET(SYSTEMDATE,0,31)
     NEWLINE
    END
  END
END

