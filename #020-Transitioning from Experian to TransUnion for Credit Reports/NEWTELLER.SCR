[NEWTELLER.SCR]
[10/31/05 GH ADDED NEW MAINPOPUP TO SCRIPT]
[11/17/05 GH ADDED SNAPSHOT.CLOSED TO DISPLAY INFORMATION ON CLOSED ACCOUNTS]
[08/30/06 GH ADDED POPUP FOR RISKASSESSMENT]
[09/27/06 GH REMOVED OPENDATE FOR RISK ASSESSMENT OF ACCOUNT:OPENDATE>='08/30/06' PER SANDIE]
[07/13/09 GH ADDED POPUP FOR BLANK PRIMARY ADDRESS]
[06/09/10 GH ADDED AUTO POPUP FOR OPTIN]
[07/19/11 GH MOVED CREDIT REPORT UPDATE AND PULL OUT OF SNAPSHOT.SUB]
[10/25/11 GH Added new Frank Dodd Fields - Database report date, Max/Min and 5th reason]
[12/06/11 GH Removed old coding no longer used and added popup for personal information change]
[10/25/12 GH Modified for new Identity scan codes]
[11/30/12 GH Excluded code 27 - Mobile Phone from popping up on Red Flag per Karen]
[06/26/13 GH Added Dormant Account Warning screen before Share and loan lost]
[02/12/14 GH Restrict all programs from running except specified ones the employe is on the account]
[06/09/14 GH Removed Red flage review for Category 2 - 23, 27 per Karen]
[06/24/15 GH Replace beacon and Red flag code with 2 new programs.]
[07/17/18 GH Moved SNAPSHOT.SUB to be the last program becuase too many people were escaping out of it and missing other programs.]
[10/18/18 GH Added new way to review tracking 12 - Red Falgs]

SUBROUTINE DEMAND

WINDOWS

TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 #INCLUDE "CREDIT.REPORT.DEF"
 #INCLUDE "GH.REDFLAG.UPDATES.DEF"
 #INCLUDE "RD.OUTPUT.DEF"
 OUTPUTCHANNEL3=NUMBER
 TMPCHR=CHARACTER
 BRNO=NUMBER
 FOUNDBUS=CHARACTER(1)
 POBOXONLY=CHARACTER(1)
 FOUNDPOBOX=CHARACTER(1)
 FOUNDADDR=CHARACTER(1)
 FOUNDSHLN=CHARACTER(1)
 FNDREDFLAG=CHARACTER(1)
 DORISKASSESSMENT=CHARACTER(1)
 FMEXPIRELOC=NUMBER               ARRAY(100)
 FMI=NUMBER
 FMU=NUMBER
 VL=NUMBER
 VC=NUMBER
 TMPV=CHARACTER
 FNDACCT=CHARACTER(1)
 UA=NUMBER
 ACCTNO=CHARACTER
 INITERROR=CHARACTER
 EXECERROR=CHARACTER
 CLR=NUMBER
 HASTEMPLATE=CHARACTER(1)
 W=NUMBER
 INUMBER=NUMBER
 IERROR=CHARACTER
 ILINE=CHARACTER
 IERROR2=CHARACTER
 DEBUGTEST=CHARACTER
 APPONLY=CHARACTER(1)
 APPI=NUMBER
 FIRSTNOTE=CHARACTER(1)
 NOTEI=NUMBER
 JI=NUMBER
 DOSOLICIT=CHARACTER(1)
 SOLICIATEDATE=DATE
 NI=NUMBER
 C=NUMBER
END

SETUP 
[ FILEOPEN("LETTER","TELLERTEST","READ",INUMBER,IERROR)       
  FILEREADLINE(INUMBER,ILINE,IERROR)
  IF LENGTH(ILINE)>0 THEN DEBUGTEST=SEGMENT(ILINE,1,1)
 FILECLOSE(INUMBER,IERROR)]
 DEBUGTEST="N"
 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~1~START~NEWTELLER.SCR "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"1~START Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
 FOR CLR=1 TO 5
  DO
   RSSN(CLR)=""
   RNAME(CLR)=""
   REASONCODES(CLR)=""
   NOHIT(CLR)=""
   ACTIVEDUTY(CLR)=""
   ADDRESSVAR(CLR)=""
   FRAUDALERT(CLR)=""
   SAFESCAN(CLR)=""
   RMAX(CLR)=0
   RMIN(CLR)=0
  END
 BRNO=GETDATANUMBER(GETCONSOLEBRANCH,SYSCONSOLENUM)
 LOOKUPDATECREDREP=SYSTEMDATE-90
 FOUNDBUS="N"
 POBOXONLY="N"
 IF ACCOUNT:CLOSEDATE='--/--/--' THEN FOUNDSHLN="Y"
 ELSE FOUNDSHLN="N"
 FNDACCT="N"
 ACCTNO=ACCOUNT:NUMBER
 HASTEMPLATE="N"
 FOR USER WITH NUMBER FORMAT("9999",SYSUSERNUMBER)
  DO
   IF CHARACTERSEARCH(USER:JOBFUNCTION,"BRANCH MANAGER")>0 OR 
      CHARACTERSEARCH(USER:JOBFUNCTION,"ABM")>0 THEN HASTEMPLATE="Y"
   FOR UA=1 TO 20
    DO
     IF USER:RESTRICTEDACCOUNTS:(UA)=ACCTNO THEN FNDACCT="Y"   
    END
  END
[======== Restrict all programs except these if employee part of account ============]
IF ACCOUNT:TYPE=97 THEN NEWLINE
 ELSE IF FNDACCT="Y" THEN                         
  DO   
   INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                              [**** CHECK PERSONAL INFORMATION CHANGES]
   EXECUTE("GH.PERSONAL.INFO.CHANGE",@ENVPARAMERRORTEXT)   
   IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)   
   INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                              [**** SHARE AND LOAN LIST POPUP]
   EXECUTE("SNAPSHOT.SUB",@ENVPARAMERRORTEXT)
   IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)   
  END
[======== End of Employee program section ============]
 ELSE IF ACCOUNT:NUMBER="0000090002" OR ACCOUNT:NUMBER="0000090003" [OR SYSUSERNUMBER=484] THEN
  DO 
   CALL DISPLAYIMPORTANTNOTE
   INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                            [Replaces BEACONSETUPCHECK ]
   EXECUTE("GH.LASTNOTES",@ENVPARAMERRORTEXT)
   IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)                             
  END
 ELSE IF SYSUSERNUMBER<>2 AND ACCOUNT:TYPE<>97 THEN                                              [EXCLUDE ALL POPUPS FOR USER 2 - EALERTS]
  DO
   IF ACCOUNT:TYPE<90 AND ACCOUNT:OPENDATE<>'--/--/--' THEN                                      [**** CHECK FOR LATEST BEACON DATA]
    DO        
     INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                          [Replaces BEACONSETUPCHECK ]
     EXECUTE("GH.NEW.CREDIT.REPORTS",@ENVPARAMERRORTEXT)
     IF SYSUSERNUMBER=484 AND @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)                                                         
    END
   IF ACCOUNT:TYPE=1 OR ACCOUNT:TYPE>=10 OR ANYWARNING(ACCOUNT,16) THEN FOUNDBUS="Y"
   ELSE IF LENGTH(NAME:FIRST)=0 THEN FOUNDBUS="Y"
   FOR EACH COMMENT WITH COMMENT:EXPIRATIONDATE='--/--/--'                                       [**** DISPLAY IMPORTANT NOTES]
    DO
     IF CHARACTERSEARCH(COMMENT:COMMENT,"DBA")>0 THEN FOUNDBUS="Y"
     IF UPPERCASE(COMMENT:COMMENT)="DO NOT ASK MEMBER FOR PRIMARY ADDRESS" THEN POBOXONLY="Y"
    END
   INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                            [**** CHECK PERSONAL INFORMATION CHANGES]
   EXECUTE("GH.PERSONAL.INFO.CHANGE",@ENVPARAMERRORTEXT)
   IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT) 
   FNDREDFLAG="N"
   FMU=0
   CALL CHECKTRACKING12
   IF FNDREDFLAG="Y"  THEN
    DO  
     INITSUBROUTINE(@ENVPARAMERRORTEXT)
     @ENVARGCHAR1="NEWTELLER"
     EXECUTE("GH.NEWSCORE",@ENVPARAMERRORTEXT)    
    END
   
   IF ACCOUNT:CLOSEDATE='--/--/--' AND ACCOUNT:TYPE<97  AND
      (ACCOUNT:BRANCH<>5 OR (ACCOUNT:BRANCH=5 AND ACCOUNT:OPENDATE<SYSTEMDATE-5)) THEN
    DO
     INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                          [**** UPDATE CONTACT INFO FOR WRG]
     EXECUTE("CB.VERIFY.CONTACT",@ENVPARAMERRORTEXT)
     IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)   
    END

   INITSUBROUTINE(INITERROR)
   IF SYSUSERNUMBER=484 AND INITERROR<>"" THEN POPUPMESSAGE(0,"INIT GH.LASTNOTES "+INITERROR) [*** CHECK NOTES AND COMMENTS ***]  
   EXECUTE("GH.LASTNOTES",INITERROR)
   IF SYSUSERNUMBER=484 AND INITERROR<>"" THEN POPUPMESSAGE(0,"EXECUTE GH.LASTNOTES "+INITERROR)

   IF ACCOUNT:CLOSEDATE='--/--/--' AND ACCOUNT:TYPE<90 AND 
      NOT ANYWARNING(ACCOUNT,4) AND NOT ANYWARNING(LOAN,4) AND       [CALL COLLECTIONS (ext 7105)]
      NOT ANYWARNING(ACCOUNT,8) AND NOT ANYWARNING(LOAN,8) AND       [BANKRUPTCY - FREEZE ACCOUNT]
      NOT ANYWARNING(ACCOUNT,19) AND NOT ANYWARNING(LOAN,19) AND     [GARNISHMENT]
      NOT ANYWARNING(ACCOUNT,30) AND NOT ANYWARNING(ACCOUNT,57) AND  [MEMBER DECEASED, Account in Dispute - Freeze Account]
      NOT ANYWARNING(ACCOUNT,54) AND NOT ANYWARNING(LOAN,54) AND     [BANKRUPTCY - DO NOT FREEZE ACCOUNT] 
      NOT ANYWARNING(ACCOUNT,62) AND NOT ANYWARNING(LOAN,62) AND     [Freeze/Levy Call Collections @ 7105]
      NOT ANYWARNING(ACCOUNT,19) AND NOT ANYWARNING(LOAN,19) THEN    [No Account Cross Selling] 
    DO
     SOLICIATEDATE=SYSTEMDATE-90
     DOSOLICIT="N"
     IF ACCOUNT:STATEMENTMAILCODE=0 OR NAME:EMAIL="" THEN DOSOLICIT="Y"
     IF DOSOLICIT="Y" THEN
      DO
       FOR EACH NOTE WITH NOTE:TEXT:1="INCENTIVE SOLICITATION"
        DO
         IF DATEVALUE(SEGMENT(NOTE:TEXT:2,1,8))>=SOLICIATEDATE THEN DOSOLICIT="N"
         ELSE
          DO
           FOR NI=4 TO 10
            DO
             IF CHARACTERSEARCH(UPPERCASE(NOTE:TEXT:(NI)),"NEVER ASK AGAIN")>0 THEN DOSOLICIT="N"
             ELSE IF (SEGMENT(NOTE:TEXT:(NI),1,14)="Next Solicit Date:" OR
                      SEGMENT(NOTE:TEXT:(NI),1,14)="Next Solicate Date:") AND
                     DATEVALUE(SEGMENT(NOTE:TEXT:(NI),16,23))>SYSTEMDATE THEN DOSOLICIT="N"
            END
          END
        END
        UNTIL DOSOLICIT="N" OR NOTE:ENTERDATE<'06/01/18' [Start date of new ACS program]
      END
     IF DOSOLICIT="Y" THEN
      DO
       INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                              [**** CHECK PERSONAL INFORMATION CHANGES]
       EXECUTE("GH.CROSS.SELL.SCR",@ENVPARAMERRORTEXT)
      END
    END

    [REMEMOVE THIS SECTION TO STOP UN-VERIFIED ADDRESS POPUP   GH ADDED 7/13/09]
   IF ACCOUNT:CLOSEDATE='--/--/--'  THEN
    DO 
     IF NAME:STREET="" THEN                                                             
      DO
       INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                          [**** POPUP FOR NEW ADDRESS INFORMATION]
       EXECUTE("CHANGE.ADDRESS.SCR",@ENVPARAMERRORTEXT)
       IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)
      END
     ELSE IF ACCOUNT:OPENDATE<>SYSTEMDATE  THEN
      DO
       INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                          [**** POPUP FOR UNVERIFIED ADDRESS CHANGES]
       EXECUTE("GH.TELLER.VERIFY.ADDRESS",@ENVPARAMERRORTEXT)
       IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)
      END
    END
   IF FOUNDSHLN="Y" THEN                                                                         [**** POPUP UP FOR PO BOX AS MAIN ADDRESS]
    DO
     IF POBOXONLY="N" THEN CALL CHECKFORPOBOX      [12/20/06 LOOK FOR PO BOX ONLY ACCOUNTS]
     IF FOUNDPOBOX="Y" AND FOUNDADDR="N" THEN
      DO
       INITSUBROUTINE(@ENVPARAMERRORTEXT)
       EXECUTE("NEWADDR.SCR",@ENVPARAMERRORTEXT)
       IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)
      END
     IF ACCOUNT:CLOSEDATE='--/--/--' AND                                                         [**** POPUP UP FOR RISK ASSESSMENT]
       (ACCOUNT:TYPE>=10 AND ACCOUNT:TYPE<=17) THEN
      DO
       DORISKASSESSMENT="Y"
       FOR EACH TRACKING WITH TRACKING:TYPE=37 AND TRACKING:EXPIREDATE='--/--/--'
        DO
         IF TRACKING:USERDATE1>=DATEOFFSET(SYSTEMDATE,-12,0) THEN DORISKASSESSMENT="N"
         IF LENGTH(TRACKING:USERCHAR18)=0 OR LENGTH(TRACKING:USERCHAR19)=0 THEN DORISKASSESSMENT="Y"
        END
       IF DORISKASSESSMENT="Y" THEN
        DO
         INITSUBROUTINE(@ENVPARAMERRORTEXT)
         EXECUTE("RISKASSESSMENT",@ENVPARAMERRORTEXT)
         IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)
        END
      END
    END

   IF ACCOUNT:CLOSEDATE='--/--/--' AND
      ACCOUNT:ACTIVITYDATE<=DATEOFFSET(SYSTEMDATE,-6,0) AND
      ACCOUNT:CORRESPONDDATE<=DATEOFFSET(SYSTEMDATE,-6,0) THEN
    DO
     INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                          [**** Dormant Account Popup]
     EXECUTE("GH.DORMANT.SCR",@ENVPARAMERRORTEXT)
     IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)
    END
    
   INITSUBROUTINE(@ENVPARAMERRORTEXT)                                                            [**** SHARE AND LOAN LIST POPUP]
   EXECUTE("SNAPSHOT.SUB",@ENVPARAMERRORTEXT)
   IF @ENVPARAMERRORTEXT<>"" THEN POPUPMESSAGE(2,@ENVPARAMERRORTEXT)
  END

 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~1~END~NEWTELLER.SCR "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"~1~END Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
END

PRINT TITLE="NEW TELLER SCREEN"
 SUPPRESSNEWLINE
END

TOTAL
END

PROCEDURE CHECKFORPOBOX
 FOUNDPOBOX="N"
 FOUNDADDR="N"
 FOR EACH NAME WITH NAME:EXPIRATIONDATE='--/--/--'
  DO
   IF CHARACTERSEARCH(NAME:STREET,"POBOX")>0 OR
      CHARACTERSEARCH(NAME:STREET,"PO BOX")>0 OR
      CHARACTERSEARCH(NAME:STREET,"P O BOX")>0 OR
      CHARACTERSEARCH(NAME:STREET,"P.O.BOX")>0 OR
      CHARACTERSEARCH(NAME:STREET,"P.O. BOX")>0 OR
      CHARACTERSEARCH(NAME:STREET,"P. O. BOX")>0 THEN FOUNDPOBOX="Y"
   ELSE FOUNDADDR="Y"
  END
END

PROCEDURE DISPLAYDORMANTWARNING
 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~1~H START~DISPLAYDORMANTWARNING "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"~1~D START Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
 HTMLVIEWOPEN(0)
 HTMLVIEWLINE("<html>")
 HTMLVIEWLINE("<head>")
 HTMLVIEWLINE("<title>Important Notes</title>")
 HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
 HTMLVIEWLINE("</head>")
 HTMLVIEWLINE("<body bgcolor=#FFFFFF text=#000000>")
 HTMLVIEWLINE("<font face=arial>")
 HTMLVIEWLINE("<form name=Dormant Warning METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
 HTMLVIEWLINE(" <table width=525 align=center>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("   <td><font size=6 color=RED><b>Warning!</b></font>")
 HTMLVIEWLINE("       <font size=5> Dormant Account Contact <u>will follow</u> the Share and Loan List.</td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("   <td>&nbsp;</td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("  <td><font size=5>Be sure to gather the information you use to verify the member to use on the ")
 HTMLVIEWLINE("      Dormant Account Contact form.</td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("   <td>&nbsp;</td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("   <td><font size=5>It is <b><u>very important</u></b> if you are in contact with the member that you ")
 HTMLVIEWLINE("       fill out the Dormant Account Contact information.</font></td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("   <td>&nbsp;</td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("   <td><font size=5>Do <b>NOT</b> Escape or Cancel out of the Share and Loan List screen.<br>")
 HTMLVIEWLINE("       Please use the <font color=red>NO RED ARROW - Click ME to Close</font> button in the ")
 HTMLVIEWLINE("       upper right corner of the screen.</font></td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("   <td>&nbsp;</td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE("  <tr>")
 HTMLVIEWLINE("  <td align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE='Continue'></td>")
 HTMLVIEWLINE("  </tr>")
 HTMLVIEWLINE(" </table>")
 HTMLVIEWLINE(" </form>")
 HTMLVIEWLINE("</body>")
 HTMLVIEWLINE("</html>")
 HTMLVIEWDISPLAY
 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~1~H END~DISPLAYDORMANTWARNING "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"~1~D END Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
END

PROCEDURE DISPLAYIMPORTANTNOTE
 if sysusernumber=484 then popupmessage(0," NEWTELLER")
 FIRSTNOTE="Y"
 FOR EACH NOTE WITH UPPERCASE(NOTE:TEXT:1)="IMPORTANT NOTE" AND 
  (NOTE:EXPIRATIONDATE='--/--/--' OR NOTE:EXPIRATIONDATE>SYSTEMDATE)
  DO
   IF FIRSTNOTE="Y" THEN
    DO
     HTMLVIEWOPEN(0)
     HTMLVIEWLINE("<html>")
     HTMLVIEWLINE("<head>")
     HTMLVIEWLINE("<title>Important Notes</title>")
     HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
     HTMLVIEWLINE("</head>")
     HTMLVIEWLINE("<body bgcolor=#FFFFFF text=#000000>")
     HTMLVIEWLINE("<font face=arial>")
     FIRSTNOTE="N"
    END
   HTMLVIEWLINE(" <table border=0 align=center WIDTH=875>")
   HTMLVIEWLINE("  <tr bgcolor=SILVER><td>")
   HTMLVIEWLINE("  <font color=RED SIZE=+4><B><center>"+NOTE:TEXT:1+"</center></B></font><br><BR>")
   HTMLVIEWLINE("<font SIZE=4>")
   FOR NOTEI=2 TO 20
    DO
     IF NOTE:TEXT:(NOTEI)<>"" THEN HTMLVIEWLINE(NOTE:TEXT:(NOTEI)+"<br>")
    END
   HTMLVIEWLINE("</font></td></tr>")
   HTMLVIEWLINE("<BR>")
  END
 IF FIRSTNOTE="N" THEN
  DO
   HTMLVIEWLINE("</TABLE>")
   HTMLVIEWLINE(" <font face=arial>")
   HTMLVIEWLINE("  <p>")
   HTMLVIEWLINE("   <form name=REDFLAGSCREEN method=POST action=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("    <div align=center>")
   HTMLVIEWLINE("    <INPUT TYPE=SUBMIT NAME=submitbutton VALUE='Continue'>")
   HTMLVIEWLINE("    </div>")
   HTMLVIEWLINE("   </form>")
   HTMLVIEWLINE("  </p>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
  END
END

PROCEDURE CHECKTRACKING12
 [POPUPMESSAGE(0,"CHECKTRACKING12")]
 FNDREDFLAG="N"
 FOR EACH TRACKING WITH TRACKING:TYPE=12 
  DO
   IF TRACKING:USERDATE1='--/--/--' AND 
      (TRACKING:EXPIREDATE='--/--/--' OR TRACKING:EXPIREDATE>SYSTEMDATE) THEN [**** POPUP FOR RED FLAG INFORMATION]
    DO
     TMPV=TRACKING:USERCHAR3
     [POPUPMESSAGE(0,"u3 "+TMPV)]
     WHILE LENGTH(TMPV)>0 AND FNDREDFLAG="N"
      DO
       C=CHARACTERSEARCH(TMPV,",")
       IF C=0 THEN 
        DO
         VC=VALUE(TMPV)
         TMPV=""
        END
       ELSE 
        DO
         VC=VALUE(SEGMENT(TMPV,1,C-1))
         TMPV=SEGMENT(TMPV,C+1,LENGTH(TMPV))
        END
       IF VC>0 AND VC<>4 THEN FNDREDFLAG="Y"
      END
     TMPV=TRACKING:USERCHAR4
     [POPUPMESSAGE(0,"u3 "+TMPV)]
     WHILE LENGTH(TMPV)>0 AND FNDREDFLAG="N"
      DO
       C=CHARACTERSEARCH(TMPV,",")
       IF C=0 THEN 
        DO
         VC=VALUE(TMPV)
         TMPV=""
        END
       ELSE 
        DO
         VC=VALUE(SEGMENT(TMPV,1,C-1))
         TMPV=SEGMENT(TMPV,C+1,LENGTH(TMPV))
        END
       IF VC>0 AND VC<>9 AND VC<>23 AND VC<>26 AND VC<>27 AND VC<>29 THEN FNDREDFLAG="Y"
      END
    END
  END
[POPUPMESSAGE(0,FNDREDFLAG)]
END
