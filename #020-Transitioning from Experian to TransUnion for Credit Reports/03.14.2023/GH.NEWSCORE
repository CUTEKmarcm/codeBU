[GH.NEWSCORE - Pulls Red flag information out of the credit reports]
[03/02/11 GH Modified to only show if the tracking record has more then Cat 1 #4 or Cat 2 # 9 per Karen]
[04/26/12 CB Modified Title to read clearly "Red Flag Review" and changed font to Arial]
[10/18/12 GH Modified fields for added safe scan codes]
[06/09/14 GH Removed Red flage review for Category 2 - 23, 27 per Karen]
[10/17/18 GH Added changes fre reading tracking 12]

SUBROUTINE DEMAND

WINDOWS 

TARGET=ACCOUNT

DEFINE
 INPUTCOUNT= NUMBER
 INPUTFORM= CHARACTER ARRAY(60) 
 TMPCHR=CHARACTER
 FNUMBER=NUMBER
 ERRORTEXT=CHARACTER
 FLINE=CHARACTER
 FERROR=CHARACTER
 REDFLAGINFO=CHARACTER             ARRAY(6,50,5)
 TRACKINGINFO=CHARACTER            ARRAY(50,6,50)
 TRACKINGLOC=NUMBER                ARRAY(50)
 CF=CHARACTER(1)                   ARRAY(50,6)
 TBEACON=NUMBER                    ARRAY(50)
 REVIEWDATE=DATE                   ARRAY(50)
 REVIEWCOMM=CHARACTER(40)          ARRAY(50)
 REVIEWERROR=CHARACTER(1)          ARRAY(50)
 FNDERROR=CHARACTER(1)
 A=NUMBER
 D=NUMBER
 I=NUMBER
 J=NUMBER
 K=NUMBER
 R=NUMBER
 P=NUMBER
 Q=NUMBER
 T=CHARACTER
 MM=NUMBER
 DD=NUMBER
 YY=NUMBER
 CY=NUMBER
 DV=DATE
 FNDI=CHARACTER(1)
 BC=CHARACTER
 CSSN=CHARACTER
 FIRST=CHARACTER(1)
 UPDATEERRORS=CHARACTER
 VC=NUMBER
 NEWTMPCHR=CHARACTER
 INUMBER=NUMBER
 IERROR=CHARACTER
 ILINE=CHARACTER
 IERROR2=CHARACTER
 DEBUGTEST=CHARACTER
 C=NUMBER
END

SETUP 
 [FILEOPEN("LETTER","TELLERTEST","READ",INUMBER,IERROR)       
  FILEREADLINE(INUMBER,ILINE,IERROR)
  IF LENGTH(ILINE)>0 THEN DEBUGTEST=SEGMENT(ILINE,1,1)
 FILECLOSE(INUMBER,IERROR)]
 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~6~START~GH.NEWSCORE "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"~6~START Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
 WHILELIMIT=900000000
 CY=FULLYEAR(SYSTEMDATE)-2000
 [IF SYSUSERNUMBER=484 THEN POPUPMESSAGE(0,"TOP")]
 FILEOPEN("LETTER","REDFLAG.CATEGORIES","READ",FNUMBER,FERROR)
 IF FERROR<>"" THEN PRINT "Error Opening REDFLAG.CATEGORIES - "+FERROR
 FILEREADLINE(FNUMBER,FLINE,FERROR)
 WHILE FERROR=""
  DO
   FILEREADLINE(FNUMBER,FLINE,FERROR)
   R=VALUE(SEGMENT(FLINE,1,2))
   IF LENGTH(FLINE)>0 AND SEGMENT(FLINE,1,1)<>"/" THEN
    DO
     IF R>0 THEN J=R
     IF SEGMENT(FLINE,1,8)="Category" THEN 
      DO
       I=VALUE(SEGMENT(FLINE,10,11))
       REDFLAGINFO(I,0,0)=SEGMENT(FLINE,15,LENGTH(FLINE))
      END
     ELSE 
      DO
       IF R>0 THEN K=1
       ELSE K=K+1
       REDFLAGINFO(I,J,K)=SEGMENT(FLINE,4,LENGTH(FLINE))
       [IF SYSUSERNUMBER=484 THEN POPUPMESSAGE(0,FORMAT("9",I)+" "+FORMAT("9",J)+" "+FORMAT("9",K)+" "+REDFLAGINFO(I,J,K))]
      END
    END
  END
 FILECLOSE(FNUMBER,FERROR)
 [IF SYSUSERNUMBER=484 THEN POPUPMESSAGE(0,"CLOSE")]
 FOR A=0 TO 49
  DO
   REVIEWDATE(A)='--/--/--'
   REVIEWCOMM(A)=""
   REVIEWERROR(A)=""
   TRACKINGLOC(A)=0
   FOR I=1 TO 6
    DO
     FOR J=1 TO 49
      DO
       TRACKINGINFO(A,I,J)=""
      END
    END
  END
 A=0
 I=0
 J=0
 K=0
 R=0
 [IF SYSUSERNUMBER=484 THEN POPUPMESSAGE(0,"BEFORE")]
 FOR EACH TRACKING WITH TRACKING:TYPE=12 AND TRACKING:USERDATE1='--/--/--' AND 
                       (TRACKING:EXPIREDATE='--/--/--' OR TRACKING:EXPIREDATE>SYSTEMDATE)
  DO
   I=1
   FIRST="Y"
   [IF SYSUSERNUMBER=484 THEN POPUPMESSAGE(0,"I  "+FORMAT("99",I))]
   TMPCHR=""
   NEWTMPCHR=""
   NEWTMPCHR=TRACKING:USERCHAR3
   WHILE LENGTH(NEWTMPCHR)>0
    DO
     C=CHARACTERSEARCH(NEWTMPCHR,",")
     IF C=0 THEN 
      DO
       VC=VALUE(NEWTMPCHR)
       NEWTMPCHR=""
      END
     ELSE 
      DO
       VC=VALUE(SEGMENT(NEWTMPCHR,1,C-1))
       NEWTMPCHR=SEGMENT(NEWTMPCHR,C+1,LENGTH(NEWTMPCHR))
      END
     IF VC>0 AND VC<>4 THEN
      DO
       IF LENGTH(TMPCHR)>0 THEN TMPCHR=TMPCHR+","
       TMPCHR=TMPCHR+FORMAT("99",VC)
      END
    END
   IF LENGTH(TMPCHR)>0 THEN CALL GETTRACKINGINFO
   I=2
   NEWTMPCHR=TRACKING:USERCHAR4
   WHILE LENGTH(NEWTMPCHR)>0
    DO
     C=CHARACTERSEARCH(NEWTMPCHR,",")
     IF C=0 THEN 
      DO
       VC=VALUE(NEWTMPCHR)
       NEWTMPCHR=""
      END
     ELSE 
      DO
       VC=VALUE(SEGMENT(NEWTMPCHR,1,C-1))
       NEWTMPCHR=SEGMENT(NEWTMPCHR,C+1,LENGTH(NEWTMPCHR))
      END
     IF VC>0 AND VC<>9 AND VC<>23 AND VC<>26 AND VC<>27 AND VC<>29 THEN
      DO
       IF LENGTH(TMPCHR)>0 THEN TMPCHR=TMPCHR+","
       TMPCHR=TMPCHR+FORMAT("99",VC)
      END
    END
   IF LENGTH(TMPCHR)>0 THEN CALL GETTRACKINGINFO
  END   
 FOR R=1 TO A
  DO
   FOR EACH NAME WITH NAME:SSN=TRACKINGINFO(R,0,1) AND TRACKINGINFO(R,0,3)=""
    DO
     TRACKINGINFO(R,0,3)=NAME:LONGNAME
     TRACKINGINFO(R,0,4)=FORMAT("99/99/99",NAME:BIRTHDATE)
    END
   FOR EACH SHARE
    DO
     FOR EACH SHARE NAME WITH SHARE NAME:SSN=TRACKINGINFO(R,0,1) AND TRACKINGINFO(R,0,3)=""
      DO
       TRACKINGINFO(R,0,3)=SHARE NAME:LONGNAME
       TRACKINGINFO(R,0,4)=FORMAT("99/99/99",SHARE NAME:BIRTHDATE)
      END
    END
   FOR EACH LOAN
    DO
     FOR EACH LOAN NAME WITH LOAN NAME:SSN=TRACKINGINFO(R,0,1) AND TRACKINGINFO(R,0,3)=""
      DO
       TRACKINGINFO(R,0,3)=LOAN NAME:LONGNAME
       TRACKINGINFO(R,0,4)=FORMAT("99/99/99",LOAN NAME:BIRTHDATE)
      END
    END
[TODO:]FOR EACH TRACKING WITH TRACKING:TYPE=31 
    DO
     IF TRACKING:USERCHAR1=TRACKINGINFO(R,0,1) THEN TBEACON(R)=TRACKING:USERNUMBER1
     ELSE IF TRACKING:USERCHAR2=TRACKINGINFO(R,0,1) THEN TBEACON(R)=TRACKING:USERNUMBER2
     ELSE IF TRACKING:USERCHAR3=TRACKINGINFO(R,0,1) THEN TBEACON(R)=TRACKING:USERNUMBER3
     ELSE IF TRACKING:USERCHAR4=TRACKINGINFO(R,0,1) THEN TBEACON(R)=TRACKING:USERNUMBER4
     ELSE IF TRACKING:USERCHAR5=TRACKINGINFO(R,0,1) THEN TBEACON(R)=TRACKING:USERNUMBER5
    END
  END
 CALL DOHTMLDISPLAY
 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~6~END~GH.NEWSCORE "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"~6~END Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
END

PRINT TITLE="Credit Report Review"
 SUPPRESSNEWLINE
END

PROCEDURE GETTRACKINGINFO
 IF FIRST="Y" THEN
  DO
   A=A+1
   TRACKINGLOC(A)=TRACKING:LOCATOR
   TRACKINGINFO(A,0,1)=TRACKING:USERCHAR1 
   TRACKINGINFO(A,0,2)=ACCOUNT:NUMBER
   TRACKINGINFO(A,0,5)=FORMAT("99/99/99",TRACKING:CREATIONDATE)
   TRACKINGINFO(A,0,6)=FORMAT("99/99/99",TRACKING:USERDATE1)
   FIRST="N"
  END
 CF(A,I)="Y"
 WHILE LENGTH(TMPCHR)>0
  DO
   K=LENGTH(TMPCHR)
   IF K<=2 THEN 
    DO
     TRACKINGINFO(A,I,VALUE(TMPCHR))="Y" 
     TMPCHR=SEGMENT(TMPCHR,D+1,K)
     TMPCHR=""
    END
   ELSE 
    DO   
     WHILE SEGMENT(TMPCHR,1,1)=" " OR SEGMENT(TMPCHR,1,1)=","
      DO
       TMPCHR=SEGMENT(TMPCHR,2,LENGTH(TMPCHR))
      END
     IF LENGTH(TMPCHR)>0 THEN
      DO
       D=CHARACTERSEARCH(TMPCHR,",")                              [SAFE SCAN CODE SEPARATOR]
       IF D>0 THEN 
        DO
         TRACKINGINFO(A,I,VALUE(SEGMENT(TMPCHR,1,D-1)))="Y"
         TMPCHR=SEGMENT(TMPCHR,D+1,K)
         K=LENGTH(TMPCHR)
        END
       D=CHARACTERSEARCH(TMPCHR,"-")
       IF D>0 THEN
        DO
         Q=VALUE(SEGMENT(TMPCHR,1,D-1))
         TMPCHR=SEGMENT(TMPCHR,D+1,K)
         K=LENGTH(TMPCHR)
         IF K<=2 THEN R=VALUE(TMPCHR)
         ELSE
          DO
           D=CHARACTERSEARCH(TMPCHR,",")
           R=VALUE(SEGMENT(TMPCHR,1,D-1))
          END
         FOR P=Q TO R
          DO
           TRACKINGINFO(A,I,P)="Y"
          END
         TMPCHR=SEGMENT(TMPCHR,D+1,K)
         K=LENGTH(TMPCHR)
        END
      END
    END
  END
END

PROCEDURE DOHTMLDISPLAY
 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~6~H START~DOHTMLDISPLAY "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"~6~H START Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
 FNDERROR="Y"
 WHILE FNDERROR="Y"
  DO
   BC=""
   FNDERROR="N"
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Red Flag Review</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#FFFFFF text=#000000>") 
   HTMLVIEWLINE("<font face=arial>")
   HTMLVIEWLINE("<form name=OPTIN METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE(" <p align=center><font size=9><b><FONT COLOR=RED>CAUTION: Red Flag Warning</b></font></p>")
   HTMLVIEWLINE(" <p align=center><font size=6 color=RED><b><u>DO NOT</u> click the red arrow or hit the escape key.</b></font><br>")
   HTMLVIEWLINE("    <font size=4>Scroll to the bottom and use the Submit Review / Close Window button.</font></p>")   
   HTMLVIEWLINE(" <table border=5 WIDTH=850 align=center bordercolor=red>")
   HTMLVIEWLINE("   <tr>") 
   HTMLVIEWLINE("    <td>")             
   IF A=0 THEN
    DO     
     HTMLVIEWLINE("<br><br><br><br>")
     HTMLVIEWLINE(" <table border=1 WIDTH=850 align=center bgcolor=#99CCFF>")
     HTMLVIEWLINE("   <tr>") 
     HTMLVIEWLINE("    <td align=center><font size=+2 color=red><b>No Open Red Flag Tracking Found to Review.</b></font></td>")
     HTMLVIEWLINE("   </tr>") 
     HTMLVIEWLINE(" </table>") 
    END      
   ELSE
    DO                                                                                                               
     FOR R=1 TO A
      DO
       MM=VALUE(SEGMENT(TRACKINGINFO(R,0,4),1,2))
       DD=VALUE(SEGMENT(TRACKINGINFO(R,0,4),4,5))
       YY=VALUE(SEGMENT(TRACKINGINFO(R,0,4),7,10))
       IF YY>=00 AND YY<=CY THEN YY=2000+YY
       ELSE YY=1900+YY
       DV=DATE(MM,DD,YY)
       CSSN=SEGMENT(TRACKINGINFO(R,0,1),1,3)+"-"+SEGMENT(TRACKINGINFO(R,0,1),4,5)+"-"+SEGMENT(TRACKINGINFO(R,0,1),6,9)
       IF DV>DATEOFFSET(SYSTEMDATE,-12*18,0) THEN BC="PINK"
       ELSE IF BC="" OR BC="#CCFFF" THEN BC="#99CCFF"
       ELSE BC="#E6E6E6"
       HTMLVIEWLINE(" <table border=1 WIDTH=850 align=center bgcolor="+BC+">")
       HTMLVIEWLINE("   <tr>") 
       HTMLVIEWLINE("     <td colspan=2>Name: "+TRACKINGINFO(R,0,3)+"</td>")
       HTMLVIEWLINE("     <td colspan=2>SSN: "+CSSN+"</td>")
       HTMLVIEWLINE("   </tr>")
       HTMLVIEWLINE("   <tr>") 
       HTMLVIEWLINE("     <td colspan=2>Birth Date: "+TRACKINGINFO(R,0,4)+"</td>")
[TODO:]HTMLVIEWLINE("     <td colspan=2>Score: "+FORMAT("#999",TBEACON(R))+"</td>") 
       HTMLVIEWLINE("   </tr>")
       HTMLVIEWLINE("   <tr>") 
       HTMLVIEWLINE("     <td colspan=2>Creation Date: "+TRACKINGINFO(R,0,5)+"</td>")
       HTMLVIEWLINE("     <td colspan=2>Review Date: "+TRACKINGINFO(R,0,6)+"</td>")
       HTMLVIEWLINE("   </tr>")
       HTMLVIEWLINE("   <tr>")
       HTMLVIEWLINE("     <td colspan=4>&nbsp;</td>")
       HTMLVIEWLINE("   </tr>")
       HTMLVIEWLINE("   <tr>")
       HTMLVIEWLINE("     <td colspan=4><font color=red><b>Red Flag Items</b></td>")
       HTMLVIEWLINE("   </tr>")
       FOR I=1 TO 6
        DO
         IF CF(R,I)="Y" THEN
          DO
           FNDI="N"
           HTMLVIEWLINE("   <tr>")
           HTMLVIEWLINE("     <td colspan=4><FONT COLOR=MAROON><B>Category "+FORMAT("9",I)+" - ")
           HTMLVIEWLINE(          REDFLAGINFO(I,0,0)+"</B></FONT></td>")
           HTMLVIEWLINE("   </tr>")
           FOR J=1 TO 49
            DO
             IF TRACKINGINFO(R,I,J)="Y"  THEN
              DO
               FNDI="Y"
               HTMLVIEWLINE("<tr>") 
               HTMLVIEWLINE("<td width=20%>&nbsp;</td>")
               HTMLVIEWLINE("  <td colspan=3><B>"+FORMAT("99",J)+".</B> ")
               FOR K=1 TO 5
                DO
                 IF LENGTH(REDFLAGINFO(I,J,K))>0 THEN
                  DO
                   HTMLVIEWLINE(REDFLAGINFO(I,J,K)+"<BR>")
                  END
                END
               HTMLVIEWLINE("</tr>")
              END
            END
           IF FNDI="N" THEN
            DO
             HTMLVIEWLINE("<tr>") 
             HTMLVIEWLINE(" <td>&nbsp;</td>")
             HTMLVIEWLINE(" <td colspan=3>No Red Flags for this Category</td>")
             HTMLVIEWLINE("</tr>")
            END
          END
        END
       HTMLVIEWLINE("<tr>") 
       HTMLVIEWLINE(" <td bgcolor=white COLSPAN=4 align=left>")
       IF REVIEWERROR(R)="Y" THEN HTMLVIEWLINE("<font color=RED><b>")
       HTMLVIEWLINE("    <input type=checkbox name="+FORMAT("99",R)+"REVIEW value="+FORMAT("99",R))
       IF REVIEWDATE(R)=SYSTEMDATE THEN HTMLVIEWLINE(" checked")
       HTMLVIEWLINE("    >Check here if information for "+TRACKINGINFO(R,0,3)+" has been reviewed and cleared.")
       IF REVIEWDATE(R)='--/--/--' AND REVIEWCOMM(R)<>"" THEN HTMLVIEWLINE("<BR><font size=-1>(** Not Checked **)</font>")
       HTMLVIEWLINE("    <br>")
       HTMLVIEWLINE("    Review Comment: <input name="+FORMAT("99",R)+"REVIEWCOMM type=text VALUE='"+REVIEWCOMM(R)+"' size=45 maxlength=40><BR>")
       IF REVIEWDATE(R)=SYSTEMDATE AND REVIEWCOMM(R)="" THEN HTMLVIEWLINE("** <u>")
       HTMLVIEWLINE("   <font size=-1>Comment is required if the Reviewed box is checked.</font>")
       IF REVIEWDATE(R)=SYSTEMDATE AND REVIEWCOMM(R)="" THEN HTMLVIEWLINE("</u> **")
       IF REVIEWERROR(R)="Y" THEN HTMLVIEWLINE("</b></font>")
       HTMLVIEWLINE("  </td>")
       HTMLVIEWLINE("</tr>")
       HTMLVIEWLINE("</table>") 
      END
    END
   HTMLVIEWLINE("   </tr>") 
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  <FORM NAME=REDFLAGSCREEN METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<center>")
   HTMLVIEWLINE(" <P>")
   HTMLVIEWLINE("  <div align=center>")
   HTMLVIEWLINE("   <INPUT TYPE=SUBMIT NAME=SUBMITBUTTON VALUE='Submit Review / Close Window'")
   HTMLVIEWLINE("  </div>")
   HTMLVIEWLINE("  </FORM>")
   HTMLVIEWLINE(" </P>")
   HTMLVIEWLINE("</center>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("<html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
   FOR R=1 TO A
    DO
     IF (REVIEWDATE(R)=SYSTEMDATE AND REVIEWCOMM(R)="") OR
        (REVIEWDATE(R)='--/--/--' AND REVIEWCOMM(R)<>"") THEN 
      DO
       FNDERROR="Y"
       REVIEWERROR(R)="Y"
      END
    END
  END
 ILINE=FORMAT("99/99/99",SYSTEMDATE)+"~"+FORMAT("99:99",SYSACTUALTIME)+"~"+ACCOUNT:NUMBER+"~"+FORMAT("9999",SYSUSERNUMBER)+"~6~H END~DOHTMLDISPLAY "
 IF DEBUGTEST="Y" THEN
  DO
   FILEOPEN("LETTER","FINDBUG","APPEND",INUMBER,IERROR)       
    FILEWRITELINE(INUMBER,ILINE,IERROR)
    IF IERROR<>"" THEN FILEWRITELINE(INUMBER,"~6~H END Error:~"+IERROR,IERROR2)
   FILECLOSE(INUMBER,IERROR)
  END
 CALL DOREVIEWFM
END

PROCEDURE LOADRESULTS
 INPUTCOUNT=0
 WHILE (INPUTFORM(INPUTCOUNT)<>"EOD")
  DO
   INPUTCOUNT=INPUTCOUNT+1
   INPUTFORM(INPUTCOUNT)=ENTERLINE(0)
  END
END


PROCEDURE SETVARIABLES
 FOR R=1 TO A
  DO
   REVIEWERROR(R)=""
   REVIEWDATE(R)='--/--/--'
   REVIEWCOMM(R)=""
  END
 FOR I=1 TO INPUTCOUNT
  DO
   TMPCHR=INPUTFORM(I)
   R=VALUE(SEGMENT(TMPCHR,1,2))
   IF SEGMENT(TMPCHR,3,9)="REVIEW="            THEN REVIEWDATE(R)=SYSTEMDATE
   ELSE IF SEGMENT(TMPCHR,3,13)="REVIEWCOMM="  THEN REVIEWCOMM(R)=SEGMENT(TMPCHR,14,LENGTH(TMPCHR))
  END
END

PROCEDURE DOREVIEWFM
 UPDATEERRORS=""
 FOR R=1 TO A
  DO
   UPDATEERRORS=""
   IF REVIEWDATE(R)=SYSTEMDATE THEN
    DO
     FMPERFORM REVISE TRACKING LOC TRACKINGLOC(R) (0,0,ERRORTEXT)
      DO
       SET USERDATE1 TO SYSTEMDATE
      END
     IF ERRORTEXT<>"" THEN UPDATEERRORS=FORMAT("99",R)+" Tracking Revision:"+ERRORTEXT+" "
     FMPERFORM CREATE NOTE BEFOREFIRST (0,0,ERRORTEXT)
      DO
       SET CODE TO 0
       SET TEXT:1 TO "Review Red Flag Tracking: "+FORMAT("9999",TRACKINGLOC(R))
       SET TEXT:2 TO "For: "+TRACKINGINFO(R,0,3)
       SET TEXT:3 TO "Reviewed by: "+SYSUSERNAME(SYSUSERNUMBER)
       SET TEXT:4 TO REVIEWCOMM(R)
      END
     IF ERRORTEXT<>"" THEN 
      DO
       IF LENGTH(UPDATEERRORS)>0 THEN UPDATEERRORS=UPDATEERRORS+" - "
       ELSE UPDATEERRORS=FORMAT("99",R)+": "
       UPDATEERRORS=UPDATEERRORS+"Note Creation: "+ERRORTEXT
      END
     IF LENGTH(UPDATEERRORS)>0 THEN POPUPMESSAGE(2,"Error Updating tracking: "+UPDATEERRORS+" "+ERRORTEXT)
    END
  END
END

