[
---------------------------------------------------------------
 Copyright 2021 Austin Telco Federal Credit Union
---------------------------------------------------------------
Repgen Name: ORDERCARDS
Type       : Demand
Programmer : Franklin Johnson 
Date       : 2/23/20
Request #  : 26094
Requestor  : Josh Boggs
Department : Card Services
Summary    : Program to capture cards and/or to mail new cards
             to member.

---------------------------------------------------------------
Requires   :

---------------------------------------------------------------
Revisions  : 08/23/21 JJ Modified code to generate random card numbers.

---------------------------------------------------------------
Notes      : 

---------------------------------------------------------------
]
SUBROUTINE DEMAND WINDOWS

TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 CARDNBR=CHARACTER
 JTMEMBER=CHARACTER
 INITERROR=CHARACTER
 EXECERROR=CHARACTER
 OUTLINE=CHARACTER
 FNUMBER=NUMBER
 FLINE=CHARACTER
 FERROR=CHARACTER
 OK=CHARACTER(1)
 ACCTNO=CHARACTER(10)
 RESTRICEDACCOUNT=CHARACTER(1)
 UA=NUMBER
 ERRORTEXT=CHARACTER
 MM=NUMBER
 YY=NUMBER
 PLOC=NUMBER
 PNAME=CHARACTER
 PLAST=CHARACTER
 PSUFFIX=CHARACTER
 PFIRST=CHARACTER
 PMIDDLE=CHARACTER
 PTITLE=CHARACTER
 PDOB=DATE
 PSSN=CHARACTER
 PEXTRA=CHARACTER
 PSTREET=CHARACTER
 PCITY=CHARACTER
 PSTATE=CHARACTER
 PZIP=CHARACTER
 PHOME=CHARACTER
 PWORK=CHARACTER
 PCELL=CHARACTER
 PPHONETYPE=NUMBER
 PADDRESSTYPE=NUMBER
 PLASTADDRESSCHANGE=DATE
 PSSNTYPE=NUMBER
 JTNAME=CHARACTER
 JTLAST=CHARACTER
 JTSUFFIX=CHARACTER
 JTFIRST=CHARACTER
 JTMIDDLE=CHARACTER
 JTTITLE=CHARACTER
 JTDOB=DATE
 JTSSN=CHARACTER
 JTEXTRA=CHARACTER
 JTSTREET=CHARACTER
 JTCITY=CHARACTER
 JTSTATE=CHARACTER
 JTZIP=CHARACTER
 JTHOME=CHARACTER
 JTWORK=CHARACTER
 JTCELL=CHARACTER
 JTPHONETYPE=NUMBER
 JTADDRESSTYPE=NUMBER
 JTSSNTYPE=NUMBER
 JTNAMETYPE=NUMBER
 INPUTCOUNT= NUMBER
 INPUTFORM=CHARACTER        ARRAY(60)  
 I=NUMBER
 TMPCHR=CHARACTER
 CHOOSETYPE=CHARACTER
 CRDINFO=CHARACTER          ARRAY(50,9)
 CRDTYPE=NUMBER             ARRAY(50,2)
 CRDDATE=DATE               ARRAY(50)
 CRDCNTS=NUMBER             ARRAY(50)  
 CRDEXP=DATE                ARRAY(50)
 CRDLASTADDRCHANGE=DATE     ARRAY(50)
 NEWCRD=CHARACTER           ARRAY(50) 
 CRDCHECK=CHARACTER         ARRAY(50)
 C=NUMBER
 II=NUMBER
 FNDREPLACE=CHARACTER(1)
 DONE=CHARACTER(1)
 CARDTOREPLACE=CHARACTER
 ACTIONTOTAKE=CHARACTER
 CARDTYPE=NUMBER
 CARDLOC=NUMBER  
 CDESCRIPTION=CHARACTER
 CSAVID=CHARACTER
 CCHKID=CHARACTER
 CLOCID=CHARACTER
 CCREDITCARDID=CHARACTER
 CACTIVEDATE=DATE
 CEXPIRATIONDATE=DATE
 CATMCOUNTLIMIT=NUMBER
 CAUTHLIMIT=MONEY
 CAUTHCOUNTLIMIT=NUMBER
 CBILLPAYLIMIT=MONEY
 CBILLPAYCOUNTLIMIT=NUMBER
 CBILLPAYMAX=MONEY
 CCREDDEBCOUNTLIMIT=NUMBER
 CCREDDEBLIMIT=MONEY
 CDEPCOUNTLIMIT=NUMBER
 CDEPLIMIT=MONEY
 CDEPMAX=MONEY
 CEXTRAEMBOSS=CHARACTER
 CLIMITAMOUNT=MONEY
 CLIMITDAYS=NUMBER
 CMISCLIMIT=MONEY
 CMISCCOUNTLIMIT=NUMBER
 CNAMETYPE=NUMBER
 CPOSLIMIT=MONEY
 CPOSCOUNTLIMIT=NUMBER
 CREISSUEMONTHS=NUMBER
 CSINGLEUSAGEMAX=MONEY
 CUSAGELIMIT=MONEY
 CWDCHECKLIMIT=MONEY
 CWDCHECKCOUNTLIMIT=NUMBER
 CWDCHECKMAX=MONEY
 CXFERCOUNTLIMIT=NUMBER
 CXFERLIMIT=MONEY
 CXFERMAX=MONEY
 OLDLOCATOR=NUMBER
 SEQNBR=NUMBER
 RESULT=NUMBER
 PARITY=NUMBER
 DIGIT=NUMBER
 RESULT2=NUMBER
 LP=NUMBER
 FILENAME=CHARACTER
 CN=CHARACTER(6)
 FILELOCKED=NUMBER          [Locked flag, true if successfully locked]
 FILENUMBER=NUMBER          [File Number of above for reading purposes]
 LOCKFILENUMBER=NUMBER      [File Number of Locked file]
 CURRFILENAME=CHARACTER
 PROCESSOK=NUMBER
 TRUE=1
 FALSE=0
 ERROR=CHARACTER
 TOTCHECKS=NUMBER
 CARDSTATUS=NUMBER
 PRIMENAME=CHARACTER
 LNAME=CHARACTER
 LGNAME=CHARACTER
 WHICHMENU=CHARACTER
 BADREPLACE=NUMBER
 RETRIEVECARD=CHARACTER
 TMPCHAR=CHARACTER
 BUSNAME=CHARACTER
 NEWBUS=CHARACTER
 TOT30=NUMBER
 TOT40=NUMBER
 TOT41=NUMBER
 FND40=CHARACTER(1)
 FND41=CHARACTER(1)
 AP=NUMBER
 NEWNAMETYPE=CHARACTER
 NEWNAMELOC=NUMBER
 LNID=CHARACTER
 SHID=CHARACTER
 FNDCARDNOTE=CHARACTER(1)
 TEXT2=CHARACTER
 OTHERREASON=CHARACTER
 STATUSREASON=CHARACTER   ARRAY(20)
 CAPTUREREASON=CHARACTER
 NEWNOTE2=CHARACTER
 BLOCKCODE=CHARACTER(1)
 BRNO=NUMBER
 FNDTRACK80=CHARACTER(1)
 LOANREF=CHARACTER
 NEWTYPE=NUMBER
 TNAME=CHARACTER
 JTLASTADDRESSCHANGE=DATE
 VANSWER=NUMBER
 BACK30=DATE
END

SETUP
 BRNO=GETDATANUMBER(GETCONSOLEBRANCH,SYSCONSOLENUM)
 CREISSUEMONTHS=48
 STATUSREASON(1)="Card Lost"
 STATUSREASON(2)="Card Stolen"
 STATUSREASON(3)="Fraud On Card"
 STATUSREASON(4)="Never Received New Card"
 STATUSREASON(5)="Merchant Data Breach"
 STATUSREASON(20)="Other Reason"
 BACK30=SYSTEMDATE-30
 PRIMENAME=NAME:LONGNAME
 IF NAME:TYPE=1 AND ACCOUNT:TYPE>=10 THEN BUSNAME=NAME:LONGNAME
 FOR EACH NAME WITH NAME:TYPE=0
  DO
   PLOC=NAME:LOCATOR
   PNAME=NAME:FIRST+" "+NAME:LAST
   PLAST=NAME:LAST
   PSUFFIX=NAME:SUFFIX
   PFIRST=NAME:FIRST
   PMIDDLE=NAME:MIDDLE
   PTITLE=NAME:TITLE
   PDOB=NAME:BIRTHDATE
   PSSN=NAME:SSN
   PSSNTYPE=NAME:SSNTYPE
   PEXTRA=NAME:EXTRAADDRESS
   PSTREET=NAME:STREET
   PCITY=NAME:CITY
   PSTATE=NAME:STATE
   PZIP=NAME:ZIPCODE
   PHOME=NAME:HOMEPHONE
   PPHONETYPE=NAME:PHONETYPE
   PWORK=NAME:WORKPHONE
   PCELL=NAME:MOBILEPHONE
   PADDRESSTYPE=NAME:ADDRESSTYPE
   PLASTADDRESSCHANGE=NAME:ADDRFMLASTDATE
  END
 FOR EACH NAME WITH NAME:TYPE=2 AND NAME:EXPIRATIONDATE='--/--/--' AND 
                    NAME:MAILOVERRIDE=1 AND NAME:STREET<>"" AND NAME:CITY<>"" AND
                    NAME:STATE<>"" AND NAME:ZIPCODE<>""
  DO
   PLOC=NAME:LOCATOR
   PNAME=NAME:FIRST+" "+NAME:LAST
   PLAST=NAME:LAST
   PSUFFIX=NAME:SUFFIX
   PFIRST=NAME:FIRST
   PMIDDLE=NAME:MIDDLE
   PTITLE=NAME:TITLE
   PDOB=NAME:BIRTHDATE
   PSSN=NAME:SSN
   PSSNTYPE=NAME:SSNTYPE
   PEXTRA=NAME:EXTRAADDRESS
   PSTREET=NAME:STREET
   PCITY=NAME:CITY
   PSTATE=NAME:STATE
   PZIP=NAME:ZIPCODE
   PHOME=NAME:HOMEPHONE
   PPHONETYPE=NAME:PHONETYPE
   PWORK=NAME:WORKPHONE
   PCELL=NAME:MOBILEPHONE
   PADDRESSTYPE=NAME:ADDRESSTYPE
   PLASTADDRESSCHANGE=NAME:ADDRFMLASTDATE
  END
 TNAME=""
 CARDTYPE=0
[======== Restrict all programs except these if employee part of account ============]
 ACCTNO=ACCOUNT:NUMBER
 RESTRICEDACCOUNT="N"
 IF (ACCOUNT:NUMBER="0000045831" AND SYSUSERNUMBER<>1619 AND SYSUSERNUMBER<>1250 AND SYSUSERNUMBER<>23 AND SYSUSERNUMBER<>159) OR ACCOUNT:TYPE=90 OR 
     ACCOUNT:TYPE=96 OR ACCOUNT:TYPE=98 THEN
  DO
   POPUPMESSAGE(1,"PROGRAM NOT ALLOWED ON THIS ACCOUNT")
   TERMINATE
  END
 FOR USER WITH NUMBER FORMAT("9999",SYSUSERNUMBER)
  DO
   TMPCHAR=USER:JOBFUNCTION
   FOR UA=1 TO 20
    DO
     IF USER:RESTRICTEDACCOUNTS:(UA)=ACCTNO THEN RESTRICEDACCOUNT="Y"  
    END
  END
 FMPERFORM REVISE ACCOUNT (1,0,ERRORTEXT)
  DO
   SET MEMBERGROUP TO ACCOUNT:MEMBERGROUP
  END
 IF ERRORTEXT<>"" THEN RESTRICEDACCOUNT="Y" 
[======== End of Employee program section                               ============]
 IF RESTRICEDACCOUNT="Y" THEN POPUPMESSAGE(1,"ACCOUNT RESTRCTED")
 ELSE
  DO
   CALL VERIFYADDRESS
   IF VANSWER=2 THEN POPUPMESSAGE(1,"Please correct address and then re-run this program.")
   ELSE IF VANSWER=1 THEN
    DO
     FMPERFORM REVISE NAME LOC PLOC (0,0,ERRORTEXT)
      DO
       SET LASTADDRVERIFDATE TO SYSTEMDATE
      END
     ACTIONTOTAKE=""
     CALL GETCARDS
     CALL DISPLAYCHOICES
     IF CHOOSETYPE<>"CANCEL" THEN CALL MAINSCREEN
    END
  END
END

PRINT TITLE="ORDERCARDS"
  SUPPRESSNEWLINE       
END


PROCEDURE DISPLAYCHOICES
 CHOOSETYPE=""
 WHILE CHOOSETYPE="" AND CARDTYPE=0 
  DO
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Card Capture and Mailing Selections</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=#000000>")
   HTMLVIEWLINE(" <p align=center><font size=6 color=NAVY><b>Card Capture and Mailing Selections</b></font></p>")
   HTMLVIEWLINE(" <form name=ORDERCARDS METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=500 border=1 align=center>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td colspan=2><font size=4 color=RED>*** This is <u>NOT</u> Instant Issue ***</font><br></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>&nbsp;&nbsp;</td>")
   HTMLVIEWLINE("   <td>")
   HTMLVIEWLINE("    <b><font size=+1><b>Choose Card Type</font></b></b><br>")
   HTMLVIEWLINE("    <input type=radio name=cardselection value=30>VISA Debit Card<br>")
   IF FND40="Y" THEN HTMLVIEWLINE("<input type=radio name=cardselection value=40>VISA Classic Credit Card<br>")
   IF FND41="Y" THEN HTMLVIEWLINE("<input type=radio name=cardselection value=41>VISA Gold Credit Card")
   HTMLVIEWLINE("  </td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>&nbsp;&nbsp;</td>")
   HTMLVIEWLINE("   <td><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit>&nbsp;&nbsp;&nbsp;&nbsp;")  
   HTMLVIEWLINE("         <INPUT TYPE=SUBMIT NAME=close VALUE=Cancel></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
   IF CHOOSETYPE="" AND CARDTYPE=0 THEN POPUPMESSAGE(0,"You must select the card type to Issue")
  END
END

PROCEDURE MAINSCREEN
 DONE="N"
 WHILE DONE="N"
  DO
   DONE="Y"
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Capture and Mail Card Selection</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=#000000>")
   HTMLVIEWLINE(" <p align=center><font size=6 color=NAVY>Capture and <b><u>Mail</u></b> Card Selections</font></p>")
   HTMLVIEWLINE(" <form name=ordercards METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=650 border=1 align=center>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("  <td>")
   HTMLVIEWLINE(" <table width=625 align=center>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE(" <td bgcolor=GREEN colspan=2><font color=White size=4><b>Please review the list carefully.<br>")
   HTMLVIEWLINE("     The Member can only have one "+TNAME+" card in their name.</b></font><br></td>")
   HTMLVIEWLINE(" </tr>")
   IF C=0 OR (TOT30=0 AND CARDTYPE=30) OR (TOT40=0 AND CARDTYPE=40) OR (TOT41=0 AND CARDTYPE=41) THEN
    DO
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><br><b>There are currently no "+TNAME+" cards on this account.</b></td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><br><b>Please select New Card to verify you want to order a new card.</b></td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><input type=radio name=actiontotake value=NEWCARD")
     IF ACTIONTOTAKE="NEWCARD" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("><b>New Card</b> - Member does not have a "+TNAME+" card and needs a new Card and PIN mailed.")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
    END
   ELSE 
    DO
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("   <td colspan=2><b>Select existing card</b><br>")
     HTMLVIEWLINE("   <select name=cardtoreplace size="+FORMAT("99",CRDCNTS(CARDTYPE)+1)+">") 
     FOR II=1 TO C
      DO
       IF CRDTYPE(II,1)=CARDTYPE THEN 
        DO
         HTMLVIEWLINE("<option value="+CRDINFO(II,1))
         IF CARDTOREPLACE=CRDINFO(II,1) THEN HTMLVIEWLINE(" selected=selected")
         HTMLVIEWLINE(">"+CRDINFO(II,1)+" ")
         IF CRDTYPE(II,2)=0 THEN HTMLVIEWLINE(" - "+PNAME)
         ELSE HTMLVIEWLINE(" - "+CRDINFO(II,2)+" "+CRDINFO(II,3))
         HTMLVIEWLINE(" - Expiring: "+FORMAT("99/99/99",CRDEXP(II))+NEWCRD(II))
         HTMLVIEWLINE(" - "+CRDCHECK(II))
         HTMLVIEWLINE("</option>")
        END
      END
     HTMLVIEWLINE("   <option></option>")
     HTMLVIEWLINE("   </select>")
     HTMLVIEWLINE("   </td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><br><b>Please select Action to Take.</b></td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><input type=radio name=actiontotake value=NEWCARD")
     IF ACTIONTOTAKE="NEWCARD" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("><b>New Card</b> - Member does not have a "+TNAME+" card and needs a new Card and PIN mailed.")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><input type=radio name=actiontotake value=RENEW")
     IF ACTIONTOTAKE="RENEW" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       ><b>Renew</b> - Mail card with same number and PIN with a NEW Expiration Date")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><input type=radio name=actiontotake value=CAPTURE")
     IF ACTIONTOTAKE="CAPTURE" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       ><b>Capture Only</b> - Capture card and do not issue a new one<br>")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><input type=radio name=actiontotake value=REPLACE")
     IF ACTIONTOTAKE="REPLACE" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       ><b>Capture and Replace</b> - Capture card and mail a replacement with a new number<br>")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("  <td colspan=2><br><b>If capturing a card, select the reason</b></td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("   <td>&nbsp;</td>")
     HTMLVIEWLINE("   <td><input type=radio name=statusreason value=1")
     IF CARDSTATUS=1 THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       >Card Lost<br>")
     HTMLVIEWLINE("       <input type=radio name=statusreason value=2")
     IF CARDSTATUS=2 THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       >Card Stolen<br>")
     HTMLVIEWLINE("       <input type=radio name=statusreason value=3")
     IF CARDSTATUS=3 THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       >Fraud On Card<br>")
     HTMLVIEWLINE("       <input type=radio name=statusreason value=4")
     IF CARDSTATUS=4 THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       >Never Received New Card<br>")
     HTMLVIEWLINE("       <input type=radio name=statusreason value=5")
     IF CARDSTATUS=5 THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("       >Merchant Data Breach<br>")
     HTMLVIEWLINE("       <input type=radio name=statusreason value=20")
     IF CARDSTATUS=20 THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("   >Other Reason")
     HTMLVIEWLINE("   </td>")
     HTMLVIEWLINE(" </tr>")
    END
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE(" </td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit>&nbsp;&nbsp;&nbsp;&nbsp;")
   HTMLVIEWLINE("                  <INPUT TYPE=SUBMIT NAME=close VALUE=Cancel></p>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
   IF CHOOSETYPE<>"CANCEL" THEN
    DO
     IF C=0 OR (TOT30=0 AND CARDTYPE=30) OR (TOT40=0 AND CARDTYPE=40) OR (TOT41=0 AND CARDTYPE=41)  THEN
      DO
       IF  ACTIONTOTAKE="" THEN 
        DO
         POPUPMESSAGE(0,"You must select New Card to Continue.")
         DONE="N"
        END
      END
     ELSE
      DO
       IF  ACTIONTOTAKE="" THEN 
        DO
         POPUPMESSAGE(0,"You must select an Action to Take.")
         DONE="N"
        END
       ELSE IF (ACTIONTOTAKE="RENEW" OR ACTIONTOTAKE="CAPTURE" OR ACTIONTOTAKE="REPLACE") AND CARDTOREPLACE="" THEN
        DO
         IF ACTIONTOTAKE="RENEW"  THEN POPUPMESSAGE(0,"You must select a card to Renew.")
         ELSE IF ACTIONTOTAKE="CAPTURE" THEN POPUPMESSAGE(0,"You must select a card to Capture.")
         ELSE POPUPMESSAGE(0,"You must select a card to Replace.")
         DONE="N"
        END
       ELSE IF CARDSTATUS>0 AND ACTIONTOTAKE<>"CAPTURE" AND ACTIONTOTAKE<>"REPLACE" THEN
        DO
         POPUPMESSAGE(0,"You selected a Capture Reason but not the option to capture the card. Please review.")
         CARDSTATUS=0
         DONE="N"
        END
       ELSE IF (ACTIONTOTAKE="CAPTURE" OR ACTIONTOTAKE="REPLACE") AND CARDSTATUS=0 THEN
        DO
         POPUPMESSAGE(0,"You must select a Capture Reason.")
         DONE="N"
        END
      END
    END
  END
 IF CHOOSETYPE<>"CANCEL" THEN
  DO
   IF ACTIONTOTAKE="NEWCARD" THEN
    DO
     IF CARDTYPE=30 THEN
      DO
       CDESCRIPTION="VISA DEBIT CARD"
       INITSUBROUTINE(INITERROR)
       IF INITERROR<>"" THEN
        DO
         OUTLINE="Init Error INSTANT.VCC.APP: "+INITERROR
         CALL WRITEERROR
        END
       EXECUTE("INSTANT.VCC.APP",EXECERROR)
       IF EXECERROR<>"" THEN
        DO
         OUTLINE="Execute Error INSTANT.VCC.APP: "+EXECERROR
         CALL WRITEERROR
        END
       CSAVID=@ENVARGCHAR2
       CCHKID=@ENVARGCHAR3
       CARDNBR=@ENVARGCHAR4
       BUSNAME=@ENVARGCHAR5
       JTLAST=@ENVARGCHAR6
       JTSUFFIX=@ENVARGCHAR7
       JTFIRST=@ENVARGCHAR8
       JTMIDDLE=@ENVARGCHAR9
       JTTITLE=@ENVARGCHAR10
       JTDOB=@ENVARGDATE1
       BADREPLACE=@ENVARGNUMBER1
       JTNAMETYPE=@ENVARGNUMBER2
       [IF CARDNBR="" THEN POPUPMESSAGE(0,"Card creation canceled.")]
       IF [CARDNBR<>"" AND] BADREPLACE=0 THEN CALL CREATEVCCRECORD 
      END
     ELSE 
      DO
       INITSUBROUTINE(INITERROR)
       @ENVARGNUMBER5=CARDTYPE
       IF INITERROR<>"" THEN
        DO
         OUTLINE="Init Error INSTANTISSUE.VISA: "+INITERROR
         CALL WRITEERROR
        END
       EXECUTE("INSTANTISSUE.VISA",EXECERROR)
       [EXECUTE("CARDPRINTING.XXX",EXECERROR)]
       IF EXECERROR<>"" THEN
        DO
         OUTLINE="Execute Error INSTANTISSUE.VISA: "+EXECERROR
         CALL WRITEERROR
        END
       NEWNAMETYPE=@ENVARGCHAR2
       LNID=@ENVARGCHAR3
       BUSNAME=@ENVARGCHAR4
       CHOOSETYPE=@ENVARGCHAR5
       NEWNAMELOC=@ENVARGNUMBER6
       IF CHOOSETYPE<>"CANCEL" THEN CALL CREATENEWCREDITCARD
      END
    END
   ELSE
    DO
     CALL GETCURRENTCARD 
     IF ACTIONTOTAKE="RENEW" THEN CALL UPDATECURRENTCARD
     ELSE IF ACTIONTOTAKE="CAPTURE" THEN CALL CAPTURECARD
     ELSE CALL REPLACECURRENTCARD
    END
  END
END

 
PROCEDURE CREATEVCCRECORD
 IF JTLAST<>"" THEN CNAMETYPE=2
 ELSE CNAMETYPE=0
 FMPERFORM CREATE CARD LOC AFTERLAST (1,CARDTYPE,CARDLOC,ERRORTEXT)
  DO
   SET NUMBER TO "NEXT"
   SET STATUS TO 0
   SET REISSUECODE TO 1
   SET PINOFFSET TO 9999
   SET NAMETYPE TO CNAMETYPE
   SET ISSUEDATE TO SYSTEMDATE
   SET EFFECTIVEDATE TO SYSTEMDATE
   SET SAVID TO CSAVID
   SET CHKID TO CCHKID 
   SET DESCRIPTION TO CDESCRIPTION 
   SET EXTRAEMBOSS TO BUSNAME
  END
 IF ERRORTEXT<>"" THEN 
  DO
   OUTLINE="Error creating new card: "+ERRORTEXT
   CALL WRITEERROR
  END
 ELSE
  DO
   FOR EACH CARD WITH CARD:LOCATOR=CARDLOC
    DO
     CARDNBR=CARD:NUMBER
    END
   POPUPMESSAGE(0,TNAME+" card and new PIN will be mailed to the member")
   FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Card created through ORDERCARDS"
     SET TEXT:2 TO "New Card "
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating card note "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   CALL CREATELOOKUP 
  END

 IF JTLAST<>"" THEN
  DO
   FOR EACH SHARE WITH SHARE:ID=CSAVID OR SHARE:ID=CCHKID 
    DO
     FOR EACH SHARE NAME WITH (SHARE NAME:EXPIRATIONDATE='--/--/--' OR 
                               SHARE NAME:EXPIRATIONDATE>SYSTEMDATE) AND
                               SHARE NAME:FIRST=JTFIRST AND
                               SHARE NAME:LAST=JTLAST AND
                               SHARE NAME:BIRTHDATE=JTDOB AND
                               JTSSN=""
      DO
       IF SHARE NAME:STREET<>"" AND JTSTREET<>SHARE NAME:STREET THEN
        DO
         JTEXTRA=SHARE NAME:EXTRAADDRESS
         JTSTREET=SHARE NAME:STREET
         JTCITY=SHARE NAME:CITY
         JTSTATE=SHARE NAME:STATE
         JTZIP=SHARE NAME:ZIPCODE
         JTADDRESSTYPE=SHARE NAME:ADDRESSTYPE
        END
       IF JTHOME<>"" AND JTHOME<>SHARE NAME:HOMEPHONE THEN 
        DO
         JTHOME=SHARE NAME:HOMEPHONE
         JTPHONETYPE=SHARE NAME:PHONETYPE
        END
       JTSSN=SHARE NAME:SSN
       JTSSNTYPE=NAME:SSNTYPE
       JTWORK=SHARE NAME:WORKPHONE
       JTCELL=SHARE NAME:MOBILEPHONE
      END
    END
   FMPERFORM CREATE CARD LOC CARDLOC NAME LOC AFTERLAST (1,2,ERRORTEXT)
    DO
     SET TITLE TO JTTITLE
     SET FIRST TO JTFIRST
     SET MIDDLE TO JTMIDDLE
     SET LAST TO JTLAST
     SET SUFFIX TO JTSUFFIX
     SET BIRTHDATE TO JTDOB
     SET SSN TO JTSSN
     SET SSNTYPE TO JTSSNTYPE
     SET HOMEPHONE TO JTHOME
     SET PHONETYPE TO JTPHONETYPE
     SET WORKPHONE TO JTWORK
     SET MOBILEPHONE TO JTCELL
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating new card name: "+ERRORTEXT
     CALL WRITEERROR
    END
  END
 CALL CHECKOPTINOUT
END

PROCEDURE CREATELOOKUP
 IF CARDTYPE=30 THEN
  DO
   FMPERFORM CREATE LOOKUP 0 (1,CARDTYPE,ERRORTEXT)
    DO
     SET TYPE TO CARDTYPE
     SET NUMBER TO CARDNBR
     SET IDTYPE TO 2
    END
  END
 ELSE
  DO
   FMPERFORM CREATE LOOKUP 0 (1,CARDTYPE,ERRORTEXT)
    DO
     SET TYPE TO CARDTYPE
     SET NUMBER TO CARDNBR
     SET IDTYPE TO 1
     SET ID TO CCREDITCARDID
    END
  END
 IF ERRORTEXT<>"" THEN
  DO
   OUTLINE="Error creating card lookup "+CARDNBR+": "+ERRORTEXT
   CALL WRITEERROR
  END
END

PROCEDURE GETCARDS
 FOR EACH CARD WITH (CARD:EXPIRATIONDATE='--/--/--' OR CARD:EXPIRATIONDATE>SYSTEMDATE) AND CARD:STATUS<>2 AND
          (CARD:TYPE=30 OR CARD:TYPE=40 OR CARD:TYPE=41)
  DO
   IF CARD:TYPE=30 THEN TOT30=TOT30+1
   ELSE IF CARD:TYPE=40 THEN TOT40=TOT40+1
   ELSE TOT41=TOT41+1
   C=C+1
   CRDCNTS(CARD:TYPE)=CRDCNTS(CARD:TYPE)+1
   CRDTYPE(C,1)=CARD:TYPE
   CRDTYPE(C,2)=CARD:NAMETYPE 
   CRDINFO(C,1)=CARD:NUMBER
   NEWCRD(C)=" (New)"
   IF CARD:TYPE=30 THEN 
    DO
     CRDCHECK(C)="SavID: "
     IF CARD:SAVID="" THEN CRDCHECK(C)="SavID: NA"
     ELSE CRDCHECK(C)="SavID: "+CARD:SAVID
     IF CARD:CHKID="" THEN CRDCHECK(C)=CRDCHECK(C)+"&nbsp;&nbsp;&nbsp;ChkID: NA"
     ELSE  CRDCHECK(C)=CRDCHECK(C)+"&nbsp;&nbsp;&nbsp;ChkID: "+CARD:CHKID
    END
   ELSE IF CARD:TYPE=40 THEN CRDCHECK(C)="Classic Loan ID: "+CARD:CREDITCARDID
   ELSE CRDCHECK(C)="Gold Loan ID: "+CARD:CREDITCARDID
   FOR EACH CARD NAME 
    DO
     CRDINFO(C,2)=CARD NAME:FIRST       
     CRDINFO(C,3)=CARD NAME:LAST
     CRDINFO(C,4)=CARD NAME:SSN
     CRDINFO(C,5)=CARD NAME:EXTRAADDRESS
     CRDINFO(C,6)=CARD NAME:STREET
     CRDINFO(C,7)=CARD NAME:CITY
     CRDINFO(C,8)=CARD NAME:STATE
     CRDINFO(C,9)=CARD NAME:ZIPCODE
     CRDDATE(C)=CARD NAME:BIRTHDATE
     CRDLASTADDRCHANGE(C)=CARD NAME:ADDRFMLASTDATE
    END
  END
 FOR EACH LOAN WITH LOAN:TYPE=40 OR LOAN:TYPE=41 AND 
         LOAN:CLOSEDATE='--/--/--' AND LOAN:CHARGEOFFDATE='--/--/--'
  DO
   IF LOAN:TYPE=40 THEN FND40="Y"
   ELSE IF LOAN:TYPE=41 THEN FND41="Y"
  END
END

PROCEDURE GETCURRENTCARD
 FOR EACH CARD WITH CARD:NUMBER=CARDTOREPLACE
  DO
   CARDNBR=CARD:NUMBER
   CARDLOC=CARD:LOCATOR
   OLDLOCATOR=CARD:LOCATOR
   CDESCRIPTION=UPPERCASE(CARD:DESCRIPTION)
   CSAVID=CARD:SAVID
   CCHKID=CARD:CHKID
   CLOCID=CARD:LOCID
   CCREDITCARDID=CARD:CREDITCARDID
   CACTIVEDATE=CARD:ACTIVEDATE
   CEXPIRATIONDATE=CARD:EXPIRATIONDATE
   CATMCOUNTLIMIT=CARD:ATMCOUNTLIMIT
   CAUTHLIMIT=CARD:AUTHLIMIT
   CAUTHCOUNTLIMIT=CARD:AUTHCOUNTLIMIT
   CBILLPAYLIMIT=CARD:BILLPAYLIMIT
   CBILLPAYCOUNTLIMIT=CARD:BILLPAYCOUNTLIMIT
   CBILLPAYMAX=CARD:BILLPAYMAX
   CCREDDEBCOUNTLIMIT=CARD:CREDDEBCOUNTLIMIT
   CCREDDEBLIMIT=CARD:CREDDEBLIMIT
   CDEPCOUNTLIMIT=CARD:DEPCOUNTLIMIT
   CDEPLIMIT=CARD:DEPLIMIT
   CDEPMAX=CARD:DEPMAX
   CEXTRAEMBOSS=UPPERCASE(CARD:EXTRAEMBOSS)
   CLIMITAMOUNT=CARD:LIMITAMOUNT
   CLIMITDAYS=CARD:LIMITDAYS
   CMISCLIMIT=CARD:MISCLIMIT
   CMISCCOUNTLIMIT=CARD:MISCCOUNTLIMIT
   CNAMETYPE=CARD:NAMETYPE
   CPOSLIMIT=CARD:POSLIMIT
   CPOSCOUNTLIMIT=CARD:POSCOUNTLIMIT
   CREISSUEMONTHS=CARD:REISSUEMONTHS
   CSINGLEUSAGEMAX=CARD:SINGLEUSAGEMAX
   CUSAGELIMIT=CARD:USAGELIMIT
   CWDCHECKLIMIT=CARD:WDCHECKLIMIT
   CWDCHECKCOUNTLIMIT=CARD:WDCHECKCOUNTLIMIT
   CWDCHECKMAX=CARD:WDCHECKMAX
   CXFERCOUNTLIMIT=CARD:XFERCOUNTLIMIT
   CXFERLIMIT=CARD:XFERLIMIT
   CXFERMAX=CARD:XFERMAX
   FOR EACH CARD NAME
    DO 
     JTLAST=CARD NAME:LAST
     JTSUFFIX=CARD NAME:SUFFIX
     JTFIRST=CARD NAME:FIRST
     JTMIDDLE=CARD NAME:MIDDLE
     JTTITLE=CARD NAME:TITLE
     JTDOB=CARD NAME:BIRTHDATE
     JTSSN=CARD NAME:SSN
     JTEXTRA=CARD NAME:EXTRAADDRESS
     JTSTREET=CARD NAME:STREET
     JTCITY=CARD NAME:CITY
     JTSTATE=CARD NAME:STATE
     JTZIP=CARD NAME:ZIPCODE
     JTHOME=CARD NAME:HOMEPHONE
     JTWORK=CARD NAME:WORKPHONE
     JTCELL=CARD NAME:MOBILEPHONE
     JTPHONETYPE=CARD NAME:PHONETYPE
     JTADDRESSTYPE=CARD NAME:ADDRESSTYPE
     JTSSNTYPE=CARD NAME:SSNTYPE
     JTLASTADDRESSCHANGE=CARD NAME:ADDRFMLASTDATE
    END
  END
END


PROCEDURE UPDATECURRENTCARD
 VANSWER=1
 IF VANSWER=1 THEN
  DO
   FMPERFORM REVISE CARD LOC CARDLOC (1,2,ERRORTEXT) [ADVANCE EXPIRATION DATE 3 YEARS]
    DO
     SET REISSUECODE TO 2
    END
   IF ERRORTEXT<>"" THEN 
    DO
     OUTLINE="Error advancing current card "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   ELSE
    DO
     POPUPMESSAGE(0,TNAME+" card with new Expiration will be mailed to the member with the same PIN.")
     FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
      DO
       SET CODE TO 0
       SET TEXT:1 TO "Card created through ORDERCARDS"
       SET TEXT:2 TO "Mail Same Number - New Expiration"
      END
     IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Error creating card note"+CARDNBR+": "+ERRORTEXT
       CALL WRITEERROR
      END
    END
  END
END

PROCEDURE REPLACECURRENTCARD
 [CALL GETNEXTCARD]
 FMPERFORM CREATE CARD LOC AFTERLAST (1,CARDTYPE,CARDLOC,ERRORTEXT)
  DO
   SET NUMBER TO "NEXT"
   SET STATUS TO 0
   SET REISSUECODE TO 1
   SET PINOFFSET TO 9999
   SET ISSUEDATE TO SYSTEMDATE
   SET EFFECTIVEDATE TO SYSTEMDATE
   SET DESCRIPTION TO CDESCRIPTION
   SET PREVNUMBER TO CARDTOREPLACE
   SET SAVID TO CSAVID
   SET CHKID TO CCHKID
   SET LOCID TO CLOCID
   SET CREDITCARDID TO CCREDITCARDID
   SET ATMCOUNTLIMIT TO CATMCOUNTLIMIT
   SET AUTHLIMIT TO CAUTHLIMIT
   SET AUTHCOUNTLIMIT TO CAUTHCOUNTLIMIT
   SET BILLPAYLIMIT TO CBILLPAYLIMIT
   SET BILLPAYCOUNTLIMIT TO CBILLPAYCOUNTLIMIT
   SET BILLPAYMAX TO CBILLPAYMAX
   SET CREDDEBCOUNTLIMIT TO CCREDDEBCOUNTLIMIT
   SET CREDDEBLIMIT TO CCREDDEBLIMIT
   SET CREDITCARDID TO CCREDITCARDID
   SET DEPCOUNTLIMIT TO CDEPCOUNTLIMIT
   SET DEPLIMIT TO CDEPLIMIT
   SET DEPMAX TO CDEPMAX
   SET EXTRAEMBOSS TO CEXTRAEMBOSS
   SET LIMITAMOUNT TO CLIMITAMOUNT
   SET LIMITDAYS TO CLIMITDAYS
   SET MISCLIMIT TO CMISCLIMIT
   SET MISCCOUNTLIMIT TO CMISCCOUNTLIMIT
   SET NAMETYPE TO CNAMETYPE
   SET POSLIMIT TO CPOSLIMIT
   SET POSCOUNTLIMIT TO CPOSCOUNTLIMIT
   SET REISSUEMONTHS TO CREISSUEMONTHS
   SET SINGLEUSAGEMAX TO CSINGLEUSAGEMAX
   SET USAGELIMIT TO CUSAGELIMIT
   SET WDCHECKLIMIT TO CWDCHECKLIMIT
   SET WDCHECKCOUNTLIMIT TO CWDCHECKCOUNTLIMIT
   SET WDCHECKMAX TO CWDCHECKMAX
   SET XFERCOUNTLIMIT TO CXFERCOUNTLIMIT
   SET XFERLIMIT TO CXFERLIMIT
   SET XFERMAX TO CXFERMAX
  END
 IF ERRORTEXT<>"" THEN
  DO
   OUTLINE="Error creating replacement card "+CARDNBR+": "+ERRORTEXT
   CALL WRITEERROR
  END
 ELSE
  DO
   FOR EACH CARD WITH CARD:LOCATOR=CARDLOC
    DO
     CARDNBR=CARD:NUMBER
    END   
   POPUPMESSAGE(0,TNAME+" card and new PIN will be mailed to the member")
   IF CARDTYPE>30 THEN
    DO
     FMPERFORM REVISE LOAN CCREDITCARDID (0,1,ERRORTEXT)
      DO
       SET REFERENCE TO CARDNBR
      END
     IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Error setting Loan Reference "+CARDNBR+": "+ERRORTEXT
       CALL WRITEERROR
      END
    END
   IF CNAMETYPE=2 THEN
    DO
     FMPERFORM CREATE CARD LOC CARDLOC NAME LOC BEFOREFIRST (1,2,ERRORTEXT)
      DO
       SET TITLE TO JTTITLE
       SET FIRST TO JTFIRST
       SET MIDDLE TO JTMIDDLE
       SET LAST TO JTLAST
       SET SUFFIX TO JTSUFFIX
       SET BIRTHDATE TO JTDOB
       SET SSN TO JTSSN
       SET HOMEPHONE TO JTHOME
       SET WORKPHONE TO JTWORK
       SET MOBILEPHONE TO JTCELL
       SET PHONETYPE TO JTPHONETYPE
       SET SSNTYPE TO JTSSNTYPE
      END
     IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Error creating card name on "+CARDNBR+": "+ERRORTEXT
       CALL WRITEERROR
      END
    END
   CALL CREATELOOKUP
   FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Card created through ORDERCARDS"
     SET TEXT:2 TO "Replaced card ending "+SEGMENT(CARDTOREPLACE,13,16)
     SET TEXT:3 TO "REASON: "+CAPTUREREASON
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating card note "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   CALL CAPTURECARD
  END
END

PROCEDURE CAPTURECARD
 IF CARDTYPE=30 THEN NEWTYPE=51
 ELSE IF CARDTYPE=40 THEN NEWTYPE=52
 ELSE NEWTYPE=53
 FMPERFORM REVISE CARD LOC OLDLOCATOR (1,2,ERRORTEXT) [NO CHANGE]
  DO
   SET STATUS TO 2
   SET TYPE TO NEWTYPE
   SET STATUSREASON TO CARDSTATUS
   SET BLOCKCODE TO BLOCKCODE
   SET CLOSEDATE TO SYSTEMDATE
  END
 IF ERRORTEXT<>"" THEN 
  DO
   OUTLINE="Error capturing old card "+CARDTOREPLACE+": "+ERRORTEXT
   CALL WRITEERROR
  END
 ELSE
  DO
   POPUPMESSAGE(0,"Old "+TNAME+" card was sucessfully captured.")
   FMPERFORM CREATE CARD LOC OLDLOCATOR NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Card captured through ORDERCARDS"
     SET TEXT:2 TO CAPTUREREASON
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating card capture note "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
END

PROCEDURE CREATENEWCREDITCARD
 CSAVID=""
 CCHKID=""
 CLOCID=""
 LOANREF=""
 CCREDITCARDID=LNID
 IF CARDTYPE=40 THEN CDESCRIPTION="VISA CLASSIC"
 ELSE IF CARDTYPE=41 THEN CDESCRIPTION="VISA GOLD"
 FOR EACH LOAN WITH LOAN:TYPE=CARDTYPE AND LOAN:CLOSEDATE='--/--/--'  [Added 6/17/14 - to get limits for card]
  DO
   CLIMITAMOUNT=LOAN:CREDITLIMIT
   CUSAGELIMIT=LOAN:CREDITLIMIT
   LOANREF=LOAN:REFERENCE
  END
 CALL GETCARDNAMEINFO
[ CALL GETNEXTCARD]
 FMPERFORM CREATE CARD LOC AFTERLAST (1,CARDTYPE,CARDLOC,ERRORTEXT)
  DO
   SET NUMBER TO "NEXT"
   SET STATUS TO 0
   SET REISSUECODE TO 1
   SET PINOFFSET TO 9999
   SET ISSUEDATE TO SYSTEMDATE
   SET EFFECTIVEDATE TO SYSTEMDATE
   SET DESCRIPTION TO CDESCRIPTION
   SET SAVID TO CSAVID
   SET CHKID TO CCHKID
   SET LOCID TO CLOCID
   SET CREDITCARDID TO CCREDITCARDID
   SET EXTRAEMBOSS TO BUSNAME
   SET NAMETYPE TO CNAMETYPE
  END
 IF ERRORTEXT<>"" THEN 
  DO
   OUTLINE="Error creating new card "+CARDNBR+": "+ERRORTEXT
   CALL WRITEERROR
  END
 ELSE
  DO
   FOR EACH CARD WITH CARD:LOCATOR=CARDLOC
    DO
     CARDNBR=CARD:NUMBER
    END
   POPUPMESSAGE(0,TNAME+" card and new PIN will be mailed to the member")
   FMPERFORM REVISE CARD LOC CARDLOC (0,1,ERRORTEXT) 
    DO
     SET LIMITAMOUNT TO CLIMITAMOUNT
     SET USAGELIMIT TO CUSAGELIMIT
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error setting CISA card limits"+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   FMPERFORM REVISE LOAN CCREDITCARDID (0,1,ERRORTEXT)
    DO
     SET REFERENCE TO CARDNBR
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error setting Loan Reference "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   IF CNAMETYPE=2 THEN
    DO
     FMPERFORM CREATE CARD LOC CARDLOC NAME LOC BEFOREFIRST (1,2,ERRORTEXT)
      DO
       SET TITLE TO JTTITLE
       SET FIRST TO JTFIRST
       SET MIDDLE TO JTMIDDLE
       SET LAST TO JTLAST
       SET SUFFIX TO JTSUFFIX
       SET BIRTHDATE TO JTDOB
       SET SSN TO JTSSN
       SET HOMEPHONE TO JTHOME
       SET WORKPHONE TO JTWORK
       SET MOBILEPHONE TO JTCELL
       SET PHONETYPE TO JTPHONETYPE
       SET SSNTYPE TO JTSSNTYPE
      END
     IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Error creating new card name on "+CARDNBR+": "+ERRORTEXT
       CALL WRITEERROR
      END
    END
   CALL CREATELOOKUP
   FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Card created through ORDERCARDS"
     SET TEXT:2 TO "New Card"
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating new card note "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
END


PROCEDURE GETCARDNAMEINFO
 IF NEWNAMETYPE="N" THEN
  DO
   FOR EACH NAME WITH NAME:LOCATOR=NEWNAMELOC
    DO
     IF NAME:TYPE=0 THEN CNAMETYPE=0
     ELSE
      DO
       CNAMETYPE=2
       JTLAST=NAME:LAST
       JTSUFFIX=NAME:SUFFIX
       JTFIRST=NAME:FIRST
       JTMIDDLE=NAME:MIDDLE
       JTTITLE=NAME:TITLE
       JTDOB=NAME:BIRTHDATE
       JTSSN=NAME:SSN
       JTEXTRA=NAME:EXTRAADDRESS
       JTSTREET=NAME:STREET
       JTCITY=NAME:CITY
       JTSTATE=NAME:STATE
       JTZIP=NAME:ZIPCODE
       JTHOME=NAME:HOMEPHONE
       JTWORK=NAME:WORKPHONE
       JTCELL=NAME:MOBILEPHONE
       JTPHONETYPE=NAME:PHONETYPE
       JTADDRESSTYPE=NAME:ADDRESSTYPE
       JTSSNTYPE=NAME:SSNTYPE
      END
    END
  END
 ELSE IF NEWNAMETYPE="L" THEN
  DO
   CNAMETYPE=2
   FOR EACH LOAN WITH LOAN:ID=LNID
    DO
     CLIMITAMOUNT=LOAN:CREDITLIMIT
     CUSAGELIMIT=LOAN:CREDITLIMIT
     FOR EACH LOAN NAME WITH LOAN NAME:LOCATOR=NEWNAMELOC
      DO
       JTLAST=LOAN NAME:LAST
       JTSUFFIX=LOAN NAME:SUFFIX
       JTFIRST=LOAN NAME:FIRST
       JTMIDDLE=LOAN NAME:MIDDLE
       JTTITLE=LOAN NAME:TITLE
       JTDOB=LOAN NAME:BIRTHDATE
       JTSSN=LOAN NAME:SSN
       JTEXTRA=LOAN NAME:EXTRAADDRESS
       JTSTREET=LOAN NAME:STREET
       JTCITY=LOAN NAME:CITY
       JTSTATE=LOAN NAME:STATE
       JTZIP=LOAN NAME:ZIPCODE
       JTHOME=LOAN NAME:HOMEPHONE
       JTWORK=LOAN NAME:WORKPHONE
       JTCELL=LOAN NAME:MOBILEPHONE
       JTPHONETYPE=LOAN NAME:PHONETYPE
       JTADDRESSTYPE=LOAN NAME:ADDRESSTYPE
       JTSSNTYPE=LOAN NAME:SSNTYPE
      END
    END
  END
END


PROCEDURE CHECKOPTINOUT
 FNDTRACK80="N"
 FOR EACH SHARE WITH SHARE:ID=CCHKID
  DO
   FOR EACH SHARE TRACKING WITH SHARE TRACKING:TYPE=80
    DO
     FNDTRACK80="Y"
    END
  END
 IF FNDTRACK80="N" THEN
  DO
   INITSUBROUTINE(INITERROR)
   IF INITERROR<>"" THEN
    DO
     OUTLINE="Init Error GH.OPTIN.HTML: "+INITERROR
     CALL WRITEERROR
    END
   @ENVARGCHAR3="INSTANTISSUE"
   @ENVARGNUMBER2=JTNAMETYPE
   EXECUTE("GH.OPTIN.HTML",EXECERROR) 
   IF EXECERROR<>"" THEN
    DO
     OUTLINE="Execute Error GH.OPTIN.HTML: "+EXECERROR
     CALL WRITEERROR
    END
  END
END

PROCEDURE LOADRESULTS
 INPUTCOUNT=0
 WHILE (INPUTFORM(INPUTCOUNT)<>"EOD")
  DO
   INPUTCOUNT=INPUTCOUNT+1
   INPUTFORM(INPUTCOUNT)=ENTERLINE(0)
  END
END

PROCEDURE SETVARIABLES
 CARDSTATUS=0
 FOR I=1 TO INPUTCOUNT
  DO
   TMPCHR=INPUTFORM(I)
   IF SEGMENT(TMPCHR,1,5)="close"                   THEN CHOOSETYPE="CANCEL"
   ELSE IF SEGMENT(TMPCHR,1,14)="cardselection="    THEN 
    DO
     CARDTYPE=VALUE(SEGMENT(TMPCHR,15,16))
     IF CARDTYPE=30 THEN TNAME="DEBIT"
     ELSE IF CARDTYPE=40 THEN TNAME="CLASSIC"
     ELSE TNAME="GOLD"
    END
   ELSE IF SEGMENT(TMPCHR,1,14)="cardtoreplace="    THEN CARDTOREPLACE=SEGMENT(TMPCHR,15,LENGTH(TMPCHR))
   ELSE IF SEGMENT(TMPCHR,1,13)="actiontotake="     THEN ACTIONTOTAKE=SEGMENT(TMPCHR,14,LENGTH(TMPCHR))
   ELSE IF SEGMENT(TMPCHR,1,13)="statusreason="     THEN 
    DO
     CARDSTATUS=VALUE(SEGMENT(TMPCHR,14,LENGTH(TMPCHR)))
     CAPTUREREASON=STATUSREASON(CARDSTATUS)
     IF CARDSTATUS=1       THEN BLOCKCODE="L"
     ELSE IF CARDSTATUS=2  THEN BLOCKCODE="S"
     ELSE IF CARDSTATUS=3  THEN BLOCKCODE="F"
     ELSE IF CARDSTATUS=4 OR CARDSTATUS=5 OR CARDSTATUS=20 THEN 
      DO
       BLOCKCODE="R"
       CARDSTATUS=20
      END
    END
   ELSE IF SEGMENT(TMPCHR,1,7)="yescard"            THEN RETRIEVECARD="YES"
   ELSE IF SEGMENT(TMPCHR,1,6)="nocard"             THEN RETRIEVECARD="NO"
   ELSE IF SEGMENT(TMPCHR,1,6)="lname="             THEN LNAME=UPPERCASE(SEGMENT(TMPCHR,7,LENGTH(TMPCHR)))
   ELSE IF SEGMENT(TMPCHR,1,8)="busname="           THEN NEWBUS=UPPERCASE(SEGMENT(TMPCHR,9,LENGTH(TMPCHR)))
   ELSE IF SEGMENT(TMPCHR,1,12)="otherreason="      THEN OTHERREASON=SEGMENT(TMPCHR,13,LENGTH(TMPCHR))
  END
END

[ JWJ Removed for random card number project 06/21/21
PROCEDURE GETNEXTCARD
 CN=SEGMENT(CARD:NUMBER,1,6)
 FILENAME=""
 
 IF CARDTYPE=30 THEN 
  DO
   CN="433739"
   FILENAME="DEBITCARD.VALUE.228"
  END
 ELSE IF CARDTYPE=40 THEN 
  DO
   CN="425100"
   FILENAME="VISA.CLASSIC.VALUE"
  END
 ELSE IF CARDTYPE=41 THEN 
  DO
   CN="425104"
   FILENAME="VISA.GOLD.VALUE"
  END
 CURRFILENAME=FILENAME
 FILELOCKED=FALSE
 PROCESSOK=TRUE
 CALL LOCKFILE
 IF FILELOCKED=TRUE THEN
  DO
   FILEOPEN("LETTER",FILENAME,"READWRITE",FNUMBER,FERROR)
   IF FERROR<>"" THEN
    DO
     PRINT CN+" FILE OPEN ERROR: "+FERROR
     NEWLINE
    END
   ELSE
    DO
     FILEREADLINE(FNUMBER,FLINE,FERROR)
     IF FERROR<>"" OR LENGTH(FLINE)<>16 THEN
      DO
       PRINT "READ ERROR: "+FERROR
       NEWLINE
      END
     ELSE 
      DO
       CARDNBR=FLINE
       SEQNBR=VALUE(SEGMENT(CARDNBR,7,15))
       IF SEQNBR>=899999999 THEN
        DO
         PRINT "ERROR! OUT OF CARD NUMBERS"
         NEWLINE
        END
       ELSE
        DO
         SEQNBR=SEQNBR+1
         CARDNBR=CN+FORMAT("999999999",SEQNBR)
         RESULT=0
         PARITY=2
         I=15
         WHILE (I>=1)
          DO
           DIGIT=VALUE(SEGMENT(CARDNBR,I,I))
           RESULT=RESULT+NUMBER((DIGIT*PARITY*11)/10)
           PARITY=3-PARITY
           I=I-1
          END
         RESULT2=NUMBER((RESULT+9)/10)
         RESULT=10*RESULT2-RESULT
         CARDNBR=CARDNBR+FORMAT("9",RESULT)
        END
      END
     FILESETPOS(FNUMBER,0,FERROR)
     FILEWRITELINE(FNUMBER,CARDNBR,FERROR)
    END
   FILECLOSE(FNUMBER,FERROR)
  END        
 CALL UNLOCKFILE      
END
]

PROCEDURE LOCKFILE
[
|  Opens a file to provide exclusive use to <CURRFILENAME>.LOCK 
|  LetterFile
|  Sets:        FILELOCKED              TRUE/FALSE
]
 FILELOCKED=FALSE
 FILECREATE("LETTER",CURRFILENAME+".LOCK",ERROR)
 IF ERROR="File exists" THEN
  CALL DELETEUSERLOCK                   [CARDFM.NUM]
 IF ERROR<>"" THEN
  DO
   PRINT "Card File In Use.  Please try again"
   NEWLINE
   PRINT "Check "+CURRFILENAME+".LOCK File."
   NEWLINE
  END
 ELSE
  DO
   FILEOPEN("LETTER",CURRFILENAME+".LOCK","APPEND",LOCKFILENUMBER,ERROR)
   IF ERROR="" THEN
    DO
     FILELOCKED=TRUE
     FILEWRITELINE(LOCKFILENUMBER,FORMAT("999",SYSUSERNUMBER),ERROR)
     FILECLOSE(LOCKFILENUMBER,ERROR)
    END
   ELSE
    DO
     PRINT "Something wrong - Could not open card file"
     NEWLINE
     CALL UNLOCKFILE                    [CARDFM.NUM]
    END
  END
END

PROCEDURE DELETEUSERLOCK
[
|  Deletes <CURRFILENAME>.LOCK LetterFile if same user.
]
 FILEOPEN("LETTER",CURRFILENAME+".LOCK","READ",LOCKFILENUMBER,ERROR)
 IF ERROR="" THEN
  DO
   FILEREADLINE(LOCKFILENUMBER,FLINE,ERROR)
   IF ERROR<>"" OR FLINE<>FORMAT("999",SYSUSERNUMBER) THEN
    DO
     FILECLOSE(LOCKFILENUMBER,ERROR)
     PRINT "Card File In Use.  Please try again"
     NEWLINE
    END
   ELSE
    DO
     FILECLOSE(LOCKFILENUMBER,ERROR)
     IF ERROR="" THEN
      FILEDELETE("LETTER",CURRFILENAME+".LOCK",ERROR)
     IF ERROR="" THEN
      FILECREATE("LETTER",CURRFILENAME+".LOCK",ERROR)
    END
  END
END

PROCEDURE UNLOCKFILE
[
|  Deletes <CURRFILENAME>.LOCK LetterFile
|  SETS:        FILELOCKED              FALSE
]
 IF FILELOCKED=TRUE THEN
  DO
   FILEDELETE("LETTER",CURRFILENAME+".LOCK",ERROR)
   IF ERROR<>"" THEN
    DO
     PRINT "Error Deleting Lock File: "+ERROR
     NEWLINE
     PRINT "Please Delete LetterFile "+CURRFILENAME+".LOCK"
     NEWLINE
    END
  END
END

PROCEDURE PRINTCARD
 LNAME=""
 FOR EACH CARD WITH CARD:NUMBER=CARDNBR
  DO
   CARDLOC=CARD:LOCATOR
   BUSNAME=CARD:EXTRAEMBOSS
   IF CARD:NAMETYPE=0 THEN LNAME=PRIMENAME
   ELSE IF CARD:NAMETYPE=2 THEN
    DO
     FOR EACH CARD NAME
      DO
       IF LNAME="" THEN LNAME=CARD NAME:LONGNAME
      END
    END
  END
 AP=CHARACTERSEARCH(LNAME,"'")
 IF AP=0 THEN AP=CHARACTERSEARCH(LNAME,"&")
 WHILE AP>0
  DO
   LNAME=SEGMENT(LNAME,1,AP-1)+SEGMENT(LNAME,AP+1,LENGTH(LNAME))
   AP=CHARACTERSEARCH(LNAME,"'")
   IF AP=0 THEN AP=CHARACTERSEARCH(LNAME,"&")
  END

 IF LENGTH(LNAME)>26 THEN
  DO
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Name Too Long</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=NAVY>")
   HTMLVIEWLINE(" <p align=center><font size=+2><b>Name Too Long</b></font></p>")
   HTMLVIEWLINE(" <form name=ORDERCARDS METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=600 border=1 align=center>")
   HTMLVIEWLINE(" <tr bgcolor=NAVY>")
   HTMLVIEWLINE("   <td><font color=WHITE><b>Card Name Too Long: "+SEGMENT(LNAME,1,26)+"</b></font></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>Please fix - Max 26 Characters: ") 
   HTMLVIEWLINE("        <input type=text name=lname size=30 maxlength=26 value='"+LNAME+"'></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit></p>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
  END
 LGNAME=LNAME

 IF ACCOUNT:TYPE>=10 AND ACCOUNT:TYPE<>21 AND ACCOUNT:TYPE<>22 AND ACCOUNT:TYPE<>23 AND ACTIONTOTAKE="NEWCARD" THEN
  DO
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Business Name</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=NAVY>")
   HTMLVIEWLINE(" <p align=center><font size=+2><b>Business Name</b></font></p>")
   HTMLVIEWLINE(" <form name=ORDERCARDS METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=500 border=1 align=center>")
   HTMLVIEWLINE(" <tr bgcolor=NAVY>")
   HTMLVIEWLINE("   <td><font color=WHITE><b>Name On Card: "+LNAME+"<br>")
   IF BUSNAME="" THEN HTMLVIEWLINE("No Business Name on Card.</b></font></td>")
   ELSE HTMLVIEWLINE("Business Name: '"+BUSNAME+"'</b></font></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>")
   IF BUSNAME="" THEN HTMLVIEWLINE("You can add a Business Name if Needed. ")
   ELSE HTMLVIEWLINE("You can Change or Remove the Business Name if Needed. ")
   HTMLVIEWLINE(" - Max 20 Characters allowed<br><br>")
   HTMLVIEWLINE("   Business Name ") 
   HTMLVIEWLINE("   <input type=text name=busname size=40 maxlength=20 value='"+SEGMENT(BUSNAME,1,20)+"'></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit></p>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
   IF NEWBUS<>BUSNAME THEN
    DO
     AP=CHARACTERSEARCH(NEWBUS,"'")
     IF AP=0 THEN AP=CHARACTERSEARCH(NEWBUS,"&")
     WHILE AP>0
      DO
       NEWBUS=SEGMENT(NEWBUS,1,AP-1)+SEGMENT(NEWBUS,AP+1,LENGTH(NEWBUS))
       AP=CHARACTERSEARCH(NEWBUS,"'")
       IF AP=0 THEN AP=CHARACTERSEARCH(NEWBUS,"&")
      END
     FMPERFORM REVISE CARD LOC CARDLOC (0,1,ERRORTEXT) 
      DO
       SET EXTRAEMBOSS TO NEWBUS
      END
     IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Init Error Setting Business Name "+ERRORTEXT
       CALL WRITEERROR
      END
    END
  END
END

PROCEDURE VERIFYADDRESS
 VANSWER=0
 DIALOGSTART("Address Verification",100%,1)
  IF PLASTADDRESSCHANGE>=BACK30 THEN DIALOGINTROTEXT("** Recent Address Change **")
  DIALOGINTROTEXT(" ")
  DIALOGINTROTEXT("Please verify the member's Primary address.")
  DIALOGINTROTEXT(" ")
  IF PEXTRA<>"" THEN DIALOGINTROTEXT(PEXTRA)
  DIALOGINTROTEXT(PSTREET)
  DIALOGINTROTEXT(PCITY+", "+PSTATE+"  "+PZIP)
  DIALOGPROMPTCOMBOSTART("Is the address Correct?",1)
   DIALOGPROMPTCOMBOOPTION(1,"YES")
   DIALOGPROMPTCOMBOOPTION(2,"NO")
  DIALOGENDGROUPBOX
 DIALOGDISPLAY
  VANSWER=ENTERCODE("Is the address Correct?",2,1)
 DIALOGCLOSE
END


PROCEDURE WRITEERROR
 POPUPMESSAGE(2,OUTLINE)
 OUTLINE=FORMAT("99/99/99",SYSTEMDATE)+"  "+FORMAT("99:99",SYSACTUALTIME)+"   "
         +ACCOUNT:NUMBER+"  "+FORMAT("9999",SYSUSERNUMBER)+" - "+OUTLINE+" ORDERCARDS"
 FILEOPEN("LETTER","ORDERCARDS.ERRORS","APPEND",FNUMBER,FERROR)       [READS SSN TYPE HELP FILE FOR AVAILABLE SSN TYPES]
  FILEWRITELINE(FNUMBER,OUTLINE,FERROR)
 FILECLOSE(FNUMBER,FERROR) 
END
