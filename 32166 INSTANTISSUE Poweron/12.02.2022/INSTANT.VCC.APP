[INSTANT.VCC.APP - Allows selection of member to receive card and verifies and created the new card if ok]
[10/27/14 GH Modified to allow Trustee and Rep PAyee to receive new cards]
[11/07/16 GH Added option for savings only]
[12/05/16 GH Modified to allow member to have more than one card as lonf and the checking and savings to not match.]
[12/18/19 GH Added Name types SSN OVERRIDE, TRUSTEE OR REP PAYEE to allowed lost for cards]
[08/23/21 JJ Modified code to generate random card numbers. ]

SUBROUTINE DEMAND 

WINDOWS

TARGET = ACCOUNT

DEFINE
 #INCLUDE "RD.OUTPUT.DEF"
 #INCLUDE "IS.DEF"
 OUTPUTCHANNEL=NUMBER
 ERRORTEXT=CHARACTER
 TRUE=1
 FALSE=0
 BUSINESS=NUMBER
 ADDLINE1=CHARACTER
 ADDLINE2=CHARACTER
 ADDLINE3=CHARACTER
 JTMEMBER=CHARACTER
 MINOR=NUMBER
 JTNAME=CHARACTER(40)      ARRAY(500)
 JTNAMEID=CHARACTER(2)     ARRAY(500)
 JTLAST=CHARACTER(40)      ARRAY(500)
 JTSUFFIX=CHARACTER(4)     ARRAY(500)
 JTFIRST=CHARACTER(20)     ARRAY(500)
 JTMIDDLE=CHARACTER(10)    ARRAY(500)
 JTTITLE=CHARACTER(8)      ARRAY(500)
 JTNAMETYPE=NUMBER         ARRAY(500)
 JTDOB=DATE                ARRAY(500)
 JTNAMENBR=NUMBER
 PARENT=CHARACTER
 CARDNAME=CHARACTER
 SAVID=CHARACTER
 SAVDESCR=CHARACTER
 CHKID=CHARACTER
 CHKDESCR=CHARACTER
 SAVMATCH=NUMBER
 CHKMATCH=NUMBER
 REPLACEMENT=CHARACTER
 REPLVER=CHARACTER
 OKAY=CHARACTER
 [FOR GETCARDNUMBER]
 FILENAME="DEBITCARD.VALUE.228"
 FLINE=CHARACTER
 FILENUMBER=NUMBER
 ERROR=CHARACTER
 CARDNBR=CHARACTER
 SEQNBR=NUMBER
 RESULT=NUMBER
 PARITY=NUMBER
 DIGIT=NUMBER
 RESULT2=NUMBER
 ADDBUS=CHARACTER(1)
 ANAME=CHARACTER(40)
 BUSNAME=CHARACTER(20)
 RESPONSIBLEFORMINOR=CHARACTER(40)
 I=NUMBER
 J=NUMBER
 K=NUMBER
 L=NUMBER
 JT=NUMBER
 R=NUMBER
 RESPLIST=NUMBER           ARRAY(500)
 RESP=NUMBER
 FOUNDBUSNAME=CHARACTER(1)
 SJ=CHARACTER(1)
 FOUNDDBA=CHARACTER(1)
 AFNAME=CHARACTER(20)
 ALNAME=CHARACTER(40)
 SHSHIDVALUE=NUMBER
 SHCKIDVALUE=NUMBER
 SHARECHAR=CHARACTER(40)   ARRAY(100,2)
 SHARENUM=NUMBER           ARRAY(100,2)
 SHAREDATE=DATE            ARRAY(100)
 SHIF=NUMBER
 CKST=NUMBER
 RUNTIMES=NUMBER
 RT=NUMBER
 THISCARD=CHARACTER(30)
 INITERROR=CHARACTER
 EXECERROR=CHARACTER
 DOSUBROUTINE=CHARACTER
 FOUNDTRACKING=CHARACTER(1)
 CURROPTPUT=CHARACTER(1)
 OUTLINE=CHARACTER
 WNUMBER=NUMBER
 WERROR=CHARACTER
 WLINE=CHARACTER
 WPROGRAM=CHARACTER
 HASATM=CHARACTER(1)
 THISNAMETYPE=CHARACTER
 FNDCHCKING=CHARACTER(1)
END

SETUP
 #INCLUDE "IS.SETUP"
 WPROGRAM=@ENVPARAMCHAR3
 @ENVPARAMCHAR2=""
 @ENVPARAMCHAR3=""
 @ENVPARAMCHAR4=""
 @ENVPARAMCHAR5=""
 @ENVPARAMCHAR6=""
 @ENVPARAMCHAR7=""
 @ENVPARAMCHAR8=""
 @ENVPARAMCHAR9=""
 @ENVPARAMCHAR10=""
 @ENVPARAMDATE1='--/--/--'
 RUNTIMES=1
 MINOR=FALSE
 FOUNDBUSNAME="Y"
 ACCTNO=ACCOUNT:NUMBER
 WHILE SEGMENT(ACCTNO,1,1)="0"
 DO
  ACCTNO=SEGMENT(ACCTNO,2,LENGTH(ACCTNO))
 END
 SHIF=0
 CKST=0
 CALL GETPRIMEINFO
 II=1
 CALL GETALLNAMES
 CALL CHECKINFO
 THISCARD=""
 DIALOGSTART("Card Information",100%,1)
  DIALOGPROMPTCOMBOSTART("Choose Name",1)
   FOR J=1 TO II
    DO 
     IF JTNAMETYPE(J)=2 THEN DIALOGPROMPTCOMBOOPTION(J,JTNAME(J)+" - SSN OVERRIDE")
     ELSE DIALOGPROMPTCOMBOOPTION(J,JTNAME(J)+" - "+NDESC(JTNAMETYPE(J)))
    END
  DIALOGPROMPTCOMBOEND
  DIALOGPROMPTCOMBOSTART("Which Share?",1)
   DIALOGPROMPTCOMBOOPTION(0,"Not on Savings")
    FOR K=1 TO SHIF
     DO
      IF SHARENUM(K,1)<>50 AND SHARENUM(K,2)=0 AND SHAREDATE(K)='--/--/--' THEN
       DO
        DIALOGPROMPTCOMBOOPTION(K,SHARECHAR(K,1)+" - "+SHARECHAR(K,2))
       END
     END
   DIALOGPROMPTCOMBOEND
  IF FNDCHCKING="Y" THEN
   DO
    DIALOGPROMPTCOMBOSTART("Which Checking?",CKST)
     DIALOGPROMPTCOMBOOPTION(0,"Not on Checking")
     FOR L=1 TO SHIF
      DO
       IF SHARENUM(L,2)=1 AND SHAREDATE(L)='--/--/--' THEN
        DO
         DIALOGPROMPTCOMBOOPTION(L,SHARECHAR(L,1)+" - "+SHARECHAR(L,2))
        END
      END
    DIALOGPROMPTCOMBOEND
   END
 DIALOGDISPLAY
  JTNAMENBR=ENTERCODE("Choose Name",II,1)
  SHSHIDVALUE=ENTERCODE("Which Share?",SHIF,1)
  IF FNDCHCKING="Y" THEN SHCKIDVALUE=ENTERCODE("Which Checking?",SHIF,CKST)
 DIALOGCLOSE
 IF JTNAMENBR>0 THEN                                                      [VALID NAME SELECTED]
  DO
   IF JTNAMENBR=1 THEN                                                    [PRIMARY MEMBER SELECTED]
    DO
     JTMEMBER="N"
     IF  ACCOUNT:TYPE<>10 AND                                             [EXCLUDE sOLE PROP ACCOUNTS]
        (SJ="J" AND ACCOUNT:TYPE>=11 AND ACCOUNT:TYPE<90) THEN 
      DO
       OUTLINE="AUTHORIZED SIGNERS ONLY ALLOWED ON BUSINESS ACCOUNTS. TERMINATING"
       CALL WRITEERROR
       TERMINATE
      END
     IF AFNAME="" THEN ERROR="Can Not order a Primary Card on Business"   [PRIMARY NAME IS A BUSINESS NAME]
[bug:4]IF @ENVPARAMNUMBER3<>31 THEN
      DO     
       IF JTDOB(1)>DATEOFFSET(SYSTEMDATE,-16*12,0) THEN                     [UNDER 16 NOT ALLOWED A CARD]
        DO
         POPUPMESSAGE(0,"PRIMARY MEMBER IS A MINOR")
         MINOR=TRUE
        END
      END
     [IF JTDOB(1)<=DATEOFFSET(SYSTEMDATE,-16*12,0) AND                     [MINOR BUT ALLOWED CARD WITH ADULT]
        JTDOB(1)>DATEOFFSET(SYSTEMDATE,-18*12,0) THEN MINOR=TRUE]
    END
   ELSE JTMEMBER="Y"
   IF JTNAMETYPE(JTNAMENBR)=1 THEN SJ="J"                                 [JOINT SELECTED]
   ELSE IF JTNAMETYPE(JTNAMENBR)=2 THEN SJ="O"                            [SSN OVERRIDE]
   ELSE IF JTNAMETYPE(JTNAMENBR)=6 THEN SJ="T"                            [TRUSTEE SELECTED]
   ELSE IF JTNAMETYPE(JTNAMENBR)=9 THEN SJ="S"                            [SIGNER SELECTED]
   ELSE IF JTNAMETYPE(JTNAMENBR)=14 THEN SJ="R"                           [REP PAYEE SELECTED]
   IF SHSHIDVALUE=0 THEN                                                  [MISSING SAVINGS ID]
    DO
     SAVID=""
     SAVDESCR="NO SAVINGS ID SELECTED"
    END
   ELSE
    DO
     SAVID=SHARECHAR(SHSHIDVALUE,1)
     SAVDESCR=SHARECHAR(SHSHIDVALUE,2)
    END
   IF SHCKIDVALUE=0 THEN                                                  [MISSING SAVINGS ID]
    DO
     CHKID=""
     CHKDESCR="NO CHECKING ID SELECTED"
    END
   ELSE
    DO
     CHKID=SHARECHAR(SHCKIDVALUE,1)
     CHKDESCR=SHARECHAR(SHCKIDVALUE,2)
    END
   [Prepare acct#, name, and address info. ]
   CARDNAME=JTFIRST(JTNAMENBR)
   IF JTMIDDLE(JTNAMENBR)<>"" THEN CARDNAME=CARDNAME+" "+SEGMENT(JTMIDDLE(JTNAMENBR),1,1)
   CARDNAME=CARDNAME+" "+JTLAST(JTNAMENBR)
   IF JTSUFFIX(JTNAMENBR)<>"" THEN CARDNAME=CARDNAME+" "+JTSUFFIX(JTNAMENBR)
   IF LENGTH(CARDNAME)>26 THEN                                              [NAME TOO LONG - TRY TO REDUCE]
    DO
     CARDNAME=JTFIRST(JTNAMENBR)+" "+JTLAST(JTNAMENBR)
     IF JTSUFFIX(JTNAMENBR)<>"" THEN CARDNAME=CARDNAME+" "+JTSUFFIX(JTNAMENBR)
     IF LENGTH(CARDNAME)>26 THEN CARDNAME=JTFIRST(JTNAMENBR)+" "+JTLAST(JTNAMENBR)
     IF LENGTH(CARDNAME)>26 THEN CARDNAME=SEGMENT(JTFIRST(JTNAMENBR),1,1)+" "+JTLAST(JTNAMENBR)
    END 
   IF LENGTH(CARDNAME)>26 THEN CARDNAME=SEGMENT(CARDNAME,1,26)
   
   [Check for joint ownership of both savings and checking.]
   IF ERROR="" THEN
    DO
     IF JTMEMBER="N" OR JTNAMEID(JTNAMENBR)="JT" THEN      [SET SAV AND CHK MATCHES TO TRUE FOR PRIMARY MEMBER]
      DO
       SAVMATCH=TRUE
       CHKMATCH=TRUE
      END
     ELSE 
      DO
       IF SAVID="" THEN SAVMATCH=TRUE 
       ELSE SAVMATCH=FALSE
       IF CHKID="" THEN CHKMATCH=TRUE
       ELSE CHKMATCH=FALSE
       IF SJ="S" THEN THISNAMETYPE="SIGNER"
       ELSE IF SJ="O" THEN THISNAMETYPE="SSN OVERRIDE"
       ELSE IF SJ="T" THEN THISNAMETYPE="TRUSTEE"
       ELSE IF SJ="R" THEN THISNAMETYPE="REP PAYEE"
       ELSE THISNAMETYPE="JOINT" 
       MINOR=FALSE
       ERROR=""
       FOR EACH SHARE WITH (SHARE:ID=SAVID OR SHARE:ID=CHKID)
        DO
         FOR EACH SHARE NAME WITH ((SJ="J" AND SHARE NAME:TYPE=1) OR (SJ="T" AND SHARE NAME:TYPE=6) OR
                                   (SJ="S" AND SHARE NAME:TYPE=9) OR (SJ="R" AND SHARE NAME:TYPE=14)) AND
                                  (SHARE NAME:EXPIRATIONDATE='--/--/--' OR SHARE NAME:EXPIRATIONDATE>SYSTEMDATE)
          DO
           IF JTNAME(JTNAMENBR)=SHARE NAME:LONGNAME THEN
            DO
             IF SHARE:ID=SAVID THEN SAVMATCH=TRUE
             ELSE IF SHARE:ID=CHKID THEN CHKMATCH=TRUE
            END
          END
        END
       IF SAVMATCH=FALSE THEN 
        DO
         IF SJ="S" THEN ERROR=JTNAME(JTNAMENBR)+" - NOT SIGNER ON SAVINGS"
         ELSE ERROR=JTNAME(JTNAMENBR)+" - NOT JOINT ON SAVINGS"
         SAVID=""
        END
       IF CHKMATCH=FALSE THEN 
        DO
         ERROR=JTNAME(JTNAMENBR)+" - NOT "+THISNAMETYPE+" ON CHECKING"
         FNDCHCKING="N"
         CHKID=""
        END
       ELSE IF SAVMATCH=FALSE THEN ERROR=JTNAME(JTNAMENBR)+" - NOT "+THISNAMETYPE+" ON SAVINGS"
       ELSE IF JTDOB(JTNAMENBR)>DATEOFFSET(SYSTEMDATE,-16*12,0) THEN 
        DO
         [ERROR=JTNAME(JTNAMENBR)+" "+THISNAMETYPE+" IS A MINOR" ]               [UNDER 16 NOT ALLOWED A CARD]
         MINOR=TRUE
        END
       ELSE IF BUSINESS=FALSE AND JTDOB(JTNAMENBR)='--/--/--' THEN 
              ERROR=JTNAME(JTNAMENBR)+" - MISSING "+THISNAMETYPE+" BIRTH DATE"
     IF JTDOB(JTNAMENBR)<=DATEOFFSET(SYSTEMDATE,-16*12,0) AND                  [MINOR BUT ALLOWED CARD WITH ADULT]
        JTDOB(JTNAMENBR)>DATEOFFSET(SYSTEMDATE,-18*12,0) THEN MINOR=TRUE
      END 
     IF MINOR=TRUE AND ERROR="" THEN CALL GETRESPONSIBLEPARTY
    END [ of ownership verification.]
    
   [Look to see if this is a replacement card.]
   REPLACEMENT=""
   IF ERROR="" THEN
    DO
     FOR EACH CARD WITH CARD:STATUS<2 AND (CARD:TYPE=20 OR CARD:TYPE=30) AND 
         ((SAVID<>"" AND CHKID<>"" AND CARD:SAVID=SAVID AND CARD:CHKID=CHKID) OR
           (SAVID="" AND CARD:CHKID=CHKID) OR (CARD:SAVID=SAVID  AND CHKID=""))
      DO
       IF SYSUSERNUMBER=484 THEN  POPUPMESSAGE(0,CARD:NUMBER+" CARD:TYPE "+FORMAT("99",CARD:TYPE)+" SAVID "+SAVID+" CHKID "+CHKID) 
       IF (CARD:NAMETYPE=0 AND JTMEMBER="N") THEN                              [PRIMARY CARD]
        DO
         IF CARD:TYPE=20 THEN REPLACEMENT="ATM"
         ELSE REPLACEMENT="DEBIT"
        END 
       ELSE                                                                       [NON PRIMARY CARD - CHECK NAME]
        DO
         IF CARD:NAMETYPE=1 THEN
          DO
           FOR EACH CARD NAME 
            DO
             IF CARD NAME:LONGNAME=JTNAME(JTNAMENBR) THEN 
              DO
               IF CARD:TYPE=20 THEN REPLACEMENT="ATM"                            [ATM MUST BE CAPTURED BEFORE ISSUING DEBIT]
               ELSE REPLACEMENT="DEBIT"
              END
            END
          END
        END
      END
    END [End of Look to see if this is a replacement card.]
    
   IF (ACCOUNT:TYPE>=10 AND ACCOUNT:TYPE<=19) AND ERROR="" THEN    [BUSINESS ACCOUNT CHECK TO ADD BUSINESS NAME]
    DO
     IF YESNOPROMPT("Add Business Name to Card?") THEN ADDBUS="Y"
     ELSE ADDBUS="N"
     IF ADDBUS="Y" THEN 
      DO
       DIALOGSTART("Business Name",100%,1)
        DIALOGPROMPTCHAR("Verify Business Name (Max 20 CHARS)",20,BUSNAME)     [BUSINESS NAME CAN BE A MAX OF 20 CHARACTERS]
       DIALOGDISPLAY
        BUSNAME=UPPERCASE(ENTERCHARACTER("Verify Business Name (Max 20 CHARS)",20,BUSNAME))
       DIALOGCLOSE
      END
     ELSE BUSNAME=""
    END
   ELSE BUSNAME=""
   IF REPLACEMENT<>"" THEN                                                     [FLAG FOR EXISTING ATM OR DEBIT CARD]
    DO
     IF REPLACEMENT="ATM" THEN OUTLINE="Member has an existing ATM Card that MUST be captured First."
     ELSE OUTLINE="Member has an existing card.  Please run Instant Issue again and review the card selection list."
     CALL WRITEERROR
    END
   ELSE IF ERROR<>"" THEN                                                      [FOUND AN ERROR - FIX BEFORE CREATING CARD]
    DO
     OUTLINE="CARD NOT ISSUED:  "+ERROR
     CALL WRITEERROR                                                           [WRITE ERROR TO LETTER FILE]
    END
   ELSE
    DO
     DIALOGSTART("V I S A   C H E C K   C A R D   A P P L I C A T I O N",300%,1) [DISPLAY ENTERED INFORMATION FOR FINAL APPROVAL]
      DIALOGTEXTLISTSTART("Please verify the following information about your new Visa Check Card:")
       DIALOGTEXTLISTOPTION("") 
        IF SJ="S" OR AFNAME="" THEN DIALOGTEXTLISTOPTION("    Account #: "+ACCTNO+"  - Business Name: "+ANAME)
        ELSE DIALOGTEXTLISTOPTION("    Account #: "+ACCTNO) 
        DIALOGTEXTLISTOPTION("Name on Card: "+CARDNAME)
        IF MINOR=TRUE THEN DIALOGTEXTLISTOPTION("* Minor Card *")
        IF BUSNAME<>"" THEN DIALOGTEXTLISTOPTION("       Line 2: "+BUSNAME) 
        DIALOGTEXTLISTOPTION(" Address for Card:") 
        IF ADDLINE1<>"" THEN DIALOGTEXTLISTOPTION(ADDLINE1)
        DIALOGTEXTLISTOPTION(ADDLINE2)
        DIALOGTEXTLISTOPTION(ADDLINE3)
        DIALOGTEXTLISTOPTION(" Savings ID:  "+SAVID+" - "+SAVDESCR)
        DIALOGTEXTLISTOPTION("Checking ID:  "+CHKID+" - "+CHKDESCR)
        IF RESPONSIBLEFORMINOR<>"" THEN DIALOGTEXTLISTOPTION("Person responsible for Minor Card: "+RESPONSIBLEFORMINOR)
        IF REPLACEMENT<>"" THEN DIALOGTEXTLISTOPTION("This is a replacement for card "+SEGMENT(REPLACEMENT,1,4)+" "
                              +SEGMENT(REPLACEMENT,5,8)+" "+SEGMENT(REPLACEMENT,9,12)+" "+SEGMENT(REPLACEMENT,13,16))
      DIALOGTEXTLISTEND
      DIALOGPROMPTYESNO("Is everything correct?",1)
     DIALOGDISPLAY
      OKAY=ENTERYESNO("Is everything correct?","1")
     DIALOGCLOSE
    END
   IF OKAY="Y" THEN CALL DOOUTPUT                        [IF APPROVED THE PROCEED WITH CARD CREATION]
   ELSE @ENVPARAMCHAR4=""                                [IF NOT RETURN BLANK FOR TERMINATION]
  END
END

PRINT TITLE="VISA CHECK CARD APP"
 NEWLINE
END

PROCEDURE DOOUTPUT
 [ JJ removed this call since we will be generating randon card numbers ]
 [CALL GETCARDNUMBER   ]                                   [PROCEDURE TO GET NEXT CARD NUMBER]
 @ENVPARAMCHAR2=SAVID
 @ENVPARAMCHAR3=CHKID
 [@ENVPARAMCHAR4=CARDNBR]
 @ENVPARAMCHAR5=BUSNAME
 @ENVPARAMNUMBER2=JTNAMETYPE(JTNAMENBR)
 IF JTNAMEID(JTNAMENBR)<>"P" THEN                        [RETURN JOINT INFORMATION IF NON PRIMARY CARD SELECTED]
  DO
   @ENVPARAMCHAR6=JTLAST(JTNAMENBR)
   @ENVPARAMCHAR7=JTSUFFIX(JTNAMENBR)
   @ENVPARAMCHAR8=JTFIRST(JTNAMENBR)
   @ENVPARAMCHAR9=JTMIDDLE(JTNAMENBR)
   @ENVPARAMCHAR10=JTTITLE(JTNAMENBR)
   @ENVPARAMDATE1=JTDOB(JTNAMENBR)
  END

 IF MINOR=TRUE THEN                                      [PRINT OUT FOR ADULT TO SIGN FOR A MINORR CARD]
  DO
   OUTPUTOPEN(OUTPUTDEVINQPRINTER,0,"","",OUTPUTCHANNEL,ERRORTEXT)
   IF ERRORTEXT<>"" THEN POPUPMESSAGE(1,"Error opening printer: "+ERRORTEXT)
   OUTPUTSWITCH(OUTPUTCHANNEL,ERRORTEXT)
   IF ERRORTEXT<>"" THEN POPUPMESSAGE(1,"Error switching to printer: "+ERRORTEXT)
   IF ERROR="" THEN
    DO
     COL=16 "V I S A   C H E C K   C A R D   A P P L I C A T I O N"
     NEWLINE
     NEWLINE
     COL=6 "Please verify the following information about your new Visa Check Card:"
     NEWLINE
     NEWLINE
     PRINT "    Account #:  "
     COL=17 ACCTNO
     IF SJ="S" OR AFNAME="" THEN PRINT "   - Business Name: "+ANAME
     NEWLINE
     NEWLINE
     PRINT " Name on Card:  "
     COL=17 CARDNAME
     COL=46 "Card Number:  "
     THISCARD=SEGMENT(CARDNBR,1,4)+" "+SEGMENT(CARDNBR,5,8)+" "
              +SEGMENT(CARDNBR,9,12)+" "+SEGMENT(CARDNBR,13,16)
     IF LENGTH(@ENVPARAMCHAR1)>0 THEN @ENVPARAMCHAR1=@ENVPARAMCHAR1+", "
     @ENVPARAMCHAR1=@ENVPARAMCHAR1+THISCARD
     PRINT THISCARD
     NEWLINE
     COL=17 "* Minor Card *"
     NEWLINE
     NEWLINE
     PRINT " Account address:  "
     IF ADDLINE1<>"" THEN
      DO
       COL=17 ADDLINE1
       NEWLINE
      END
     COL=17 ADDLINE2
     NEWLINE
     COL=17 ADDLINE3
     NEWLINE
     PRINT "   Savings ID:  "
     IF SAVID="" THEN PRINT "   - "+SAVDESCR
     ELSE PRINT SAVID+" - "+SAVDESCR
     NEWLINE
     PRINT "  Checking ID:  "
     IF CHKID="" THEN PRINT "   - "+CHKDESCR
     ELSE PRINT CHKID+" - "+CHKDESCR
     NEWLINE
     NEWLINE
     IF REPLACEMENT<>"" THEN
      DO
       PRINT "This is a replacement for card "
       PRINT SEGMENT(REPLACEMENT,1,4)+" "+SEGMENT(REPLACEMENT,5,8)+" "
       PRINT SEGMENT(REPLACEMENT,9,12)+" "+SEGMENT(REPLACEMENT,13,16)
       NEWLINE
       NEWLINE
      END
     PRINT "By requesting this card we agree to the"
     NEWLINE
     PRINT "terms and conditions of the EFT agreement.  X_________________________________"
     NEWLINE
     PRINT REPEATCHR(" ",44)+CARDNAME
     NEWLINE
     PRINT REPEATCHR(" ",44)+"Minor"
     IF JTMEMBER="N" THEN PRINT " - Primary on account"
     ELSE
      DO
       IF SJ="S" THEN PRINT " - Authorized Signer on share(s)"
       ELSE PRINT " - Joint on share(s)"
      END
     NEWLINE
     NEWLINE
     NEWLINE
     NEWLINE
     PRINT REPEATCHR(" ",44)
     PRINT "X_________________________________"
     NEWLINE
     PRINT REPEATCHR(" ",44)
     PRINT RESPONSIBLEFORMINOR
     NEWLINE
     PRINT REPEATCHR(" ",44)
     PRINT "Parent or Guardian"
     NEWLINE
    END
   IF SJ="S" THEN
    DO
     PRINT "For Business cards, please call Bookkeeping at 302-5555"
     NEWLINE
     PRINT "ext 7190 to activate the Card when received"
     NEWLINE
    END
   NEWLINE NEWLINE
   NEWLINE NEWLINE
   PRINT SYSUSERNUMBER
   PRINT " "
   PRINT SYSTEMDATE
   PRINT " "
   PRINT FORMAT("99:99",SYSACTUALTIME)
   NEWLINE
   NEWPAGE
   OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
   OUTPUTCLOSE(OUTPUTCHANNEL,ERRORTEXT)
  END
END

PROCEDURE GETCARDNUMBER
 
 FILEOPEN("LETTER",FILENAME,"READWRITE",FILENUMBER,ERROR)
 IF ERROR="" THEN
  DO
   FILEREADLINE(FILENUMBER,FLINE,ERROR)
   IF (ERROR="" AND LENGTH(FLINE)=16) THEN
    DO
     CARDNBR=FLINE
     SEQNBR=VALUE(SEGMENT(CARDNBR,7,15))
     IF SEQNBR>=899999999 THEN 
      DO
       OUTLINE="ERROR! OUT OF CARD NUMBERS"
       CALL WRITEERROR
      END
     ELSE
      DO
       SEQNBR=SEQNBR+1
       CARDNBR="433739"+FORMAT("999999999",SEQNBR)
       RESULT=0
       PARITY=2
       I=15
       WHILE (I>=1)
        DO
         DIGIT=VALUE(SEGMENT(CARDNBR,I,I))
         RESULT=RESULT+NUMBER((DIGIT*PARITY*11)/10)
         PARITY=3-PARITY
         I=I-1
        END
       RESULT2=NUMBER((RESULT+9)/10)
       RESULT=10*RESULT2-RESULT
       CARDNBR=CARDNBR+FORMAT("9",RESULT)
      END
    END
  END
 FILESETPOS(FILENUMBER,0,ERROR)
 FILEWRITELINE(FILENUMBER,CARDNBR,ERROR)
 FILECLOSE(FILENUMBER,ERROR)
END

PROCEDURE GETRESPONSIBLEPARTY
 R=0
 RESP=0
 RESPONSIBLEFORMINOR=""
 DIALOGSTART("Select Responsible Party",100%,1)
  DIALOGPROMPTCOMBOSTART("Select Parent or Guardian",1)
   FOR J=1 TO II
    DO 
     IF JTDOB(J)<=DATEOFFSET(SYSTEMDATE,-18*12,0) THEN            [RESPONSIBLE PARTY MUST BE OVER 18]
      DO
       R=R+1
       RESPLIST(R)=J
       DIALOGPROMPTCOMBOOPTION(R,JTNAME(J)+" - "+JTNAMEID(J))
      END
    END
  DIALOGPROMPTCOMBOEND
 DIALOGDISPLAY
  RESP=ENTERCODE("Select Parent or Guardian",R,1)
 DIALOGCLOSE
 RESPONSIBLEFORMINOR=JTNAME(RESPLIST(RESP))
END

PROCEDURE GETALLNAMES
 [Only get names from name records if NOT business account]
 FOR EACH NAME WITH (NAME:EXPIRATIONDATE='--/--/--' OR NAME:EXPIRATIONDATE>SYSTEMDATE)
  DO
   IF (NAME:TYPE=0 AND NAME:NAMEFORMAT=0) OR (NAME:TYPE=2 AND NAME:SSNOVERRIDE=1) OR 
      (NAME:TYPE=1 OR NAME:TYPE=6 OR NAME:TYPE=9 OR NAME:TYPE=14) THEN  [JOINT, SSN OVERRIDE, TRUSTEE OR REP PAYEE]
    DO
     IF II<=80 THEN
      DO
       II=II+1
       JTNAME(II)=NAME:LONGNAME 
       JTLAST(II)=NAME:LAST
       JTSUFFIX(II)=NAME:SUFFIX
       JTFIRST(II)=NAME:FIRST
       JTMIDDLE(II)=NAME:MIDDLE
       JTTITLE(II)=NAME:TITLE
       JTDOB(II)=NAME:BIRTHDATE
       IF NAME:TYPE=0 THEN JTNAMEID(II)="P"
       ELSE JTNAMEID(II)="JT"
       JTNAMETYPE(II)=NAME:TYPE
      END
     ELSE ERROR="TOO MANY NAMES ON ACCOUNT"
    END
  END
 FOR EACH SHARE WITH SHARE:CLOSEDATE='--/--/--'
  DO
   SHIF=SHIF+1
   SHARECHAR(SHIF,1)=SHARE:ID
   SHARECHAR(SHIF,2)=SHARE:DESCRIPTION
   SHARENUM(SHIF,1)=SHARE:TYPE
   SHARENUM(SHIF,2)=SHARE:SHARECODE
   SHAREDATE(SHIF)=SHARE:CLOSEDATE
   IF SHARE:SHARECODE=1 THEN 
    DO
     IF CKST=0 THEN CKST=SHIF
     FNDCHCKING="Y"
    END
   IF SHARE:CLOSEDATE='--/--/--' THEN
    DO
     FOR EACH SHARE NAME WITH (SHARE NAME:EXPIRATIONDATE='--/--/--' OR SHARE NAME:EXPIRATIONDATE>SYSTEMDATE)
      DO
       IF AFNAME=SHARE NAME:FIRST AND ALNAME=SHARE NAME:LAST THEN FOUNDBUSNAME="N"
       IF SHARE NAME:TYPE=1 OR SHARE NAME:TYPE=6 OR 
          SHARE NAME:TYPE=9 OR SHARE NAME:TYPE=14 THEN
        DO
         IF II<=80 THEN
          DO
           II=II+1
           JTNAME(II)=SHARE NAME:LONGNAME
           JTLAST(II)=SHARE NAME:LAST
           JTSUFFIX(II)=SHARE NAME:SUFFIX
           JTFIRST(II)=SHARE NAME:FIRST
           JTMIDDLE(II)=SHARE NAME:MIDDLE
           JTTITLE(II)=SHARE NAME:TITLE
           JTDOB(II)=SHARE NAME:BIRTHDATE
           JTNAMEID(II)=SHARE:ID
           JTNAMETYPE(II)=SHARE NAME:TYPE
           IF SJ="" AND SHARE NAME:TYPE=1 THEN SJ="J"
           ELSE IF SJ="" AND SHARE NAME:TYPE=9 THEN SJ="S"
          END
         ELSE ERROR="TOO MANY SHARE NAMES"
        END
      END
    END
  END
END

PROCEDURE CHECKINFO
 FOUNDDBA="N"
 FOR EACH COMMENT WITH COMMENT:EXPIRATIONDATE='--/--/--'  [LOOK FOR DBA COMMENT]
  DO
   IF CHARACTERSEARCH(COMMENT:COMMENT,"DBA")>0 THEN
   FOUNDDBA="Y"
  END
  UNTIL FOUNDDBA="Y"
 IF ACCOUNT:TYPE<>10 AND ACCOUNT:TYPE<>15 THEN
  DO
   IF BUSINESS=TRUE AND (SJ="S" OR FOUNDDBA="N") AND LENGTH(AFNAME)>0 
    THEN ERROR="Business Account setup incorrectly. - Name needs to be Blank"
   ELSE IF (BUSINESS=TRUE AND FOUNDBUSNAME="N" AND SJ="J" AND FOUNDDBA="N")
    THEN ERROR="Business Account setup incorrectly. - Found Primary name on Share/Loan"
  END
END

PROCEDURE GETPRIMEINFO
 IF NAME:NAMEFORMAT=0 THEN
  DO
   AFNAME=NAME:FIRST
   ALNAME=NAME:LAST
   JTNAME(1)=NAME:LONGNAME 
   JTLAST(1)=NAME:LAST
   JTSUFFIX(1)=NAME:SUFFIX
   JTFIRST(1)=NAME:FIRST
   JTMIDDLE(1)=NAME:MIDDLE
   JTTITLE(1)=NAME:TITLE
   JTDOB(1)=NAME:BIRTHDATE
   JTNAMEID(1)="P"
   JTNAMETYPE(1)=0
  END
 ELSE BUSNAME=SEGMENT(NAME:LONGNAME,1,20)
 IF ACCOUNT:TYPE>=10 AND ACCOUNT:TYPE<=20 THEN
  DO
   BUSINESS=TRUE 
   ANAME=NAME:LONGNAME
   IF NAME:ADDRESSTYPE=1 THEN ERROR="FOREIGN ADDRESS"
   FOR EACH NAME WITH NAME:TYPE=2 AND NAME:MAILOVERRIDE=1 AND 
      (NAME:EXPIRATIONDATE='--/--/--' OR NAME:EXPIRATIONDATE>SYSTEMDATE)
    DO
     BUSNAME=SEGMENT(NAME:LONGNAME,1,20)
     IF NAME:ADDRESSTYPE=1 THEN ERROR="FOREIGN ADDRESS"
    END
  END
 ELSE IF ACCOUNT:TYPE<21 OR ACCOUNT:TYPE>23 THEN 
  DO
   BUSINESS=FALSE
   IF NAME:BIRTHDATE='--/--/--' THEN ERROR="MISSING BIRTH DATE ON PRIMARY NAME"
  END
 IF ANYWARNING(ACCOUNT,38) OR ANYWARNING(SHARE,38) THEN ERROR="NO VISA CHECK CARDS PERMITED"
 [IF NOT ANY SHARE WITH (SHARE:SHARECODE=1 AND SHARE:CLOSEDATE='--/--/--' AND 
    SHARE:CHARGEOFFDATE='--/--/--') THEN ERROR="NO CHECKING ON ACCOUNT"]
 FOR EACH NAME WITH (NAME:TYPE=2 AND NAME:MAILOVERRIDE=1 AND NAME:EFFECTIVEDATE<=SYSTEMDATE AND 
                     (NAME:EXPIRATIONDATE>SYSTEMDATE OR NAME:EXPIRATIONDATE='--/--/--'))
  DO
   ADDLINE1=NAME:STREET
   ADDLINE2=NAME:CITY+", "+NAME:STATE+"  "+NAME:ZIPCODE
   IF NAME:EXTRAADDRESS<>"" THEN
    DO
     ADDLINE3=ADDLINE2
     ADDLINE2=ADDLINE1
     ADDLINE1=NAME:EXTRAADDRESS
    END
  END
 IF ADDLINE1="" AND ADDLINE2="" THEN
  DO
   FOR EACH NAME WITH (NAME:TYPE=0)
    DO
     ADDLINE1=NAME:STREET
     ADDLINE2=NAME:CITY+", "+NAME:STATE+"  "+NAME:ZIPCODE
     IF NAME:EXTRAADDRESS<>"" THEN
      DO
       ADDLINE3=ADDLINE2
       ADDLINE2=ADDLINE1
       ADDLINE1=NAME:EXTRAADDRESS
      END
    END
  END
END

PROCEDURE WRITEERROR
 @ENVPARAMNUMBER1=1
 POPUPMESSAGE(2,OUTLINE)
 OUTLINE=FORMAT("99/99/99",SYSTEMDATE)+"  "+FORMAT("99:99",SYSACTUALTIME)+"   "
         +ACCOUNT:NUMBER+"  "+FORMAT("9999",SYSUSERNUMBER)+" - "+OUTLINE+" INSTANT ISSUE"
 FILEOPEN("LETTER","INSTANTISSUE.ERRORS","APPEND",WNUMBER,WERROR)       [READS SSN TYPE HELP FILE FOR AVAILABLE SSN TYPES]
  FILEWRITELINE(WNUMBER,OUTLINE,WERROR)
 FILECLOSE(WNUMBER,WERROR) 
END 
