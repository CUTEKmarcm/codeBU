[INSTANTISSUE - Program to get get card information and create or modify the card record for the card@once program to use.]
[04/25/14 GH Added new Visa Classic and Gold card selection]
[05/08/14 GH Added card print reason and capture reason to notes]
[06/06/14 GH Fixed problem with rerun not displaying properly]
[07/17/14 GH Added OptIn/OptOut program if the tracking 80 record is not found]
[10/27/14 GH Modified to allow Trustee and Rep PAyee to receive new cards]
[04/01/15 GH Added card number to loan record.]
[10/30/15 GH Added email for new cards]
[12/05/16 GH Modified to allow member to have more than one card as lonf and the checking and savings to not match.]
[08/23/21 JJ Modified code to generate random card numbers. ]

SUBROUTINE DEMAND WINDOWS

TARGET=ACCOUNT

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 CARDNBR=CHARACTER
 [JTMEMBER=CHARACTER]
 INITERROR=CHARACTER
 EXECERROR=CHARACTER
 OUTLINE=CHARACTER
 FNUMBER=NUMBER
 FLINE=CHARACTER
 FERROR=CHARACTER
 [OK=CHARACTER(1)]
 ACCTNO=CHARACTER(10)
 RESTRICTEDACCOUNT=CHARACTER(1)
 UA=NUMBER
 ERRORTEXT=CHARACTER
 EXPDATE=DATE
 [MM=NUMBER]
 [YY=NUMBER]
 [JTNAME=CHARACTER]
 JTLAST=CHARACTER
 JTSUFFIX=CHARACTER
 JTFIRST=CHARACTER
 JTMIDDLE=CHARACTER
 JTTITLE=CHARACTER
 JTDOB=DATE
 JTSSN=CHARACTER
 JTEXTRA=CHARACTER
 JTSTREET=CHARACTER
 JTCITY=CHARACTER
 JTSTATE=CHARACTER
 JTZIP=CHARACTER
 JTHOME=CHARACTER
 JTWORK=CHARACTER
 JTCELL=CHARACTER
 JTPHONETYPE=NUMBER
 JTADDRESSTYPE=NUMBER
 JTSSNTYPE=NUMBER
 JTNAMETYPE=NUMBER
 INPUTCOUNT= NUMBER
 INPUTFORM=CHARACTER        ARRAY(60)  
 I=NUMBER
 TMPCHR=CHARACTER
 CHOOSETYPE=CHARACTER
 CRDINFO=CHARACTER          ARRAY(50,4)
 CRDTYPE=NUMBER             ARRAY(50,2)
 CRDDATE=DATE               ARRAY(50)
 CRDCNTS=NUMBER             ARRAY(50)  
 CRDEXP=DATE                ARRAY(50)
 NEWCRD=CHARACTER           ARRAY(50) 
 CRDCHECK=CHARACTER         ARRAY(50)
 C=NUMBER
 II=NUMBER
 ANAME=CHARACTER
 [FNDREPLACE=CHARACTER(1)]
 DONE=CHARACTER(1)
 CARDTOREPLACE=CHARACTER
 REPRINT=CHARACTER
 REPLACESAMECARD=CHARACTER
 REPLACENEWCARD=CHARACTER
 CARDREQUEST=CHARACTER
 CARDTYPE=NUMBER
 CARDLOC=NUMBER  
 CDESCRIPTION=CHARACTER
 CSAVID=CHARACTER
 CCHKID=CHARACTER
 CLOCID=CHARACTER
 CCREDITCARDID=CHARACTER
 CACTIVEDATE=DATE
 CEXPIRATIONDATE=DATE
 CATMCOUNTLIMIT=NUMBER
 CAUTHLIMIT=MONEY
 CAUTHCOUNTLIMIT=NUMBER
 CBILLPAYLIMIT=MONEY
 CBILLPAYCOUNTLIMIT=NUMBER
 CBILLPAYMAX=MONEY
 CCREDDEBCOUNTLIMIT=NUMBER
 CCREDDEBLIMIT=MONEY
 CDEPCOUNTLIMIT=NUMBER
 CDEPLIMIT=MONEY
 CDEPMAX=MONEY
 CEXTRAEMBOSS=CHARACTER
 CLIMITAMOUNT=MONEY
 CLIMITDAYS=NUMBER
 CMISCLIMIT=MONEY
 CMISCCOUNTLIMIT=NUMBER
 CNAMETYPE=NUMBER
 CPOSLIMIT=MONEY
 CPOSCOUNTLIMIT=NUMBER
 CREISSUEMONTHS=NUMBER
 CSINGLEUSAGEMAX=MONEY
 CUSAGELIMIT=MONEY
 CWDCHECKLIMIT=MONEY
 CWDCHECKCOUNTLIMIT=NUMBER
 CWDCHECKMAX=MONEY
 CXFERCOUNTLIMIT=NUMBER
 CXFERLIMIT=MONEY
 CXFERMAX=MONEY
 OLDLOCATOR=NUMBER
 SEQNBR=NUMBER
 RESULT=NUMBER
 PARITY=NUMBER
 DIGIT=NUMBER
 RESULT2=NUMBER
 [LP=NUMBER]
 FILENAME=CHARACTER
 CN=CHARACTER(6)
 FILELOCKED=NUMBER          [Locked flag, true if successfully locked]
 [FILENUMBER=NUMBER          [File Number of above for reading purposes]]
 LOCKFILENUMBER=NUMBER      [File Number of Locked file]
 CURRFILENAME=CHARACTER
 PROCESSOK=NUMBER
 TRUE=1
 FALSE=0
 ERROR=CHARACTER
 DOPRINT=CHARACTER(1)
 NEWCARDEXPIRATION=DATE
 TOTCHECKS=NUMBER
 CARDSTATUS=NUMBER
 PRIMENAME=CHARACTER
 LNAME=CHARACTER
 LGNAME=CHARACTER
 WHICHMENU=CHARACTER
 BADREPLACE=NUMBER
 RETRIEVECARD=CHARACTER
 HASTEMPLATE=CHARACTER(1)
 TMPCHAR=CHARACTER
 BUSNAME=CHARACTER
 NEWBUS=CHARACTER
 TOT30=NUMBER
 TOT40=NUMBER
 TOT41=NUMBER
 FND40=CHARACTER(1)
 FND41=CHARACTER(1)
 AP=NUMBER
 NEWNAMETYPE=CHARACTER
 NEWNAMELOC=NUMBER
 LNID=CHARACTER
 [SHID=CHARACTER]
 FNDCARDNOTE=CHARACTER(1)
 REPRINTRESSON=CHARACTER    ARRAY(20)
 RI=NUMBER
 RP=NUMBER
 RPSEL=NUMBER
 TEXT2=CHARACTER
 OTHERREASON=CHARACTER
 STATUSREASON=CHARACTER   ARRAY(20)
 CAPTUREREASON=CHARACTER
 NEWNOTE2=CHARACTER
 BLOCKCODE=CHARACTER(1)
 BRNO=NUMBER
 FNDTRACK80=CHARACTER(1)
 LOANREF=CHARACTER
 [Youth Account Variables
 PRIMAGE=NUMBER
 ACCTTYPE=NUMBER
 YOUTHACCTTYPE=NUMBER]
END

SETUP
[FIXME:YOUTHACCTTYPE = 1]
 BRNO=GETDATANUMBER(GETCONSOLEBRANCH,SYSCONSOLENUM)
 [INITSUBROUTINE(INITERROR)
  IF INITERROR<>"" THEN
   DO
    OUTLINE="Init Error INSTANTISSUE.PRINTER.CHECK: "+INITERROR
    CALL WRITEERROR
   END
  EXECUTE("INSTANTISSUE.PRINTER.CHECK",EXECERROR)
  IF EXECERROR<>"" THEN
   DO
    OUTLINE="Execute Error INSTANTISSUE.PRINTER.CHECK: "+EXECERROR
    CALL WRITEERROR
  END]
 CREISSUEMONTHS=48
 REPRINTRESSON(1)="Never got past Waiting for Pin"
 REPRINTRESSON(2)="Never got past Pin Received"
 REPRINTRESSON(3)="Problem logging into card@once"
 REPRINTRESSON(4)="Problem entering the Pin number"
 REPRINTRESSON(5)="Card did not print"
 REPRINTRESSON(6)="Bad card picture"
 REPRINTRESSON(7)="Printer was offline"
 REPRINTRESSON(8)="Showed Error - Stock not loaded"
 REPRINTRESSON(9)="Member did not like the picture"
 REPRINTRESSON(10)="Card was damaged"
 REPRINTRESSON(11)="Card did not work"
 REPRINTRESSON(12)="Member lost NEW card"
 REPRINTRESSON(13)="Fraud on NEW card"
 REPRINTRESSON(14)="NEW card stolen"
 RP=14
 STATUSREASON(1)="Card Lost"
 STATUSREASON(2)="Card Stolen"
 STATUSREASON(3)="Fraud On Card"
 STATUSREASON(4)="Never Received New Card"
 STATUSREASON(5)="Home Depot Breach"
 STATUSREASON(20)="Other Reason"
 DOPRINT="Y"
 BUSNAME=""
 PRIMENAME=""
 FOR EACH NAME WITH (NAME:EXPIRATIONDATE='--/--/--' OR NAME:EXPIRATIONDATE>SYSTEMDATE)
  DO
   IF NAME:TYPE=0 OR (NAME:TYPE=2 AND NAME:SSNOVERRIDE=1) THEN 
    DO
     IF NAME:NAMEFORMAT=1 THEN BUSNAME=NAME:LONGNAME 
     ELSE PRIMENAME=NAME:LONGNAME
    END
  END
 NEWCARDEXPIRATION=DATEOFFSET(SYSTEMDATE,48,31)
 EXPDATE=NEWCARDEXPIRATION+1
 ANAME=NAME:FIRST+" "+NAME:LAST
 JTEXTRA=NAME:EXTRAADDRESS
 JTSTREET=NAME:STREET
 JTCITY=NAME:CITY
 JTSTATE=NAME:STATE
 JTZIP=NAME:ZIPCODE
 JTHOME=NAME:HOMEPHONE
 JTPHONETYPE=NAME:PHONETYPE
 JTADDRESSTYPE=NAME:ADDRESSTYPE
[======== Restrict all programs except these if employee part of account ============]
 ACCTNO=ACCOUNT:NUMBER
 RESTRICTEDACCOUNT="N"
 HASTEMPLATE="N"
 [IF (ACCOUNT:NUMBER="0000045831" AND SYSUSERNUMBER<>1619 AND SYSUSERNUMBER<>23 AND SYSUSERNUMBER<>106 AND
     SYSUSERNUMBER<>484 AND SYSUSERNUMBER<>159 AND SYSUSERNUMBER<>1250 AND SYSUSERNUMBER<>1585 AND SYSUSERNUMBER<>1694 AND SYSUSERNUMBER<>674) OR ACCOUNT:TYPE=90 OR 
     ACCOUNT:TYPE=96 OR ACCOUNT:TYPE=98 THEN
  DO
   POPUPMESSAGE(1,"PROGRAM NOT ALLOWED ON THIS ACCOUNT")
  END]
 FOR USER WITH NUMBER FORMAT("9999",SYSUSERNUMBER)
  DO
   TMPCHAR=USER:JOBFUNCTION
   HASTEMPLATE="Y"
   [Removed the Template requirement per Raye Anne 2/3/2020]
   [IF CHARACTERSEARCH(TMPCHAR,"BRANCH MANAGER")>0      OR CHARACTERSEARCH(TMPCHAR,"ABM")>0 OR sysusernumber=51 or 
       CHARACTERSEARCH(TMPCHAR,"SENIOR TELLER")>0        OR CHARACTERSEARCH(TMPCHAR,"SR TELLER")>0 OR
       CHARACTERSEARCH(TMPCHAR,"HEAD TELLER")>0          OR CHARACTERSEARCH(TMPCHAR,"MEMBER SERVICE")>0 OR
       CHARACTERSEARCH(TMPCHAR,"LOAN PROCESSOR")>0       OR CHARACTERSEARCH(TMPCHAR,"LP COLL DOCS")>0 OR 
       CHARACTERSEARCH(TMPCHAR,"LOAN OFFICER")>0         OR CHARACTERSEARCH(TMPCHAR,"SR VP LENDING")>0 OR 
       CHARACTERSEARCH(TMPCHAR,"VP ACCT")>0              OR CHARACTERSEARCH(TMPCHAR,"AVP BRANCH OPERATION")>0 OR 
       CHARACTERSEARCH(TMPCHAR,"AVP IT")>0               OR CHARACTERSEARCH(TMPCHAR,"ADVANCED TELLER")>0 OR
       CHARACTERSEARCH(TMPCHAR,"ADV P/T TELLER")>0       OR CHARACTERSEARCH(TMPCHAR,"P/T TELL INSTANT ISS")>0 OR
       CHARACTERSEARCH(TMPCHAR,"ACCT SV COORDINATOR")>0  OR CHARACTERSEARCH(TMPCHAR,"MBR SERVICES/CP MENU")>0 OR
       CHARACTERSEARCH(TMPCHAR,"LOAN CENTER MGR")>0      OR CHARACTERSEARCH(TMPCHAR,"LOAN PROCESS WITH CP")>0 OR
       CHARACTERSEARCH(TMPCHAR,"ASST LN CENTER MGR")>0   OR CHARACTERSEARCH(TMPCHAR,"FULL TIME TELLER")>0 OR
       CHARACTERSEARCH(TMPCHAR,"SR MBR SV COORDINATR")>0 OR CHARACTERSEARCH(TMPCHAR,"BM LN OFFICER")>0 OR
       CHARACTERSEARCH(TMPCHAR,"BM LOAN OFFICER")>0 THEN HASTEMPLATE="Y"]
   FOR UA=1 TO 20
    DO
     IF USER:RESTRICTEDACCOUNTS:(UA)=ACCTNO THEN RESTRICTEDACCOUNT="Y"  
    END
  END
 FMPERFORM REVISE ACCOUNT (1,0,ERRORTEXT)
  DO
   SET MEMBERGROUP TO ACCOUNT:MEMBERGROUP
  END
 IF ERRORTEXT<>"" THEN RESTRICTEDACCOUNT="Y" 
[======== End of Employee program section                               ============]
 IF RESTRICTEDACCOUNT="Y" THEN POPUPMESSAGE(1,"ACCOUNT RESTRICTED")
 ELSE IF HASTEMPLATE="N" AND SYSUSERNUMBER<>159 AND SYSUSERNUMBER<>324 THEN 
   POPUPMESSAGE(1,"User does not have the privilege to run this program.")
 ELSE
  DO
   CHOOSETYPE=""
   [CALL CHECKFORPINPAD]
   IF CHOOSETYPE<>"CANCEL" THEN
    DO
     BADREPLACE=0
     CARDREQUEST=""
     CARDTOREPLACE=""
     REPRINT=""
     REPLACESAMECARD=""
     REPLACENEWCARD=""
     CALL GETCARDS
     CALL DISPLAYCHOICES
    END
   IF CHOOSETYPE<>"CANCEL" THEN
    DO
     IF (CARDTYPE=30 AND TOT30=0) OR 
        (CARDTYPE=40 AND TOT40=0 AND FND40="Y") OR 
        (CARDTYPE=41 AND TOT41=0 AND FND41="Y") THEN CARDREQUEST="YES"
     ELSE 
      DO
       RETRIEVECARD="NO"
       WHILE RETRIEVECARD="NO"
        DO
         RETRIEVECARD="YES" 
         CALL CARDREPLACESELECTION
         IF REPRINT="YES" THEN 
          DO
           IF FNDCARDNOTE<>"Y" THEN CALL CHECKRUNAGAIN
           IF CHOOSETYPE<>"CANCEL" AND RPSEL=0 OR RPSEL=9 OR RPSEL=10 OR RPSEL=11 THEN CALL GETCARDFROMMEMBER
          END
        END
      END         
     IF CHOOSETYPE<>"CANCEL" THEN
      DO  
       IF CARDREQUEST="YES" THEN  
        DO
         IF CARDTYPE=30 THEN
          DO
           CDESCRIPTION="VISA DEBIT CARD"
           CALL INSTANTVCCAPP
           [IF CARDNBR="" THEN POPUPMESSAGE(0,"Card creation canceled.")]
           IF [CARDNBR<>"" AND ] BADREPLACE=0 THEN CALL CREATEVCCRECORD 
          END
         ELSE                  
  [FIXME:IF CARDTYPE=31 THEN
          DO
           CDESCRIPTION="YOUTH CARD"
           CALL INSTANTVCCAPP
           [IF CARDNBR="" THEN POPUPMESSAGE(0,"Card creation canceled.")]
           IF [CARDNBR<>"" AND ] BADREPLACE=0 THEN CALL CREATEVCCRECORD 
          END
         ELSE]         
          DO
           INITSUBROUTINE(INITERROR)
           @ENVARGNUMBER5=CARDTYPE
           IF INITERROR<>"" THEN
            DO
             OUTLINE="Init Error INSTANTISSUE.VISA: "+INITERROR
            [OUTLINE="Init Error CARDPRINTING.ATFCU: "+INITERROR]
             CALL WRITEERROR
            END
           EXECUTE("INSTANTISSUE.VISA",EXECERROR)
           [EXECUTE("CARDPRINTING.XXX",EXECERROR)] 
           [EXECUTE("CARDPRINTING.ATFCU",EXECERROR)] [TODO:seeNewCode-using INSTANTISSUE.VISA]
           IF EXECERROR<>"" THEN
            DO
             OUTLINE="Execute Error INSTANTISSUE.VISA: "+EXECERROR
            [OUTLINE="Execute Error CARDPRINTING.ATFCU: "+EXECERROR]
             CALL WRITEERROR
            END
           NEWNAMETYPE=@ENVARGCHAR2
           LNID=@ENVARGCHAR3

           BUSNAME=@ENVARGCHAR4
           CHOOSETYPE=@ENVARGCHAR5
           NEWNAMELOC=@ENVARGNUMBER6
           IF CHOOSETYPE<>"CANCEL" THEN CALL CREATENEWCARD
          END
        END
       ELSE 
        DO
         CALL GETCURRENTCARD 
         IF REPRINT="YES" OR REPLACESAMECARD="YES" THEN 
          DO
           IF FNDCARDNOTE<>"Y" THEN CALL CHECKRUNAGAIN
           IF CHOOSETYPE<>"CANCEL" THEN CALL UPDATECURRENTCARD
          END
         ELSE CALL REPLACECURRENTCARD
        END
      END
    END
   IF CHOOSETYPE<>"CANCEL" AND DOPRINT="Y" AND BADREPLACE=0 AND CARDNBR<>"" THEN 
    DO
     IF SYSSYMDIRECTORY<>0 [AND SYSUSERNUMBER<>159 AND SYSUSERNUMBER<>484 AND 
                               SYSUSERNUMBER<>106] THEN POPUPMESSAGE(0,"Test Sym - No Card Printing")
     ELSE CALL PRINTCARD
    END
  END
END

PRINT TITLE="INSTANT ISSUE"
  SUPPRESSNEWLINE       
END

PROCEDURE CHECKFORPINPAD
 HTMLVIEWOPEN(0)
 HTMLVIEWLINE("<html>")
 HTMLVIEWLINE("<head>")
 HTMLVIEWLINE("<title>Status Checks</title>")
 HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
 HTMLVIEWLINE("</head>")
 HTMLVIEWLINE("<body bgcolor=#CCCCCC text=#000000>")
 HTMLVIEWLINE(" <p align=center><font size=+2><b>Check Pin Pad and Printer Status</b></font></p>")
 HTMLVIEWLINE(" <form name=INSTANTISSUE METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
 HTMLVIEWLINE("  <p align=center><font size=5>Is the Pin Pad Connected to this computer and the Printer Status OK?</font></p>")
 HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Yes>")
 HTMLVIEWLINE("                  <INPUT TYPE=SUBMIT NAME=close VALUE=No></p>")
 HTMLVIEWLINE(" </form>")
 HTMLVIEWLINE("</body>")
 HTMLVIEWLINE("</html>")
 HTMLVIEWDISPLAY
 CALL LOADRESULTS
 CALL SETVARIABLES
END

PROCEDURE DISPLAYCHOICES
[FIXME:Create Youth Card
 FOR ACCOUNT ACCOUNT:NUMBER
  DO
   ACCTTYPE=ACCOUNT:TYPE
   PRIMAGE=NUMBER((SYSTEMDATE-NAME:BIRTHDATE)/365.25)    
   [popupmessage(0,"PRIMAGE: "+format("#####9",PRIMAGE))]
  END]
 CHOOSETYPE=""
 WHILE CHOOSETYPE="" AND CARDTYPE=0 
  DO
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Instant Issue Selection</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=#000000>")
   HTMLVIEWLINE(" <form name=INSTANTISSUE METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=200 border=0 align=center>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td colspan=2><font size=+2><b>Instant Issue Selection</b></font></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>&nbsp;&nbsp;</td>")
   HTMLVIEWLINE("   <td>") 
   HTMLVIEWLINE("    <b><font size=+1><b>Choose Card Type</font></b></b><br>")
[FIXME:IF PRIMAGE<18 AND ACCTTYPE=YOUTHACCTTYPE THEN HTMLVIEWLINE("<input type=radio name=cardselection value=31>Youth Card<br>")]
   HTMLVIEWLINE("    <input type=radio name=cardselection value=30>VISA Debit Card<br>")
   [Credit Cards]
   IF FND40="Y" THEN HTMLVIEWLINE("<input type=radio name=cardselection value=40>VISA Classic Credit Card<br>")
   IF FND41="Y" THEN HTMLVIEWLINE("<input type=radio name=cardselection value=41>VISA Gold Credit Card")
   HTMLVIEWLINE("  </td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>&nbsp;&nbsp;</td>")
   HTMLVIEWLINE("   <td><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit>&nbsp;&nbsp;&nbsp;&nbsp;")  
   HTMLVIEWLINE("         <INPUT TYPE=SUBMIT NAME=close VALUE=Exit></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
   IF CHOOSETYPE="" AND CARDTYPE=0 THEN POPUPMESSAGE(0,"You must select the card type to Issue")
  END
END

PROCEDURE CARDREPLACESELECTION
 WHICHMENU="CARDREPLACESELECTION"
 DONE="N"
 WHILE DONE="N"
  DO
   DONE="Y"
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Card Instant Issue</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=#000000>")
   HTMLVIEWLINE(" <p align=center><font size=6 color=NAVY><b>Card Instant Issue</b></font></p>")
   HTMLVIEWLINE(" <form name=INSTANTISSUE METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=600 border=1 align=center>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE(" <td bgcolor=Blue><font color=White size=4><b>Please review the list carefully.")
   HTMLVIEWLINE("     The Member can only have one card in their name.</b></font><br></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td><b>Select existing card</b><br>")
   IF CRDCNTS(CARDTYPE)=0 THEN
    DO
     HTMLVIEWLINE("<br>No Existing cards found on account.<br><br></td>")
     HTMLVIEWLINE(" </tr>") 
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("    <td><input type=checkbox name=cardrequest value=YES")
     IF CARDREQUEST="YES" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("     >Check to create a new card")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
    END
   ELSE
    DO
     HTMLVIEWLINE("   <select name=cardtoreplace size="+FORMAT("99",CRDCNTS(CARDTYPE)+1)+">") 
     FOR II=1 TO C
      DO
       IF CRDTYPE(II,1)=CARDTYPE THEN 
        DO
         HTMLVIEWLINE("<option value="+CRDINFO(II,1))
         IF CARDTOREPLACE=CRDINFO(II,1) THEN HTMLVIEWLINE(" selected=selected")
         HTMLVIEWLINE(">"+CRDINFO(II,1)+" ")
         IF CRDTYPE(II,2)=0 THEN HTMLVIEWLINE(" - "+ANAME)
         ELSE HTMLVIEWLINE(" - "+CRDINFO(II,2)+" "+CRDINFO(II,3))
         HTMLVIEWLINE(" - Expiring: "+FORMAT("99/99/99",CRDEXP(II))+NEWCRD(II))
         HTMLVIEWLINE(" - "+CRDCHECK(II))
         HTMLVIEWLINE("</option>")
        END
       END
     HTMLVIEWLINE("<option></option>")
     HTMLVIEWLINE("   </select>")
     HTMLVIEWLINE("   <br><br>")
     HTMLVIEWLINE("    <b>Do you want to Reprint, Renew or Replace the Card?</b><br>")
     HTMLVIEWLINE("       <input type=checkbox name=reprint value=YES")
     IF REPRINT="YES" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE(">Reprint - Same card number and Same Expiration Date <br>")
     HTMLVIEWLINE("&nbsp;&nbsp; &nbsp; &nbsp;** Reprint requires member to turn in old card **<BR>")
     HTMLVIEWLINE("       <input type=checkbox name=replacesamecard value=YES")
     IF REPLACESAMECARD="YES" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE(">Renew - Same card number with NEW Expiration Date of "+FORMAT("99/99/99",NEWCARDEXPIRATION)+"<br><br>")
     HTMLVIEWLINE("       <input type=checkbox name=replacenewcard value=YES")
     IF REPLACENEWCARD="YES" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE(">Capture card and issue a New card number - Select Reason for Replacement<br>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<input type=radio name=statusreason value=1>Card Lost<br>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<input type=radio name=statusreason value=2>Card Stolen<br>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<input type=radio name=statusreason value=3>Fraud On Card<br>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<input type=radio name=statusreason value=4>Never Received New Card<br>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<input type=radio name=statusreason value=5>Home Depot Breach<br>")
     HTMLVIEWLINE("&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<input type=radio name=statusreason value=20>Other Reason<br><br></td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("    <td><input type=checkbox name=cardrequest value=YES")
     IF CARDREQUEST="YES" THEN HTMLVIEWLINE(" checked")
     HTMLVIEWLINE("     >Check here if the member does NOT have a card listed above")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
    END
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  </td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit>&nbsp;&nbsp;&nbsp;&nbsp;")
   HTMLVIEWLINE("                  <INPUT TYPE=SUBMIT NAME=close VALUE=Exit></p>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
   TOTCHECKS=0
   IF CHOOSETYPE="CANCEL" THEN
    DO
     CARDREQUEST=""
     CARDTOREPLACE=""
     REPRINT=""
     REPLACESAMECARD=""
     REPLACENEWCARD=""
     CARDSTATUS=0
     CHOOSETYPE=""
    END
   ELSE
    DO
     IF REPRINT<>""         THEN TOTCHECKS=TOTCHECKS+1
     IF REPLACESAMECARD<>"" THEN TOTCHECKS=TOTCHECKS+1
     IF REPLACENEWCARD<>""  THEN TOTCHECKS=TOTCHECKS+1
     IF CARDREQUEST<>""     THEN TOTCHECKS=TOTCHECKS+1
     IF TOTCHECKS<>1 THEN
      DO
       IF TOTCHECKS=0 THEN POPUPMESSAGE(1, "No selections made. Please review.")
       ELSE POPUPMESSAGE(1,"Too many selection boxes checked.  Only on box may be checked.")
       DONE="N"
      END
     ELSE IF CARDTOREPLACE="" AND CARDREQUEST="" THEN
      DO
       POPUPMESSAGE(1, "Must card select card to replace.")
       DONE="N"
      END
     ELSE IF (REPLACENEWCARD="YES" AND CARDSTATUS=0) OR (REPLACENEWCARD<>"YES" AND CARDSTATUS>0) THEN
      DO
       IF REPLACENEWCARD="YES" AND CARDSTATUS=0 THEN POPUPMESSAGE(1,"Reason for card replacement must be selected")
       ELSE POPUPMESSAGE(1,"Replacement reason selected but Card Replacement not checked")
       DONE="N"
      END
     IF DONE="N" THEN
      DO
       CARDREQUEST=""
       REPRINT=""
       REPLACESAMECARD=""
       REPLACENEWCARD=""
       CARDSTATUS=0
       CHOOSETYPE=""
      END
    END
  END
END

PROCEDURE GETCARDFROMMEMBER
 HTMLVIEWOPEN(0)
 HTMLVIEWLINE("<html>")
 HTMLVIEWLINE("<head>")
 HTMLVIEWLINE("<title>Retrieve old card</title>")
 HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
 HTMLVIEWLINE("</head>")
 HTMLVIEWLINE("<body bgcolor=#CCCCCC text=#000000>")
 HTMLVIEWLINE(" <p align=center><font size=6 color=NAVY><b>Retrieve Old Card</b></font></p>")
 HTMLVIEWLINE(" <form name=GETCARD METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
 HTMLVIEWLINE("<table width=600 border=1 align=center>")
 HTMLVIEWLINE(" <tr>")
 HTMLVIEWLINE("   <td><font size=4><b>The member must turn in the card they want reprinted to obtain a new one.")
 HTMLVIEWLINE("       If the member does not have their card with them, then you MUST select Renew")
 HTMLVIEWLINE("       instead of Reprint.</font><br><br>")
 HTMLVIEWLINE("       <font size=4 color=MAROON>Please note that when you select Renew, the Expiration Date and CVV ")
 HTMLVIEWLINE("       code will change.  If the member has any automated withdrawals associated with their card,")
 HTMLVIEWLINE("       they will have to contact those vendors to update the card information.</b><font><br><br></td>")
 HTMLVIEWLINE(" </tr>")
 HTMLVIEWLINE(" </table>")
 HTMLVIEWLINE("  <p align=center>Did you get the old card from the member?</p>")
 HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=yescard VALUE=Yes>")
 HTMLVIEWLINE("                  <INPUT TYPE=SUBMIT NAME=nocard VALUE=No></p>")
 HTMLVIEWLINE(" </form>")
 HTMLVIEWLINE("</body>")
 HTMLVIEWLINE("</html>")
 HTMLVIEWDISPLAY
 CALL LOADRESULTS
 CALL SETVARIABLES
END

PROCEDURE INSTANTVCCAPP
 INITSUBROUTINE(INITERROR)
 IF INITERROR<>"" THEN
  DO
   OUTLINE="Init Error INSTANT.VCC.APP: "+INITERROR
   CALL WRITEERROR
  END
 @ENVARGNUMBER3=CARDTYPE
 EXECUTE("INSTANT.VCC.APP",EXECERROR)
 IF EXECERROR<>"" THEN
  DO
   OUTLINE="Execute Error INSTANT.VCC.APP: "+EXECERROR
   CALL WRITEERROR
  END
 CSAVID=@ENVARGCHAR2
 CCHKID=@ENVARGCHAR3
 CARDNBR=@ENVARGCHAR4
 BUSNAME=@ENVARGCHAR5
 JTLAST=@ENVARGCHAR6
 JTSUFFIX=@ENVARGCHAR7
 JTFIRST=@ENVARGCHAR8
 JTMIDDLE=@ENVARGCHAR9
 JTTITLE=@ENVARGCHAR10
 JTDOB=@ENVARGDATE1
 BADREPLACE=@ENVARGNUMBER1
 JTNAMETYPE=@ENVARGNUMBER2
END
 
PROCEDURE CREATEVCCRECORD
 IF JTLAST<>"" THEN CNAMETYPE=2
 ELSE CNAMETYPE=0
 FMPERFORM CREATE CARD LOC AFTERLAST (1,CARDTYPE,CARDLOC,ERRORTEXT)
  DO
   SET NUMBER TO "NEXT" 
   SET STATUS TO 01
   SET ACTIVEDATE TO SYSTEMDATE
   SET NAMETYPE TO CNAMETYPE
   SET ISSUEDATE TO SYSTEMDATE
   SET EFFECTIVEDATE TO SYSTEMDATE
   SET EXPIRATIONDATE TO EXPDATE
   SET SAVID TO CSAVID
   SET CHKID TO CCHKID 
   SET DESCRIPTION TO CDESCRIPTION 
   SET EXTRAEMBOSS TO BUSNAME
  END

 IF ERRORTEXT<>"" THEN
  DO
   OUTLINE="Error creating new card: "+ERRORTEXT
   CALL WRITEERROR
  END
 ELSE
  DO
   FOR EACH CARD WITH CARD:LOCATOR=CARDLOC
    DO
     CARDNBR=CARD:NUMBER
    END

   FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0 
     SET TEXT:1 TO "Card created through Instant Issue"
     SET TEXT:2 TO "New Card "
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating card note "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   CALL CREATELOOKUP 
  END

 IF JTLAST<>"" THEN
  DO
   FOR EACH SHARE WITH SHARE:ID=CSAVID OR SHARE:ID=CCHKID 
    DO
     FOR EACH SHARE NAME WITH (SHARE NAME:EXPIRATIONDATE='--/--/--' OR 
                               SHARE NAME:EXPIRATIONDATE>SYSTEMDATE) AND
                               SHARE NAME:TYPE<>4 AND
                               SHARE NAME:FIRST=JTFIRST AND
                               SHARE NAME:LAST=JTLAST AND
                               SHARE NAME:BIRTHDATE=JTDOB AND
                               JTSSN=""
      DO
       IF SHARE NAME:STREET<>"" AND JTSTREET<>SHARE NAME:STREET THEN
        DO
         JTEXTRA=SHARE NAME:EXTRAADDRESS
         JTSTREET=SHARE NAME:STREET
         JTCITY=SHARE NAME:CITY
         JTSTATE=SHARE NAME:STATE
         JTZIP=SHARE NAME:ZIPCODE
         JTADDRESSTYPE=SHARE NAME:ADDRESSTYPE
        END
       IF JTHOME<>"" AND JTHOME<>SHARE NAME:HOMEPHONE THEN 
        DO
         JTHOME=SHARE NAME:HOMEPHONE
         JTPHONETYPE=SHARE NAME:PHONETYPE
        END
       JTSSN=SHARE NAME:SSN
       JTSSNTYPE=NAME:SSNTYPE
       JTWORK=SHARE NAME:WORKPHONE
       JTCELL=SHARE NAME:MOBILEPHONE
      END
    END
   FMPERFORM CREATE CARD LOC CARDLOC NAME LOC AFTERLAST (1,2,ERRORTEXT)
    DO
     SET TITLE TO JTTITLE
     SET FIRST TO JTFIRST
     SET MIDDLE TO JTMIDDLE
     SET LAST TO JTLAST
     SET SUFFIX TO JTSUFFIX
     SET BIRTHDATE TO JTDOB
     SET SSN TO JTSSN
     SET SSNTYPE TO JTSSNTYPE
     SET HOMEPHONE TO JTHOME
     SET PHONETYPE TO JTPHONETYPE
     SET WORKPHONE TO JTWORK
     SET MOBILEPHONE TO JTCELL
     SET LASTADDRVERIFDATE TO SYSTEMDATE
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating new card name: "+ERRORTEXT
     CALL WRITEERROR
    END
  END

 IF SEGMENT(CARDNBR,1,4)="4337" AND CCHKID<>"" THEN CALL CHECKOPTINOUT
END

PROCEDURE CREATELOOKUP
 IF CARDTYPE=30 THEN
  DO
   FMPERFORM CREATE LOOKUP 0 (1,CARDTYPE,ERRORTEXT)
    DO
     SET TYPE TO CARDTYPE
     SET NUMBER TO CARDNBR
     SET IDTYPE TO 2
    END
  END
 ELSE
  DO
   FMPERFORM CREATE LOOKUP 0 (1,CARDTYPE,ERRORTEXT)
    DO
     SET TYPE TO CARDTYPE
     SET NUMBER TO CARDNBR
     SET IDTYPE TO 1
     SET ID TO CCREDITCARDID
    END
  END
 IF ERRORTEXT<>"" THEN
  DO
   OUTLINE="Error creating card lookup "+CARDNBR+": "+ERRORTEXT
   CALL WRITEERROR
  END
END

PROCEDURE GETCARDS
 FOR EACH CARD WITH (CARD:EXPIRATIONDATE='--/--/--' OR CARD:EXPIRATIONDATE>SYSTEMDATE) AND CARD:STATUS<>2 AND
          (CARD:TYPE=30 OR CARD:TYPE=40 OR CARD:TYPE=41)
  DO
   IF CARD:TYPE=30 THEN TOT30=TOT30+1
   ELSE IF CARD:TYPE=40 THEN TOT40=TOT40+1
   ELSE TOT41=TOT41+1
   C=C+1
   CRDCNTS(CARD:TYPE)=CRDCNTS(CARD:TYPE)+1
   CRDTYPE(C,1)=CARD:TYPE
   CRDTYPE(C,2)=CARD:NAMETYPE 
   CRDINFO(C,1)=CARD:NUMBER
   IF CARD:EXPIRATIONDATE='--/--/--' THEN 
    DO
     CRDEXP(C)=NEWCARDEXPIRATION
     NEWCRD(C)=" (New)"
    END
   ELSE CRDEXP(C)=CARD:EXPIRATIONDATE-1
   IF CARD:TYPE=30 THEN 
    DO
     CRDCHECK(C)="SavID: "
     IF CARD:SAVID="" THEN CRDCHECK(C)="SavID: NA"
     ELSE CRDCHECK(C)="SavID: "+CARD:SAVID
     IF CARD:CHKID="" THEN CRDCHECK(C)=CRDCHECK(C)+"&nbsp;&nbsp;&nbsp;ChkID: NA"
     ELSE  CRDCHECK(C)=CRDCHECK(C)+"&nbsp;&nbsp;&nbsp;ChkID: "+CARD:CHKID
    END
   ELSE IF CARD:TYPE=40 THEN CRDCHECK(C)="Classic Loan ID: "+CARD:CREDITCARDID
   ELSE CRDCHECK(C)="Gold Loan ID: "+CARD:CREDITCARDID
   FOR EACH CARD NAME 
    DO
     CRDINFO(C,2)=CARD NAME:FIRST       
     CRDINFO(C,3)=CARD NAME:LAST
     CRDINFO(C,4)=CARD NAME:SSN
     CRDDATE(C)=CARD NAME:BIRTHDATE
    END
  END
 FOR EACH LOAN WITH LOAN:TYPE=40 OR LOAN:TYPE=41 AND 
         LOAN:CLOSEDATE='--/--/--' AND LOAN:CHARGEOFFDATE='--/--/--'
  DO
   IF LOAN:TYPE=40 THEN FND40="Y"
   ELSE IF LOAN:TYPE=41 THEN FND41="Y"
  END
END

PROCEDURE GETCURRENTCARD
 FOR EACH CARD WITH CARD:NUMBER=CARDTOREPLACE
  DO
   CARDNBR=CARD:NUMBER
   CARDLOC=CARD:LOCATOR
   OLDLOCATOR=CARD:LOCATOR
   CDESCRIPTION=UPPERCASE(CARD:DESCRIPTION)
   CSAVID=CARD:SAVID
   CCHKID=CARD:CHKID
   CLOCID=CARD:LOCID
   CCREDITCARDID=CARD:CREDITCARDID
   CACTIVEDATE=CARD:ACTIVEDATE
   CEXPIRATIONDATE=CARD:EXPIRATIONDATE
   CATMCOUNTLIMIT=CARD:ATMCOUNTLIMIT
   CAUTHLIMIT=CARD:AUTHLIMIT
   CAUTHCOUNTLIMIT=CARD:AUTHCOUNTLIMIT
   CBILLPAYLIMIT=CARD:BILLPAYLIMIT
   CBILLPAYCOUNTLIMIT=CARD:BILLPAYCOUNTLIMIT
   CBILLPAYMAX=CARD:BILLPAYMAX
   CCREDDEBCOUNTLIMIT=CARD:CREDDEBCOUNTLIMIT
   CCREDDEBLIMIT=CARD:CREDDEBLIMIT
   CDEPCOUNTLIMIT=CARD:DEPCOUNTLIMIT
   CDEPLIMIT=CARD:DEPLIMIT
   CDEPMAX=CARD:DEPMAX
   CEXTRAEMBOSS=UPPERCASE(CARD:EXTRAEMBOSS)
   CLIMITAMOUNT=CARD:LIMITAMOUNT
   CLIMITDAYS=CARD:LIMITDAYS
   CMISCLIMIT=CARD:MISCLIMIT
   CMISCCOUNTLIMIT=CARD:MISCCOUNTLIMIT
   CNAMETYPE=CARD:NAMETYPE
   CPOSLIMIT=CARD:POSLIMIT
   CPOSCOUNTLIMIT=CARD:POSCOUNTLIMIT
   CREISSUEMONTHS=CARD:REISSUEMONTHS
   CSINGLEUSAGEMAX=CARD:SINGLEUSAGEMAX
   CUSAGELIMIT=CARD:USAGELIMIT
   CWDCHECKLIMIT=CARD:WDCHECKLIMIT
   CWDCHECKCOUNTLIMIT=CARD:WDCHECKCOUNTLIMIT
   CWDCHECKMAX=CARD:WDCHECKMAX
   CXFERCOUNTLIMIT=CARD:XFERCOUNTLIMIT
   CXFERLIMIT=CARD:XFERLIMIT
   CXFERMAX=CARD:XFERMAX
   FOR EACH CARD NAME
    DO 
     JTLAST=CARD NAME:LAST
     JTSUFFIX=CARD NAME:SUFFIX
     JTFIRST=CARD NAME:FIRST
     JTMIDDLE=CARD NAME:MIDDLE
     JTTITLE=CARD NAME:TITLE
     JTDOB=CARD NAME:BIRTHDATE
     JTSSN=CARD NAME:SSN
     JTEXTRA=CARD NAME:EXTRAADDRESS
     JTSTREET=CARD NAME:STREET
     JTCITY=CARD NAME:CITY
     JTSTATE=CARD NAME:STATE
     JTZIP=CARD NAME:ZIPCODE
     JTHOME=CARD NAME:HOMEPHONE
     JTWORK=CARD NAME:WORKPHONE
     JTCELL=CARD NAME:MOBILEPHONE
     JTPHONETYPE=CARD NAME:PHONETYPE
     JTADDRESSTYPE=CARD NAME:ADDRESSTYPE
     JTSSNTYPE=CARD NAME:SSNTYPE
    END
  END
END

PROCEDURE CHECKOPTINOUT
 FNDTRACK80="N"
 FOR EACH SHARE WITH SHARE:ID=CCHKID
  DO
   FOR EACH SHARE TRACKING WITH SHARE TRACKING:TYPE=80
    DO
     FNDTRACK80="Y"
    END
  END
 IF FNDTRACK80="N" THEN
  DO
   INITSUBROUTINE(INITERROR)
   IF INITERROR<>"" THEN
    DO
     OUTLINE="Init Error GH.OPTIN.HTML: "+INITERROR
     CALL WRITEERROR
    END
   @ENVARGCHAR3="INSTANTISSUE"
   @ENVARGNUMBER2=JTNAMETYPE

   EXECUTE("GH.OPTIN.HTML",EXECERROR) 
   IF EXECERROR<>"" THEN
    DO
     OUTLINE="Execute Error GH.OPTIN.HTML: "+EXECERROR
     CALL WRITEERROR
    END
  END
END

PROCEDURE UPDATECURRENTCARD
 IF SEGMENT(CARDNBR,1,4)="4337" AND CCHKID<>"" THEN CALL CHECKOPTINOUT
 IF REPRINT="YES" THEN
  DO
   IF CEXPIRATIONDATE<>'--/--/--' THEN EXPDATE=CEXPIRATIONDATE
   FMPERFORM REVISE CARD LOC CARDLOC (1,2,ERRORTEXT) [NO CHANGE]
    DO
     SET STATUS TO 01
     SET ACTIVEDATE TO SYSTEMDATE
     SET EXPIRATIONDATE TO EXPDATE
     SET CREATEDATBRANCH TO BRNO
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error updating current card "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
 ELSE
  DO
   FMPERFORM REVISE CARD LOC CARDLOC (1,2,ERRORTEXT) [ADVANCE EXPIRATION DATE 3 YEARS]
    DO
     SET STATUS TO 01
     SET ACTIVEDATE TO SYSTEMDATE           
     SET EXPIRATIONDATE TO EXPDATE
     SET PREVACTIVATIONDATE TO CACTIVEDATE
     SET PREVEXPIRATIONDATE TO EXPDATE 
     SET CREATEDATBRANCH TO BRNO
    END  
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error advancing current card "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
 IF TEXT2<>"" THEN
  DO
   FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Instant Issue was Re-Run"
     SET TEXT:2 TO TEXT2
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating RERUN card note"+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
 ELSE
  DO
   IF REPRINT="YES" THEN NEWNOTE2="Reprinted Card"
   ELSE NEWNOTE2="Renewed Card"
   FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Card created through Instant Issue"
     SET TEXT:2 TO NEWNOTE2
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating card note"+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
END

PROCEDURE REPLACECURRENTCARD
 IF SEGMENT(CARDNBR,1,4)="4337" AND CCHKID<>"" THEN CALL CHECKOPTINOUT
 [CALL GETNEXTCARD]
 FMPERFORM CREATE CARD LOC AFTERLAST (1,CARDTYPE,CARDLOC,ERRORTEXT)
  DO
   SET NUMBER TO "NEXT"
   SET STATUS TO 1
   SET ACTIVEDATE TO SYSTEMDATE
   SET ISSUEDATE TO SYSTEMDATE
   SET EFFECTIVEDATE TO SYSTEMDATE
   SET EXPIRATIONDATE TO EXPDATE
   SET DESCRIPTION TO CDESCRIPTION
   SET SAVID TO CSAVID
   SET CHKID TO CCHKID
   SET LOCID TO CLOCID
   SET CREDITCARDID TO CCREDITCARDID
   SET ATMCOUNTLIMIT TO CATMCOUNTLIMIT
   SET AUTHLIMIT TO CAUTHLIMIT
   SET AUTHCOUNTLIMIT TO CAUTHCOUNTLIMIT
   SET BILLPAYLIMIT TO CBILLPAYLIMIT
   SET BILLPAYCOUNTLIMIT TO CBILLPAYCOUNTLIMIT
   SET BILLPAYMAX TO CBILLPAYMAX
   SET CREDDEBCOUNTLIMIT TO CCREDDEBCOUNTLIMIT
   SET CREDDEBLIMIT TO CCREDDEBLIMIT
   SET CREDITCARDID TO CCREDITCARDID
   SET DEPCOUNTLIMIT TO CDEPCOUNTLIMIT
   SET DEPLIMIT TO CDEPLIMIT
   SET DEPMAX TO CDEPMAX
   SET EXTRAEMBOSS TO CEXTRAEMBOSS
   SET LIMITAMOUNT TO CLIMITAMOUNT
   SET LIMITDAYS TO CLIMITDAYS
   SET MISCLIMIT TO CMISCLIMIT
   SET MISCCOUNTLIMIT TO CMISCCOUNTLIMIT
   SET NAMETYPE TO CNAMETYPE
   SET POSLIMIT TO CPOSLIMIT
   SET POSCOUNTLIMIT TO CPOSCOUNTLIMIT
   SET REISSUEMONTHS TO CREISSUEMONTHS
   SET SINGLEUSAGEMAX TO CSINGLEUSAGEMAX
   SET USAGELIMIT TO CUSAGELIMIT
   SET WDCHECKLIMIT TO CWDCHECKLIMIT
   SET WDCHECKCOUNTLIMIT TO CWDCHECKCOUNTLIMIT
   SET WDCHECKMAX TO CWDCHECKMAX
   SET XFERCOUNTLIMIT TO CXFERCOUNTLIMIT
   SET XFERLIMIT TO CXFERLIMIT
   SET XFERMAX TO CXFERMAX
  END
   [SET PREVNUMBER TO CARDTOREPLACE REMOVED BECAUSE SYMITAR NOT READY FOR VAU]
 IF ERRORTEXT<>"" THEN
  DO
   OUTLINE="Error creating replacement card "+CARDNBR+": "+ERRORTEXT
   CALL WRITEERROR
  END
 ELSE
  DO
   FOR EACH CARD WITH CARD:LOCATOR=CARDLOC
    DO
     CARDNBR=CARD:NUMBER
    END

   IF CARDTYPE>30 AND CNAMETYPE=0 THEN
    DO
     FMPERFORM REVISE LOAN CCREDITCARDID (0,1,ERRORTEXT)
      DO
       SET REFERENCE TO CARDNBR
      END
     [IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Error setting Loan Reference "+CARDNBR+": "+ERRORTEXT
       CALL WRITEERROR
      END]
    END
   IF CNAMETYPE=2 THEN
    DO
     FMPERFORM CREATE CARD LOC CARDLOC NAME LOC BEFOREFIRST (1,2,ERRORTEXT)
      DO
       SET TITLE TO JTTITLE
       SET FIRST TO JTFIRST
       SET MIDDLE TO JTMIDDLE
       SET LAST TO JTLAST
       SET SUFFIX TO JTSUFFIX
       SET BIRTHDATE TO JTDOB
       SET SSN TO JTSSN
       SET HOMEPHONE TO JTHOME
       SET WORKPHONE TO JTWORK
       SET MOBILEPHONE TO JTCELL
       SET PHONETYPE TO JTPHONETYPE
       SET SSNTYPE TO JTSSNTYPE
       SET LASTADDRVERIFDATE TO SYSTEMDATE
      END
     IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Error creating card name on "+CARDNBR+": "+ERRORTEXT
       CALL WRITEERROR
      END
    END
   CALL CREATELOOKUP
   FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Card created through Instant Issue"
     SET TEXT:2 TO "Replaced card ending "+SEGMENT(CARDTOREPLACE,13,16)
     SET TEXT:3 TO "REASON: "+CAPTUREREASON
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating card note "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   FMPERFORM REVISE CARD LOC OLDLOCATOR (1,2,ERRORTEXT) [NO CHANGE]
    DO
     SET STATUS TO 2
     SET STATUSREASON TO CARDSTATUS
     SET BLOCKCODE TO BLOCKCODE
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error capturing old card "+CARDTOREPLACE+": "+ERRORTEXT
     CALL WRITEERROR
    END
   FMPERFORM CREATE CARD LOC OLDLOCATOR NOTE 0 (1,2,ERRORTEXT)
    DO
     SET CODE TO 0
     SET TEXT:1 TO "Card captured through Instant Issue"
     SET TEXT:2 TO CAPTUREREASON
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating card capture note "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
END

PROCEDURE CREATENEWCARD
 IF CARDTYPE=30 THEN 
  DO
   CDESCRIPTION="VISA DEBIT CARD"
   [CSAVID=""
   CCHKID=""
   CLOCID=""
   CCREDITCARDID=""]
  END
 ELSE
  DO
   CSAVID=""
   CCHKID=""
   CLOCID=""
   LOANREF=""
   CCREDITCARDID=LNID
   IF CARDTYPE=40 THEN 
    CDESCRIPTION="VISA CLASSIC"
   ELSE IF CARDTYPE=41 THEN 
    CDESCRIPTION="VISA GOLD"
   FOR EACH LOAN WITH LOAN:TYPE=CARDTYPE AND LOAN:CLOSEDATE='--/--/--'  [Added 6/17/14 - to get limits for card]
    DO
     CLIMITAMOUNT=LOAN:CREDITLIMIT
     CUSAGELIMIT=LOAN:CREDITLIMIT
     LOANREF=LOAN:REFERENCE
    END
  END
 CALL GETCARDNAMEINFO
 [CALL GETNEXTCARD]
 FMPERFORM CREATE CARD LOC AFTERLAST (1,CARDTYPE,CARDLOC,ERRORTEXT)
  DO
   SET NUMBER TO "NEXT"
   SET STATUS TO 1
   SET ACTIVEDATE TO SYSTEMDATE
   SET ISSUEDATE TO SYSTEMDATE
   SET EFFECTIVEDATE TO SYSTEMDATE
   SET EXPIRATIONDATE TO EXPDATE
   SET DESCRIPTION TO CDESCRIPTION
   SET SAVID TO CSAVID
   SET CHKID TO CCHKID
   SET LOCID TO CLOCID
   SET CREDITCARDID TO CCREDITCARDID
   SET EXTRAEMBOSS TO BUSNAME
   SET NAMETYPE TO CNAMETYPE
  END
 IF ERRORTEXT<>"" THEN
  DO
   OUTLINE="Error creating new card "+CARDNBR+": "+ERRORTEXT
   CALL WRITEERROR
  END

 FOR EACH CARD WITH CARD:LOCATOR=CARDLOC
  DO
   CARDNBR=CARD:NUMBER
  END

 IF CARDTYPE>30 AND CNAMETYPE=0 THEN
  DO
   FMPERFORM REVISE CARD LOC CARDLOC (0,1,ERRORTEXT) 
    DO
     SET LIMITAMOUNT TO CLIMITAMOUNT
     SET USAGELIMIT TO CUSAGELIMIT
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error setting CISA card limits"+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
   FMPERFORM REVISE LOAN CCREDITCARDID (0,1,ERRORTEXT)
    DO
     SET REFERENCE TO CARDNBR
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error setting Loan Reference "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
 IF CNAMETYPE=2 THEN
  DO
   FMPERFORM CREATE CARD LOC CARDLOC NAME LOC BEFOREFIRST (1,2,ERRORTEXT)
    DO
     SET TITLE TO JTTITLE
     SET FIRST TO JTFIRST
     SET MIDDLE TO JTMIDDLE
     SET LAST TO JTLAST
     SET SUFFIX TO JTSUFFIX
     SET BIRTHDATE TO JTDOB
     SET SSN TO JTSSN
     SET HOMEPHONE TO JTHOME
     SET WORKPHONE TO JTWORK
     SET MOBILEPHONE TO JTCELL
     SET PHONETYPE TO JTPHONETYPE
     SET SSNTYPE TO JTSSNTYPE
     SET LASTADDRVERIFDATE TO SYSTEMDATE
    END
   IF ERRORTEXT<>"" THEN
    DO
     OUTLINE="Error creating new card name on "+CARDNBR+": "+ERRORTEXT
     CALL WRITEERROR
    END
  END
 CALL CREATELOOKUP
 FMPERFORM CREATE CARD LOC CARDLOC NOTE 0 (1,2,ERRORTEXT)
  DO
   SET CODE TO 0
   SET TEXT:1 TO "Card created through Instant Issue"
   SET TEXT:2 TO "New Card"
  END
 IF ERRORTEXT<>"" THEN
  DO
   OUTLINE="Error creating new card note "+CARDNBR+": "+ERRORTEXT
   CALL WRITEERROR
  END
END


PROCEDURE GETCARDNAMEINFO
 IF NEWNAMETYPE="N" THEN
  DO
   FOR EACH NAME WITH NAME:LOCATOR=NEWNAMELOC
    DO
     IF NAME:TYPE=0 THEN CNAMETYPE=0
     ELSE
      DO
       CNAMETYPE=2
       JTLAST=NAME:LAST
       JTSUFFIX=NAME:SUFFIX
       JTFIRST=NAME:FIRST
       JTMIDDLE=NAME:MIDDLE
       JTTITLE=NAME:TITLE
       JTDOB=NAME:BIRTHDATE
       JTSSN=NAME:SSN
       JTEXTRA=NAME:EXTRAADDRESS
       JTSTREET=NAME:STREET
       JTCITY=NAME:CITY
       JTSTATE=NAME:STATE
       JTZIP=NAME:ZIPCODE
       JTHOME=NAME:HOMEPHONE
       JTWORK=NAME:WORKPHONE
       JTCELL=NAME:MOBILEPHONE
       JTPHONETYPE=NAME:PHONETYPE
       JTADDRESSTYPE=NAME:ADDRESSTYPE
       JTSSNTYPE=NAME:SSNTYPE
      END
    END
  END
 ELSE IF NEWNAMETYPE="L" THEN
  DO
   CNAMETYPE=2
   FOR EACH LOAN WITH LOAN:ID=LNID
    DO
     CLIMITAMOUNT=LOAN:CREDITLIMIT
     CUSAGELIMIT=LOAN:CREDITLIMIT
     FOR EACH LOAN NAME WITH LOAN NAME:LOCATOR=NEWNAMELOC
      DO
       JTLAST=LOAN NAME:LAST
       JTSUFFIX=LOAN NAME:SUFFIX
       JTFIRST=LOAN NAME:FIRST
       JTMIDDLE=LOAN NAME:MIDDLE
       JTTITLE=LOAN NAME:TITLE
       JTDOB=LOAN NAME:BIRTHDATE
       JTSSN=LOAN NAME:SSN
       JTEXTRA=LOAN NAME:EXTRAADDRESS
       JTSTREET=LOAN NAME:STREET
       JTCITY=LOAN NAME:CITY
       JTSTATE=LOAN NAME:STATE
       JTZIP=LOAN NAME:ZIPCODE
       JTHOME=LOAN NAME:HOMEPHONE
       JTWORK=LOAN NAME:WORKPHONE
       JTCELL=LOAN NAME:MOBILEPHONE
       JTPHONETYPE=LOAN NAME:PHONETYPE
       JTADDRESSTYPE=LOAN NAME:ADDRESSTYPE
       JTSSNTYPE=LOAN NAME:SSNTYPE
      END
    END
  END
END

PROCEDURE LOADRESULTS
 INPUTCOUNT=0
 WHILE (INPUTFORM(INPUTCOUNT)<>"EOD")
  DO
   INPUTCOUNT=INPUTCOUNT+1
   INPUTFORM(INPUTCOUNT)=ENTERLINE(0)
  END
END

PROCEDURE SETVARIABLES
 FOR I=1 TO INPUTCOUNT
  DO
   TMPCHR=INPUTFORM(I)
   IF SEGMENT(TMPCHR,1,5)="close"                   THEN CHOOSETYPE="CANCEL"
   ELSE IF SEGMENT(TMPCHR,1,14)="cardselection="    THEN CARDTYPE=VALUE(SEGMENT(TMPCHR,15,16))
   ELSE IF SEGMENT(TMPCHR,1,12)="cardrequest="      THEN CARDREQUEST=SEGMENT(TMPCHR,13,LENGTH(TMPCHR))
   ELSE IF SEGMENT(TMPCHR,1,14)="cardtoreplace="    THEN CARDTOREPLACE=SEGMENT(TMPCHR,15,LENGTH(TMPCHR))
   ELSE IF SEGMENT(TMPCHR,1,8)="reprint="           THEN REPRINT=SEGMENT(TMPCHR,9,LENGTH(TMPCHR))
   ELSE IF SEGMENT(TMPCHR,1,16)="replacesamecard="  THEN REPLACESAMECARD=SEGMENT(TMPCHR,17,LENGTH(TMPCHR))
   ELSE IF SEGMENT(TMPCHR,1,15)="replacenewcard="   THEN REPLACENEWCARD=SEGMENT(TMPCHR,16,LENGTH(TMPCHR))
   ELSE IF SEGMENT(TMPCHR,1,13)="statusreason="     THEN 
    DO
     CARDSTATUS=VALUE(SEGMENT(TMPCHR,14,LENGTH(TMPCHR)))
     CAPTUREREASON=STATUSREASON(CARDSTATUS)
     IF CARDSTATUS=1       THEN BLOCKCODE="L"
     ELSE IF CARDSTATUS=2  THEN BLOCKCODE="S"
     ELSE IF CARDSTATUS=3  THEN BLOCKCODE="F"
     ELSE IF CARDSTATUS=4 OR CARDSTATUS=5 OR CARDSTATUS=20 THEN 
      DO
       BLOCKCODE="R"
       CARDSTATUS=20
      END
    END
   ELSE IF SEGMENT(TMPCHR,1,7)="yescard"            THEN RETRIEVECARD="YES"
   ELSE IF SEGMENT(TMPCHR,1,6)="nocard"             THEN RETRIEVECARD="NO"
   ELSE IF SEGMENT(TMPCHR,1,6)="lname="             THEN LNAME=UPPERCASE(SEGMENT(TMPCHR,7,LENGTH(TMPCHR)))
   ELSE IF SEGMENT(TMPCHR,1,8)="busname="           THEN NEWBUS=UPPERCASE(SEGMENT(TMPCHR,9,LENGTH(TMPCHR)))
   ELSE IF SEGMENT(TMPCHR,1,9)="runagain="          THEN 
    DO
     RPSEL=VALUE(SEGMENT(TMPCHR,10,LENGTH(TMPCHR)))
     TEXT2=REPRINTRESSON(RPSEL)
    END
   ELSE IF SEGMENT(TMPCHR,1,12)="otherreason="      THEN OTHERREASON=SEGMENT(TMPCHR,13,LENGTH(TMPCHR))
  END
END

PROCEDURE GETNEXTCARD
 CN=SEGMENT(CARD:NUMBER,1,6)
 FILENAME=""
 
 IF CARDTYPE=30 THEN 
  DO
   CN="433739"
   FILENAME="DEBITCARD.VALUE.228"
  END
 ELSE IF CARDTYPE=40 THEN 
  DO
   CN="425100"
   FILENAME="VISA.CLASSIC.VALUE"
  END
 ELSE IF CARDTYPE=41 THEN 
  DO
   CN="425104"
   FILENAME="VISA.GOLD.VALUE"
  END
 CURRFILENAME=FILENAME
 FILELOCKED=FALSE
 PROCESSOK=TRUE
 CALL LOCKFILE
 IF FILELOCKED=TRUE THEN
  DO
   FILEOPEN("LETTER",FILENAME,"READWRITE",FNUMBER,FERROR)
   IF FERROR<>"" THEN
    DO
     PRINT CN+" FILE OPEN ERROR: "+FERROR
     NEWLINE
    END
   ELSE
    DO
     FILEREADLINE(FNUMBER,FLINE,FERROR)
     IF FERROR<>"" OR LENGTH(FLINE)<>16 THEN
      DO
       PRINT "READ ERROR: "+FERROR
       NEWLINE
      END
     ELSE 
      DO
       CARDNBR=FLINE
       SEQNBR=VALUE(SEGMENT(CARDNBR,7,15))
       IF SEQNBR>=899999999 THEN
        DO
         PRINT "ERROR! OUT OF CARD NUMBERS"
         NEWLINE
        END
       ELSE
        DO
         SEQNBR=SEQNBR+1
         CARDNBR=CN+FORMAT("999999999",SEQNBR)
         RESULT=0
         PARITY=2
         I=15
         WHILE (I>=1)
          DO
           DIGIT=VALUE(SEGMENT(CARDNBR,I,I))
           RESULT=RESULT+NUMBER((DIGIT*PARITY*11)/10)
           PARITY=3-PARITY
           I=I-1
          END
         RESULT2=NUMBER((RESULT+9)/10)
         RESULT=10*RESULT2-RESULT
         CARDNBR=CARDNBR+FORMAT("9",RESULT)
        END
      END
     FILESETPOS(FNUMBER,0,FERROR)
     FILEWRITELINE(FNUMBER,CARDNBR,FERROR)
    END
   FILECLOSE(FNUMBER,FERROR)
  END        
 CALL UNLOCKFILE      
END

PROCEDURE LOCKFILE
[
|  Opens a file to provide exclusive use to <CURRFILENAME>.LOCK 
|  LetterFile
|  Sets:        FILELOCKED              TRUE/FALSE
]
 FILELOCKED=FALSE
 FILECREATE("LETTER",CURRFILENAME+".LOCK",ERROR)
 IF ERROR="File exists" THEN
  CALL DELETEUSERLOCK                   [CARDFM.NUM]
 IF ERROR<>"" THEN
  DO
   PRINT "Card File In Use.  Please try again"
   NEWLINE
   PRINT "Check "+CURRFILENAME+".LOCK File."
   NEWLINE
  END
 ELSE
  DO
   FILEOPEN("LETTER",CURRFILENAME+".LOCK","APPEND",LOCKFILENUMBER,ERROR)
   IF ERROR="" THEN
    DO
     FILELOCKED=TRUE
     FILEWRITELINE(LOCKFILENUMBER,FORMAT("999",SYSUSERNUMBER),ERROR)
     FILECLOSE(LOCKFILENUMBER,ERROR)
    END
   ELSE
    DO
     PRINT "Something wrong - Could not open card file"
     NEWLINE
     CALL UNLOCKFILE                    [CARDFM.NUM]
    END
  END
END

PROCEDURE DELETEUSERLOCK
[
|  Deletes <CURRFILENAME>.LOCK LetterFile if same user.
]
 FILEOPEN("LETTER",CURRFILENAME+".LOCK","READ",LOCKFILENUMBER,ERROR)
 IF ERROR="" THEN
  DO
   FILEREADLINE(LOCKFILENUMBER,FLINE,ERROR)
   IF ERROR<>"" OR FLINE<>FORMAT("999",SYSUSERNUMBER) THEN
    DO
     FILECLOSE(LOCKFILENUMBER,ERROR)
     PRINT "Card File In Use.  Please try again"
     NEWLINE
    END
   ELSE
    DO
     FILECLOSE(LOCKFILENUMBER,ERROR)
     IF ERROR="" THEN
      FILEDELETE("LETTER",CURRFILENAME+".LOCK",ERROR)
     IF ERROR="" THEN
      FILECREATE("LETTER",CURRFILENAME+".LOCK",ERROR)
    END
  END
END

PROCEDURE UNLOCKFILE
[
|  Deletes <CURRFILENAME>.LOCK LetterFile
|  SETS:        FILELOCKED              FALSE
]
 IF FILELOCKED=TRUE THEN
  DO
   FILEDELETE("LETTER",CURRFILENAME+".LOCK",ERROR)
   IF ERROR<>"" THEN
    DO
     PRINT "Error Deleting Lock File: "+ERROR
     NEWLINE
     PRINT "Please Delete LetterFile "+CURRFILENAME+".LOCK"
     NEWLINE
    END
  END
END

PROCEDURE PRINTCARD
 LNAME=""
 FOR EACH CARD WITH CARD:NUMBER=CARDNBR
  DO
   CARDLOC=CARD:LOCATOR
   BUSNAME=CARD:EXTRAEMBOSS
   IF CARD:NAMETYPE=0 THEN LNAME=PRIMENAME
   ELSE IF CARD:NAMETYPE=2 THEN
    DO
     FOR EACH CARD NAME
      DO
       IF LNAME="" THEN LNAME=CARD NAME:LONGNAME
      END
    END
  END
 AP=CHARACTERSEARCH(LNAME,"'")
 IF AP=0 THEN AP=CHARACTERSEARCH(LNAME,"&")
 WHILE AP>0
  DO
   LNAME=SEGMENT(LNAME,1,AP-1)+SEGMENT(LNAME,AP+1,LENGTH(LNAME))
   AP=CHARACTERSEARCH(LNAME,"'")
   IF AP=0 THEN AP=CHARACTERSEARCH(LNAME,"&")
  END

 IF LENGTH(LNAME)>26 THEN
  DO
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Name Too Long</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=NAVY>")
   HTMLVIEWLINE(" <p align=center><font size=+2><b>Name Too Long</b></font></p>")
   HTMLVIEWLINE(" <form name=INSTANTISSUE METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=600 border=1 align=center>")
   HTMLVIEWLINE(" <tr bgcolor=NAVY>")
   HTMLVIEWLINE("   <td><font color=WHITE><b>Card Name Too Long: "+SEGMENT(LNAME,1,26)+"</b></font></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>Please fix - Max 26 Characters: ") 
   HTMLVIEWLINE("        <input type=text name=lname size=30 maxlength=26 value='"+LNAME+"'></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit></p>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
  END
 LGNAME=LNAME

 IF ACCOUNT:TYPE>=10 AND ACCOUNT:TYPE<>21 AND ACCOUNT:TYPE<>22 AND ACCOUNT:TYPE<>23 AND CARDREQUEST<>"YES" THEN
  DO
   HTMLVIEWOPEN(0)
   HTMLVIEWLINE("<html>")
   HTMLVIEWLINE("<head>")
   HTMLVIEWLINE("<title>Business Name</title>")
   HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
   HTMLVIEWLINE("</head>")
   HTMLVIEWLINE("<body bgcolor=#CCCCCC text=NAVY>")
   HTMLVIEWLINE(" <p align=center><font size=+2><b>Business Name</b></font></p>")
   HTMLVIEWLINE(" <form name=INSTANTISSUE METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
   HTMLVIEWLINE("<table width=500 border=1 align=center>")
   HTMLVIEWLINE(" <tr bgcolor=NAVY>")
   HTMLVIEWLINE("   <td><font color=WHITE><b>Name On Card: "+LNAME+"<br>")
   IF BUSNAME="" THEN HTMLVIEWLINE("No Business Name on Card.</b></font></td>")
   ELSE HTMLVIEWLINE("Business Name: '"+BUSNAME+"'</b></font></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" <tr>")
   HTMLVIEWLINE("   <td>")
   IF BUSNAME="" THEN HTMLVIEWLINE("You can add a Business Name if Needed. ")
   ELSE HTMLVIEWLINE("You can Change or Remove the Business Name if Needed. ")
   HTMLVIEWLINE(" - Max 20 Characters allowed<br><br>")
   HTMLVIEWLINE("   Business Name ") 
   HTMLVIEWLINE("   <input type=text name=busname size=40 maxlength=20 value='"+SEGMENT(BUSNAME,1,20)+"'></td>")
   HTMLVIEWLINE(" </tr>")
   HTMLVIEWLINE(" </table>")
   HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit></p>")
   HTMLVIEWLINE(" </form>")
   HTMLVIEWLINE("</body>")
   HTMLVIEWLINE("</html>")
   HTMLVIEWDISPLAY
   CALL LOADRESULTS
   CALL SETVARIABLES
   IF NEWBUS<>BUSNAME THEN
    DO
     AP=CHARACTERSEARCH(NEWBUS,"'")
     IF AP=0 THEN AP=CHARACTERSEARCH(NEWBUS,"&")
     WHILE AP>0
      DO
       NEWBUS=SEGMENT(NEWBUS,1,AP-1)+SEGMENT(NEWBUS,AP+1,LENGTH(NEWBUS))
       AP=CHARACTERSEARCH(NEWBUS,"'")
       IF AP=0 THEN AP=CHARACTERSEARCH(NEWBUS,"&")
      END
     FMPERFORM REVISE CARD LOC CARDLOC (0,1,ERRORTEXT) 
      DO
       SET EXTRAEMBOSS TO NEWBUS
      END
     IF ERRORTEXT<>"" THEN
      DO
       OUTLINE="Init Error Setting Business Name "+ERRORTEXT
       CALL WRITEERROR
      END
    END
  END
 INITSUBROUTINE(INITERROR)
 @ENVARGCHAR1=CARDNBR
 @ENVARGCHAR2=LNAME
 IF INITERROR<>"" THEN
  DO
  [OUTLINE="Init Error INSTANTISSUE.PRINT: "+INITERROR]
   OUTLINE="Init Error CARDPRINTING.ATFCU: "+INITERROR
   CALL WRITEERROR
  END
 [EXECUTE("CARDPRINTING.XXX",EXECERROR)]  
 [EXECUTE("INSTANTISSUE.PRINT",EXECERROR)]
 EXECUTE("CARDPRINTING.ATFCU",EXECERROR) [TODO:seeNewCode]
 IF EXECERROR<>"" AND CHARACTERSEARCH(EXECERROR,"TERMINATE")=0 THEN
  DO
   POPUPMESSAGE(0,EXECERROR)  
  [OUTLINE="Execute Error INSTANTISSUE.PRINT: "+EXECERROR]
   OUTLINE="Execute Error CARDPRINTING.ATFCU: "+EXECERROR
   CALL WRITEERROR
  END
END

PROCEDURE CHECKRUNAGAIN
 FNDCARDNOTE="N"
 OTHERREASON=""
 TEXT2=""
 FOR EACH CARD WITH CARD:NUMBER=CARDTOREPLACE
  DO
   FOR EACH CARD NOTE WITH CHARACTERSEARCH(UPPERCASE(CARD NOTE:TEXT:1),"INSTANT ISSUE")>0
    DO
     FNDCARDNOTE="Y"
    END
  END
 IF FNDCARDNOTE="Y" THEN
  DO
   DONE="N"
   WHILE DONE="N"
    DO
     DONE="Y"
     RPSEL=0
     HTMLVIEWOPEN(0)
     HTMLVIEWLINE("<html>")
     HTMLVIEWLINE("<head>")
     HTMLVIEWLINE("<title>Instant Issue Re-Run</title>")
     HTMLVIEWLINE("<meta http-equiv=Content-Type content=text/html; charset=iso-8859-1>")
     HTMLVIEWLINE("</head>")
     HTMLVIEWLINE("<body bgcolor=#CCCCCC text=#000000>")
     HTMLVIEWLINE(" <p align=center><font size=6 color=NAVY><b>Instant Issue has already been run for this card.</b></font></p>")
     HTMLVIEWLINE(" <form name=GETCARD METHOD=POST ACTION=symitar://HTMLView~Action=Post>")
     HTMLVIEWLINE("<table width=500 border=1 align=center>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("   <td>")
     HTMLVIEWLINE("       <font size=4><b><u>If the card printed last time</u></b>, even with program errors, click ")
     HTMLVIEWLINE("       <font size=4 color=RED><b>EXIT</b></font> and re-pin the card using the old")
     HTMLVIEWLINE("       <b>Magtek</b>.&nbsp;&nbsp;Enter the offset and image code into Episys.</font><br><br>")
     HTMLVIEWLINE("       Otherwise, please select the reason why it is being run again.</font>")
     HTMLVIEWLINE("  </td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("   <td>")
     FOR RI=1 TO RP
      DO
       HTMLVIEWLINE("<input type=radio name=runagain value="+FORMAT("99",RI)+">"+REPRINTRESSON(RI)+"<br>")
      END
     HTMLVIEWLINE("   </td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" <tr>")
     HTMLVIEWLINE("   <td><font size=4><b>Other Reason, if not listed above</b></font><br>") 
     HTMLVIEWLINE("        <input type=text name=otherreason size=42 maxlength=40 value='"+OTHERREASON+"'></td>")
     HTMLVIEWLINE(" </tr>")
     HTMLVIEWLINE(" </table>")
     HTMLVIEWLINE("  <p align=center><INPUT TYPE=SUBMIT NAME=submitbutton VALUE=Submit>&nbsp;&nbsp;&nbsp;&nbsp;")
     HTMLVIEWLINE("                  <INPUT TYPE=SUBMIT NAME=close VALUE=Exit></p>")
     HTMLVIEWLINE(" </form>")
     HTMLVIEWLINE("</body>")
     HTMLVIEWLINE("</html>")
     HTMLVIEWDISPLAY
     CALL LOADRESULTS
     CALL SETVARIABLES
     IF CHOOSETYPE<>"CANCEL" THEN
      DO
       IF RPSEL=0 AND OTHERREASON="" THEN 
        DO
         POPUPMESSAGE(1,"You must enter reason why the program is being run again.")
         DONE="N"
        END
       ELSE IF RPSEL>0 AND OTHERREASON<>"" THEN 
        DO
         POPUPMESSAGE(1,"You must enter reason why the program is being run again.")
         DONE="N"
        END
      END
    END
   IF CHOOSETYPE<>"CANCEL" AND OTHERREASON<>"" THEN TEXT2=OTHERREASON
  END
END

PROCEDURE WRITEERROR
 POPUPMESSAGE(2,OUTLINE)
 OUTLINE=FORMAT("99/99/99",SYSTEMDATE)+"  "+FORMAT("99:99",SYSACTUALTIME)+"   "
         +ACCOUNT:NUMBER+"  "+FORMAT("9999",SYSUSERNUMBER)+" - "+OUTLINE+" INSTANT ISSUE"
 FILEOPEN("LETTER","INSTANTISSUE.ERRORS","APPEND",FNUMBER,FERROR)       [READS SSN TYPE HELP FILE FOR AVAILABLE SSN TYPES]
  FILEWRITELINE(FNUMBER,OUTLINE,FERROR)
 FILECLOSE(FNUMBER,FERROR) 
 DOPRINT="N"
END
