[GH.DAILY.CLOSEREASON.RPT - Looks for all share type 0 that are taken to $0.00 in
                            in transaction history and then looks in LETTERFILE 
                            CLOSED.RESAONS for a reason taken from Teller Transaction
                            asked for in VALIDATE.TELLERTRAN]
[01/07/08 GH ADDED CODE TO PRINT OUT SCORE FROM ACCOUNT IF SCORE IN LETTERFILE IS 000]

TARGET=ACCOUNT

DEFINE
 FERROR=CHARACTER
 FLINE=CHARACTER
 FNUMBER=NUMBER
 CNT=NUMBER
 FOUNDCLOSE=CHARACTER(1)
 FIRST=CHARACTER(1)
 SNAME=CHARACTER(16)
 CLOSETLR=NUMBER
 TCNT=NUMBER
 RPTDATE=DATE
 NSSN=CHARACTER(9)
 PSCORE=NUMBER
END

SETUP
 RPTDATE=SYSTEMDATE
END

SELECT
 ACCOUNT:CLOSEDATE='--/--/--' OR ACCOUNT:CLOSEDATE>='01/01/08'
END

PRINT TITLE="DAILY CLOSE REASONS"

 HEADERS
  COL=1 "Account"
  COL=13 "Name"
  COL=35 "Opened"
  COL=44 "Tlr#"
  COL=50 "Scr"
  COL=55 "Reason"
  COL=100 RIGHT "Balance" 
  NEWLINE
  COL=1 REPEATCHR("-",100)
  NEWLINE
 END
 FIRST="Y"
 SNAME=NAME:SHORTNAME
 NSSN=NAME:SSN
 PSCORE=0
 FOR EACH TRACKING 
   DO
    IF TRACKING:TYPE=31 THEN
     DO
      IF NSSN=TRACKING:USERCHAR1 THEN PSCORE=TRACKING:USERNUMBER1
      ELSE IF NSSN=TRACKING:USERCHAR2 THEN PSCORE=TRACKING:USERNUMBER2
      ELSE IF NSSN=TRACKING:USERCHAR3 THEN PSCORE=TRACKING:USERNUMBER3
      ELSE IF NSSN=TRACKING:USERCHAR4 THEN PSCORE=TRACKING:USERNUMBER4
      ELSE IF NSSN=TRACKING:USERCHAR5 THEN PSCORE=TRACKING:USERNUMBER5
     END
   END
 FOR EACH SHARE WITH SHARE:TYPE=0
  DO
   FOUNDCLOSE="N"
   CLOSETLR=0
   FOR EACH SHARE TRANSACTION WITH
            SHARE TRANSACTION:NEWBALANCE=$0.00 AND
            SHARE TRANSACTION:POSTDATE>=RPTDATE AND
            SHARE TRANSACTION:COMMENTCODE=0 AND
            SHARE TRANSACTION:VOIDCODE=0
    DO
     IF FIRST="Y" THEN
      DO
       COL=1 ACCOUNT:NUMBER
       COL=13 SNAME
       COL=35 FORMAT("99/99/99",ACCOUNT:OPENDATE)
       FIRST="N"
      END
     CNT=0
     FOUNDCLOSE="Y"
     IF CLOSETLR=0 THEN CLOSETLR=SHARE TRANSACTION:USERNUMBER
     FILEOPEN("LETTER","CLOSE.REASONS","READ",FNUMBER,FERROR)
     WHILE FERROR=""
      DO
       FILEREADLINE(FNUMBER,FLINE,FERROR)
       IF SEGMENT(FLINE,1,10)=ACCOUNT:NUMBER AND DATEVALUE(SEGMENT(FLINE,13,20))>=RPTDATE THEN
        DO
         COL=44 SEGMENT(FLINE,23,26)
         IF SEGMENT(FLINE,29,31)="000" THEN 
          COL=49 FORMAT("#999",PSCORE)
         ELSE 
          COL=50 SEGMENT(FLINE,29,31)
         COL=55 SEGMENT(FLINE,34,LENGTH(FLINE))
         COL=100 SHARE TRANSACTION:TRANAMOUNT
         NEWLINE
         CNT=CNT+1
        END
      END
     FILECLOSE(FNUMBER,FERROR)
    END
   UNTIL SHARE TRANSACTION:POSTDATE<RPTDATE
   IF FOUNDCLOSE="Y" THEN
    DO
     IF CNT=0 THEN
      DO
       COL=44 FORMAT("9999",CLOSETLR)
       COL=49 FORMAT("#999",PSCORE)
       IF (CLOSETLR>=800 AND CLOSETLR<=999) THEN  
        DO
         COL=55 "Closed by System."
         COL=100 RIGHT "$0.00" 
        END
       ELSE
        DO 
         COL=55 "NA"
         COL=100 RIGHT "NA" 
        END
       NEWLINE
      END
     TCNT=TCNT+1
    END
  END
END

TOTAL
 NEWLINE
 COL=1 "Total Closed: "
 PRINT TCNT
END
