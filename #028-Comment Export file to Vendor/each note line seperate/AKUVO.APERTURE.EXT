[
****************************************************************************************
*****  Copyright 2022 CUTEK. All rights reserved.                     rev 01-2022  *****
*****                                                                              *****
*****  This licensed source code may not be reproduced, displayed, modified or     *****
*****  distributed without the express prior written permission of the copyright   *****
*****  holder. As implemented with CUTEK products, code is licensed only for the   *****
*****  license term of the agreement. For further information, contact             *****
*****  sales@cutek.com                                                             *****
****************************************************************************************

 Client name:  Austin Telco FCU
 File name:    AKUVO.APERTURE.EXT
 Created by:   CUTEK, Inc. - Marc Middleton
 Created on:   MAY 2023
 CUTEK Ticket: 33240 - Retainer
 
 Description:
 Collections Notes Export file to Vendor
 
 Modified by:  
 Modified on:  
 Modification: 
]
TARGET=ACCOUNT

DEFINE
 TRUE=1
 FALSE=0
 IDTYPE=CHARACTER
 I=NUMBER
 XXX=NUMBER
 FNUM=NUMBER
 FTYPE=CHARACTER
 COLINDEX=NUMBER
 FERROR=CHARACTER
 QUOTE=CHARACTER(1) 
 TEMPCHAR=CHARACTER
 COLDATA=CHARACTER ARRAY(3) 
 DOUBLEQUOTE=CHARACTER
 DELIMITER=CHARACTER
 TEMPLINE=CHARACTER
 FNAME=CHARACTER
 TOPCOL=NUMBER
 QPOS=NUMBER 
 COMMENTTEXT=CHARACTER
 DES=CHARACTER(1)
 TMPCHR=CHARACTER
 ACCTNO=CHARACTER
 DESID=CHARACTER
 ACCOUNTIDENTIFIER=CHARACTER
 COMMENTDATE=CHARACTER
 POS=NUMBER
 RLZDONE=NUMBER
 COMMENTTEXTCREATOR=CHARACTER
END

SETUP
 TOPCOL       = 3 
 DELIMITER    = CTRLCHR(44) [comma] 
 DOUBLEQUOTE  = CTRLCHR(34) ["]
 FTYPE        = "LETTER"
 FNAME        = "CommentExport.clientCode.2.txt"
 FILEDELETE(FTYPE,FNAME,FERROR)
 FILEOPEN(FTYPE,FNAME,"APPEND",FNUM,FERROR) 
 IF FERROR<>"" THEN
  DO
   PRINT "Error Opening "+FTYPE+" File: "+FERROR
   NEWLINE
   PRINT "Terminating PowerOn...."
   TERMINATE
  END   
 COLDATA(1) = "ACCOUNTIDENTIFIER"
 COLDATA(2) = "COMMENTDATE"
 COLDATA(3) = "COMMENTTEXT"
 CALL OUTPUTCSVLINEFILE1
END

SELECT
 ACCOUNT:CLOSEDATE='--/--/--'
END

PRINT TITLE="AKUVO.APERTURE.EXT" REPORTCATEGORY=""
 HEADER=""  
 FOR EACH CPWORKCARD
  DO 
   FOR EACH CPWORKCARD NOTE
    DO
     DES=""
     IDTYPE=""
     IF CPWORKCARD NOTE:IDTYPE = 0 THEN 
      DO
       IDTYPE = "Share"
       DES = "S"
      END
     IF CPWORKCARD NOTE:IDTYPE = 1 THEN 
      DO
       IDTYPE = "Loan"
       DES = "L"
      END
     IF CPWORKCARD NOTE:IDTYPE = 2 THEN IDTYPE = "App."
     IF CPWORKCARD NOTE:IDTYPE = 3 THEN IDTYPE = "Account"
     IF CPWORKCARD NOTE:IDTYPE = 4 THEN IDTYPE = "Port."
     COMMENTTEXTCREATOR = FORMAT("CREATED BY 9999: ",CPWORKCARD NOTE:USERID)
     FOR I=1 TO CPWORKCARD NOTE:TEXTLINECOUNT
      DO
       IF CPWORKCARD NOTE:TEXT:(I)<>"" THEN
        DO
         COMMENTTEXT = [COMMENTTEXT +] CPWORKCARD NOTE:TEXT:(I) [+ " "]
         CALL CREATEEXTRACT
        END
      END
     [CALL CREATEEXTRACT]
    END
  END
END

TOTAL
 NEWLINE
 COL=005 "View "+FTYPE+" file "+FNAME
 NEWLINE
END

PROCEDURE CREATEEXTRACT
 TMPCHR=ACCOUNT:NUMBER
 CALL REMOVELEADINGZEROS
 ACCTNO=TMPCHR 
 TMPCHR=CPWORKCARD NOTE:ID
 CALL REMOVELEADINGZEROS
 DESID=TMPCHR 
 ACCOUNTIDENTIFIER = ACCTNO+"|"+DES+"|"+DESID
 
 COMMENTDATE = FORMAT("9999-",FULLYEAR(CPWORKCARD NOTE:ENTERDATE))+
               FORMAT("99-",MONTH(CPWORKCARD NOTE:ENTERDATE))+
               FORMAT("99 ",DAY(CPWORKCARD NOTE:ENTERDATE))+
               FORMAT("99:99",CPWORKCARD NOTE:ENTERTIME)+":00"
 
 COLDATA(1) = ACCOUNTIDENTIFIER
 COLDATA(2) = COMMENTDATE
 COLDATA(3) = COMMENTTEXTCREATOR+COMMENTTEXT
 CALL OUTPUTCSVLINEFILE1 
END

PROCEDURE REMOVELEADINGZEROS
 POS=1
 RLZDONE=FALSE
 WHILE POS<LENGTH(TMPCHR) AND RLZDONE=FALSE
  DO
   IF SEGMENT(TMPCHR,POS,POS)="0" THEN
    POS=POS+1
   ELSE
    RLZDONE=TRUE
  END
 TMPCHR=SEGMENT(TMPCHR,POS,LENGTH(TMPCHR))
END

PROCEDURE OUTPUTCSVLINEFILE1
 FOR COLINDEX = 1 TO TOPCOL
  DO
   TEMPCHAR = COLDATA(COLINDEX)
   CALL REMOVEQUOTES
   IF CHARACTERSEARCH(TEMPCHAR,DELIMITER)>0 THEN
    TEMPCHAR = QUOTE + TEMPCHAR + QUOTE    
   CALL FORMATTYPE  
   IF COLINDEX<TOPCOL THEN
    DO
     TEMPCHAR = TEMPCHAR+DELIMITER
     FILEWRITE(FNUM,TEMPCHAR,FERROR)
    END
   ELSE
    FILEWRITELINE(FNUM,TEMPCHAR,FERROR)
  END 
END

PROCEDURE REMOVEQUOTES
 QPOS = CHARACTERSEARCH(TEMPCHAR,QUOTE)
 WHILE QPOS>0
  DO
   TEMPCHAR = SEGMENT(TEMPCHAR,1,QPOS-1)+SEGMENT(TEMPCHAR,QPOS+1,LENGTH(TEMPCHAR))
   QPOS = CHARACTERSEARCH(TEMPCHAR,QUOTE)
  END
END

PROCEDURE FORMATTYPE
 IF CHARACTERSEARCH(TEMPCHAR,DOUBLEQUOTE)>0 OR CHARACTERSEARCH(TEMPCHAR,DELIMITER)>0 THEN
  DO
   IF CHARACTERSEARCH(TEMPCHAR,DOUBLEQUOTE)>0 THEN
    DO
     TEMPLINE=""
     FOR XXX=1 TO LENGTH(TEMPCHAR)
      DO
       IF SEGMENT(TEMPCHAR,XXX,XXX)=DOUBLEQUOTE THEN
        TEMPLINE=TEMPLINE+DOUBLEQUOTE     [Precede embedded double-quote with a double-quote]
       TEMPLINE=TEMPLINE+SEGMENT(TEMPCHAR,XXX,XXX)
      END
     TEMPCHAR=TEMPLINE
    END
   TEMPCHAR=DOUBLEQUOTE+TEMPCHAR+DOUBLEQUOTE
  END
END
