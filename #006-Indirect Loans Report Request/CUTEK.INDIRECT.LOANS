[
****************************************************************************************
*****  Copyright 2022 CUTEK. All rights reserved.                     rev 01-2022  *****
*****                                                                              *****
*****  This licensed source code may not be reproduced, displayed, modified or     *****
*****  distributed without the express prior written permission of the copyright   *****
*****  holder. As implemented with CUTEK products, code is licensed only for the   *****
*****  license term of the agreement. For further information, contact             *****
*****  sales@cutek.com                                                             *****
****************************************************************************************

 Client name:  Austin Telco
 File name:    CUTEK.INDIRECT.LOANS
 Created by:   CUTEK, Inc. - Marc Middleton
 Created on:   December 2022
 CUTEK Ticket: 32283
 
 Description:
 Delimited Letter File Extract for Indirect Loan information.
 
 Modified by:  CUTEK, Inc. - Marc Middleton
 Modified on:  January 2023
 Modification: Add Paid Off Loan Info to Totals.
]
TARGET=LOAN

DEFINE
 INDLOANTYPES="0081,0082,0083,0084"
 PRIMESHARETYPE=0000
 PRIMESHAREOPENED=DATE
 XXX=NUMBER
 FNUM=NUMBER
 FTYPE=CHARACTER
 COLINDEX=NUMBER
 FERROR=CHARACTER
 QUOTE=CHARACTER(1) 
 TEMPCHAR=CHARACTER
 COLDATA=CHARACTER ARRAY(12)
 DOUBLEQUOTE=CHARACTER
 DELIMITER=CHARACTER
 TEMPLINE=CHARACTER
 FNAME=CHARACTER
 TOPCOL=NUMBER
 QPOS=NUMBER 
 OLDNEW=CHARACTER
 APPFEE=MONEY
 Dealership=CHARACTER
 LASTACCTNUMBER=CHARACTER
 ACCTCOUNT=NUMBER
 NEWLOANCOUNT=NUMBER
 NEWLOANBALANCE=MONEY
 LOANCOUNT=NUMBER
 LOANBALANCE=MONEY
 STARTDATE=DATE
 ENDDATE=DATE
 DeferredRate=RATE
 UnamortizedFees=MONEY
 LOANPAIDOFFCOUNT=NUMBER
 LOANPAIDOFFBALANCE=MONEY
 LOANDEFFEREDFEESPAID=MONEY
 LOANDEFFEREDFEESCOUNT=NUMBER
 LOANDEFFEREDFEESBALANCE=MONEY
 LOANDEFFEREDFEES=MONEY
 ORIGLOANDEFFEREDFEES=MONEY
END

SETUP
 STARTDATE=DATEREAD("Start Date")
 ENDDATE=DATEREAD("End Date")
 IF STARTDATE='--/--/--' THEN STARTDATE=DATEOFFSET(SYSTEMDATE,0,1)
 IF ENDDATE='--/--/--' THEN ENDDATE=DATEOFFSET(SYSTEMDATE,0,31)
 IF STARTDATE>ENDDATE THEN STARTDATE=ENDDATE
 TOPCOL       = 12
 DELIMITER    = CTRLCHR(44) [comma]  
 DOUBLEQUOTE  = CTRLCHR(34) ["]
 FTYPE        = "LETTER"
 FNAME        = "INDIRECT.LOANS.csv" 
 FILEDELETE(FTYPE,FNAME,FERROR)
 FILEOPEN(FTYPE,FNAME,"APPEND",FNUM,FERROR) 
 IF FERROR<>"" THEN
  DO
   PRINT "Error Opening "+FTYPE+" File: "+FERROR
   NEWLINE
   PRINT "Terminating PowerOn...."
   TERMINATE
  END   
 COLDATA(01) = "Account"
 COLDATA(02) = "Name" 
 COLDATA(03) = "Term"
 COLDATA(04) = "Unamortized Fees"
 COLDATA(05) = "Deferred Rate"
 COLDATA(06) = "Loan #"
 COLDATA(07) = "Mbr"
 COLDATA(08) = "Rate" 
 COLDATA(09) = "Amount" 
 COLDATA(10) = "Opened"
 COLDATA(11) = "App"  
 COLDATA(12) = "Dealership"
 CALL OUTPUTCSVLINEFILE1
END

SELECT
 CHARACTERSEARCH(INDLOANTYPES,FORMAT("9999",LOAN:TYPE))>0 AND
(LOAN:OPENDATE>=STARTDATE AND LOAN:OPENDATE<=ENDDATE)
END

PRINT TITLE="INDIRECT.LOANS" REPORTCATEGORY="LOANREPORT"
 HEADER="" 
 LOANDEFFEREDFEESPAID = $0.00 
 IF LOAN:CLOSEDATE<>'--/--/--' THEN
  DO
   LOANPAIDOFFCOUNT = LOANPAIDOFFCOUNT + 1 
   LOANPAIDOFFBALANCE = LOANPAIDOFFBALANCE + LOAN:ORIGINALBALANCE
   IF ((MONTH(LOAN:CLOSEDATE) = MONTH(LOAN:OPENDATE)) AND (FULLYEAR(LOAN:CLOSEDATE) = FULLYEAR(LOAN:OPENDATE))) THEN
    DO   
     LOANDEFFEREDFEESCOUNT = LOANDEFFEREDFEESCOUNT + 1
     LOANDEFFEREDFEES = LOANDEFFEREDFEES + LOAN:UNAMORTIZEDFEES*-1               
     LOANDEFFEREDFEESBALANCE = LOANDEFFEREDFEESBALANCE + LOAN:ORIGINALBALANCE
     FOR EACH LOAN TRANSACTION WITH LOAN TRANSACTION:ID = LOAN:ID AND (LOAN TRANSACTION:POSTDATE >= LOAN:OPENDATE AND LOAN TRANSACTION:POSTDATE <= LOAN:CLOSEDATE) AND
                                    LOAN TRANSACTION:SOURCECODE = "F" AND LOAN TRANSACTION:VOIDCODE = 0
      DO
       LOANDEFFEREDFEESPAID = LOANDEFFEREDFEESPAID + LOAN TRANSACTION:TRANAMOUNT
      END                                    
    END
  END
 IF LOAN:CLOSEDATE='--/--/--' AND LOAN:CHARGEOFFDATE='--/--/--' THEN
  DO
   OLDNEW = ""
   APPFEE = $75.00
   PRIMESHAREOPENED = '--/--/--'
   FOR ACCOUNT ACCOUNT:NUMBER
    DO
     FOR EACH SHARE WITH SHARE:TYPE=PRIMESHARETYPE
      DO
       PRIMESHAREOPENED = SHARE:OPENDATE
      END
     IF ACCOUNT:NUMBER <> LASTACCTNUMBER THEN
      DO
       ACCTCOUNT = ACCTCOUNT + 2
      END 
     LASTACCTNUMBER = ACCOUNT:NUMBER 
    END 
   IF PRIMESHAREOPENED = LOAN:OPENDATE THEN
    DO
     OLDNEW = "New"
     APPFEE = $50.00
     NEWLOANCOUNT = NEWLOANCOUNT + 1
     NEWLOANBALANCE = NEWLOANBALANCE + LOAN:ORIGINALBALANCE
    END 
   Dealership = "" 
   FOR EACH LOAN TRACKING WITH LOAN TRACKING:TYPE=30
    DO
     Dealership = LOAN TRACKING:USERCHAR12
    END  
   ORIGLOANDEFFEREDFEES = ORIGLOANDEFFEREDFEES + LOAN:ORIGUNAMORTIZEDFEES*-1 
   UnamortizedFees = LOAN:UNAMORTIZEDFEES*-1 [Field contains negative value]
   DeferredRate = 000.000%
   IF UnamortizedFees<>$0.00 THEN
    DO   
     DeferredRate = (UnamortizedFees/LOAN:ORIGINALBALANCE)
    END
   IF DeferredRate <> 000.000% THEN
    DO
     IF DeferredRate <= 001.001% THEN
      DO
       DeferredRate = 001.000%
      END
     ELSE
      DO
       DeferredRate = 001.500%
      END 
    END  
   LOANCOUNT   = LOANCOUNT + 1
   LOANBALANCE = LOANBALANCE + LOAN:ORIGINALBALANCE 
   COLDATA(01) = ACCOUNT:NUMBER
   COLDATA(02) = NAME:SHORTNAME 
   COLDATA(03) = FORMAT("9999",LOAN:PAYMENTCOUNT)
   COLDATA(04) = FORMAT("###,###,###,##9.99",UnamortizedFees)
   COLDATA(05) = FORMAT("##9.999%",DeferredRate)
   COLDATA(06) = LOAN:ID
   COLDATA(07) = OLDNEW
   COLDATA(08) = FORMAT("999.999%",LOAN:INTERESTRATE)
   COLDATA(09) = FORMAT("###,###,###,##9.99",LOAN:ORIGINALBALANCE)
   COLDATA(10) = FORMAT("99/99/9999",LOAN:OPENDATE)
   COLDATA(11) = FORMAT("$99.99",APPFEE )
   COLDATA(12) = Dealership
   CALL OUTPUTCSVLINEFILE1
  END 
END

TOTAL
 NEWLINE
 COL=005 "Start Date: "
 COL=060 STARTDATE
 NEWLINE
 COL=005 "End Date: "
 COL=060 ENDDATE 
 NEWLINE
 COL=005 "Total Loan Count: "
 COL=060 LOANCOUNT
 NEWLINE
 COL=005 "Total Loan Amount: "
 COL=060 LOANBALANCE
 NEWLINE
 COL=005 "Total New Loan Count: "
 COL=060 NEWLOANCOUNT
 NEWLINE
 COL=005 "Total New Loan Amount: "
 COL=060 NEWLOANBALANCE
 NEWLINE 
 COL=005 "Total Loan Paid Off Count: "               [# of loans paid off, ]
 COL=060 LOANPAIDOFFCOUNT
 NEWLINE
 COL=005 "Total Loan Paid Off Amount: "              [the total $ amount paid off, ]
 COL=060 LOANPAIDOFFBALANCE
 NEWLINE 
 COL=005 "Total Original Unamortised Fees: "         [total for Original Unamortized Fees] 
 COL=060 ORIGLOANDEFFEREDFEES
 NEWLINE
 COL=005 "Loan Open&Closed Same Month: "             [the total $ amount paid off, ]
 COL=060 LOANDEFFEREDFEESCOUNT
 NEWLINE
 COL=005 "Loan Open&Closed Same Month Amount: "      [total amount of deferred fees for new loans opened and paid off during the same month] 
 COL=060 LOANDEFFEREDFEESBALANCE
 NEWLINE
 COL=005 "Loan Open&Closed Unamortised Fee Amount: " [total amount of deferred fees for new loans opened and paid off during the same month] 
 COL=060 LOANDEFFEREDFEES [LOANDEFFEREDFEESPAID]
 NEWLINE 
 NEWLINE
 COL=005 "View "+FTYPE+" file "+FNAME
 NEWLINE
END

PROCEDURE OUTPUTCSVLINEFILE1
 FOR COLINDEX = 1 TO TOPCOL
  DO
   TEMPCHAR = COLDATA(COLINDEX)
   CALL REMOVEQUOTES
   IF CHARACTERSEARCH(TEMPCHAR,DELIMITER)>0 THEN
    TEMPCHAR = QUOTE + TEMPCHAR + QUOTE    
   CALL FORMATTYPE  
   IF COLINDEX<TOPCOL THEN
    DO
     TEMPCHAR = TEMPCHAR+DELIMITER
     FILEWRITE(FNUM,TEMPCHAR,FERROR)
    END
   ELSE
    FILEWRITELINE(FNUM,TEMPCHAR,FERROR)
  END 
END

PROCEDURE REMOVEQUOTES
 QPOS = CHARACTERSEARCH(TEMPCHAR,QUOTE)
 WHILE QPOS>0
  DO
   TEMPCHAR = SEGMENT(TEMPCHAR,1,QPOS-1)+SEGMENT(TEMPCHAR,QPOS+1,LENGTH(TEMPCHAR))
   QPOS = CHARACTERSEARCH(TEMPCHAR,QUOTE)
  END
END

PROCEDURE FORMATTYPE
 IF CHARACTERSEARCH(TEMPCHAR,DOUBLEQUOTE)>0 OR CHARACTERSEARCH(TEMPCHAR,DELIMITER)>0 THEN
  DO
   IF CHARACTERSEARCH(TEMPCHAR,DOUBLEQUOTE)>0 THEN
    DO
     TEMPLINE=""
     FOR XXX=1 TO LENGTH(TEMPCHAR)
      DO
       IF SEGMENT(TEMPCHAR,XXX,XXX)=DOUBLEQUOTE THEN
        TEMPLINE=TEMPLINE+DOUBLEQUOTE     [Precede embedded double-quote with a double-quote]
       TEMPLINE=TEMPLINE+SEGMENT(TEMPCHAR,XXX,XXX)
      END
     TEMPCHAR=TEMPLINE
    END
   TEMPCHAR=DOUBLEQUOTE+TEMPCHAR+DOUBLEQUOTE
  END
END

