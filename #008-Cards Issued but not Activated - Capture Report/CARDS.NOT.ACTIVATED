[
****************************************************************************************
*****  Copyright 2022 CUTEK. All rights reserved.                     rev 01-2022  *****
*****                                                                              *****
*****  This licensed source code may not be reproduced, displayed, modified or     *****
*****  distributed without the express prior written permission of the copyright   *****
*****  holder. As implemented with CUTEK products, code is licensed only for the   *****
*****  license term of the agreement. For further information, contact             *****
*****  sales@cutek.com                                                             *****
****************************************************************************************

 Client name:  Austin Telco
 File name:    CARDS.NOT.ACTIVATED
 Created by:   CUTEK, Inc. - Marc Middleton
 Created on:   DECEMBER 2022
 CUTEK Ticket: 32283
 
 Description:
 #008-Cards Issued but not Activated - Capture Report
 -Batch FM
 -Capture cards when Issued, and Not Activated, for > 105 Days.
 -COMMENT on CARD: "Card captured due to no activation date."
 
 Modified by:  
 Modified on:  
 Modification: 
]
TARGET=CARD

DEFINE
 ACTDAYS=105
 #INCLUDE "RD.OUTPUT.DEF"
 OUTPUTCHANNEL1=NUMBER
 ERRORTEXT=CHARACTER
 ERRORTEXTIGNORE=CHARACTER 
 XXX=NUMBER
 FNUM=NUMBER
 FTYPE=CHARACTER
 COLINDEX=NUMBER
 FERROR=CHARACTER
 QUOTE=CHARACTER(1) 
 TEMPCHAR=CHARACTER
 COLDATA=CHARACTER ARRAY(14)
 DOUBLEQUOTE=CHARACTER
 DELIMITER=CHARACTER
 TEMPLINE=CHARACTER
 FNAME=CHARACTER
 TOPCOL=NUMBER
 QPOS=NUMBER 
 NFIRST  = CHARACTER
 NMIDDLE = CHARACTER
 NLAST   = CHARACTER
 NSTREET = CHARACTER
 NEXADDR = CHARACTER
 NCITY   = CHARACTER
 NSTATE  = CHARACTER
 NZIP    = CHARACTER
 NEMAIL  = CHARACTER 
END

SETUP
 OUTPUTOPEN(OUTPUTDEVREPORT,0,"CARDS.NOT.ACTIVATED.FM","FMREPORTS",OUTPUTCHANNEL1,ERRORTEXT)
 IF ERRORTEXT<>"" THEN CALL ERRORHANDLER
 
 TOPCOL       = 14
 DELIMITER    = CTRLCHR(44) [comma]  
 DOUBLEQUOTE  = CTRLCHR(34) ["] 
 FTYPE        = "LETTER"
 FNAME        = "CARDS.NOT.ACTIVATED.csv" 
 FILEDELETE(FTYPE,FNAME,FERROR)
 FILEOPEN(FTYPE,FNAME,"APPEND",FNUM,FERROR) 
 IF FERROR<>"" THEN
  DO
   PRINT "Error Opening "+FTYPE+" File: "+FERROR
   NEWLINE
   PRINT "Terminating PowerOn...."
   TERMINATE
  END   
 COLDATA(01) = "Account"
 COLDATA(02) = "Card Number"
 COLDATA(03) = "Issued"
 COLDATA(04) = "Activated"
 COLDATA(05) = "Closed"
 COLDATA(06) = "First Name"
 COLDATA(07) = "Middle Name"
 COLDATA(08) = "Last Name"
 COLDATA(09) = "Street"
 COLDATA(10) = "Ex. Address"
 COLDATA(11) = "City"
 COLDATA(12) = "State"
 COLDATA(13) = "ZipCode"
 COLDATA(14) = "email"
 CALL OUTPUTCSVLINEFILE1 
END

SELECT
 CARD:STATUS<>2 AND 
 CARD:ACTIVEDATE='--/--/--' AND
(CARD:ISSUEDATE<>'--/--/--' AND
 CARD:ISSUEDATE<SYSTEMDATE-ACTDAYS)
END

SORT
 CARD:ISSUEDATE
END 

PRINT TITLE="CARDS.NOT.ACTIVATED" REPORTCATEGORY="CARDARCHV"
 HEADERS
  COL=001 "Account"
  COL=014 "Card Number"
  COL=040 RIGHT "Issued"
  COL=050 RIGHT "Activated"
  COL=060 RIGHT "Closed" 
  NEWLINE
  COL=001 REPEATCHR("=",60)
 END  
 COL=001 ACCOUNT:NUMBER
 COL=014 CARD:NUMBER
 COL=040 CARD:ISSUEDATE
 COL=050 CARD:ACTIVEDATE
 COL=060 CARD:CLOSEDATE
 CALL MODIFYCARDS
 COLDATA(01) = ACCOUNT:NUMBER
 COLDATA(02) = SEGMENT(CARD:NUMBER,LENGTH(CARD:NUMBER)-3,LENGTH(CARD:NUMBER))
 COLDATA(03) = FORMAT("99/99/9999",CARD:ISSUEDATE)
 COLDATA(04) = FORMAT("99/99/9999",CARD:ACTIVEDATE)
 COLDATA(05) = FORMAT("99/99/9999",CARD:CLOSEDATE)
 CALL GETBESTADDRESS
 COLDATA(06) = NFIRST
 COLDATA(07) = NMIDDLE
 COLDATA(08) = NLAST
 COLDATA(09) = NSTREET
 COLDATA(10) = NEXADDR
 COLDATA(11) = NCITY
 COLDATA(12) = NSTATE
 COLDATA(13) = NZIP
 COLDATA(14) = NEMAIL
 CALL OUTPUTCSVLINEFILE1 
END

TOTAL
 NEWLINE
 COL=005 "View "+FTYPE+" file "+FNAME
 NEWLINE
END

PROCEDURE GETBESTADDRESS
 NFIRST  = NAME:FIRST
 NMIDDLE = NAME:MIDDLE
 NLAST   = NAME:LAST
 NSTREET = NAME:STREET
 NEXADDR = NAME:EXTRAADDRESS
 NCITY   = NAME:CITY
 NSTATE  = NAME:STATE
 NZIP    = NAME:ZIPCODE
 NEMAIL  = NAME:EMAIL
 FOR ACCOUNT ACCOUNT:NUMBER
  DO
   FOR EACH NAME WITH (NAME:TYPE=2 OR NAME:TYPE=3) AND NAME:MAILOVERRIDE=1 AND
                      (NAME:EXPIRATIONDATE='--/--/--' OR NAME:EXPIRATIONDATE>SYSTEMDATE) AND
                      (NAME:EFFECTIVEDATE='--/--/--' OR NAME:EFFECTIVEDATE<=SYSTEMDATE)
    DO
     NFIRST  = NAME:FIRST
     NMIDDLE = NAME:MIDDLE
     NLAST   = NAME:LAST
     NSTREET = NAME:STREET
     NEXADDR = NAME:EXTRAADDRESS
     NCITY   = NAME:CITY
     NSTATE  = NAME:STATE
     NZIP    = NAME:ZIPCODE
     IF NAME:EMAIL <> "" THEN
      DO
       NEMAIL  = NAME:EMAIL
      END
    END
  END
END
 
PROCEDURE OUTPUTCSVLINEFILE1
 FOR COLINDEX = 1 TO TOPCOL
  DO
   TEMPCHAR = COLDATA(COLINDEX)
   CALL REMOVEQUOTES
   IF CHARACTERSEARCH(TEMPCHAR,DELIMITER)>0 THEN
    TEMPCHAR = QUOTE + TEMPCHAR + QUOTE    
   CALL FORMATTYPE  
   IF COLINDEX<TOPCOL THEN
    DO
     TEMPCHAR = TEMPCHAR+DELIMITER
     FILEWRITE(FNUM,TEMPCHAR,FERROR)
    END
   ELSE
    FILEWRITELINE(FNUM,TEMPCHAR,FERROR)
  END 
END

PROCEDURE REMOVEQUOTES
 QPOS = CHARACTERSEARCH(TEMPCHAR,QUOTE)
 WHILE QPOS>0
  DO
   TEMPCHAR = SEGMENT(TEMPCHAR,1,QPOS-1)+SEGMENT(TEMPCHAR,QPOS+1,LENGTH(TEMPCHAR))
   QPOS = CHARACTERSEARCH(TEMPCHAR,QUOTE)
  END
END

PROCEDURE FORMATTYPE
 IF CHARACTERSEARCH(TEMPCHAR,DOUBLEQUOTE)>0 OR CHARACTERSEARCH(TEMPCHAR,DELIMITER)>0 THEN
  DO
   IF CHARACTERSEARCH(TEMPCHAR,DOUBLEQUOTE)>0 THEN
    DO
     TEMPLINE=""
     FOR XXX=1 TO LENGTH(TEMPCHAR)
      DO
       IF SEGMENT(TEMPCHAR,XXX,XXX)=DOUBLEQUOTE THEN
        TEMPLINE=TEMPLINE+DOUBLEQUOTE     [Precede embedded double-quote with a double-quote]
       TEMPLINE=TEMPLINE+SEGMENT(TEMPCHAR,XXX,XXX)
      END
     TEMPCHAR=TEMPLINE
    END
   TEMPCHAR=DOUBLEQUOTE+TEMPCHAR+DOUBLEQUOTE
  END
END

PROCEDURE MODIFYCARDS
 OUTPUTSWITCH(OUTPUTCHANNEL1,ERRORTEXT)
 HEADER="" 
 COL=01 "ACCOUNT "+ACCOUNT:NUMBER+" MODIFY CARD LOC "
 PRINT  CARD:LOCATOR
 NEWLINE
 COL=03 "CHANGE STATUS FROM"
 COL=49 CARD:STATUS
 COL=90 "TO"
 COL=93 "2"
 NEWLINE
 COL=03 "CHANGE CLOSEDATE FROM"
 COL=49 CARD:CLOSEDATE
 COL=90 "TO"
 COL=93 FORMAT("99/99/9999",SYSTEMDATE)
 NEWLINE
 COL=01 "ACCOUNT "+ACCOUNT:NUMBER+" CREATE CARD LOC "
 PRINT  CARD:LOCATOR
 PRINT  " NOTE BEFOREFIRST"
 NEWLINE
 COL=03 "SET TEXT:1 TO" 
 COL=49 "Card captured due to no activation date"
 NEWLINE 
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
END 

PROCEDURE ERRORHANDLER
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXTIGNORE)
 PRINT "Error: "+ERRORTEXT
 NEWLINE
 TERMINATE
END
